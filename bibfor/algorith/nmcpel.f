      SUBROUTINE NMCPEL (FAMI,KPG,KSP,NDIM,TYPMOD,ANGMAS,IMATE,COMPOR,
     &             CRIT,TEMP,TREF,OPTION,EPS,SIG,VI,DSIDEP,CODRET)
C ----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 29/09/2006   AUTEUR VABHHTS J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C ----------------------------------------------------------------------
C     LOIS DE COMPORTEMENT HYPER-ELASTIQUES (ELEMENTS ISOPARAMETRIQUES)
C
C IN  NDIM    : DIMENSION DE L'ESPACE
C IN  TYPMOD  : TYPE DE MODELISATION
C IN  IMATE   : ADRESSE DU MATERIAU CODE
C IN  COMPOR  : COMPORTEMENT  (1) = TYPE DE RELATION COMPORTEMENT
C                             (2) = NB VARIABLES INTERNES / PG
C                             (3) = HYPOTHESE SUR LES DEFORMATIONS
C                             (4) = COMP_ELAS (OU COMP_INCR)
C IN  CRIT    : CRITERES DE CONVERGENCE LOCAUX
C                               (1) = NB ITERATIONS MAXI A CONVERGENCE
C                                     (ITER_INTE_MAXI == ITECREL)
C                               (2) = TYPE DE JACOBIEN A T+DT
C                                     (TYPE_MATR_COMP == MACOMP)
C                                     0 = EN VITESSE     >SYMETRIQUE
C                                     1 = EN INCREMENTAL >NON-SYMETRIQUE
C                               (3) = VALEUR TOLERANCE DE CONVERGENCE
C                                     (RESI_INTE_RELA == RESCREL)
C IN  TEMP    : TEMPERATURE A L'INSTANT DU CALCUL
C IN  TREF    : TEMPERATURE DE REFERENCE POUR LES CONTRAINTES THERMIQUES
C IN  OPTION  : OPTION DEMANDEE : RIGI_MECA_TANG , FULL_MECA , RAPH_MECA
C IN  EPS     : DEFORMATIONS
C OUT SIG     : CONTRAINTES
C OUT VI      : VARIABLES INTERNES
C OUT DSIDEP  : RIGIDITE TANGENTE
C
C               ATTENTION LES TENSEURS ET MATRICES SONT RANGES DANS
C               L'ORDRE :  XX YY ZZ SQRT(2)*XY SQRT(2)*XZ SQRT(2)*YZ
C   -------------------------------------------------------------------
C     ASTER INFORMATIONS:
C       11/12/00 (OB): TOILETTAGE FORTRAN,
C                      RAJOUT DE PARAMETRE DANS L'APPEL A NMELNL POUR
C                      COHERENCE AVEC TE0096 ET OPTION='CALC_DG' EN 2D.
C-----------------------------------------------------------------------
C CORPS DU PROGRAMME
      IMPLICIT REAL*8 (A-H,O-Z)

C DECLARATION PARAMETRES D'APPELS
      INTEGER            NDIM,IMATE,CODRET,KPG,KSP
      CHARACTER*8        TYPMOD(*)
      CHARACTER*16       COMPOR(4), OPTION
      REAL*8             CRIT(3), TEMP , TREF , EPSM(6),SIGM(6)
      REAL*8             EPS(6), SIG(6), VI(*), DSIDEP(6,6),ANGMAS(3)
      CHARACTER*2  VALRET
      CHARACTER*4  FAMI
      CHARACTER*16 PHENOM
      REAL*8       ENERGI(2)

C DECLARATION VARIABLES LOCALES
      LOGICAL  LPIPO
      REAL*8   RPIPO,T1PIPO(6),T2PIPO(2),T3PIPO(6)


C INITIALISATIONS POUR APPEL A NMELNL
      LPIPO = .FALSE.
      RPIPO = 0.D0
      T2PIPO(1) = 0.D0
      T2PIPO(2) = 0.D0
      DO 1 I=1,6
        T1PIPO(I) = 0.D0
        T3PIPO(I) = 0.D0
1     CONTINUE

      CODRET = 0

      CALL RCCOMA(IMATE,'ELAS',PHENOM,VALRET)

C - ORTHOTROPIE OU ISOTROPIE TRANSVERSE LINEAIRE
      IF (VALRET.EQ.'OK' .AND.
     &    (PHENOM(1:6).EQ.'ELAS_O'.OR.PHENOM(1:6).EQ.'ELAS_I')) THEN
        IF ( COMPOR(1)(1:5) .EQ. 'ELAS ')THEN
          DO 30 I=1,6
            EPSM(I)=0.D0
            SIGM(I)=0.D0
 30     CONTINUE
          CALL NMORTH (FAMI, KPG, KSP, NDIM, PHENOM, TYPMOD, IMATE,
     &                 0.D0,TEMP,TREF,EPSM,EPS,SIGM,
     &                 OPTION,ANGMAS, SIG,R8BID, DSIDEP)
        ELSE
          CALL U2MESS('F','ALGORITH7_7')
        ENDIF

C   ELASTICITE NON LINEAIRE ISOTROPE
      ELSE IF (COMPOR(1)(1:5)  .EQ. 'ELAS '          .OR.
     &         COMPOR(1)(1:14) .EQ. 'ELAS_VMIS_LINE' .OR.
     &         COMPOR(1)(1:14) .EQ. 'ELAS_VMIS_TRAC' ) THEN
           CALL NMELNL ( NDIM, TYPMOD, IMATE, COMPOR, CRIT, TEMP, TREF,
     &                   OPTION, EPS, SIG, VI, DSIDEP, ENERGI,
     &                   LPIPO, RPIPO, T1PIPO, T2PIPO, T3PIPO)

C    LOI ELASTIQUE POUR METHODE ZAC
      ELSE IF (COMPOR(1)  .EQ. 'ZAC  ') THEN
        CALL LCZAC ( NDIM, TYPMOD, IMATE, TEMP, OPTION,
     &               EPS, SIG, DSIDEP)
C    LOI ELASTIQUE POUR MODELE SIGNORINI
      ELSEIF (COMPOR(1)(1:10).EQ. 'ELAS_HYPER') THEN
        IF (COMPOR(3).NE.'GREEN') THEN
          CALL U2MESS('F','ALGORITH6_96')
        ENDIF
        CALL HYPELA(NDIM,TYPMOD,IMATE,COMPOR,CRIT,
     &              TEMP,EPS,
     &              SIG,DSIDEP,CODRET)

      ELSE
        CALL U2MESS('F','ALGORITH7_8')
      ENDIF
      END
