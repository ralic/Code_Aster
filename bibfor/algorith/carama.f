      SUBROUTINE CARAMA(NOMA  ,DEFICO,RESOCO,NEWGEO,POSMA ,
     &                  IZONE ,POSNO ,MATYP ,NBNOM ,COOR  ,
     &                  ZONESY,MOYEN ,LISSA ,NORMMA,TOLEIN,
     &                  TOLEOU,DIAGNO)
C     
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 13/03/2007   AUTEUR ABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2004  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT     NONE
      CHARACTER*8  NOMA
      CHARACTER*24 DEFICO
      CHARACTER*24 RESOCO      
      CHARACTER*24 NEWGEO
      CHARACTER*19 NORMMA
      INTEGER      POSMA
      INTEGER      IZONE
      INTEGER      POSNO(10)
      CHARACTER*4  MATYP
      INTEGER      NBNOM
      REAL*8       COOR(27)
      INTEGER      MOYEN
      INTEGER      LISSA
      INTEGER      ZONESY
      REAL*8       TOLEIN
      REAL*8       TOLEOU
      INTEGER      DIAGNO
C      
C ----------------------------------------------------------------------
C
C ROUTINE CONTACT (METHODES DISCRETES - APPARIEMENT - MAIT/ESCL)
C
C CARACTERISTIQUES DE LA MAILLE MAITRE 
C
C ----------------------------------------------------------------------
C
C
C IN  NOMA   : NOM DU MAILLAGE
C IN  DEFICO : SD DE CONTACT (DEFINITION)
C IN  RESOCO : SD DE TRAITEMENT NUMERIQUE DU CONTACT
C IN  NEWGEO : COORDONNEES REACTUALISEES DES NOEUDS DU MAILLAGE
C IN  POSMA  : INDICE DE LA MAILLE MAITRE
C IN  IZONE  : REPERAGE ZONE DE CONTACT
C IN  ZONESY : 0   ZONE DE CONTACT NORMAL
C              1/2 ZONE DE CONTACT AVEC APPARIEMENT SYMETRIQUE
C I/O POSNO  : INDICES DANS CONTNO DES NOEUDS ESCLAVE ET MAITRES
C               IN  POSNO(1) : INDICE DU NOEUD ESCLAVE
C               OUT POSNO(>1): INDICES DES NOEUDS MAITRES
C OUT MATYP  : TYPE DE LA MAILLE MAITRE
C OUT NBNOM  : NOMBRE DE NOEUDS DE LA MAILLE MAITRE
C OUT COOR   : COORDONNEES DES NOEUDS DE LA MAILLE MAITRE
C OUT MOYEN  : NORMALES D'APPARIEMENT
C               0 MAIT
C               1 MAIT_ESCL
C OUT LISSA  : LISSAGE DES NORMALES
C               0 PAS DE LISSAGE
C               1 LISSAGE
C OUT NORMMA : NOM DE L'OBJET CONTENANT LES NORMALES AUX NOEUDS MAITRES
C OUT TOLEIN : TOLERANCE <IN> POUR LA PROJECTION GEOMETRIQUE
C              DETERMINE SI PROJECTION SUR ARETE OU NOEUD
C OUT TOLEOU : TOLERANCE <OUT> POUR LA PROJECTION GEOMETRIQUE
C              DETERMINE SI PROJECTION EN DEHORS ELEMENT
C OUT DIAGNO : DRAPEAU POUR DIAGNOSTIC GEOMETRIQUE
C               VERIF DE LA PROJECTION SUR NOEUDS/ARETES/DIAGONALES
C               AVEC LA TOLERANCE TOLEIN
C
C -------------- DEBUT DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER            ZI
      COMMON  / IVARJE / ZI(1)
      REAL*8             ZR
      COMMON  / RVARJE / ZR(1)
      COMPLEX*16         ZC
      COMMON  / CVARJE / ZC(1)
      LOGICAL            ZL
      COMMON  / LVARJE / ZL(1)
      CHARACTER*8        ZK8
      CHARACTER*16                ZK16
      CHARACTER*24                          ZK24
      CHARACTER*32                                    ZK32
      CHARACTER*80                                              ZK80
      COMMON  / KVARJE / ZK8(1) , ZK16(1) , ZK24(1) , ZK32(1) , ZK80(1)
C
C ---------------- FIN DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER      CFDISI
      REAL*8       CFDISR
      INTEGER      NUMA,JDEC,INO,NO(9),PRONOR,NUTYP
      CHARACTER*24 CONTMA,PNOMA,NOMACO,CONTNO,NORLIS
      INTEGER      JMACO,JPONO,JNOMA,JNOCO,JCOOR,JNORLI
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ ()
C
      CONTMA = DEFICO(1:16)//'.MAILCO'
      PNOMA  = DEFICO(1:16)//'.PNOMACO'
      NOMACO = DEFICO(1:16)//'.NOMACO'
      CONTNO = DEFICO(1:16)//'.NOEUCO'
      NORLIS = DEFICO(1:16)//'.NORLIS'
C
      CALL JEVEUO(CONTMA,'L',JMACO)
      CALL JEVEUO(PNOMA, 'L',JPONO)
      CALL JEVEUO(NOMACO,'L',JNOMA)
      CALL JEVEUO(CONTNO,'L',JNOCO)
      CALL JEVEUO(NEWGEO(1:19)//'.VALE','L',JCOOR)
      CALL JEVEUO(NORLIS,'L',JNORLI)
C
C --- NUMERO ABSOLU DE LA MAILLE MAITRE ET NOMBRE DE NOEUDS
C
      NUMA  = ZI(JMACO+POSMA-1)
      NBNOM = ZI(JPONO+POSMA) - ZI(JPONO+POSMA-1)
      IF (NBNOM.GT.9) THEN
        CALL U2MESS('F','CONTACT_74')
      END IF
C
C --- TYPE DE LA MAILLE MAITRE DE NUMERO ABSOLU NUMA
C
      CALL TYPEMA(NOMA,NUMA,NUTYP,MATYP)
      IF ( MATYP .EQ. 'POI1' ) THEN
        CALL U2MESS('F','CONTACT_75')
      END IF
C
C --- INDICE DANS CONTMA ET NUMERO ABSOLU DES NOEUDS DE LA MAILLE MAITRE
C
      JDEC = ZI(JPONO+POSMA-1)
      DO 60 INO = 1,NBNOM
        POSNO(INO+1) = ZI(JNOMA+JDEC+INO-1)
        NO(INO)      = ZI(JNOCO+POSNO(INO+1)-1)
 60   CONTINUE
C
C --- COORDONNEES DES NOEUDS DE LA MAILLE MAITRE
C
      DO 70 INO = 1,NBNOM
        COOR(3*(INO-1)+1) = ZR(JCOOR+3*(NO(INO)-1))
        COOR(3*(INO-1)+2) = ZR(JCOOR+3*(NO(INO)-1)+1)
        COOR(3*(INO-1)+3) = ZR(JCOOR+3*(NO(INO)-1)+2)
  70  CONTINUE
C
C --- PROPRIETES DES NORMALES
C
      PRONOR = CFDISI(DEFICO,'NORMALE',IZONE)
      PRONOR = PRONOR + 2 * ZI(JNORLI+1)

      LISSA = 0
      MOYEN = 0

      IF (PRONOR.EQ.1) THEN
        MOYEN = 1
      ENDIF
      IF (PRONOR.GE.2) THEN
        LISSA = 1
        IF (PRONOR.EQ.3) THEN
          MOYEN = 1
        ENDIF
      ENDIF
C
C --- TOLEIN/TOLEOU : TOLERANCES GEOMETRIQUES
C
      TOLEOU = CFDISR(DEFICO,'TOLE_PROJ_EXT',IZONE)
      TOLEIN = CFDISR(DEFICO,'TOLE_PROJ_INT',IZONE)
C
C --- SI APPARIEMENT SYMETRIQUE: ACTIVATION DU DIAGNOSTIC FIN POUR 
C --- LA DETECTION DE LA PROJECTION SUR LES NOEUDS
C
      IF (ZONESY.NE.0) THEN 
        DIAGNO = 1
      ELSE
        DIAGNO = 0            
      ENDIF        
C
C --- LISSAGE DES NORMALES: RECOPIE DES NORMALES MAITRES DANS NORMMA
C
      IF (LISSA.EQ.1) THEN
        CALL CFLISS(RESOCO,NBNOM,POSNO,NORMMA)
      ENDIF
C
      CALL JEDEMA()
C ----------------------------------------------------------------------
      END
