      SUBROUTINE PROJT1(COORDA,COORDB,COORDC,COORDP,
     &                  COORDM,LAMOLD,LAMBDA,DEBORD)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 13/03/2007   AUTEUR ABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2007  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT NONE
      REAL*8      COORDA(3)
      REAL*8      COORDB(3)
      REAL*8      COORDC(3)
      REAL*8      COORDP(3)
      REAL*8      COORDM(3)
      REAL*8      DEBORD
      REAL*8      LAMBDA(3),LAMOLD(3)
C      
C ----------------------------------------------------------------------
C
C ROUTINE CONTACT (METHODES DISCRETES - APPARIEMENT - MAIT/ESCL - TRI)
C
C PROJECTION D'UN NOEUD ESCLAVE P SUR UN TRIANGLE LINEAIRE ABC
C UTILISATION DE LA NORMALE AU TRIANGLE MAITRE
C
C ----------------------------------------------------------------------
C
C
C IN  COORDA : COORDONNEES DU SOMMET A DU TRIANGLE
C IN  COORDB : COORDONNEES DU SOMMET B DU TRIANGLE
C IN  COORDC : COORDONNEES DU SOMMET C DU TRIANGLE
C IN  COORDP : COORDONNEES DU NOEUD ESCLAVE P
C OUT COORDM : COORDONNEES DE LA "PROJECTION" M
C OUT LAMOLD : COORDONNEES PARAMETRIQUES DE LA "PROJECTION" M AVANT
C                RABATTEMENT DANS LA MAILLE SI ON DEPASSE
C OUT LAMBDA : COORDONNEES PARAMETRIQUES DE LA "PROJECTION" M APRES
C                RABATTEMENT DANS LA MAILLE SI ON DEPASSE
C OUT DEBORD : PROJECTION HORS DE LA MAILLE
C              >0 : PROJECTION HORS DE LA MAILLE
C              <0 : PROJECTION SUR LA MAILLE
C
C ----------------------------------------------------------------------
C
      INTEGER K
      REAL*8  AP(3),AB(3),AC(3),BC(3),ABAC(3)
      REAL*8  SURFS,KSI1,KSI2,KSI3,DDOT,W(3)
      REAL*8  LAB,LAC,LBC,ABSAC,ACSAB,ABSBC,ACSBC
      REAL*8  OUTSID(3),NORM(3)
C
C ----------------------------------------------------------------------
C    
      DEBORD    = -1.D0
      OUTSID(1) = -1.D0
      OUTSID(2) = -1.D0
      OUTSID(3) = -1.D0      
C
C --- CALCUL DE AP ET DES COTES DU TRIANGLE ABC
C
      LAB = 0.D0
      LAC = 0.D0
      LBC = 0.D0
      DO 10 K = 1,3
         AP(K) = COORDP(K) - COORDA(K)
         AB(K) = COORDB(K) - COORDA(K)
         AC(K) = COORDC(K) - COORDA(K)
         BC(K) = COORDC(K) - COORDB(K)
         LAB = LAB + AB(K)*AB(K)
         LAC = LAC + AC(K)*AC(K)
         LBC = LBC + BC(K)*BC(K)
 10   CONTINUE
C
C --- CALCUL DE LA SURFACE (SIGNEE) SURFS DE LA MAILLE
C  
      CALL PROVEC(AB,AC,ABAC)   
      CALL DCOPY (3,ABAC,1,NORM,1) 
C               
      SURFS = DDOT (3,ABAC,1,NORM,1)
      
      IF (SURFS.EQ.0) THEN
        CALL U2MESS('F','CONTACT_15')
      END IF
C
C --- CALCUL DES COORDONNEES PARAMETRIQUES KSI1 ET KSI2 DE M DANS ABC
C
      CALL PROVEC (AP,NORM,W)
      KSI1 = - DDOT (3,W,1,AC,1) / SURFS
      KSI2 =   DDOT (3,W,1,AB,1) / SURFS
      KSI3 = 1.D0 - KSI1 - KSI2  
C
C --- SAUVEGARDE DES COORDONNEES PARAMETRIQUES DE M AVANT RABATTEMENT
C
      LAMOLD(1) = KSI1
      LAMOLD(2) = KSI2
      LAMOLD(3) = KSI3            
C      
C --- RABATTEMENT DE LA PROJECTION SUR LA MAILLE (ARETE)
C
      IF ((KSI1.LT.0.D0).OR.(KSI2.LT.0.D0).OR.
     &    (KSI3.LT.0.D0)) THEN   
         ABSAC = DDOT (3,AB,1,AC,1) / LAC
         ACSAB = DDOT (3,AB,1,AC,1) / LAB
         ABSBC = DDOT (3,AB,1,BC,1) / LBC
         ACSBC = DDOT (3,AC,1,BC,1) / LBC
         IF (KSI1.LT.0.D0) THEN
           OUTSID(1) = ABS(KSI1)
         END IF
         IF (KSI2.LT.0.D0) THEN
           OUTSID(2) = ABS(KSI2)
         END IF
         IF (KSI3.LT.0.D0) THEN
           OUTSID(3) = ABS(KSI3)
         END IF
         CALL AJUSTT (ABSAC,ACSAB,ABSBC,ACSBC,KSI1,KSI2)
      END IF
C
C --- CALCUL DES COORDONNEES CARTESIENNES DE M ("PROJECTION" DE P)
C
      DO 30 K = 1,3
        COORDM(K) = COORDA(K) + KSI1*AB(K) + KSI2*AC(K)
 30   CONTINUE
C
C --- SAUVEGARDE DES COORDONNEES PARAMETRIQUES
C 
      LAMBDA(1) = KSI1
      LAMBDA(2) = KSI2
      LAMBDA(3) = 1.D0 - KSI1 - KSI2
C
C --- PROJECTION HORS DE LA MAILLE ?
C --- SI OUI: DEBORD EST LA VALEUR DE DEBORDEMENT
C
      IF ((OUTSID(1).GT.0.D0).OR.(OUTSID(2).GT.0.D0)
     &                       .OR.(OUTSID(3).GT.0.D0)) THEN
         DEBORD = MAX(OUTSID(1),OUTSID(2))
         DEBORD = MAX(DEBORD,OUTSID(3))
      ENDIF      
C
      END
