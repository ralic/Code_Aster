      SUBROUTINE FETRIN(NBSD,NBI,VDO,VD1,MATAS,VSDF,VDDL,COLAUX,COLAUI,
     &                  CHSECM,SDFETI,VLAGI,OPTION,CHSOL,TESTCO,LRIGID,
     &                  DIMGI,IRR,JGI,JGITGI)
C-----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 09/11/2004   AUTEUR BOITEAU O.BOITEAU 
C ======================================================================
C COPYRIGHT (C) 1991 - 2004  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C-----------------------------------------------------------------------
C    - FONCTION REALISEE:  CALCUL, POUR FETI, DU RESIDU INITIAL OU 
C    RECONSTRUCTION VECTEUR DEPLACEMENT GLOBAL SOLUTION SI CONVERGENCE
C
C      IN   NBSD: IN   : NOMBRE DE SOUS-DOMAINES
C      IN    NBI: IN   : NOMBRE DE NOEUDS D'INTERFACE
C      IN    VD1: VR8  : VECTEUR AUXILIAIRE DE TAILLE NBI
C      IN  MATAS: CH19 : NOM DE LA MATR_ASSE GLOBALE
C      IN   VSDF: VIN  : VECTEUR MATR_ASSE.FETF INDIQUANT SI 
C                         SD FLOTTANT
C      IN   VDDL: VIN  : VECTEUR DES NBRES DE DDLS DES SOUS-DOMAINES
C      IN COLAUX: COL  : COLLECTION TEMPORAIRE DE REEL
C      IN COLAUI: COL  : COLLECTION TEMPORAIRE D'ENTIER
C      IN CHSECM: K19  : CHAM_NO SECOND MEMBRE GLOBAL
C      IN SDFETI: CH19 : SD DECRIVANT LE PARTIONNEMENT FETI
C      IN VLAGI : VR8  : VECTEUR LAGRANGE INITIAL OU SOLUTION
C      IN OPTION:  IN  : 1 -> RESIDU INIT., 2-> RECONSTRUCTION U SOL
C
C      SI OPTION=1
C      OUT   VDO: VR8  : VECTEUR OUTPUT DE TAILLE NBI
C
C      SI OPTION=2
C      IN  TESTCO: R8 : PARAMETRE DE TEST DE LA CONT. A L'INTERFACE
C      IN    IRR : IN : ADRESSE OBJET JEVEUX VECTEUR RESIDU PROJETE 
C      IN    JGI: IN  : ADRESSE OBJET JEVEUX GI
C      IN JGITGI: IN  : ADRESSE OBJET JEVEUX (GI)T*GI
C      IN LRIGID: LO  : LOGICAL INDIQUANT LA PRESENCE D'AU MOINS UN
C         SOUS-DOMAINES FLOTTANT
C      IN  DIMGI:  IN : TAILLE DE GIT*GI
C      OUT CHSOL: CH19 : CHAM_NO SOLUTION GLOBAL
C   -------------------------------------------------------------------
C     ASTER INFORMATIONS:
C       26/01/04 (OB): CREATION.
C       04/06/04 (OB): TRAITEMENT DES MODES DE CORPS RIGIDES
C----------------------------------------------------------------------
C RESPONSABLE BOITEAU O.BOITEAU
C CORPS DU PROGRAMME
      IMPLICIT NONE

C DECLARATION PARAMETRES D'APPELS
      INTEGER      NBSD,NBI,VDDL(NBSD),VSDF(NBSD),OPTION,DIMGI,JGITGI,
     &             JGI,IRR,I1
      REAL*8       VDO(NBI),VD1(NBI),VLAGI(NBI),TESTCO
      CHARACTER*19 MATAS,CHSECM,SDFETI,CHSOL
      CHARACTER*24 COLAUX,COLAUI
      LOGICAL      LRIGID
      
C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ---------------------
      INTEGER            ZI
      COMMON  / IVARJE / ZI(1)
      REAL*8             ZR
      COMMON  / RVARJE / ZR(1)
      COMPLEX*16         ZC
      COMMON  / CVARJE / ZC(1)
      LOGICAL            ZL
      COMMON  / LVARJE / ZL(1)
      CHARACTER*8        ZK8
      CHARACTER*16                ZK16
      CHARACTER*24                          ZK24
      CHARACTER*32                                    ZK32
      CHARACTER*80                                              ZK80
      COMMON  / KVARJE / ZK8(1) , ZK16(1) , ZK24(1) , ZK32(1) , ZK80(1)
C --------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ---------------------
      
C DECLARATION VARIABLES LOCALES
      INTEGER      IDD,IFETM,NBDDL,IDD1,JXSOL,NEQ1,IFETS,IVALE,IVALG,
     &             NBSOL,LMAT,IFETC,NBBLOC,TYPSYM,J,IFM,NIV,NEQ,NBSDF,
     &             OPTIOB,IREFE,IDEEQ,INO,ICMP,NBDDL1,IPRNO,LPRNO,NEC2,
     &             IDIME,NBNO,IVALS,DVALG,IVALCS,IMSG(2),NBMC,INLAGR,
     &             IFETP,NBMC1,JXSOL1,IALPHA,OPTIOP,IFETR,IDECAI,IDECAA,
     &             NBLILI,ILIL,LPRNOL,IPRNOL,NBCMP,IRET,IKFLIN,ILIG,
     &             NBCHA,ICHA,IFETL1,K,IFETL3,LFETL3,IPRNOG,ILILIL
      CHARACTER*8  K8BID,NOMSD
      CHARACTER*19 MATDD,CHSMDD,PRFCHN,CHAMLS,PRFCHG
      CHARACTER*24 NOMSDP,NOMSDR,K24B,LILIL,LIGRL,PRNOL,KFLIN,LIGR2,
     &             NOMSDA
      CHARACTER*32 JEXNUM,JEXNOM
      REAL*8       EPS,RAUX,RAUXL,RMIN,R8MIEM,RAUX1,UMOY,ALPHA
      LOGICAL      LLAGR,LDUP
      
C CORPS DU PROGRAMME
      CALL JEMARQ()

C PLUS PETITE VALEUR REELLE
      RMIN=R8MIEM()
      
C RECUPERATION DU NIVEAU D'IMPRESSION
      CALL INFNIV(IFM,NIV)
            
C INIT. NBRE DE SECOND MEMBRES SOLUTION POUR RLTFR8
      NBSOL=1

C INIT. NOM OBJET JEVEUX POUR PRODUIT PAR PSEUDO-INVERSE LOCALE      
      NOMSDP=MATAS//'.FETP'
      NOMSDR=MATAS//'.FETR'

     
      IF (OPTION.EQ.1) THEN
C INIT. VECTEUR SOLUTION LOCAL ET AUX.
        DO 10 J=1,NBI
          VD1(J)=0.D0
          VDO(J)=0.D0
   10   CONTINUE
      ELSE IF (OPTION.EQ.2) THEN
C OBJETS JEVEUX POINTANT SUR LA LISTE DES CHAM_NOS LOCAUX
        CALL JEVEUO(CHSOL//'.FETC','L',IFETS)
C PROF_CHNO DU DOMAINE GLOBAL   
        CALL JEVEUO(CHSOL//'.REFE','L',IREFE)
        PRFCHG=ZK24(IREFE+1)(1:19)
        CALL JEVEUO(PRFCHG//'.PRNO','L',IPRNO)
        CALL JELIRA(JEXNUM(PRFCHG//'.PRNO',1),'LONMAX',LPRNO,K8BID)     
C NBRE DE NOEUDS DU MAILLAGE
        CALL JEVEUO(SDFETI//'.FDIM','L',IDIME)  
        NBNO=ZI(IDIME+4)
C LONGUEUR DU VECTEUR D'ENTIERS CODES + 2
        NEC2=LPRNO/NBNO
C .VALE DU CHAM8NO SOLUTION GLOBAL
        CALL JEVEUO(CHSOL//'.VALE','E',IVALS)
        
C CALCUL ALPHA SI MODES DE CORPS RIGIDES
        IF (LRIGID) THEN     
           CALL WKVECT('&&FETI.ALPHA.MCR','V V R',NBI,IALPHA)
           OPTIOP=2
           CALL FETPRJ(NBI,ZR(IRR),ZR(IALPHA),ZR(JGI),JGITGI,LRIGID,
     &                 DIMGI,OPTIOP)
           IDECAA=IALPHA           
        ENDIF
C OBJET POUR RECONSTRUCTION DES DIRICHLETS
        KFLIN=SDFETI(1:19)//'.FLIN'
C POUR RECUPERER LE NOM DES SOUS-DOMAINES PAR JENUNO      
        NOMSDA=SDFETI(1:19)//'.FETA'             
      ELSE
        CALL UTMESS('F','FETRIN','OPTION DE CALCUL NON PREVUE !')
      ENDIF 
           
C OBJET JEVEUX POINTANT SUR LA LISTE DES MATR_ASSE
      CALL JEVEUO(MATAS//'.FETM','L',IFETM)
C OBJET JEVEUX POINTANT SUR LA LISTE DES CHAM_NO SECOND MEMBRE
      CALL JEVEUO(CHSECM//'.FETC','L',IFETC)
      
C ----------------------------------------------------------------------
C ----  BOUCLE SUR LES SOUS-DOMAINES
C ----------------------------------------------------------------------
C NOMBRE DE SOUS-DOMAINES FLOTTANTS      
      NBSDF=0
      DO 100 IDD=1,NBSD
        IDD1=IDD-1
        IF (OPTION.EQ.2) CALL JENUNO(JEXNUM(NOMSDA,IDD),NOMSD)
        
C MATR_ASSE ASSOCIEE AU SOUS-DOMAINE IDD      
        MATDD=ZK24(IFETM+IDD1)(1:19)              
C DESCRIPTEUR DE LA MATRICE DU SOUS-DOMAINE     
        CALL JEVEUO(MATDD//'.&INT','L',LMAT)
C NOMBRE DE BLOC DE STOCKAGE DE LA MATRICE KI/ TYPE DE SYMETRIE
        NBBLOC=ZI(LMAT+13)
        TYPSYM=ZI(LMAT+4)                       
C NBRE DE DDL DU SOUS-DOMAINE   
        NBDDL=VDDL(IDD)
        NBDDL1=NBDDL-1
C VECTEUR AUXILIAIRE DE TAILLE NDDL(SOUS_DOMAINE_IDD)     
        CALL JEVEUO(JEXNUM(COLAUX,IDD),'E',JXSOL)       
C SECOND MEMBRE LOCAL AU SOUS-DOMAINE
        CHSMDD=ZK24(IFETC+IDD1)(1:19)
        CALL JEVEUO(CHSMDD//'.VALE','L',IVALE)

C EXTRACTION AU SOUS-DOMAINE IDD: (RIDD)T * LANDA (0 OU SOL)
        OPTIOB=2        
        CALL FETREX(OPTIOB,IDD,NBI,VLAGI,NBDDL,ZR(JXSOL),SDFETI,COLAUI)
        
C RECOPIE DE FIDD - (RIDD)T*LANDA (0 OU SOL) DANS VECTEUR AUX     
        DO 20 J=0,NBDDL1
          ZR(JXSOL+J)=ZR(IVALE+J)-ZR(JXSOL+J)
   20   CONTINUE

C SCALING VIA ALPHA DES COMPOSANTES DU SECOND MEMBRE DUES AUX LAGRANGES
C SYSTEME: K * U= ALPHA * F ---> K * U/ALPHA = F
        CALL MRCONL(LMAT,0,'R',ZR(JXSOL),1)   
C -------------------------------------------------
C ----  SOUS-DOMAINE NON FLOTTANT
C -------------------------------------------------
C NOMBRES DE MODES DE CORPS RIGIDES DU SOUS-DOMAINE IDD
        NBMC=VSDF(IDD)     
        IF (NBMC.EQ.-1) THEN 

C CALCUL DE (KI)-FI PAR MULT_FRONT   
          CALL RLTFR8(MATDD,NBDDL,ZR(JXSOL),NBSOL,TYPSYM)
          
C MONITORING
          IF (NIV.GE.3) THEN
            WRITE(IFM,*)
            WRITE(IFM,*)'DDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDD'
            IF (OPTION.EQ.1) THEN
          WRITE(IFM,*)'<FETI/FETRIN> CALCUL (KI)-*(FI-RIT*LANDA0) I= ',
     &        IDD
            ELSE
          WRITE(IFM,*)'<FETI/FETRIN> CALCUL (KI)-*(FI-RIT*LANDAS) I= ',
     &        IDD           
            ENDIF                                   
            WRITE(IFM,*)'NBDDL/NBBLOC/TYPSYM',NBDDL,NBBLOC,TYPSYM
          ENDIF         

        ELSE
C -------------------------------------------------
C ----  SOUS-DOMAINE FLOTTANT
C -------------------------------------------------
          NBSDF=NBSDF+1
C CALCUL DE (KI)+FI PAR MULT_FRONT   
          CALL RLTFR8(MATDD,NBDDL,ZR(JXSOL),NBSOL,TYPSYM)         
          CALL JEVEUO(JEXNUM(NOMSDP,NBSDF),'L',IFETP)
          
          NBMC1=NBMC-1
          JXSOL1=JXSOL-1
          DO 25 J=0,NBMC1
            ZR(JXSOL1+ZI(IFETP+J))=0.D0   
   25     CONTINUE
C MONITORING
          IF (NIV.GE.3) THEN
            WRITE(IFM,*)
            WRITE(IFM,*)'DDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDD'
            IF (OPTION.EQ.1) THEN
           WRITE(IFM,*)'<FETI/FETRIN> CALCUL (KI)+*(FI-RIT*LANDA0) I= ',
     &        IDD
            ELSE
           WRITE(IFM,*)'<FETI/FETRIN> CALCUL (KI)+*(FI-RIT*LANDAS) I= ',
     &        IDD           
            ENDIF
            WRITE(IFM,*)'NBDDL/NBBLOC/TYPSYM',NBDDL,NBBLOC,TYPSYM
            WRITE(IFM,*)'NBMC/NBSDF',NBMC,NBSDF
          ENDIF
        ENDIF
C SCALING DES COMPOSANTES DE ZR(LXSOL) POUR CONTENIR LA SOL. REELLE U
        CALL MRCONL(LMAT,0,'R',ZR(JXSOL),1)
        
C -------------------------------------------------
C ----  CALCUL RESIDU INITIAL
C -------------------------------------------------
        IF (OPTION.EQ.1) THEN
C RESTRICTION DU SOUS-DOMAINE IDD SUR L'INTERFACE: (RIDD) * ...
          OPTIOB=1      
          CALL FETREX(OPTIOB,IDD,NBDDL,ZR(JXSOL),NBI,VD1,SDFETI,COLAUI)
          
C CUMUL DANS LE VECTEUR VD1=SOMME(I=1,NBSD)(RI*((KI)+ *(FI-RIT*LANDA0))
          DO 30 J=1,NBI
            VDO(J)=VDO(J)+VD1(J)
   30     CONTINUE

C MONITORING
          IF (NIV.GE.3) THEN
            WRITE(IFM,*)'<FETI/FETRIN> CUMUL  R = R + '//
     &                  'RI*((KI)+*(FI-RIT*LANDA0)) '
            WRITE(IFM,*)'DDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDD'
          ENDIF
          
        ELSE

C -------------------------------------------------
C ----  RECONSTRUCTION SOLUTION U GLOBALE
C -------------------------------------------------
C RAJOUT EVENTUEL DES PARTIES DUES AUX MODES DE CORPS RIGIDES
          IF (NBMC.NE.-1) THEN
C COMPOSANTES DES MODES DE CORPS RIGIDES
            CALL JEVEUO(JEXNUM(NOMSDR,NBSDF),'L',IFETR)
            IDECAI=IFETR
            DO 32 J=1,NBMC
C COEFF. ALPHAI MULTIPLICATEUR      
              ALPHA=-ZR(IDECAA)               
C USOLI = USOLI + BI * ALPHAI         
              CALL R8AXPY(NBDDL,ALPHA,ZR(IDECAI),1,ZR(JXSOL),1)
              IDECAI=IDECAI+NBDDL
              IDECAA=IDECAA+1
   32       CONTINUE      
          ENDIF
          
C PROF_CHNO DU SOUS-DOMAINE IDD 
          CALL JEVEUO(CHSMDD//'.REFE','L',IREFE)
          PRFCHN=ZK24(IREFE+1)(1:19)
          CALL JEVEUO(PRFCHN//'.DEEQ','L',IDEEQ)

C .VALE DU CHAM_NO LOCAL IDD
          CHAMLS=ZK24(IFETS+IDD1)(1:19)
          CALL JEVEUO(CHAMLS//'.VALE','L',IVALCS)
          
C --------------------------------
C ----  BOUCLE SUR LES DDLS PHYSIQUE DU CHAM_NO LOCAL
C --------------------------------        
          DO 35 J=0,NBDDL1
          
C NUMERO DU NOEUD DU MAILLAGE (INO) ET DE SA COMPOSANTE (ICMP) CORRES
C PONDANT A L'EQUATION J DU CHAM_NO LOCAL       
            INO=ZI(IDEEQ+2*J)
            ICMP=ZI(IDEEQ+2*J+1)
            
            IF (ICMP*INO.GT.0) THEN
C NOEUD PHYSIQUE            
C DECALAGE DANS LE .VALE DU CHAM_NO GLOBAL CORRESPONDANT A (INO,ICMP)
              DVALG=ZI(IPRNO+(INO-1)*NEC2) + ICMP-1
              
C VALEUR UI A TRANSFERRER SUR LE CHAM_NO GLOBAL ET SUR LE LOCAL
              RAUXL=ZR(JXSOL+J)
              ZR(IVALCS+J)=RAUXL      
C TEST POUR VERIFIER LA CONTINUITE AUX INTERFACES
              IVALG=IVALS-1+DVALG
              RAUX=ZR(IVALG)
              IF (ABS(ZR(IVALG)).GT.RMIN) THEN
                UMOY=(RAUX+RAUXL)*0.5D0       
                RAUX1=ABS((RAUX-RAUXL)/UMOY)
                IF (RAUX1.GT.TESTCO) THEN
                  IMSG(1)=INO
                  IMSG(2)=ICMP
                  RAUX1=100.D0*RAUX1
                CALL UTDEBM('A','FETRIN','PB POTENTIEL DE CONTINUITE ?')
                  CALL UTIMPI('L','INTERFACE (INO,ICMP)= ',2,IMSG)
                  CALL UTIMPR('L','ERREUR INTERFACE (EN %)= ',1,RAUX1)
                  CALL UTFINM()         
                ENDIF
              ELSE
                UMOY=RAUXL           
              ENDIF
             
C AFFECTATION EFFECTIVE DE UI VERS U         
              ZR(IVALG)=UMOY

C MONITORING
              IF (NIV.GE.5) THEN
                WRITE(IFM,*)'NOEUD PHYSIQUE '
                WRITE(IFM,*)IDD,J,INO,ICMP,RAUX,RAUXL
              ENDIF
            ELSE
C MONITORING
            IF (NIV.GE.5) THEN
              WRITE(IFM,*)'NOEUD TARDIF NON PRIS DANS CETTE PASSE '
              WRITE(IFM,*)IDD,J,INO,ICMP
            ENDIF           
            ENDIF 
   35     CONTINUE
   
C --------------------------------
C ----  FIN BOUCLE SUR LES DDLS PHYSIQUE DU CHAM_NO LOCAL
C --------------------------------              

C --------------------------------
C ----  BOUCLE SUR LES LIGRELS TARDIFS DU CHAM_NO LOCAL
C --------------------------------

          LILIL=PRFCHN//'.LILI'
          PRNOL=PRFCHN//'.PRNO'
          CALL JELIRA(LILIL,'NOMMAX',NBLILI,K8BID)
C LILI(1)=MAILLAGE, LILI(2)=MODELE, LILI(3...)=LIGREL TARDIF 
C SI NECESSAIRE
          CALL JEEXIN(JEXNOM(KFLIN,NOMSD),IRET)
          IF (NBLILI.LE.2) THEN
            IF (IRET.NE.0)
     &        CALL UTMESS('F','FETRIN','INCOHERENCE 1 .LILI ET .FLIN !')
          ELSE
            IF (IRET.EQ.0) THEN
              CALL UTMESS('F','FETRIN','INCOHERENCE 2 .LILI ET .FLIN !')
            ELSE
              CALL JEVEUO(JEXNOM(KFLIN,NOMSD),'L',IKFLIN)
              IKFLIN=IKFLIN-1
              CALL JELIRA(JEXNOM(KFLIN,NOMSD),'LONMAX',NBCHA,K8BID)
            ENDIF
          ENDIF           
          DO 90 ILIL=3,NBLILI
C NOM DU LIGREL ILI, LIGRL
            CALL JENUNO(JEXNUM(LILIL,ILIL),LIGRL)
            CALL JELIRA(JEXNUM(PRNOL,ILIL),'LONMAX',LPRNOL,K8BID)
            CALL JEVEUO(JEXNUM(PRNOL,ILIL),'L',IPRNOL)
            LPRNOL=LPRNOL/NEC2
C --------------------------------            
C BOUCLE SUR LES NOEUDS TARDIFS DU LIGREL LIGRL
C --------------------------------              
            DO 80 INO=1,LPRNOL
C ADRESSE DANS LE CHAM_NO LOCAL       
              J=ZI(IPRNOL+(INO-1)*NEC2)
              CALL ASSERT(J.GT.0)               
C NOMBRE DE COMPOSANTE
              NBCMP=ZI(IPRNOL+(INO-1)*NEC2+1)
              CALL ASSERT(NBCMP.EQ.1)

C --------------------------------              
C ON PARCOURT LA LISTE DES LIGRELS TARDIFS DU SOUS-DOMAINE IDD POUR
C CONFIRMER LE NOM DU LIGREL A RECHERCHER DANS LE PRNO GLOBAL
C --------------------------------              
              DO 50 ICHA=1,NBCHA
                LIGR2=ZK24(IKFLIN+ICHA)
                IF (LIGR2.EQ.LIGRL) THEN
C LIGREL TARDIF NON DUPLIQUE DE NOM LIGR2=LIGRL
                  LDUP=.FALSE.            
                  GOTO 55
                ELSE
C ON PARCOURT LES FILS DU LIGREL POUR LE SOUS-DOMAINE CONCERNE
                  CALL JEVEUO(LIGR2(1:19)//'.FEL1','L',IFETL1)
                  K24B=ZK24(IFETL1+IDD-1)
C ON A TROUVE LE LIGREL DE CHARGE                 
                  IF (K24B.EQ.LIGRL) THEN
C LIGRL LIGREL TARDIF DUPLIQUE DE PERE LIGR2                
                    LDUP=.TRUE.
                    GOTO 55
                  ENDIF
                ENDIF  
   50         CONTINUE
              CALL UTMESS('F','FETRIN','INCOHERENCE 3 .LILI ET .FLIN !')
   55         CONTINUE

C SI LIGREL DUPLIQUE, IL FAUT RETROUVER SON INDICE DANS LE  PRNO GLOBAL
              IF (LDUP) THEN
                CALL JEVEUO(LIGR2(1:19)//'.FEL3','L',IFETL3)
                CALL JELIRA(LIGR2(1:19)//'.FEL3','LONMAX',LFETL3,K8BID)
                LFETL3=LFETL3/2
                DO 60 K=1,LFETL3
                  IF (ZI(IFETL3+2*(K-1)+1).EQ.IDD) THEN
C K EST L'INDICE GLOBAL RECHERCHE                   
                    IF (ZI(IFETL3+2*(K-1)).EQ.-INO) GOTO 65
                  ENDIF
   60           CONTINUE
                CALL UTMESS('F','FETRIN','INCOHERENCE .LILI ET .FEL3 !')
   65           CONTINUE
              ELSE
                K=INO
              ENDIF
C INFO DU PRNO GLOBAL
              CALL JENONU(JEXNOM(PRFCHG//'.LILI',LIGR2),ILIG)
              CALL JEVEUO(JEXNUM(PRFCHG//'.PRNO',ILIG),'L',IPRNOG)
C DECALAGE DANS LE .VALE DU CHAM_NO GLOBAL              
              DVALG=ZI(IPRNOG+(K-1)*NEC2)

C VALEUR UI A TRANSFERRER SUR LE CHAM_NO GLOBAL ET SUR LE LOCAL
              RAUXL=ZR(JXSOL+J)
              ZR(IVALCS+J)=RAUXL      
C TEST POUR VERIFIER LA CONTINUITE AUX INTERFACES
              IVALG=IVALS-1+DVALG
              RAUX=ZR(IVALG)
              IF (ABS(ZR(IVALG)).GT.RMIN) THEN
                UMOY=(RAUX+RAUXL)*0.5D0       
                RAUX1=ABS((RAUX-RAUXL)/UMOY)
                IF (RAUX1.GT.TESTCO) THEN
                  RAUX1=100.D0*RAUX1
                  CALL UTDEBM('A',
     &                'FETRIN','PB POTENTIEL DE CONTINUITE ?')
                  CALL UTIMPI('L','LAGRANGE INO= ',1,-INO)
                  CALL UTIMPK('L','DU LIGREL TARDIF ',1,LIGRL)
                  CALL UTIMPR('L','ERREUR INTERFACE (EN %)= ',1,RAUX1)
                  CALL UTFINM()         
                ENDIF
              ELSE
                UMOY=RAUXL           
              ENDIF
             
C AFFECTATION EFFECTIVE DE UI VERS U         
              ZR(IVALG)=UMOY

C MONITORING
              IF (NIV.GE.5) THEN
                WRITE(IFM,*)'NOEUD TARDIF',LDUP
                WRITE(IFM,*)IDD,LIGRL,INO,J,K,RAUX,RAUXL
              ENDIF             
   80       CONTINUE
   90     CONTINUE
          
C MONITORING
          IF (NIV.GE.3) THEN
            WRITE(IFM,*)'CALCUL  UI = (KI)+*(FI-RIT*LANDAS) '       
            WRITE(IFM,*)'DDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDDD'        
          ENDIF
          IF (NIV.GE.4)
     &      CALL UTIMSD(IFM,2,.FALSE.,.TRUE.,CHAMLS(1:19),1,' ')
        ENDIF
                
  100 CONTINUE
C ----------------------------------------------------------------------
C ----  FIN BOUCLE SUR LES SOUS-DOMAINES
C ----------------------------------------------------------------------

      IF (OPTION.EQ.2) THEN
        IF (LRIGID) CALL JEDETR('&&FETI.ALPHA.MCR')
      ENDIF     
      CALL JEDEMA()
      END
