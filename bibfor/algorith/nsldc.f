      SUBROUTINE NSLDC (MODELE,MATE  ,CARELE,COMPOR,SDSENS,
     &                  INPSCO,NRPASE,TYPESE,NOPASE,STYPSE)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 13/06/2012   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT NONE
      INCLUDE 'jeveux.h'
      CHARACTER*24 MATE,MODELE,COMPOR,STYPSE,CARELE,SDSENS
      CHARACTER*13 INPSCO
      CHARACTER*8  NOPASE
      INTEGER      NRPASE,TYPESE
C
C ----------------------------------------------------------------------
C
C ROUTINE MECA_NON_LINE (CALCUL)
C
C SENSIBILITE: INTEGRATION DE LA LOI DE COMPORTEMENT DERIVEE
C               CALCUL DE L'OPTION MECA_SENS_RAPH
C
C ----------------------------------------------------------------------
C
C
C IN  MODELE : MODELE
C IN  MATE   : CHAMP MATERIAU
C IN  CARELE : CARACTERISTIQUES DES ELEMENTS DE STRUCTURE
C IN  SDSENS : SD SENSIBILITE
C IN  COMPOR : COMPORTEMENT
C IN  INPSCO : SD CONTENANT LISTE DES NOMS POUR SENSIBILITE
C IN  NRPASE : NUMERO DU PARAMETRE SENSIBLE (0=CALCUL CLASSIQUE)
C IN  NOPASE : NOM DU PARAMETRE SENSIBLE
C IN  TYPESE : TYPE DE DERIVATION
C              -1 : DERIVATION EULERIENNE (VIA UN CHAMP THETA)
C               1 : CALCUL INSENSIBLE
C               2 : DEPLACEMENT IMPOSE
C               3 : PARAMETRE MATERIAU
C               4 : CARACTERISTIQUE ELEMENTAIRE (COQUES, ...)
C               5 : FORCE
C IN  STYPSE : SOUS-TYPE DU PARAMETRE SENSIBLE
C
C OUT VARPLS/SIGPLS
C      VARIABLES INTERNES ET CONTRAINTES "SENSIBLES"
C
C
C
C
      INTEGER      NBOUT,NBIN
      PARAMETER    (NBOUT=2, NBIN=19)
      CHARACTER*8  LPAOUT(NBOUT),LPAIN(NBIN)
      CHARACTER*19 LCHOUT(NBOUT),LCHIN(NBIN)
C
      INTEGER      IBID,JDUTMP,JDEPMS,NEQ
      INTEGER      IRET,IRET1
      REAL*8       R8BID
      COMPLEX*16   C16BID
      CHARACTER*8  NOMA,K8BID,MATERS
      CHARACTER*24 DEPPLU,DEPMOI,SIGMOI,VARMOI
      CHARACTER*24 DEPMOS,SIGPLS,SIGMOS,VARMOS
      CHARACTER*24 SIGPLU,VARPLU,DEPPLS,VARPLS

      CHARACTER*24 CHCARA(18),DUTMP,CACO3D,SIGTMP

      CHARACTER*24 MATERI,MATSEN
      CHARACTER*19 LIGRMO,CHDERI
      CHARACTER*19 CHGEOM,CANBSP
      LOGICAL      EXICAR
      CHARACTER*16 OPTION
      LOGICAL      DEBUG
      INTEGER      IFMDBG,NIVDBG


      DATA DUTMP  /'&&NSLDC.DUTMP'/
      DATA SIGTMP /'&&NSLDC.SIGTMP'/
      DATA CACO3D /'&&MERIMO.CARA_ROTA_FICTI'/
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
      CALL INFDBG('PRE_CALCUL',IFMDBG,NIVDBG)
C
C --- CONTROLE INITIAL
C
      IF (NRPASE.EQ.0) GO TO 9999
C
C --- INITIALISATIONS
C
      CALL DISMOI('F','NOM_MAILLA',MODELE,'MODELE',IBID,NOMA  ,IBID)
      CALL DISMOI('F','NOM_LIGREL',MODELE,'MODELE',IBID,LIGRMO,IBID)
      IF (NIVDBG.GE.2) THEN
        DEBUG  = .TRUE.
      ELSE
        DEBUG  = .FALSE.
      ENDIF
      IF ((TYPESE.EQ.2).OR.(TYPESE.EQ.3).OR.(TYPESE.EQ.5)) THEN
        OPTION = 'MECA_SENS_RAPH'
      ELSE IF (TYPESE.EQ.4 ) THEN
        OPTION = 'MECA_SENS_RAPHEP'
      ELSE
        CALL ASSERT(.FALSE.)
      ENDIF
C
C --- INITIALISATION DES CHAMPS POUR CALCUL
C
      CALL INICAL(NBIN  ,LPAIN ,LCHIN ,
     &            NBOUT ,LPAOUT,LCHOUT)
C
C --- LECTURE DES CHAMPS DIRECTS
C
      CALL NMNSLE(SDSENS,0     ,'DEPPLU',DEPPLU)
      CALL NMNSLE(SDSENS,0     ,'DEPMOI',DEPMOI)
      CALL NMNSLE(SDSENS,0     ,'SIGPLU',SIGPLU)
      CALL NMNSLE(SDSENS,0     ,'SIGMOI',SIGMOI)
      CALL NMNSLE(SDSENS,0     ,'VARPLU',VARPLU)
      CALL NMNSLE(SDSENS,0     ,'VARMOI',VARMOI)
C
C --- LECTURE DES CHAMPS DERIVES
C
      CALL NMNSLE(SDSENS,NRPASE,'DEPPLU',DEPPLS)
      CALL NMNSLE(SDSENS,NRPASE,'DEPMOI',DEPMOS)
      CALL NMNSLE(SDSENS,NRPASE,'SIGPLU',SIGPLS)
      CALL NMNSLE(SDSENS,NRPASE,'SIGMOI',SIGMOS)
      CALL NMNSLE(SDSENS,NRPASE,'VARPLU',VARPLS)
      CALL NMNSLE(SDSENS,NRPASE,'VARMOI',VARMOS)
C
C --- CHAMP DE GEOMETRIE
C
      CHGEOM = NOMA//'.COORDO'
C
C --- CHAMP DE CARACTERISTIQUES ELEMENTAIRES
C
      CALL MECARA(CARELE,EXICAR,CHCARA)
C
C --- CALCUL DE L'INCREMENT DE DEPLACEMENT DERIVE
C
      CALL COPISD('CHAMP_GD','V',DEPPLS,DUTMP)
      CALL JEVEUO(DUTMP (1:19)//'.VALE','E',JDUTMP)
      CALL JEVEUO(DEPMOS(1:19)//'.VALE','L',JDEPMS)
      CALL JELIRA(DEPMOS(1:19)//'.VALE','LONMAX',NEQ,K8BID)
      CALL DAXPY(NEQ,-1.D0,ZR(JDEPMS),1,ZR(JDUTMP),1)
C
C --- NOM DU CHAMP CONTENANT LA VARIABLE SENSIBLE
C
      CHDERI = '&&NSLDC.PARSENS'
C
C --- COPIE DE SIGPLS DANS UN CHAMP BIDON (NE PEUT ETRE IN/OUT!)
C
      CALL COPISD('CHAMP_GD','V',SIGPLS,SIGTMP)
C
C --- DEFINITION DU MATERIAU DERIVE CODE
C
      IF (TYPESE.EQ.3) THEN
        MATERI = MATE(1:8)
        CALL PSGENC(MATERI,NOPASE,MATERS,IRET)
        IF (IRET.NE.0) THEN
          CALL U2MESK('F','CALCULEL2_87',1,MATERI)
        END IF
        MATSEN = ' '
        MATSEN(1:24) = MATERS(1:8)//MATE(9:24)
      ELSE
        MATSEN = ' '
      ENDIF
C
C --- REMPLISSAGE DES CHAMPS D'ENTREE
C
      LPAIN(1)  = 'PGEOMER'
      LCHIN(1)  = CHGEOM
      LPAIN(2)  = 'PMATERC'
      LCHIN(2)  = MATE
      LPAIN(3)  = 'PCOMPOR'
      LCHIN(3)  = COMPOR
      LPAIN(4)  = 'PDEPLMS'
      LCHIN(4)  = DEPMOS
      LPAIN(5)  = 'PDEPLPS'
      LCHIN(5)  = DUTMP
      LPAIN(6)  = 'PCONTMS'
      LCHIN(6)  = SIGMOS
      LPAIN(7)  = 'PVARIMS'
      LCHIN(7)  = VARMOS
      LPAIN(8)  = 'PDEPLMR'
      LCHIN(8)  = DEPMOI
      LPAIN(9)  =  'PDEPLPR'
      LCHIN(9)  =  DEPPLU
      LPAIN(10) = 'PVARIMR'
      LCHIN(10) = VARMOI
      LPAIN(11) = 'PVARIPR'
      LCHIN(11) = VARPLU
      LPAIN(12) = 'PCONTMR'
      LCHIN(12) = SIGMOI
      LPAIN(13) = 'PCONTPR'
      LCHIN(13) = SIGPLU
      LPAIN(14) = 'PARSENS'
      LCHIN(14) = CHDERI
      LPAIN(15) = 'PCOPARS'
      LCHIN(15) = SIGTMP
      LPAIN(16) = 'PMATSEN'
      LCHIN(16) = MATSEN
      LPAIN(17) = 'PCACOQU'
      IF (EXICAR) THEN
        LCHIN(17) = CHCARA(7)
      ENDIF
      LPAIN(18) = 'PNBSP_I'
      IF(EXICAR) THEN
        LCHIN(18) = CHCARA(1)(1:8)//'.CANBSP'
      ENDIF
      LPAIN(19) = 'PCACO3D'
      LCHIN(19) = CACO3D
C
C --- CHAMPS DE SORTIE
C
      LPAOUT(1) = 'PVARIPS'
      LCHOUT(1) = VARPLS
      CALL COPISD('CHAM_ELEM_S','V',COMPOR,VARPLS)
      LPAOUT(2) = 'PCONTPS'
      LCHOUT(2) = SIGPLS
C
C --- DANS LE CAS DES COQUES ON ETEND LE CHAMP DE CONTRAINTES SENSIBLES
C
      IF (EXICAR) THEN
        CANBSP = '&&NSLDC.NBSP'
        CALL EXISD('CHAM_ELEM_S',CANBSP,IRET1)
        IF (IRET1.NE.1) CALL CESVAR(CHCARA(1),' ',LIGRMO,CANBSP)
        CALL COPISD('CHAM_ELEM_S','V',CANBSP,SIGPLS)
      ENDIF
C
C --- CREATION D'UNE CARTE UNIFORME CONTENANT STYPSE
C
      CALL MECACT('V'   ,CHDERI,'MAILLAGE',NOMA  ,'NEUT_K24',
     &            1     ,'Z1'  ,IBID      ,R8BID ,C16BID    ,
     &            STYPSE)
C
C --- APPEL A CALCUL
C
      IF (DEBUG) THEN
        CALL DBGCAL(OPTION,IFMDBG,
     &              NBIN  ,LPAIN ,LCHIN ,
     &              NBOUT ,LPAOUT,LCHOUT)
      ENDIF
C
      CALL CALCUL('S',OPTION,LIGRMO,NBIN  ,LCHIN ,LPAIN ,
     &                              NBOUT ,LCHOUT,LPAOUT,'V','OUI')
C
C --- MENAGE
C
      CALL JEDETR(DUTMP)
      CALL JEDETR(SIGTMP)

9999  CONTINUE

      CALL JEDEMA()
      END
