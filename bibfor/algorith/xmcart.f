      SUBROUTINE XMCART(NOMA  ,DEFICO,MODELE,RESOCO)
C 
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 28/09/2010   AUTEUR MASSIN P.MASSIN 
C ======================================================================
C COPYRIGHT (C) 1991 - 2006  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C
      IMPLICIT NONE
      CHARACTER*8  NOMA,MODELE
      CHARACTER*24 DEFICO,RESOCO     
C      
C ----------------------------------------------------------------------
C
C ROUTINE XFEM-GG 
C
C CREATION DE LA CARTE CONTENANT LES INFOS DE CONTACT
C
C ----------------------------------------------------------------------
C
C ----------------------------------------------------------------------
C ROUTINE SPECIFIQUE A L'APPROCHE <<GRANDS GLISSEMENTS AVEC XFEM>>,
C TRAVAIL EFFECTUE EN COLLABORATION AVEC I.F.P.
C ----------------------------------------------------------------------
C
C IN  NOMA   : NOM DU MAILLAGE
C IN  DEFICO : SD DE DEFINITION DU CONTACT
C IN  RESOCO : SD POUR LA RESOLUTION DE CONTACT
C IN  MODELE   : NOM DU MODELE
C
C CONTENU DE LA CARTE
C
C 1  XPC    : COORDONNEE PARAMETRIQUE X DU POINT DE CONTACT
C 2  XPR    : COORDONNEE PARAMETRIQUE X DU PROJETE DU POINT DE CONTACT
C 3  YPR    : COORDONNEE PARAMETRIQUE Y DU PROJETE DU POINT DE CONTACT
C 4  TAU1(1): COMPOSANTE 1 DU VECTEUR TANGENT 1
C 5  TAU1(2): COMPOSANTE 2 DU VECTEUR TANGENT 1
C 6  TAU1(3): COMPOSANTE 3 DU VECTEUR TANGENT 1
C 7  TAU2(1): COMPOSANTE 1 DU VECTEUR TANGENT 2
C 8  TAU2(2): COMPOSANTE 2 DU VECTEUR TANGENT 2
C 9  TAU2(3): COMPOSANTE 3 DU VECTEUR TANGENT 2
C 10 YPC    : COORDONNEE PARAMETRIQUE Y DU POINT DE CONTACT
C 11 INDCO  : ETAT DE CONTACT (0 PAS DE CONTACT)
C 13 COEFCA : COEF_REGU_CONT
C 14 COEFFA : COEF_REGU_FROT
C 15 COEFFF : COEFFICIENT DE FROTTEMENT DE COULOMB
C 16 IFROTT : FROTTEMENT (0 SI PAS, 3 SI COULOMB)
C 17 INDNOR : NOEUD EXCLU PAR PROJECTION HORS ZONE
C 18 NINTER : NOMBRE DE POINT D'INTERSECTION DE LA MAILLE ESCLAVE
C 19 HPG    : POIDS DU POINT INTEGRATION DU POINT DE CONTACT
C 20 IGLISS : CONTACT GLISSIERE
C 21 MEMCO  : MEMOIRE DE CONTACT
C 22 NFAES  : NUMEROS DE LA FACETTES ESCLAVE 
C 23 NFAMA  : NUMEROS DE LA FACETTES MAITRE
C
C -------------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ----------------
C
      INTEGER            ZI
      COMMON  / IVARJE / ZI(1)
      REAL*8             ZR
      COMMON  / RVARJE / ZR(1)
      COMPLEX*16         ZC
      COMMON  / CVARJE / ZC(1)
      LOGICAL            ZL
      COMMON  / LVARJE / ZL(1)
      CHARACTER*8        ZK8
      CHARACTER*16                ZK16
      CHARACTER*24                          ZK24
      CHARACTER*32                                    ZK32
      CHARACTER*80                                              ZK80
      COMMON  / KVARJE / ZK8(1) , ZK16(1) , ZK24(1) , ZK32(1) , ZK80(1)
C
C -------------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ----------------
C
      INTEGER      NCMP,NCMP1,NCMP2,NCMP3,NCMP4
      PARAMETER    (NCMP=30)
C
      INTEGER      NUMMAE,NUMMAM,NNOE,NNOM,NFISE,NFISM
      INTEGER      I,J,IPC,K,NTPC,NDIM,IZONE,MMINFI,NFACE,NPTE
      INTEGER      CFMMVD,ZTABF,JTABF,JNOSDC
      CHARACTER*24 TABFIN,NOSDCO    
      REAL*8       MMINFR
      INTEGER      CFDISI
      INTEGER      JVALV,JVALV1,JVALV2,JVALV3,JVALV4,IAD
      INTEGER      JNCMP,JNCMP1,JNCMP2,JNCMP3,JNCMP4
      INTEGER      JCESL1,JCESL2,JCESL3,JCESL4
      INTEGER      JCESD1,JCESD2,JCESD3,JCESD4
      INTEGER      JCESV1,JCESV2,JCESV3,JCESV4
      CHARACTER*2  CH2
      CHARACTER*8  KBID
      CHARACTER*19 CSTANO,CPOINT,CPINTE,CAINTE,CCFACE
      CHARACTER*19 LIGRXF
      CHARACTER*19 CHS1,CHS2,CHS3,CHS4
      INTEGER      ZXAIN,XXMMVD
      INTEGER      IFM,NIV,JCONX
      CHARACTER*32  JEXATR
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
      CALL INFDBG('CONTACT',IFM,NIV)
C
C --- AFFICHAGE
C      
      IF (NIV.GE.2) THEN
        WRITE (IFM,*) '<CONTACT> CREATION DE LA CARTE POUR LES'//
     &        ' ELEMENTS DE CONTACT X-FEM' 
      ENDIF  
C
C --- INITIALISATIONS
C    
      NDIM   = CFDISI(DEFICO,'NDIM') 
      NTPC   = CFDISI(DEFICO,'NTPC')
      IF (NDIM.EQ.2) THEN
        NCMP1  = 8
        NCMP2  = 4
        NCMP3  = 10
        NCMP4  = 2
      ELSEIF (NDIM.EQ.3) THEN
        NCMP1  = 16
        NCMP2  = 18
        NCMP3  = 30
        NCMP4  = 15
      ENDIF         
C
C --- ACCES OBJETS
C
      TABFIN = RESOCO(1:14)//'.TABFIN'
      CALL JEVEUO(TABFIN,'L',JTABF )
      NOSDCO = RESOCO(1:14)//'.NOSDCO'
      CALL JEVEUO(NOSDCO,'L',JNOSDC)      
C
C --- LIGREL DES ELEMENTS TARDIFS DE CONTACT/FROTTEMENT    
C
      LIGRXF = ZK24(JNOSDC+3-1)(1:19)    
C      
C --- CARTES POUR ELEMENTS TARDIFS DE CONTACT/FROTTEMENT    
C
      CPOINT = RESOCO(1:14)//'.XFPO'
      CSTANO = RESOCO(1:14)//'.XFST'
      CPINTE = RESOCO(1:14)//'.XFPI'
      CAINTE = RESOCO(1:14)//'.XFAI'
      CCFACE = RESOCO(1:14)//'.XFCF'
C
      CHS1 = '&&XMCART.CHS1'
      CHS2 = '&&XMCART.CHS2'
      CHS3 = '&&XMCART.CHS3'
      CHS4 = '&&XMCART.CHS4'
C
      CALL CELCES(MODELE//'.STNO','V',CHS1)
      CALL CELCES(MODELE//'.TOPOFAC.OE','V',CHS2)
      CALL CELCES(MODELE//'.TOPOFAC.AI','V',CHS3)
      CALL CELCES(MODELE//'.TOPOFAC.CF','V',CHS4)
C
      CALL JEVEUO(CHS1//'.CESD','L',JCESD1)
      CALL JEVEUO(CHS2//'.CESD','L',JCESD2)
      CALL JEVEUO(CHS3//'.CESD','L',JCESD3)
      CALL JEVEUO(CHS4//'.CESD','L',JCESD4)
      CALL JEVEUO(CHS1//'.CESV','L',JCESV1)
      CALL JEVEUO(CHS2//'.CESV','L',JCESV2)
      CALL JEVEUO(CHS3//'.CESV','L',JCESV3)
      CALL JEVEUO(CHS4//'.CESV','L',JCESV4)
      CALL JEVEUO(CHS1//'.CESL','L',JCESL1)
      CALL JEVEUO(CHS2//'.CESL','L',JCESL2)
      CALL JEVEUO(CHS3//'.CESL','L',JCESL3)
      CALL JEVEUO(CHS4//'.CESL','L',JCESL4)
C
      ZTABF = CFMMVD('ZTABF')
      ZXAIN = XXMMVD('ZXAIN')      
C
C --- DESTRUCTION DES CARTES SI ELLES EXISTENT
C
      CALL DETRSD('CARTE',CPOINT)
      CALL DETRSD('CARTE',CSTANO)
      CALL DETRSD('CARTE',CPINTE)
      CALL DETRSD('CARTE',CAINTE)
      CALL DETRSD('CARTE',CCFACE)
C
      CALL ALCART('V',CPOINT,NOMA,'NEUT_R')
      CALL JEVEUO(CPOINT//'.NCMP','E',JNCMP)
      CALL JEVEUO(CPOINT//'.VALV','E',JVALV)
C
      CALL ALCART('V',CSTANO,NOMA,'NEUT_I')
      CALL JEVEUO(CSTANO//'.NCMP','E',JNCMP1)
      CALL JEVEUO(CSTANO//'.VALV','E',JVALV1)
C
      CALL ALCART('V',CPINTE,NOMA,'NEUT_R')
      CALL JEVEUO(CPINTE//'.NCMP','E',JNCMP2)
      CALL JEVEUO(CPINTE//'.VALV','E',JVALV2)
C
      CALL ALCART('V',CAINTE,NOMA,'NEUT_R')
      CALL JEVEUO(CAINTE//'.NCMP','E',JNCMP3)
      CALL JEVEUO(CAINTE//'.VALV','E',JVALV3)
C
      CALL ALCART('V',CCFACE,NOMA,'NEUT_R')
      CALL JEVEUO(CCFACE//'.NCMP','E',JNCMP4)
      CALL JEVEUO(CCFACE//'.VALV','E',JVALV4)
C
      CALL JEVEUO(JEXATR(NOMA//'.CONNEX','LONCUM'),'L',JCONX)
C
      DO 100,K = 1,NCMP
        CALL CODENT(K,'G',CH2)
        ZK8(JNCMP-1+K) = 'X'//CH2
  100 CONTINUE
C
      DO 101,K = 1,NCMP1
        CALL CODENT(K,'G',CH2)
        ZK8(JNCMP1-1+K) = 'X'//CH2
  101 CONTINUE
C
      DO 102,K = 1,NCMP2
        CALL CODENT(K,'G',CH2)
        ZK8(JNCMP2-1+K) = 'X'//CH2
  102 CONTINUE
C
      DO 103,K = 1,NCMP3
        CALL CODENT(K,'G',CH2)
        ZK8(JNCMP3-1+K) = 'X'//CH2
  103 CONTINUE
C
      DO 104,K = 1,NCMP4
        CALL CODENT(K,'G',CH2)
        ZK8(JNCMP4-1+K) = 'X'//CH2
  104 CONTINUE     
C
C --- REMPLISSAGE DE LA CARTE 
C     
      DO 110 IPC = 1,NTPC
        IZONE  = NINT(ZR(JTABF+ZTABF*(IPC-1)+15))
C ----- NUMEROS MAILLE ESCLAVE 
        NUMMAE = NINT(ZR(JTABF+ZTABF*(IPC-1)+1))
C ----- NUMEROS MAILLE MAITRE
        NUMMAM = NINT(ZR(JTABF+ZTABF*(IPC-1)+2))
C ----- NOMBRE DE POINTS D'INTERSECTION ET DE FACETTES
C ----- DE LA MAILLE ESCLAVE
        NPTE   = ZR(JTABF+ZTABF*(IPC-1)+24)
        NFACE  = ZR(JTABF+ZTABF*(IPC-1)+26)
C ----- REMPLISSAGE DE LA CARTE CARTCF.POINT
        ZR(JVALV-1+1)  = ZR(JTABF+ZTABF*(IPC-1)+3)
        ZR(JVALV-1+2)  = ZR(JTABF+ZTABF*(IPC-1)+4)
        ZR(JVALV-1+3)  = ZR(JTABF+ZTABF*(IPC-1)+5)
        ZR(JVALV-1+4)  = ZR(JTABF+ZTABF*(IPC-1)+6)
        ZR(JVALV-1+5)  = ZR(JTABF+ZTABF*(IPC-1)+7)
        ZR(JVALV-1+6)  = ZR(JTABF+ZTABF*(IPC-1)+8)
        ZR(JVALV-1+7)  = ZR(JTABF+ZTABF*(IPC-1)+9)
        ZR(JVALV-1+8)  = ZR(JTABF+ZTABF*(IPC-1)+10)
        ZR(JVALV-1+9)  = ZR(JTABF+ZTABF*(IPC-1)+11)
        ZR(JVALV-1+10) = ZR(JTABF+ZTABF*(IPC-1)+12)
        ZR(JVALV-1+11) = ZR(JTABF+ZTABF*(IPC-1)+13)
        ZR(JVALV-1+12) = NPTE
        ZR(JVALV-1+13) = MMINFR(DEFICO,'COEF_REGU_CONT' ,IZONE )
        ZR(JVALV-1+14) = MMINFR(DEFICO,'COEF_REGU_FROT' ,IZONE )
        ZR(JVALV-1+15) = MMINFR(DEFICO,'COEF_COULOMB'   ,IZONE )
        ZR(JVALV-1+16) = MMINFI(DEFICO,'FROTTEMENT_ZONE',IZONE )
        ZR(JVALV-1+17) = ZR(JTABF+ZTABF*(IPC-1)+22)
        ZR(JVALV-1+18) = ZR(JTABF+ZTABF*(IPC-1)+30)
        ZR(JVALV-1+19) = ZR(JTABF+ZTABF*(IPC-1)+16)
        ZR(JVALV-1+20) = ZR(JTABF+ZTABF*(IPC-1)+29)
        ZR(JVALV-1+21) = ZR(JTABF+ZTABF*(IPC-1)+28)
        ZR(JVALV-1+22) = ZR(JTABF+ZTABF*(IPC-1)+25)
        ZR(JVALV-1+23) = ZR(JTABF+ZTABF*(IPC-1)+31)
        ZR(JVALV-1+24) = ZR(JTABF+ZTABF*(IPC-1)+17)
        ZR(JVALV-1+25) = ZR(JTABF+ZTABF*(IPC-1)+18)
        ZR(JVALV-1+26) = ZR(JTABF+ZTABF*(IPC-1)+19)
        ZR(JVALV-1+27) = ZR(JTABF+ZTABF*(IPC-1)+20)
        ZR(JVALV-1+28) = ZR(JTABF+ZTABF*(IPC-1)+21)
        ZR(JVALV-1+29) = ZR(JTABF+ZTABF*(IPC-1)+23)
        ZR(JVALV-1+30) = ZR(JTABF+ZTABF*(IPC-1)+27)
        CALL NOCART(CPOINT,-3,KBID,'NUM',1,KBID,-IPC,LIGRXF,NCMP)
C ----- REMPLISSAGE DE LA CARTE CARTCF.STANO

C        STANOE = NINT(ZR(JTABF+ZTABF*(IPC-1)+14))
C        STANOM = NINT(ZR(JTABF+ZTABF*(IPC-1)+32))
C        IF (STANOE.EQ.3.OR.STANOM.EQ.3) THEN
C        ON PEUT BLINDER AVEC CETTE CONDITION POUR GAGNER DU TEMPS
C        A CONDITION DE MODIFIER LES TE366 ET 377
          NNOE = ZI(JCONX+NUMMAE) - ZI(JCONX+NUMMAE-1)
          NNOM = ZI(JCONX+NUMMAM) - ZI(JCONX+NUMMAM-1)
          NFISE=1
          NFISM=1
          DO 1 I=1,NNOE
            DO 2 J=1,NFISE
              CALL CESEXI('S',JCESD1,JCESL1,NUMMAE,I,J,1,IAD)
              CALL ASSERT(IAD.GT.0)
              ZI(JVALV1-1+NFISE*(I-1)+J)=ZI(JCESV1-1+IAD)
  2         CONTINUE
  1       CONTINUE
          DO 3 I=1,NNOM
            DO 4 J=1,NFISM                  
              CALL CESEXI('S',JCESD1,JCESL1,NUMMAM,I,J,1,IAD)
              CALL ASSERT(IAD.GT.0)
              ZI(JVALV1-1+NFISE*NNOE+NFISM*(I-1)+J)=ZI(JCESV1-1+IAD)
  4         CONTINUE
  3       CONTINUE
C          NCMP1=NNOE+NNOM
          CALL NOCART(CSTANO,-3,KBID,'NUM',1,KBID,-IPC,LIGRXF,NCMP1)
C        ENDIF
C ----- REMPLISSAGE DE LA CARTE CARTCF.PINTER
        DO 10 I=1,NDIM
          DO 20 J=1,NPTE                  
            CALL CESEXI('S',JCESD2,JCESL2,NUMMAE,1,1,NDIM*(J-1)+I,IAD)
            CALL ASSERT(IAD.GT.0)
            ZR(JVALV2-1+NDIM*(J-1)+I)=ZR(JCESV2-1+IAD)
 20       CONTINUE
 10     CONTINUE
        CALL NOCART(CPINTE,-3,KBID,'NUM',1,KBID,-IPC,LIGRXF,NCMP2)
C ----- REMPLISSAGE DE LA CARTE CARTCF.AINTER
        DO 40 I=1,ZXAIN
          DO 50 J=1,NPTE
            CALL CESEXI('S',JCESD3,JCESL3,NUMMAE,1,1,ZXAIN*(J-1)+I,IAD)
            CALL ASSERT(IAD.GT.0)
            ZR(JVALV3-1+ZXAIN*(J-1)+I)=ZR(JCESV3-1+IAD)
 50       CONTINUE
 40     CONTINUE          
        CALL NOCART(CAINTE,-3,KBID,'NUM',1,KBID,-IPC,LIGRXF,NCMP3)
C ----- REMPLISSAGE DE LA CARTE CARTCF.CCFACE	
        DO 70 I=1,NDIM
          DO 80 J=1,NFACE
            CALL CESEXI('S',JCESD4,JCESL4,NUMMAE,1,1,NDIM*(J-1)+I,IAD)
            CALL ASSERT(IAD.GT.0)
            ZR(JVALV4-1+NDIM*(J-1)+I)=ZI(JCESV4-1+IAD)
 80       CONTINUE
 70     CONTINUE
        CALL NOCART(CCFACE,-3,KBID,'NUM',1,KBID,-IPC,LIGRXF,NCMP4)
C
        IF (NIV.GE.2) THEN
          CALL XMIMP3(IFM,NOMA,IPC,JVALV,JTABF)
        ENDIF

  110 CONTINUE       
C
C --- MENAGE 
C
      CALL JEDETR(CPOINT//'.NCMP')
      CALL JEDETR(CPOINT//'.VALV')     
      CALL JEDETR(CPINTE//'.NCMP')
      CALL JEDETR(CPINTE//'.VALV')
      CALL JEDETR(CAINTE//'.NCMP')
      CALL JEDETR(CAINTE//'.VALV') 
      CALL JEDETR(CCFACE//'.NCMP')
      CALL JEDETR(CCFACE//'.VALV')
      CALL DETRSD('CHAM_ELEM_S',CHS1)
      CALL DETRSD('CHAM_ELEM_S',CHS2)
      CALL DETRSD('CHAM_ELEM_S',CHS3)
      CALL DETRSD('CHAM_ELEM_S',CHS4)         
C
      CALL JEDEMA()      
      END
