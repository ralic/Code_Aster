      SUBROUTINE CFGCRL(RESOCO,NEQ   ,NBLIAI,MATASS,SOLVEU,
     &                  ALPHA)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 02/10/2012   AUTEUR DESOZA T.DESOZA 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C
      IMPLICIT     NONE
      INCLUDE 'jeveux.h'
      CHARACTER*24 RESOCO
      INTEGER      NEQ,NBLIAI
      CHARACTER*19 MATASS,SOLVEU
      REAL*8       ALPHA
C
C ----------------------------------------------------------------------
C
C ROUTINE CONTACT (RESOLUTION - GCP)
C
C RECHERCHE LINEAIRE
C
C ----------------------------------------------------------------------
C
C
C IN  RESOCO : SD DE TRAITEMENT NUMERIQUE DU CONTACT
C IN  SOLVEU : SD SOLVEUR
C IN  MATASS : NOM DE LA MATRICE DU PREMIER MEMBRE ASSEMBLEE
C IN  NBLIAI : NOMBRE DE LIAISONS DE CONTACT
C IN  NEQ    : NOMBRE D'EQUATIONS
C OUT ALPHA  : COEFFICIENT DE RECHERCHE LINEAIRE
C
C
C
C
      INTEGER      IFM,NIV
      REAL*8       DDOT,NUMER,DENOM
      INTEGER      ILIAI,JDECAL,NBDDL
      COMPLEX*16   C16BID
      REAL*8       R8BID
      CHARACTER*19 K19BLA
      CHARACTER*24 APCOEF,APDDL,APPOIN
      INTEGER      JAPCOE,JAPDDL,JAPPTR
      CHARACTER*19 SGRADP,SGRPRP,DIRECT
      INTEGER      JSGRAP,JSGPRP,JDIREC
      CHARACTER*24 SECMBR,DDELT,CNCIN0
      INTEGER      JSECMB,JDDELT
      INTEGER      IRET
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
      CALL INFDBG('CONTACT',IFM,NIV)
      K19BLA = ' '
C
C --- LECTURE DES STRUCTURES DE DONNEES DE CONTACT
C
      APPOIN = RESOCO(1:14)//'.APPOIN'
      APCOEF = RESOCO(1:14)//'.APCOEF'
      APDDL  = RESOCO(1:14)//'.APDDL'
      DIRECT = RESOCO(1:14)//'.DIRE'
      SGRADP = RESOCO(1:14)//'.SGDP'
      SGRPRP = RESOCO(1:14)//'.SGPP'
C
      CALL JEVEUO(APPOIN,'L',JAPPTR)
      CALL JEVEUO(APCOEF,'L',JAPCOE)
      CALL JEVEUO(APDDL ,'L',JAPDDL)
      CALL JEVEUO(DIRECT,'L',JDIREC)
      CALL JEVEUO(SGRADP,'L',JSGRAP)
      CALL JEVEUO(SGRPRP,'L',JSGPRP)
C
C --- ACCES AUX CHAMPS DE TRAVAIL
C
      SECMBR = RESOCO(1:14)//'.SECM'
      CNCIN0 = RESOCO(1:14)//'.CIN0'
      DDELT  = RESOCO(1:14)//'.DDEL'
      CALL JEVEUO(DDELT(1:19) //'.VALE','E',JDDELT)
      CALL JEVEUO(SECMBR(1:19)//'.VALE','E',JSECMB)
C
C --- INITIALISATIONS DES VECTEURS DE TRAVAIL
C
      CALL R8INIR(NEQ,0.D0,ZR(JSECMB),1)
      CALL R8INIR(NEQ,0.D0,ZR(JDDELT),1)
C
C --- SECOND MEMBRE: [A]T .{DIRECP}
C
      DO 50 ILIAI = 1,NBLIAI
        JDECAL = ZI(JAPPTR+ILIAI-1)
        NBDDL  = ZI(JAPPTR+ILIAI) - ZI(JAPPTR+ILIAI-1)
        CALL CALATM(NEQ,NBDDL,ZR(JDIREC+ILIAI-1),ZR(JAPCOE+JDECAL),
     &              ZI(JAPDDL+JDECAL),ZR(JSECMB))
   50 CONTINUE
C
C --- RESOLUTION [K].{DDELT} = [A]T .{DIRECP} -> {DDELT}
C
      CALL RESOUD(MATASS,K19BLA,SOLVEU,CNCIN0,0     ,
     &            SECMBR,DDELT ,'V'   ,R8BID ,C16BID,
     &            K19BLA,.TRUE.,0     ,IRET  )
C
C --- PRODUIT SCALAIRE  NUMER = <DIRECP>.{DIRECP}
C
      NUMER = DDOT(NBLIAI,ZR(JSGPRP),1,ZR(JSGRAP),1)
C
C --- PRODUIT SCALAIRE  DENOM = <DIRECP>.[A].[K]-1.[A]T .{DIRECP}
C
      CALL JEVEUO(DDELT(1:19) //'.VALE','L',JDDELT)
      DENOM = DDOT(NEQ,ZR(JDDELT),1,ZR(JSECMB),1)
C
      IF (DENOM.LT.0.D0) THEN
        CALL U2MESS('A','CONTACT_7')
      END IF
C
C --- COEFFICIENT DE RECHERCHE LINEAIRE
C
      ALPHA = NUMER/DENOM
C
C --- AFFICHAGE
C
      IF (NIV.EQ.2) THEN
        WRITE (IFM,9040) ALPHA
      ENDIF
C
 9040 FORMAT (' <CONTACT><CALC> PAS D''AVANCEMENT INITIAL : ',1PE12.5)
C
      CALL JEDEMA()
C
      END
