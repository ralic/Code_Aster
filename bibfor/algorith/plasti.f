      SUBROUTINE PLASTI ( FAMI,KPG,KSP,TYPMOD,IMAT,COMP,CRIT,TIMED,
     &                       TIMEF, TEMPD, TEMPF, TREF,EPSDT,DEPST,
     &                       SIGD,VIND,OPT,ANGMAS,SIGF,VINF,DSDE,ICOMP,
     &                       NVI,TAMPON,IRTETI)
      IMPLICIT NONE
C ----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 18/06/2012   AUTEUR PROIX J-M.PROIX 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE PROIX J.M.PROIX
C TOLE CRP_21
C ======================================================================
C     INTEGRATION DE LOIS DE COMPORTEMENT ELASTO PLASTIQUE ET VISCO
C     PLASTIQUE PAR UNE MATHODE DE NEWTON (DISCRETISATION IMPLICITE)
C     
C     CALCUL DES CONTRAINTES           = SIGF(T+DT)
C     CALCUL DES VARIABLES INTERNES    = VINF(T+DT)
C     CALCUL DU JACOBIEN ASSOCIE       = DS/DE(T+DT) OU DS/DE(T)
C     CONVENTION : 
C                 SUFFIXE D : DEBUT DU PAS DE TEMPS
C                 SUFFIXE F : FIN DU PAS DE TEMPS
C     ==================================================================
C     ARGUMENTS
C
C     IN FAMI    FAMILLE DE POINT DE GAUSS (RIGI,MASS,...)
C        KPG,KSP NUMERO DU (SOUS)POINT DE GAUSS
C        TYPMOD  TYPE DE MODELISATION
C        IMAT    ADRESSE DU MATERIAU CODE
C        COMP    COMPORTEMENT DE L ELEMENT
C                COMP(1) = RELATION DE COMPORTEMENT (ROUSSELIER.)
C                COMP(2) = NB DE VARIABLES INTERNES
C                COMP(3) = TYPE DE DEFORMATION (PETIT,JAUMANN...)
C        CRIT    CRITERES  LOCAUX
C                CRIT(1) = NOMBRE D ITERATIONS MAXI (ITER_INTE_MAXI)
C                CRIT(3) = TOLERANCE DE CONVERGENCE(RESI_INTE_RELA)
C                CRIT(4) = THETA
C                CRIT(5) = ITER_INTE_PAS (UTILISE PAR REDECE EN AMONT)
C                CRIT(6) = ALGO_INTE(NEWTON, NEWTON_PERT, NEWTON_RELI)
C        TIMED   INSTANT T
C        TIMEF   INSTANT T+DT
C        TEMPD   TEMPERATURE A T           POUR LA THM
C        TEMPF   TEMPERATURE A T+DT        POUR LA THM
C        TREF    TEMPERATURE DE REFERENCE  POUR LA THM
C        CES PARAMETRES DE TEMPERATURE NE SONT PAS PRIS EN COMPTE EN
C        MECANIQUE PURE (ON UTILISE LES VARIABLES DE COMMANDES)
C
C        EPSDT   DEFORMATION TOTALE A T
C        DEPST   INCREMENT DE DEFORMATION TOTALE
C        SIGD    CONTRAINTE A T
C        VIND    VARIABLES INTERNES A T    + INDICATEUR ETAT T
C        OPT     OPTION DE CALCUL
C                        'RIGI_MECA_TANG'> DSDE(T)
C                        'FULL_MECA'     > DSDE(T+DT), SIGF, VINF
C                        'RAPH_MECA'     > SIGF, VINF
C        ANGMAS  ANGLES DU MOT_CLEF MASSIF (AFFE_CARA_ELEM)
C                +  0 SI NAUTIQUIES OU 2 SI EULER
C                + LES 3 ANGLES D'EULER
C     OUT
C        SIGF    CONTRAINTE A T+DT
C        VINF    VARIABLES INTERNES A T+DT + INDICATEUR ETAT T+DT
C        DSDE    MATRICE DE COMPORTEMENT TANGENT A T+DT OU T
C        ICOMP   COMPTEUR POUR LE REDECOUPAGE DU PAS DE TEMPS
C        NVI     NB DE VARIABLES INTERNES
C        IRTETI  CODE RETOUR =0 OK, =1 => REDECOUPAGE DU PAS DE TEMPS
C     ------------------------------------------------------------------
C     INFO    MATERD        (*,1) = CARACTERISTIQUES ELASTIQUES A T
C                           (*,2) = CARACTERISTIQUES PLASTIQUES A T
C             MATERF        (*,1) = CARACTERISTIQUES ELASTIQUES A T+DT
C                           (*,2) = CARACTERISTIQUES PLASTIQUES A T+DT
C             MATCST          'OUI' SI MATERIAU CST ENTRE T ET T+DT
C                             'NON' SINON
C             NDT             NB DE COMPOSANTE TOTALES DES TENSEURS
C                                     = 6  3D
C                                     = 4  AXIS  C_PLAN  D_PLAN
C             NDI             NB DE COMPOSANTE DIRECTES DES TENSEURS
C             NR              NB EQUATION SYSTEME INTEGRE A RESOUDRE
C     ------------------------------------------------------------------
C     ATTENTION
C     SI OPT = 'RIGI_MECA_TANG' NE PAS TOUCHER AUX VARIABLES SIGF,VINF
C     QUI N ONT PAS DE PLACE MEMOIRE ALLOUEE
C
C     SIG EPS DEPS  ONT DEJA LEURS COMPOSANTES DE CISAILLEMENT
C     MULTIPLIES PAR RACINE DE 2 > PRISE EN COMPTE DES DOUBLES
C     PRODUITS TENSORIELS ET CONSERVATION DE LA SYMETRIE
C
C     ------------------------------------------------------------------
      
C     NMAT = NOMBRE MAXI DE PARMETRES MATERIAU
C     POUR LE MONOCRISTAL, DIMENSIONS MAX
C     NSG=NOMBRE DE SYSTEMES DE GLISSEMENT MAXIMUM
C     NFS=NOMBRE DE FAMILLES DE SYSTEMES DE GLISSEMENT MAXIMUM
      INTEGER      NMAT,NSG,NFS,NRM
      PARAMETER  ( NSG=30)
      PARAMETER  ( NFS=5)
      PARAMETER  ( NRM=NFS*NSG+6)
      PARAMETER  ( NMAT=90)
      
      CHARACTER*(*) FAMI
      CHARACTER*3   MATCST
      CHARACTER*7   ETATD,ETATF
      CHARACTER*8   MOD,TYPMA,TYPMOD(*)
      CHARACTER*16  COMP(*),OPT,LOI,CPMONO(5*NMAT+1)
      
      INTEGER IMAT,NDT,NDI,NR,NVI,ITMAX,ICOMP,KPG,KSP,IRTETI,IRTET
      INTEGER K,L,IRET,NBCOMM(NMAT,3),NUMHSR(1)
      REAL*8 TOLER,EPSI,MATERD(NMAT,2),MATERF(NMAT,2)
      REAL*8 CRIT(*),VIND(*),VINF(*),TIMED,TIMEF,TEMPD,TEMPF,TREF
      REAL*8 EPSD(9),DEPS(9),EPSDT(9),DEPST(9),SIGD(6),SIGF(6)
      REAL*8 SEUIL,THETA,DT,DEVG(6),DEVGII
      REAL*8 VP(3),VECP(3,3),TAMPON(*),DSDE(6,*),PGL(3,3),ANGMAS(3)
      REAL*8 TOUTMS(NFS,NSG,6),HSR(NSG,NSG),DRDY(NRM*NRM)
C     POUR BETON_BURGER_FP - ATTENTION DIMENSION MAXI POUR CE MODELE
      REAL*8  YD(21),YF(21)      
      PARAMETER  ( EPSI = 1.D-15 )
      LOGICAL RESI,RIGI,GDEF
C     ----------------------------------------------------------------
      COMMON /TDIM/   NDT  , NDI
C     ----------------------------------------------------------------
C
C --  INITIALISATION DES PARAMETRES DE CONVERGENCE ET ITERATIONS
C
      IRTETI = 0
      ITMAX  = INT(CRIT(1))
      TOLER  = CRIT(3)
      THETA  = CRIT(4)
      LOI    = COMP(1)
      MOD    = TYPMOD(1)
      DT     = TIMEF - TIMED
      RESI   = OPT(1:9).EQ.'RAPH_MECA' .OR. OPT(1:9).EQ.'FULL_MECA'
      RIGI   = OPT(1:9).EQ.'RIGI_MECA' .OR. OPT(1:9).EQ.'FULL_MECA'
      GDEF   = COMP(3).EQ.'SIMO_MIEHE'
      NUMHSR(1)=1

      TYPMA = 'VITESSE '
C
C --  RECUPERATION COEF MATERIAU A T ET/OU T+DT

      CALL LCMATE(FAMI,KPG,KSP,COMP,MOD,IMAT,NMAT,TEMPD,TEMPF,
     &            0,TYPMA,HSR,MATERD,MATERF,MATCST,NBCOMM,
     &            CPMONO,ANGMAS,PGL,ITMAX,TOLER,NDT,NDI,NR,CRIT,
     &            NVI,VIND,NFS,NSG,TOUTMS,1,NUMHSR,SIGD)


      IF (GDEF) THEN
C        GDEF_MONO : PAS DE DEFORM. THERMIQUE
         CALL DCOPY(9,DEPST,1,DEPS,1)
         CALL DCOPY(9,EPSDT,1,EPSD,1)      
      ELSE
C --     RETRAIT INCREMENT DE DEFORMATION DUE A LA DILATATION THERMIQUE
         CALL LCDEDI(FAMI,KPG,KSP, NMAT,  MATERD, MATERF,
     &            TEMPD, TEMPF,TREF,DEPST, EPSDT, DEPS, EPSD )
C --     RETRAIT ENDOGENNE ET RETRAIT DE DESSICCATION
         CALL LCDEHY(FAMI, KPG, KSP, NMAT, MATERD, MATERF,
     &            DEPS, EPSD )
      ENDIF

C --    SEUIL A T > ETAT ELASTIQUE OU PLASTIQUE A T
      IF  ( ABS(VIND (NVI)) .LE. EPSI ) THEN
         ETATD = 'ELASTIC'
      ELSE
         ETATD = 'PLASTIC'
      ENDIF
C
C --> REDECOUPAGE IMPOSE
      IF ( ICOMP .EQ. -1 .AND. OPT .NE. 'RIGI_MECA_TANG') THEN
         IRTETI = 0
         GOTO 9999
      ENDIF
C
C     ----------------------------------------------------------------
C     OPTIONS 'FULL_MECA' ET 'RAPH_MECA' = CALCUL DE SIG(T+DT)
C     ----------------------------------------------------------------
C
      IF ( RESI ) THEN

         IF (GDEF) THEN
C           GDEF_MONO : PAS DE SEUIL CAR C'EST PLUS COMPLIQUE
            SEUIL=1.D0
         ELSE
C --        INTEGRATION ELASTIQUE SUR DT
            CALL LCELAS(LOI, MOD , NMAT, MATERD, MATERF, MATCST,
     &               NVI, DEPS, SIGD, VIND,   SIGF,   VINF,
     &               THETA)

C --        PREDICTION ETAT ELASTIQUE A T+DT : F(SIG(T+DT),VIN(T)) = 0 ?
            SEUIL=1.D0
            CALL LCCNVX(FAMI, KPG, KSP, LOI, IMAT, NMAT, MATERD,
     &               MATERF,SIGD,SIGF,DEPS,VIND,VINF,NBCOMM, CPMONO,PGL,
     &               NVI,VP,VECP, HSR,NFS,NSG, TOUTMS, TIMED,TIMEF,
     &               NR,YD,YF,TOLER,SEUIL)
 
         ENDIF
C
         IF ( SEUIL .GE. 0.D0 ) THEN
C --        PREDICTION INCORRECTE > INTEGRATION ELASTO-PLASTIQUE SUR DT
            ETATF = 'PLASTIC'
C
            CALL LCPLAS(FAMI,KPG,KSP,LOI,TOLER,ITMAX,MOD,IMAT,NMAT,
     &                  MATERD,MATERF, NR, NVI,
     &                  TIMED,TIMEF, DEPS, EPSD, SIGD ,VIND, SIGF,
     &                 VINF,COMP,NBCOMM, CPMONO, PGL,NFS,NSG,TOUTMS,HSR,
     &                  ICOMP, IRTET, THETA,VP,VECP,SEUIL, DEVG,
     &                  DEVGII,DRDY,TAMPON,CRIT)
C
            IF ( IRTET.GT.0 ) GOTO (1,2), IRTET
         ELSE
C --        PREDICTION CORRECTE > INTEGRATION ELASTIQUE FAITE
            ETATF = 'ELASTIC'
C ---       MISE A JOUR DE VINF EN FONCTION DE LA LOI
C           ET POST-TRAITEMENTS POUR DES LOIS PARTICULIERES
            CALL LCELPL(MOD,LOI,NMAT,MATERD,MATERF,TIMED,TIMEF,
     &                  DEPS,NVI,VIND,VINF,NR,YD,YF,SIGD,SIGF,DRDY)
         ENDIF
         
C        POST-TRAITEMENTS PARTICULIERS
         CALL LCPOPL(LOI,NMAT,MATERD,MATERF,MOD,SIGF,VIND,VINF)

      ENDIF
C
C     ----------------------------------------------------------------
C     OPTIONS 'FULL_MECA' ET 'RIGI_MECA_TANG' = CALCUL DE DSDE
C     ----------------------------------------------------------------
C     EVALUATION DU JACOBIEN DSDE A (T+DT) POUR 'FULL_MECA'
C     ET CALCUL ELASTIQUE    ET   A (T)    POUR 'RIGI_MECA_TANG'
C     ----------------------------------------------------------------
C
      IF ( RIGI ) THEN
         CALL LCOTAN(OPT,ETATD,ETATF,FAMI,KPG,KSP,LOI,MOD,IMAT,
     &               NMAT,MATERD,MATERF,EPSD,DEPS,SIGD,SIGF,
     &               NVI,VIND,VINF,DRDY,
     &               VP,VECP,THETA,DT,DEVG,DEVGII,
     &               TIMED,TIMEF,COMP,NBCOMM,CPMONO,
     &               PGL,NFS,NSG,TOUTMS,HSR,NR,ITMAX,TOLER,TYPMA,
     &               DSDE,IRTET)
         IF (IRTET.NE.0) GOTO 1
      ENDIF
C
C       ----------------------------------------------------------------
C
      IRTETI = 0
      GOTO 9999
1     CONTINUE
      IRTETI = 1
      GOTO 9999
C
2     CONTINUE
      IRTETI = 2
      GOTO 9999
C
9999  CONTINUE

      END
