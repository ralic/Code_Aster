      SUBROUTINE NMACTN(SDIMPR,SDDISC,SDERRO,DEFICO,RESOCO,
     &                  SOLVEU,PARCRI,ITERAT,NUMINS)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 02/10/2012   AUTEUR DESOZA T.DESOZA 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT     NONE
      INCLUDE 'jeveux.h'
      CHARACTER*24 SDIMPR,SDERRO
      CHARACTER*24 DEFICO,RESOCO
      CHARACTER*19 SDDISC,SOLVEU
      REAL*8       PARCRI(*)
      INTEGER      ITERAT,NUMINS
C
C ----------------------------------------------------------------------
C
C ROUTINE MECA_NON_LINE (ALGORITHME)
C
C GESTION DES ACTIONS A LA FIN D'UNE BOUCLE DE NEWTON
C
C BOUCLE NEWTON -> BOUCLE POINT FIXE
C
C ----------------------------------------------------------------------
C
C
C IN  SDIMPR : SD AFFICHAGE
C IN  SDDISC : SD DISCRETISATION
C IN  SDERRO : SD GESTION DES ERREURS
C IN  DEFICO : SD POUR LA DEFINITION DE CONTACT
C IN  RESOCO : SD POUR LA RESOLUTION DE CONTACT
C IN  SOLVEU : SD SOLVEUR
C IN  PARCRI : CRITERES DE CONVERGENCE
C IN  ITERAT : NUMERO D'ITERATION DE NEWTON
C IN  NUMINS : NUMERO D'INSTANT
C
C
C
C
      INTEGER      RETACT,IEVDAC,ACTNEW
      LOGICAL      ARRET
      CHARACTER*4  ETNEWT
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
C
C --- INITIALISATIONS
C
      ARRET  = (NINT(PARCRI(4)).EQ.0)
      RETACT = 4
      ACTNEW = 3
C
C --- ETAT DE NEWTON ?
C
      CALL NMLEEB(SDERRO,'NEWT',ETNEWT)
C
C --- ACTIONS SUITE A UN EVENEMENT
C
      IF (ETNEWT.EQ.'CONV') THEN
        RETACT = 0
      ELSEIF (ETNEWT.EQ.'EVEN') THEN
        CALL NMACTO(SDDISC,IEVDAC)
        CALL NMEVAC(SDIMPR,SDDISC,SDERRO,DEFICO,RESOCO,
     &              SOLVEU,IEVDAC,NUMINS,ITERAT,RETACT)
      ELSEIF (ETNEWT.EQ.'CONT') THEN
C ----- TROP TARD POUR CONTINUE NEWTON -> IMPOSSIBLE
        CALL ASSERT(.FALSE.)
      ELSEIF (ETNEWT.EQ.'ERRE') THEN
C ----- ERRREUR NON TRAITE DANS NMCVGN -> IMPOSSIBLE
        CALL ASSERT(.FALSE.)
      ELSEIF (ETNEWT.EQ.'STOP') THEN
        RETACT = 4
      ELSE
        CALL ASSERT(.FALSE.)
      ENDIF
C
C --- TRAITEMENT DE L'ACTION
C
      IF (RETACT.EQ.0) THEN
C
C ----- TOUT EST OK -> ON PASSE A LA SUITE
C
        ACTNEW = 0
      ELSEIF (RETACT.EQ.1) THEN
C
C ----- ON REFAIT LE PAS DE TEMPS
C
        ACTNEW = 1
      ELSEIF (RETACT.EQ.2)  THEN
C
C ----- ON CONTINUE LES ITERATIONS DE NEWTON
C
        ACTNEW = 2
      ELSEIF (RETACT.EQ.3) THEN
C
C ----- ECHEC DE L'ACTION
C
        IF (.NOT.ARRET) THEN
C
C ------- CONVERGENCE FORCEE -> ON PASSE A LA SUITE
C
          CALL U2MESS('A','MECANONLINE2_37')
          ACTNEW = 0
          CALL NMECEB(SDERRO,'RESI','CONV')
        ELSE
C
C ------- ARRET DU CALCUL
C
          ACTNEW = 3
        ENDIF
      ELSEIF (RETACT.EQ.4) THEN
C
C ----- ARRET DU CALCUL
C
        ACTNEW = 3
      ELSE
        CALL ASSERT(.FALSE.)
      ENDIF
C
C --- CHANGEMENT DE STATUT DE LA BOUCLE
C
      IF (ACTNEW.EQ.0) THEN
        CALL NMECEB(SDERRO,'NEWT','CONV')
      ELSEIF (ACTNEW.EQ.1) THEN
        CALL NMECEB(SDERRO,'NEWT','ERRE')
      ELSEIF (ACTNEW.EQ.2)  THEN
        CALL NMECEB(SDERRO,'NEWT','CONT')
      ELSEIF (ACTNEW.EQ.3) THEN
        CALL NMECEB(SDERRO,'NEWT','STOP')
      ELSE
        CALL ASSERT(.FALSE.)
      ENDIF
C
      CALL JEDEMA()
      END
