      SUBROUTINE  NIRM2D (NNO1, NNO2, NPG1, IPOIDS, IVF1, IVF2, IDFDE1,
     &                    IGEOM, IMATE, KUU, KUA, KAA)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 09/05/2011   AUTEUR SFAYOLLE S.FAYOLLE 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE SFAYOLLE S.FAYOLLE
C TOLE CRS_1404
      IMPLICIT NONE

      INTEGER NNO1, NNO2, NPG1, IMATE
      INTEGER IPOIDS, IVF1, IVF2, IDFDE1

      REAL*8 KUU(2,9,2,9), KUA(2,9,2,4), KAA(2,4,2,4)


C......................................................................
C     BUT:CALCUL  DES OPTIONS RIGI_MECA POUR LA FORMULATION UPG EN 2D
C......................................................................
C IN  NNO1    : NOMBRE DE NOEUDS DE L'ELEMENT LIES AUX DEPLACEMENTS
C IN  NNO2    : NOMBRE DE NOEUDS DE L'ELEMENT LIES A LA PRESSION
C IN  NPG1    : NOMBRE DE POINTS DE GAUSS
C IN  POIDSG  : POIDS DES POINTS DE GAUSS
C IN  VFF1    : VALEUR  DES FONCTIONS DE FORME LIES AUX DEPLACEMENTS
C IN  VFF2    : VALEUR  DES FONCTIONS DE FORME LIES A LA PRESSION
C                  ET AU GONFLEMENT
C IN  DFDE1   : DERIVEE DES FONCTIONS DE FORME ELEMENT DE REFERENCE
C IN  DFDK1   : DERIVEE DES FONCTIONS DE FORME ELEMENT DE REFERENCE
C IN  GEOM    : COORDONEES DES NOEUDS
C IN  TYPMOD  : TYPE DE MODELISATION
C IN  OPTION  : OPTION DE CALCUL
C IN  IMATE   : MATERIAU CODE
C IN  COMPOR  : COMPORTEMENT
C IN  LGPG    : "LONGUEUR" DES VARIABLES INTERNES POUR 1 POINT DE GAUSS
C               CETTE LONGUEUR EST UN MAJORANT DU NBRE REEL DE VAR. INT.
C IN  CRIT    : CRITERES DE CONVERGENCE LOCAUX
C IN  INSTAM  : INSTANT PRECEDENT
C IN  INSTAP  : INSTANT DE CALCUL
C IN  TM      : TEMPERATURE AUX NOEUDS A L'INSTANT PRECEDENT
C IN  TP      : TEMPERATURE AUX NOEUDS A L'INSTANT DE CALCUL
C IN  TREF    : TEMPERATURE DE REFERENCE
C IN  DEPLM   : DEPLACEMENT A L'INSTANT PRECEDENT
C IN  DDEPL   : INCREMENT DE DEPLACEMENT
C IN  ANGMAS  : LES TROIS ANGLES DU MOT_CLEF MASSIF (AFFE_CARA_ELEM)
C IN  GONFLM  : P ET G  A L'INSTANT PRECEDENT
C IN  DGONFL  : INCREMENT POUR P ET G
C IN  SIGM    : CONTRAINTES A L'INSTANT PRECEDENT
C IN  VIM     : VARIABLES INTERNES A L'INSTANT PRECEDENT
C OUT DFDI    : DERIVEE DES FONCTIONS DE FORME  AU DERNIER PT DE GAUSS
C OUT SIGP    : CONTRAINTES DE CAUCHY (RAPH_MECA ET FULL_MECA)
C OUT VIP     : VARIABLES INTERNES    (RAPH_MECA ET FULL_MECA)
C OUT FINTU   : FORCES INTERNES
C OUT FINTA   : FORCES INTERNES LIEES AUX MULTIPLICATEURS
C OUT KUU     : MATRICE DE RIGIDITE (RIGI_MECA_TANG ET FULL_MECA)
C OUT KUA     : MATRICE DE RIGIDITE TERMES CROISES U - PG
C                                       (RIGI_MECA_TANG ET FULL_MECA)
C OUT KAA     : MATRICE DE RIGIDITE TERME PG (RIGI_MECA_TANG, FULL_MECA)
C......................................................................
C
C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ---------------------
      INTEGER  ZI
      COMMON  / IVARJE / ZI(1)
      REAL*8             ZR
      COMMON  / RVARJE / ZR(1)
      COMPLEX*16         ZC
      COMMON  / CVARJE / ZC(1)
      LOGICAL            ZL
      COMMON  / LVARJE / ZL(1)
      CHARACTER*8        ZK8
      CHARACTER*16                ZK16
      CHARACTER*24                          ZK24
      CHARACTER*32                                    ZK32
      CHARACTER*80                                              ZK80
      COMMON  / KVARJE / ZK8(1) , ZK16(1) , ZK24(1) , ZK32(1) , ZK80(1)
C --------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ---------------------

      INTEGER KPG, N, KL, PQ, I, J, M, NBSIGM, NBSIG, IDIM, IGEOM
      INTEGER NDIM

      REAL*8 DSIDEP(6,6), DEF(4,NNO1,2), DEFD(4,9,2), DEFTR(9,2)
      REAL*8 TMP, TMP2, B(4,81), POIDS, VFF2, VFFN, VFFM
      REAL*8 BARY(3), REPERE(7)

C-----------------------------------------------------------------------
C - INITIALISATION
      CALL R8INIR(324, 0.D0, KUU, 1)
      CALL R8INIR(144, 0.D0, KUA, 1)
      CALL R8INIR(64, 0.D0, KAA, 1)

      NDIM = 2

C - NOMBRE DE CONTRAINTES ASSOCIE A L'ELEMENT
      NBSIG = NBSIGM()

C - RECUPERATION  DES DONNEEES RELATIVES AU REPERE D'ORTHOTROPIE
C - COORDONNEES DU BARYCENTRE ( POUR LE REPRE CYLINDRIQUE )
      BARY(1) = 0.D0
      BARY(2) = 0.D0
      BARY(3) = 0.D0
      DO 150 I = 1,NNO1
        DO 40 IDIM = 1,NDIM
          BARY(IDIM) = BARY(IDIM)+ZR(IGEOM+IDIM+NDIM*(I-1)-1)/NNO1
 40    CONTINUE
 150  CONTINUE
      CALL ORTREP(ZI(IMATE),NDIM,BARY,REPERE)

C - CALCUL POUR CHAQUE POINT DE GAUSS
      DO 800 KPG = 1,NPG1

C - CALCUL DES ELEMENTS GEOMETRIQUES
C - CALCUL DE DFDI,F,EPS,R(EN AXI) ET POIDS
        CALL BMATMC(KPG,NBSIG,ZR(IGEOM),IPOIDS,IVF1,IDFDE1,
     +                  NNO1, 0.D0, POIDS, B)

        DO 10 I = 1, 4
          DO 20 J = 1, NNO1
            DO 30 N = 1, 2
              DEF(I,J,N) = B(I,(J-1)*2+N)
  30        CONTINUE
  20      CONTINUE
  10    CONTINUE

C - CALCUL DE LA TRACE ET DEVIATEUR DE B
        DO 70 N = 1,NNO1
          DO 65 I = 1,2
            DEFTR(N,I) =  DEF(1,N,I) + DEF(2,N,I) + DEF(3,N,I)
            DO 60 KL = 1,3
              DEFD(KL,N,I) = DEF(KL,N,I) - DEFTR(N,I)/3.D0
 60         CONTINUE
            DEFD(4,N,I) = DEF(4,N,I)
 65       CONTINUE
 70     CONTINUE

C - CALCUL DE LA MATRICE DE HOOKE (LE MATERIAU POUVANT
C - ETRE ISOTROPE, ISOTROPE-TRANSVERSE OU ORTHOTROPE)
        CALL DMATDP('RIGI',ZI(IMATE),0.0D0,'+',KPG,1,REPERE,DSIDEP)

C - CALCUL DE LA MATRICE DE RIGIDITE
C - TERME K_UU
        DO 105 N=1,NNO1
          DO 104 I=1,2
            DO 103 M=1,NNO1
              DO 102 J=1,2
                TMP = 0.D0
                DO 101 KL = 1,4
                  DO 100 PQ = 1,4
                    TMP = TMP + DEFD(KL,N,I)*DSIDEP(KL,PQ)*DEFD(PQ,M,J)
 100              CONTINUE
 101            CONTINUE
                KUU(I,N,J,M) = KUU(I,N,J,M) + POIDS*TMP
 102          CONTINUE
 103        CONTINUE
 104      CONTINUE
 105    CONTINUE

C - TERME K_UP
        DO 112 N = 1, NNO1
          DO 111 I = 1,2
            DO 110 M = 1, NNO2
              VFF2 = ZR(IVF2-1+M+(KPG-1)*NNO2)
              TMP = DEFTR(N,I)*VFF2
              KUA(I,N,1,M) = KUA(I,N,1,M) + POIDS*TMP
 110        CONTINUE
 111      CONTINUE
 112    CONTINUE

C - TERME K_UG
        DO 130 N = 1, NNO1
          DO 129 I = 1,2
            TMP = 0.D0
            DO 128 KL = 1,4
              DO 127 PQ = 1,3
                TMP = TMP + DEFD(KL,N,I)*DSIDEP(KL,PQ)
 127          CONTINUE
 128        CONTINUE
            TMP = TMP/3.D0
            DO 126 M = 1, NNO2
              VFF2 = ZR(IVF2-1+M+(KPG-1)*NNO2)
              KUA(I,N,2,M) = KUA(I,N,2,M) + POIDS*TMP*VFF2
 126        CONTINUE
 129      CONTINUE
 130    CONTINUE

C - TERME K_PP = 0
C - TERME K_PG
        DO 145 N = 1,NNO2
          VFFN = ZR(IVF2-1+N+(KPG-1)*NNO2)
          DO 140 M = 1,NNO2
            VFFM = ZR(IVF2-1+M+(KPG-1)*NNO2)
            TMP = VFFN * VFFM
            KAA(1,N,2,M) = KAA(1,N,2,M) - POIDS*TMP
            KAA(2,M,1,N) = KAA(2,M,1,N) - POIDS*TMP
 140      CONTINUE
 145    CONTINUE

C - TERME K_GG
        TMP = 0.D0
        DO 160 KL = 1,3
          DO 159 PQ = 1,3
            TMP = TMP + DSIDEP(KL,PQ)
 159      CONTINUE
 160    CONTINUE
        TMP = TMP/9.D0
        DO 170 N = 1,NNO2
          VFFN = ZR(IVF2-1+N+(KPG-1)*NNO2)
          DO 169 M = 1,NNO2
            VFFM = ZR(IVF2-1+M+(KPG-1)*NNO2)
            TMP2 = VFFN * VFFM
            KAA(2,N,2,M) = KAA(2,N,2,M) + POIDS*TMP*TMP2
 169      CONTINUE
 170    CONTINUE
 800  CONTINUE
      END
