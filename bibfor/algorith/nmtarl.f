      SUBROUTINE NMTARL(MODE,NDIMSI,CRIT,MAT,SIGEL,VIM,EPM,DP,SP,XI,
     &                  DIRDP,DIRSP,DIRXI,MIN,RHO,ENER)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 18/02/97   AUTEUR GJBHHEL E.LORENTZ 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================

      IMPLICIT NONE
      INTEGER  MODE,NDIMSI
      REAL*8   CRIT(*),MAT(*),SIGEL(*),VIM(*),EPM(*),DP,SP,XI
      REAL*8   DIRDP,DIRSP,DIRXI,MIN
      REAL*8   RHO,ENER
C ----------------------------------------------------------------------
C TAHERI: MINIMISATION DE F**2+G**2 RT A (P,SP,XI)+RHO(DIRP,DIRSP,DIRXI)
C ----------------------------------------------------------------------
C IN  MODE   2: (P,SP)     3: (XI,SP)
C IN  NDIMSI DIMENSION DES TENSEURS
C IN  CRIT   CRITERES DE CONVERGENCE LOCAUX
C IN  MAT    TABLEAU DES CONSTANTES MATERIAUX
C IN  SIGEL  DEVIATEUR DES CONTRAINTES ELASTIQUES
C IN  VIM    VARIABLES INTERNES EN T-
C IN  EPM    DEFORMATION PLASTIQUE EN T-
C IN  DP     INCREMENT DE DEFORMATION PLASTIQUE CUMULEE
C IN  SP     CONTRAINTE DE PIC
C IN  XI     PILOTAGE DE EPN
C IN  DIRDP  DIRECTION POUR DP
C IN  DIRSP  DIRECTION POUR SP
C IN  DIRXI  DIRECTION POUR XI
C IN  MIN    VALEUR DE LA DERIVEE EN RHO=0
C VAR RHO    DISTANCE PARCOURUE  IN: RHOMAX  OUT: RHO
C VAR ENER   VALEUR DE (F**2+G**2)/2   IN: EN RHO=0   OUT: EN RHO
C ----------------------------------------------------------------------



      INTEGER NITER,ITELIN
      REAL*8  F,G,FDS,GDS,FDP,GDP,FDX,GDX,DPMAX,SIG(6),TANG(6,6)
      REAL*8  X(4),Y(4),ENERG(4)
      REAL*8  RHOMAX,REFE,PRELIN

      PARAMETER (PRELIN = 1.D-2, ITELIN = 3)


C    INITIALISATION DE LA SOLUTION RHO = 0
      X(1) = 0.D0
      Y(1) = MIN
      REFE = MIN
      IF (REFE.GE.0.D0) THEN
        RHO = 0.D0
        GOTO 9999
      END IF
      ENERG(1) = ENER


C    EXAMEN DE LA SOLUTION RHO = RHOMAX
      RHOMAX = RHO
      X(2) = RHOMAX
      CALL NMTACR(MODE,NDIMSI,MAT,SIGEL,VIM,EPM,
     &            DP+RHOMAX*DIRDP, SP+RHOMAX*DIRSP, XI+RHOMAX*DIRXI,
     &            F,G,FDS,GDS,FDP,GDP,FDX,GDX,DPMAX,SIG,TANG)
      IF (MODE.EQ.2) THEN
        Y(2) = (F*FDP+G*GDP)*DIRDP + (F*FDS+G*GDS)*DIRSP
      ELSE
        Y(2) = (F*FDX+G*GDX)*DIRXI + (F*FDS+G*GDS)*DIRSP
      END IF
      ENER = (F**2+G**2)/2.D0
      IF (Y(2).LE.0.D0) THEN
        RHO  = RHOMAX
        GOTO 9999
      END IF
      ENERG(2) = ENER


C    CALCUL DE RHO

      X(3)     = X(1)
      Y(3)     = Y(1)
      ENERG(3) = ENERG(1)
      X(4)     = X(2)
      Y(4)     = Y(2)
      ENERG(4) = ENERG(2)

      DO 100 NITER = 1,ITELIN
        IF ( ABS( Y(4)/REFE ) .LT. PRELIN) GOTO 110
        CALL ZEROCO(X,Y)
        CALL NMTACR(MODE,NDIMSI,MAT,SIGEL,VIM,EPM,
     &            DP+X(4)*DIRDP, SP+X(4)*DIRSP, XI+X(4)*DIRXI,
     &            F,G,FDS,GDS,FDP,GDP,FDX,GDX,DPMAX,SIG,TANG)
        IF (MODE.EQ.2) THEN
          Y(4) = (F*FDP+G*GDP)*DIRDP + (F*FDS+G*GDS)*DIRSP
        ELSE
          Y(4) = (F*FDX+G*GDX)*DIRXI + (F*FDS+G*GDS)*DIRSP
        END IF
        ENERG(4) = (F**2+G**2)/2.D0
 100  CONTINUE
 110  CONTINUE
      RHO  = X(4)
      ENER = ENERG(4)


 9999 CONTINUE
      END
