      SUBROUTINE SUBACO(NNO,DFF,GEOM,COVA)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 05/01/96   AUTEUR GJBHHEL E.LORENTZ 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================

      IMPLICIT NONE

      INTEGER NNO
      REAL*8  DFF(2,NNO),GEOM(3,NNO)
      REAL*8  COVA(3,3)

C.......................................................................
C     CALCUL DE LA BASE COVARIANTE POUR UN ELEMENT SURFACIQUE
C.......................................................................
C IN  NNO     NOMBRE DE NOEUDS
C IN  DFF     DERIVEE DES F. DE FORME
C IN  GEOM    COORDONNEES DES NOEUDS
C OUT COVA    COORDONNEES DES VECTEURS DE LA BASE COVARAINTE
C.......................................................................

      INTEGER N,I
      REAL*8  NORME


      DO 10 I = 1,3
        COVA(I,1) = 0.D0
        COVA(I,2) = 0.D0
 10   CONTINUE


C    CALCUL DES VECTEURS TANGENTS
      DO 20 N = 1,NNO
        DO 25 I=1,3
          COVA(I,1) = COVA(I,1) + DFF(1,N)*GEOM(I,N)
          COVA(I,2) = COVA(I,2) + DFF(2,N)*GEOM(I,N)
 25     CONTINUE
 20   CONTINUE


C    CALCUL DE LA NORMALE (PRODUIT VECTORIEL DES VECTEURS TANGENTS)
      COVA(1,3) = COVA(2,1)*COVA(3,2) - COVA(3,1)*COVA(2,2)
      COVA(2,3) = COVA(3,1)*COVA(1,2) - COVA(1,1)*COVA(3,2)
      COVA(3,3) = COVA(1,1)*COVA(2,2) - COVA(2,1)*COVA(1,2)

      NORME = SQRT(COVA(1,3)**2 + COVA(2,3)**2 + COVA(3,3)**2)
      COVA(1,3) = COVA(1,3) / NORME
      COVA(2,3) = COVA(2,3) / NORME
      COVA(3,3) = COVA(3,3) / NORME

      END
