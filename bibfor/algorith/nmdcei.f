      SUBROUTINE NMDCEI(SDDISC,NUMINS,NEWINS,NBINI ,NBINS ,
     &                  TYPEXT,DT0   )
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 13/06/2012   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT NONE
      INCLUDE 'jeveux.h'
      CHARACTER*19 SDDISC
      REAL*8       NEWINS(NBINS)
      REAL*8       DT0
      INTEGER      NBINS,NUMINS,NBINI
      CHARACTER*4  TYPEXT
C
C ----------------------------------------------------------------------
C
C ROUTINE MECA_NON_LINE (ALGORITHME - DECOUPE)
C
C EXTENSION DE LA LISTE D'INSTANTS
C
C ----------------------------------------------------------------------
C
C
C IN  SDDISC : SD DISCRETISATION
C IN  NEWINS : VECTEUR DES NOUVEAUX INSTANTS A AJOUTER (LONGUEUR NBINS)
C IN  NUMINS : NUMERO D'INSTANT
C IN  NBINS  : NOMBRE DE PAS DE TEMPS A INSERER
C IN  NBINI  : ANCIEN NOMBRE D'INSTANTS
C IN  TYPEXT : TYPE D'EXTENSION
C               'ADAP' - ADAPTATION DU PAS DE TEMPS
C               'DECO' - DECOUPAGE DU PAS DE TEMPS
C OUT DT0    : NOUVEAU DELTAT
C
C
C
C
      INTEGER      IPAS,NBNEW,IBID
      CHARACTER*24 TPSDIT
      INTEGER      JTEMPS
      CHARACTER*8  K8BID
      REAL*8       INST,DELTAT,R8MAEM,R8BID,DTMIN
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
C
C --- ACCES SD LISTE D'INSTANTS
C
      TPSDIT = SDDISC(1:19)//'.DITR'
C
C --- NOUVEAU NOMBRE D'INSTANTS
C
      NBNEW  = NBINI + NBINS
C
C --- ALLONGEMENT DE LA LISTE D'INSTANTS
C
      CALL JUVECA(TPSDIT,NBNEW )
      CALL JEVEUO(TPSDIT,'E',JTEMPS)
C
C --- RECOPIE DE LA PARTIE HAUTE DE LA LISTE
C
      DO 10 IPAS = NBINI, NUMINS+1, -1
        INST   = ZR(JTEMPS+IPAS-1)
        ZR(JTEMPS+IPAS+NBINS-1) = INST
 10   CONTINUE
C
C --- INSERTION DES INSTANTS SUPPLEMENTAIRES
C
      DO 20 IPAS = 1, NBINS
        INST   = NEWINS(IPAS)
        IF (TYPEXT.EQ.'DECO') THEN
          ZR(JTEMPS+IPAS+NUMINS-1) = INST
        ELSEIF (TYPEXT.EQ.'ADAP') THEN
          ZR(JTEMPS+IPAS+NUMINS)   = INST
        ELSE
          CALL ASSERT(.FALSE.)
        ENDIF
 20   CONTINUE
C
C --- VALEUR DU NOUVEAU DELTAT
C
      DT0    = ZR(JTEMPS-1+NUMINS+1)-ZR(JTEMPS-1+NUMINS)
C
C --- NOUVEL INTERVALLE DE TEMPS MINIMAL : DTMIN
C
      DTMIN  = R8MAEM()
      DO 25 IPAS = 1,NBNEW-1
        DELTAT = ZR(JTEMPS-1+IPAS+1) - ZR(JTEMPS-1+IPAS)
        DTMIN  = MIN(DELTAT,DTMIN)
 25   CONTINUE
C
C --- ENREGISTREMENT INFOS
C
      CALL UTDIDT('E'   ,SDDISC,'LIST',IBID,'NBINST',
     &            R8BID ,NBNEW ,K8BID )
      CALL UTDIDT('E'   ,SDDISC,'LIST',IBID,'DTMIN',
     &            DTMIN ,IBID  ,K8BID )
C
      CALL JEDEMA()

      END
