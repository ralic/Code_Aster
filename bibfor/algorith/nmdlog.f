      SUBROUTINE NMDLOG(FAMI,OPTION,TYPMOD,
     &                   NDIM,NNO,NPG,IW,VFF,IDFF,GEOMI,DFF,
     &                   COMPOR,MATE,LGPG,CRIT,ANGMAS,
     &                   INSTM,INSTP,MATSYM,
     &                   DEPLM,DEPLD,SIGM,VIM,
     &                   SIGP,VIP,FINT,MATUU,CODRET)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 10/01/2011   AUTEUR PROIX J-M.PROIX 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C TOLE CRP_21
C ----------------------------------------------------------------------
C     BUT:  CALCUL  DES OPTIONS RIGI_MECA_*, RAPH_MECA ET FULL_MECA_*
C           EN GRANDES DEFORMATIONS 2D (D_PLAN ET AXI) ET 3D LOG
C           SUIVANT ARTICLE MIEHE APEL LAMBRECHT CMAME 2002
C ----------------------------------------------------------------------
C IN  FAMI    : FAMILLE DE POINTS DE GAUSS
C IN  OPTION  : OPTION DE CALCUL
C IN  TYPMOD  : TYPE DE MODELISATION
C IN  NDIM    : DIMENSION DE L'ESPACE
C IN  NNO     : NOMBRE DE NOEUDS DE L'ELEMENT
C IN  NPG     : NOMBRE DE POINTS DE GAUSS
C IN  IW      : PTR. POIDS DES POINTS DE GAUSS
C IN  VFF     : VALEUR  DES FONCTIONS DE FORME
C IN  IDFF    : PTR. DERIVEE DES FONCTIONS DE FORME ELEMENT DE REF.
C IN  GEOMI   : COORDONNEES DES NOEUDS (CONFIGURATION INITIALE)
C MEM DFF     : ESPACE MEMOIRE POUR LA DERIVEE DES FONCTIONS DE FORME
C               DIM :(NNO,3) EN 3D, (NNO,4) EN AXI, (NNO,2) EN D_PLAN
C IN  COMPOR  : COMPORTEMENT
C IN  MATE    : MATERIAU CODE
C IN  LGPG    : DIMENSION DU VECTEUR DES VAR. INTERNES POUR 1 PT GAUSS
C IN  CRIT    : CRITERES DE CONVERGENCE LOCAUX
C IN  ANGMAS  : LES TROIS ANGLES DU MOT_CLEF MASSIF (AFFE_CARA_ELEM)
C IN  INSTM   : VALEUR DE L'INSTANT T-
C IN  INSTP   : VALEUR DE L'INSTANT T+
C IN  MATSYM  : .TRUE. SI MATRICE SYMETRIQUE
C IN  DEPLM   : DEPLACEMENT EN T-
C IN  DEPLD   : INCREMENT DE DEPLACEMENT ENTRE T- ET T+
C IN  SIGM    : CONTRAINTES DE CAUCHY EN T-
C IN  VIM     : VARIABLES INTERNES EN T-
C OUT SIGP    : CONTRAINTES DE CAUCHY (RAPH_MECA ET FULL_MECA_*)
C OUT VIP     : VARIABLES INTERNES    (RAPH_MECA ET FULL_MECA_*)
C OUT FINT    : FORCES INTERIEURES (RAPH_MECA ET FULL_MECA_*)
C OUT MATUU   : MATR. DE RIGIDITE NON SYM. (RIGI_MECA_* ET FULL_MECA_*)
C OUT CODRET  : CODE RETOUR DE L'INTEGRATION DE LA LDC
C
      IMPLICIT NONE
C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ---------------------
      INTEGER  ZI
      COMMON  / IVARJE / ZI(1)
      REAL*8             ZR
      COMMON  / RVARJE / ZR(1)
      COMPLEX*16         ZC
      COMMON  / CVARJE / ZC(1)
      LOGICAL            ZL
      COMMON  / LVARJE / ZL(1)
      CHARACTER*8        ZK8
      CHARACTER*16                ZK16
      CHARACTER*24                          ZK24
      CHARACTER*32                                    ZK32
      CHARACTER*80                                              ZK80
      COMMON  / KVARJE / ZK8(1) , ZK16(1) , ZK24(1) , ZK32(1) , ZK80(1)
C --------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ---------------------
      LOGICAL GRAND, AXI, RESI, RIGI, MATSYM
      PARAMETER (GRAND = .TRUE.)
      INTEGER G,I,NDDL,COD(27)
      INTEGER NDIM,NNO,NPG,MATE,LGPG,CODRET,IW,IDFF
      CHARACTER*8   TYPMOD(*)
      CHARACTER*(*) FAMI
      CHARACTER*16  OPTION, COMPOR(*)
      REAL*8 GEOMI(*),DFF(NNO,*),CRIT(*),INSTM,INSTP
      REAL*8 VFF(NNO,NPG),DTDE(6,6)
      REAL*8 ANGMAS(3),DEPLM(*), DEPLD(*),SIGM(2*NDIM,NPG),EPSML(6)
      REAL*8 VIM(LGPG,NPG),SIGP(2*NDIM,NPG),VIP(LGPG,NPG)
      REAL*8 MATUU(*), FINT(NDIM*NNO)
      REAL*8 GEOMM(3*27),GEOMP(3*27),FM(3,3),FP(3,3),DEPLT(3*27)
      REAL*8 JP,R,POIDS,TAMPON(10),TN(6),TP(6),DEPS(6),JM,PES(6,6)
      REAL*8 GN(3,3),FETA(4),XI(3,3),ME(3,3,3,3)
      
C-----------------------------TEST AVANT CALCUL---------------------

C     TEST SUR LE NOMBRE DE NOEUDS SI TEST NON VERIFIE MESSAGE ERREUR
      CALL ASSERT (NNO.LE.27)
      IF (TYPMOD(1).EQ.'C_PLAN') CALL U2MESS('F','ALGORITH8_1')

C -----------------------------DECLARATION-----------------------------
      NDDL = NDIM*NNO
                             
C     AFFECTATION DES VARIABLES LOGIQUES  OPTIONS ET MODELISATION
      AXI  = TYPMOD(1).EQ.'AXIS'
      RESI = OPTION(1:4).EQ.'RAPH' .OR. OPTION(1:4).EQ.'FULL'
      RIGI = OPTION(1:4).EQ.'RIGI' .OR. OPTION(1:4).EQ.'FULL'
            
C--------------------------INITIALISATION------------------------

C     MISE A ZERO TAUP[6]=0, DSIDEP[54]=0, TAMPON[10]=0, COD[27]=0
      CALL R8INIR(10,0.D0,TAMPON,1)
      DO 9 I=1,27
        COD(I)=0
   9  CONTINUE

C------------------------------DEPLACEMENT ET GEOMETRIE-------------
                     
C    DETERMINATION DES CONFIGURATIONS EN T- (GEOMM) ET T+ (GEOMP)
      CALL DCOPY(NDDL,GEOMI,1,GEOMM,1)
      CALL DAXPY(NDDL,1.D0,DEPLM,1,GEOMM,1)
      CALL DCOPY(NDDL,GEOMM,1,GEOMP,1)
C     DEPLT : DEPLACEMENT TOTAL ENTRE CONF DE REF ET INSTANT T_N+1  
      CALL DCOPY(NDDL,DEPLM,1,DEPLT,1)
      IF (RESI) THEN
        CALL DAXPY(NDDL,1.D0,DEPLD,1,GEOMP,1)
        CALL DAXPY(NDDL,1.D0,DEPLD,1,DEPLT,1)
      ENDIF 

C****************************BOUCLE SUR LES POINTS DE GAUSS************

C - CALCUL POUR CHAQUE POINT DE GAUSS
      DO 10 G=1,NPG

C---      CALCUL DE F_N, F_N+1 PAR RAPPORT A GEOMI GEOM INITIAL

         CALL PRELOG(NDIM,NNO,AXI,GRAND,VFF(1,G),GEOMI,G,IW,IDFF,
     &               DEPLM,DEPLT,SIGM(1,G),LGPG,VIM(1,G),
     &               GN,FETA,XI,ME,
     &               R,POIDS,DFF,FM,FP,JM,JP,EPSML,DEPS,TN,RESI,PES)
     
         CALL R8INIR(36,0.D0,DTDE,1)
         CALL R8INIR(6,0.D0,TP,1)
         CALL NMCOMP(FAMI,G,1,NDIM,TYPMOD,MATE,COMPOR,CRIT,
     &             INSTM,INSTP,
     &             EPSML,DEPS,TN,VIM(1,G),OPTION,
     &             ANGMAS,TAMPON,
     &             TP,VIP(1,G),DTDE,COD(G))
      
C       TEST SUR LES CODES RETOUR DE LA LOI DE COMPORTEMENT
         IF(COD(G).NE.0) THEN
            IF (RESI) THEN
               CODRET = 1
               GOTO 9999
            ENDIF 
         ENDIF

         CALL POSLOG(RESI,RIGI,TN,TP,VFF,DFF,FM,JM,POIDS,LGPG,VIP(1,G),
     &               NNO,NDIM,FP,JP,PES,AXI,G,R,DTDE,MATSYM,SIGM(1,G),
     &               GN,FETA,XI,ME, SIGP(1,G),FINT,MATUU )

 10   CONTINUE

C - SYNTHESE DES CODES RETOURS
      CALL CODERE(COD,NPG,CODRET)

 9999 CONTINUE
      END
