      SUBROUTINE  PRASMP (OPTION,NOMSTO,NOMNUM,TMINBL,NOMPRN,MODGEN,
     &TMNOBL,TMADBL,KNOMBL,INUMBL,SSMAX)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 14/01/98   AUTEUR VABHHTS J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
      IMPLICIT REAL*8 (A-H,O-Z)
C
C***********************************************************************
C    P. RICHARD     DATE 13/10/92
C-----------------------------------------------------------------------
C  BUT:      < PREPARATION ASSEMBLAGE MATRICE PROJETEE >
C
C  PREPARER L'ASSEMBLAGE POUR UN LIGREL CORRESPONDANT AUX MATRICES
C   PROJETEES DES SOUS-STRUCTURE
C   ON CONSIDERE POUR L'ASSEMBLAGE UN LISTE GENERALE DES BLOC
C   ELEMENTAIRES A ASSEMBLER DANS UNE MATRICE STOCKEE PROFIL BLOC
C   (EN GENERAL MATRICE PROJETEE=1BLOC,MATRICE DE LIAISON=NBLOCS)
C   NOEUD TARDIF = NOEUD FICTIF SUPPORTANT UNE SOUS-STRUCTURE
C
C   ON REMPLIT TMNOBL TMADBL KNOMBL INUMBL
C
C-----------------------------------------------------------------------
C
C NOM----- / /:
C
C OPTION   /I/: NOM K9 DE L'OPTION D'ASSEMBLAGE
C NOMSTO   /I/: NOM K19 DU STOCKAGE DE LA MATRICE
C NOMNUM   /I/: NOM K19 DE LA NUMEROTATION GENERALISEE
C NOMPRN   /I/: NOM K8 DU LIGREL COURANT A TRAITER
C TMINBL   /I/: NOM K24 DE LA FAMILLE NOMMEE AU NOM DES LIGRELS
C               ET DONNANT POUR CHAQUE NOEUD TARDIF DU LIGREL
C               LE NUMERO DE SON 1 BLOC DANS LA LISTE GENERALE ET
C               LE NOMBRE DE BLOC
C MODGEN   /I/: NOM K8 MODELE_GENERALISE AMONT
C TMNOBL   /I/: NOM K24 DE LA FAMILLE NUMEROTE DONNANT POUR CHAQUE
C               TERME D'UN BLOC ELEMENTAIRE LE NUMERO DU BLOC ASSEMBLE
C               D'ARRIVE
C TMADBL   /I/: NOM K24 DE LA FAMILLE NUMEROTE DONNANT POUR CHAQUE
C               TERME D'UN BLOC ELEMENTAIRE LE RANG D'ARRIVEE
C               DANS LE  BLOC ASSEMBLE
C KNOMBL   /M/: VECTEUR DES NOM K24 DES OBJETS OU FAMILLE CONTENANT
C               LES BLOCS ELEMENTAIRES
C INUMBL   /M/: VECTEUR NUMERO  BLOCS ELEMNTAIRE DANS LEUR FAMILLE OU 0
C               LES BLOCS ELEMENTAIRES
C SSMAX    /M/: MAXIMUM DE LA VALEURS ABSOLUE DES TERMES TRAITES
C
C-------- DEBUT COMMUNS NORMALISES  JEVEUX  ----------------------------
C
      INTEGER          ZI
      COMMON  /IVARJE/ ZI(1)
      REAL*8           ZR
      COMMON  /RVARJE/ ZR(1)
      COMPLEX*16       ZC
      COMMON  /CVARJE/ ZC(1)
      LOGICAL          ZL
      COMMON  /LVARJE/ ZL(1)
      CHARACTER*8      ZK8
      CHARACTER*16              ZK16
      CHARACTER*24                        ZK24
      CHARACTER*32                                  ZK32
      CHARACTER*80                                            ZK80
      COMMON  /KVARJE/ ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
      CHARACTER*32  JEXNUM,JEXNOM
C
C----------  FIN  COMMUNS NORMALISES  JEVEUX  --------------------------
C
      CHARACTER*6      PGC
      CHARACTER*8 MODGEN,NOMPRN,NOMMCL,KBID
      CHARACTER*19 NOMNUM,NOMSTO
      CHARACTER*9  OPTION,RIGOPT,MASOPT,AMOOPT,KSST
      CHARACTER*24 TMADBL,TMNOBL,TMINBL,KNOMBL(*)
      CHARACTER*11 ADNOM
      INTEGER      INUMBL(*)
      CHARACTER*1 K1BID
C
C-----------------------------------------------------------------------
      DATA PGC /'PRASMP'/
      DATA RIGOPT,MASOPT,AMOOPT/'RIGI_GENE','MASS_GENE','AMOR_GENE'/
      DATA KSST /'&SOUSSTR'/
C-----------------------------------------------------------------------
C
      CALL JEMARQ()
        IF(NOMPRN.NE.KSST) GOTO 9999
C
C------------------RECUPERATION DU NOMBRE DE SOUS-STRUCTURE-------------
C
      CALL JENONU(JEXNOM(NOMNUM//'.LILI',KSST),IBID)
      CALL JELIRA(JEXNUM(NOMNUM//'.PRNO',IBID),'LONMAX',NBSST,K1BID)
      NBSST=NBSST/2
C
C--------------------RECUPERATION DES CARACTERISTIQUES BLOCS------------
C
      CALL JEVEUO(NOMSTO//'.DESC','L',LLDESC)
      CALL JELIBE(NOMSTO//'.DESC')
C
C------------------CREATION DU NOM A CONCATENER-------------------------
C   POUR RECUPERER LE NOM DES MATRICES PROJETEES
C
      IF(OPTION.EQ.RIGOPT)THEN
        ADNOM='.MAEL.RAID '
      ELSE IF(OPTION.EQ.MASOPT) THEN
        ADNOM='.MAEL.MASS '
      ELSE IF (OPTION.EQ.AMOOPT) THEN
        ADNOM='.MAEL.AMOR '
      ENDIF
C
C---------------------REMPLISSAGE DES OBJETS DE TRAVAIL-----------------
C
C
      CALL JEVEUO(NOMNUM//'.NUEQ','L',LLNUEQ)
      CALL JEVEUO(NOMSTO//'.ADIA','L',LLADIA)
      CALL JEVEUO(NOMSTO//'.IABL','L',LLIABL)
C
      CALL JENONU(JEXNOM('&&ASSGEN.REP.NOM.PROF',NOMPRN),IBID)
      CALL JEVEUO(JEXNUM(TMINBL,IBID),'L',LTINBL)
      CALL JELIRA(JEXNUM(TMINBL,IBID),'LONMAX',NTPRNO,K1BID)
      NTPRNO=NTPRNO/2
C
      CALL JENONU(JEXNOM(NOMNUM//'.LILI',NOMPRN),IBID)
      CALL JEVEUO(JEXNUM(NOMNUM//'.ORIG',IBID),'L',LLORS)
      CALL JENONU(JEXNOM(NOMNUM//'.LILI',NOMPRN),IBID)
      CALL JEVEUO(JEXNUM(NOMNUM//'.PRNO',IBID),'L',LLPRS)
C
C   BOUCLE SUR LES ELEMENTS DU LIGREL
C
      DO 10 J=1,NTPRNO
C
C  RECUPERATION NUMERO SOUS-STRUCTURE
        NUSST=ZI(LLORS+J-1)
C RECUPERATION DU PROFIL BLOC ELEMENTAIRE
        IBL1=ZI(LTINBL+(J-1)*2)
C
C POUR UNE MATRICE ELEMENTAIRE BLOC IL FAUDRA FAIRE UNE BOUCLE
C SUR LES BLOC A ASSEMBLER (DO IBLOC=1,NBBL)
C
C RECUPERATION DU NOM DU MACR_ELEM AMONT
        KBID='   '
        CALL MGUTDM(MODGEN,KBID,NUSST,'NOM_MACR_ELEM',IBID,NOMMCL)
        KNOMBL(IBL1)=NOMMCL//ADNOM//'.VALE         '
C
C VERIFICATION DE L'EXISTENCE DE LA MATRICE D'AMORTISSEMENT ASSOCIEE
C AU MACRO-ELEMENT
C
        IF (OPTION.EQ.AMOOPT) THEN
            CALL JEEXIN(KNOMBL(IBL1),IRET)
            IF (IRET.EQ.0) THEN
                CALL UTDEBM('F',PGC,
     &   'MATRICE D''AMORTISSEMENT NON CREEE')
                CALL UTIMPK('L','DANS LE MACRO-ELEMENT : ',1,NOMMCL)
                CALL UTFINM
            ENDIF
        ENDIF
C
        INUMBL(IBL1)=0
        CALL MAXBLO(NOMMCL//ADNOM//'.VALE           ',SSMAX)
C  RECUPERATION DIMENSIONS ET NUMERO PREMIERE EQUATION DANS NUEQ
        NBLIG=ZI(LLPRS+(J-1)*2+1)
        NBCOL=NBLIG
        INUL=ZI(LLPRS+(J-1)*2)
        INUC=INUL
C
C TAILLE BLOC MATRICE PROJETEE (STOCKAGE TRAINGLE SUPERIEUR)
C
        NTAIL=NBCOL*(NBCOL+1)/2
C
        CALL JECROC(JEXNUM(TMNOBL,IBL1))
        CALL JEECRA(JEXNUM(TMNOBL,IBL1),'LONMAX',NTAIL,' ')
        CALL JEVEUO(JEXNUM(TMNOBL,IBL1),'E',LTNOBL)
        CALL JECROC(JEXNUM(TMADBL,IBL1))
        CALL JEECRA(JEXNUM(TMADBL,IBL1),'LONMAX',NTAIL,' ')
        CALL JEVEUO(JEXNUM(TMADBL,IBL1),'E',LTADBL)
C
C     BOUCLES SUR LES TERMES DU BLOC ELEMENTAIRE
C    (TRIANGLE SUPERIEUR SEULEMENT)
        DO 20 LL=1,NBLIG
          DO 30 LC=LL,NBCOL
C    ADRESSE DANS BLOC ELEMENTAIRE
            IAD=(LC-1)*LC/2+LL
C    NUMERO D'EQUATION DU TERME COURANT
            IEQC=ZI(LLNUEQ+(INUC-1)+(LC-1))
C
            ZI(LTNOBL+IAD-1)=ZI(LLIABL+IEQC-1)
            ZI(LTADBL+IAD-1)=ZI(LLADIA+IEQC-1)-(LC-LL)
30        CONTINUE
20      CONTINUE
CALL JEIMPO('MESSAGE',JEXNUM(TMNOBL,IBL1),' ','BIDON')
CALL JEIMPO('MESSAGE',JEXNUM(TMADBL,IBL1),' ','BIDON')
        CALL JELIBE(JEXNUM(TMNOBL,IBL1))
        CALL JELIBE(JEXNUM(TMADBL,IBL1))
10    CONTINUE
      CALL JELIBE(NOMNUM//'.NUEQ')
      CALL JELIBE(NOMSTO//'.ADIA')
      CALL JELIBE(NOMSTO//'.IABL')
C
 9999 CONTINUE
      CALL JEDEMA()
      END
