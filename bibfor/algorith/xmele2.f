      SUBROUTINE XMELE2(NOMA  ,MODELE,DEFICO,LIGREL,NFISS ,
     &                  CHELEM)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 15/10/2012   AUTEUR GENIAUT S.GENIAUT 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT      NONE
      INCLUDE 'jeveux.h'
      CHARACTER*8   NOMA
      CHARACTER*8   MODELE
      INTEGER       NFISS
      CHARACTER*19  CHELEM
      CHARACTER*19  LIGREL
      CHARACTER*24  DEFICO
C
C ----------------------------------------------------------------------
C
C ROUTINE XFEM (METHODE XFEM - CREATION CHAM_ELEM)
C
C CREATION DU CHAM_ELEM PDONCO (DONNEES DU CONTACT)
C
C ----------------------------------------------------------------------
C
C
C IN  NOMA   : NOM DU MAILLAGE
C IN  MODELE : NOM DU MODELE
C IN  NFISS  : NOMBRE TOTAL DE FISSURES
C IN  LIGREL : NOM DU LIGREL DES MAILLES TARDIVES
C IN  CHELEM : NOM DU CHAM_ELEM CREEE
C IN  DEFICO : SD POUR LA DEFINITION DE CONTACT
C
C

C
C
      INTEGER       NBCMP
      PARAMETER     (NBCMP = 11)
      CHARACTER*8   LICMP(NBCMP)
C
      INTEGER       IFM,NIV
      INTEGER       IBID,IAD,IGRP,I,IMA,IFIS,IZONE,XXCONI
      INTEGER       NMAENR,MMINFI,NBMA,JNBSP,JCELD,ISPT,ICMP,IRET
      CHARACTER*8   NOMFIS,K8BID
      INTEGER       JCESL,JCESV,JCESD,JMOFIS
      CHARACTER*24  GRP(3)
      INTEGER       JGRP
      CHARACTER*19  CHELSI
      REAL*8        MMINFR,COEF(NBCMP)
      CHARACTER*19  VALK(2)
      INTEGER       VALI(1)
      INTEGER       JMAIL,ITYELE
      CHARACTER*16  TYPELE,ENR
C
      DATA LICMP    /'RHON','MU','RHOTK','INTEG','COECH',
     &    'COSTCO','COSTFR','COPECO','COPEFR','RELA','CZMFE'/
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
      CALL INFDBG('XFEM',IFM,NIV)
C
C --- AFFICHAGE
C
      IF (NIV.GE.2) THEN
        WRITE (IFM,*) '<XFEM  > CREATION DU CHAM_ELEM PDONCO'
      ENDIF
C
C --- INITIALISATIONS
C
      CHELSI = '&&XMELE2.CES'
C
C --- RECOPIE DU NOMBRE DE SOUS POINTS DE TOPOSE.HEA DANS LE CHAMP NBSP
C
      CALL CELCES(MODELE//'.TOPOSE.HEA','V','&&XMELE2.HEAV')
      CALL JEVEUO('&&XMELE2.HEAV      .CESD','L', JCESD)
      CALL DISMOI('F','NB_MA_MAILLA',NOMA,'MAILLAGE',NBMA,K8BID,IBID)
      CALL WKVECT('&&XMELE2.NBSP' ,'V V I',NBMA,JNBSP)
      DO 10 IMA = 1,NBMA
        ZI(JNBSP-1+IMA) = ZI(JCESD-1+5+4*(IMA-1)+2)
  10  CONTINUE
C

C
C --- CREATION DU CHAM_ELEM_S
C
      CALL CESCRE('V',CHELSI,'ELEM',NOMA,'XCONTAC',NBCMP,LICMP,
     &            -1,ZI(JNBSP),-NBCMP)
C
C --- ACCES AU CHAM_ELEM_S
C
      CALL JEVEUO(CHELSI//'.CESD','L',JCESD)
      CALL JEVEUO(CHELSI//'.CESL','E',JCESL)
      CALL JEVEUO(CHELSI//'.CESV','E',JCESV)
C
C --- ACCES AUX FISSURES
C
      CALL JEVEUO(MODELE//'.FISS','L',JMOFIS)

C --- RECUPERATION DES MAILLES DU MODELE
      CALL JEVEUO(MODELE//'.MAILLE','L',JMAIL)
C
C --- ENRICHISSEMENT DU CHAM_ELEM POUR LA MULTIFISSURATION
C
      DO 110 IFIS = 1,NFISS
C
C --- ACCES FISSURE COURANTE
C
        NOMFIS = ZK8(JMOFIS-1 + IFIS)
C
C --- INFORMATIONS SUR LA FISSURE
C
        GRP(1) = NOMFIS(1:8)//'.MAILFISS.HEAV'
        GRP(2) = NOMFIS(1:8)//'.MAILFISS.CTIP'
        GRP(3) = NOMFIS(1:8)//'.MAILFISS.HECT'
C
C --- ZONE DE CONTACT IZONE CORRESPONDANTE
C
        IZONE  = XXCONI(DEFICO,NOMFIS,'MAIT')
C
C --- CARACTERISTIQUES DU CONTACT POUR LA FISSURE EN COURS
C
        COEF(1) = MMINFR(DEFICO,'COEF_REGU_CONT',IZONE )
        COEF(2) = MMINFR(DEFICO,'COEF_COULOMB'  ,IZONE )
        COEF(3) = MMINFR(DEFICO,'COEF_REGU_FROT',IZONE )
        COEF(4) = MMINFI(DEFICO,'INTEGRATION'   ,IZONE )
        COEF(6) = MMINFR(DEFICO,'COEF_STAB_CONT',IZONE )
        COEF(7) = MMINFR(DEFICO,'COEF_STAB_FROT',IZONE )
        COEF(8) = MMINFR(DEFICO,'COEF_PENA_CONT',IZONE )
        COEF(9) = MMINFR(DEFICO,'COEF_PENA_FROT',IZONE )
        COEF(10)= MMINFR(DEFICO,'RELATION'      ,IZONE )
        COEF(11)= MMINFR(DEFICO,'CZM_FERMETURE' ,IZONE )
CC
        DO 1000 IGRP = 1,3
C
C --- ON COPIE LES CHAMPS CORRESP. AUX ELEM. HEAV, CTIP ET HECT
C
          CALL JEEXIN(GRP(IGRP),IRET)
          IF (IRET.NE.0) THEN
            CALL JEVEUO(GRP(IGRP),'L',JGRP)
            CALL JELIRA(GRP(IGRP),'LONMAX',NMAENR,K8BID)
            DO 120 I = 1,NMAENR
              IMA = ZI(JGRP-1+I)
C
C --- ON NE TRAITE QUE LES MAILLES DE CONTACT
C
              ITYELE=ZI(JMAIL-1+IMA)
              CALL JENUNO(JEXNUM('&CATA.TE.NOMTE',ITYELE),TYPELE)
              CALL TEATTR(TYPELE,'S','XFEM',ENR,IBID)
              IF (ENR(3:3).NE.'C'.AND.ENR(4:4).NE.'C') GOTO 120
C
C --- RECUPERATION DU NUMÉRO DE SOUS POINT ISPT
C
              DO 130 ISPT=1,ZI(JNBSP-1+IMA)
                CALL CESEXI('S',JCESD,JCESL,IMA,1,ISPT,1,IAD)
                IF (IAD.LT.0) GOTO 140
 130          CONTINUE
              CALL ASSERT(.FALSE.)
 140          CONTINUE
C
C --- RECOPIE EFFECTIVE DES CHAMPS
C
              DO 150 ICMP = 1,NBCMP
                CALL CESEXI('S',JCESD,JCESL,IMA,1,ISPT,ICMP,IAD)
                ZL(JCESL-1-IAD) = .TRUE.
                ZR(JCESV-1-IAD) = COEF(ICMP)
 150          CONTINUE
 120       CONTINUE
          ENDIF
 1000    CONTINUE

 110  CONTINUE
C
C --- CONVERSION CHAM_ELEM_S -> CHAM_ELEM
C
      CALL CESCEL(CHELSI,LIGREL,'RIGI_CONT','PDONCO','NON',IBID,'V',
     &            CHELEM,'F',IBID)
C
C --- MENAGE
C
      CALL DETRSD('CHAM_ELEM_S',CHELSI)
      CALL DETRSD('CHAM_ELEM_S','&&XMELE2.HEAV')
      CALL JEDETR('&&XMELE2.NBSP')
C
      CALL JEDEMA()
C
      END
