      SUBROUTINE NMDOME(MODELE,MATE  ,CARELE,LISCHA,RESULT,
     &                  NUORD )
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 02/10/2012   AUTEUR DELMAS J.DELMAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT      NONE
      INTEGER       NUORD
      CHARACTER*8   RESULT
      CHARACTER*19  LISCHA
      CHARACTER*24  MODELE,MATE,CARELE
C
C ----------------------------------------------------------------------
C
C ROUTINE UTILITAIRE
C
C SAISIE ET VERIFICATION DE LA COHERENCE DES DONNEES MECANIQUES
C
C ----------------------------------------------------------------------
C
C
C VAR MODELE  : NOM DU MODELE
C OUT MATE    : NOM DU CHAMP DE MATERIAU CODE
C OUT CARELE  : CARACTERISTIQUES DES POUTRES ET COQUES
C I/O LISCHA  : SD L_CHARGES
C IN  RESULT  : NOM DE LA SD RESULTAT
C IN  NUORD   : NUMERO D'ORDRE
C
C ----------------------------------------------------------------------
C
      INTEGER      IEXCIT,N1,IBID,IRET
      CHARACTER*8  K8BID ,K8BLA
      CHARACTER*8  CARA  ,NOMO  ,MATERI,REPONS
      CHARACTER*16 NOMCMD,TYPESD
      CHARACTER*19 EXCIT
      INTEGER      IARG
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
      CALL GETRES(K8BID,TYPESD,NOMCMD)
C
C --- INITIALISATIONS
C
      IEXCIT = 1
      EXCIT  = ' '
      K8BLA  = ' '
C
C --- LECTURES
C
      IF   (NOMCMD.EQ.'LIRE_RESU') GOTO 500
C
      IF ( (NOMCMD.EQ.'CALC_CHAMP').OR.
     &     (NOMCMD.EQ.'POST_ELEM')) THEN
C
C ------ RECUPERATION DU MODELE, MATERIAU, CARA_ELEM et EXCIT
C        POUR LE NUMERO D'ORDRE NUORDR
C
        CALL RSLESD(RESULT,NUORD ,MODELE(1:8),MATERI,CARELE(1:8),
     &              EXCIT ,IEXCIT)

        IF (MATERI .NE. K8BLA) THEN
          CALL RCMFMC(MATERI , MATE )
        ELSE
          MATE = ' '
        ENDIF
        CARA   = CARELE(1:8)
      ELSE

C ------ LE MODELE

        IF (MODELE.EQ.' ') THEN
          CALL GETVID(' ','MODELE',1,IARG,1,NOMO  ,N1    )
          IF (N1.EQ.0) CALL U2MESS('F','CALCULEL3_50')
          MODELE = NOMO
        END IF

C ------ LE MODELE NE DOIT PAS CONTENIR DE MAILLES TARDIVES POUR OP0070:

         CALL JEEXIN(MODELE(1:8)//'.MODELE    .NEMA',IRET)
         IF (IRET.GT.0) THEN
            IF ((NOMCMD.EQ.'STAT_NON_LINE').OR.
     &          (NOMCMD.EQ.'DYNA_NON_LINE'))
     &      CALL U2MESK('F','CALCULEL3_51',1,NOMCMD)
         END IF

C ------ LE MATERIAU

        MATERI = ' '
        CALL GETVID(' '   ,'CHAM_MATER',1,IARG,1,MATERI,N1    )
        CALL DISMOI('F'   ,'BESOIN_MATER',MODELE,'MODELE',IBID  ,
     &              REPONS,IRET  )
        IF ((N1.EQ.0) .AND. (REPONS(1:3).EQ.'OUI'))
     &    CALL U2MESS('A','CALCULEL3_40')
        IF (N1.NE.0) THEN
          CALL RCMFMC(MATERI,MATE)
        ELSE
          MATE   = ' '
        ENDIF

C ------ LES CARACTERISTIQUES ELEMENTAIRES

        CARA = ' '

        CALL GETVID(' '   ,'CARA_ELEM',1,IARG,1,CARA  ,N1    )
        CALL DISMOI('F'   ,'EXI_RDM'  ,MODELE,'MODELE',IBID,
     &              REPONS,IRET)
        IF ((N1.EQ.0) .AND. (REPONS(1:3).EQ.'OUI')) THEN
          CALL U2MESS('A','CALCULEL3_39')
        ENDIF

        CARELE = CARA
      ENDIF
C
 500  CONTINUE
C
C --- TRAITEMENT DES CHARGES
C
      CALL NMDOCH(LISCHA,IEXCIT,EXCIT )
C
      CALL JEDEMA()
      END
