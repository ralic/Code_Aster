      SUBROUTINE DELALO(MODELE, NUMEDE, MATE, COMREF, COMPOR,
     &               PENCST, CARCRI, VALMOI, DEPDEL, VALPLU, VARDEP,
     &                  LAGDEP, PROELE, RESASS, GRADIA, ENEREL)

C MODIF ALGORITH  DATE 29/09/2006   AUTEUR VABHHTS J.PELLET 
C ======================================================================
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE PBADEL P.BADEL

      IMPLICIT NONE
      CHARACTER*(*) MATE
      CHARACTER*8  MODELE, PROELE
      CHARACTER*19 PENCST
      CHARACTER*24 VALMOI, VALPLU, COMREF, NUMEDE
      CHARACTER*24 DEPDEL, COMPOR, RESASS, ENEREL
      CHARACTER*24 CARCRI, VARDEP, LAGDEP, GRADIA

C ----------------------------------------------------------------------
C  LAGRANGIEN PRIMAL :  RESOLUTION LOCALE
C ----------------------------------------------------------------------
C IN        MODELE  NOM DU MODELE DELOCALISE
C IN        NUMEDE  NUME_DDL
C IN        MATE    CHAMP MATERIAU
C IN        COMREF  VALEURS DE REFERENCE DES VAR. COM.
C IN        COMPOR  COMPORTEMENT MECANIQUE
C IN        PENCST  CARTE DE PENALISATION DU LAGRANGIEN AUGMENTE
C IN        CARCRI  CRITERES POUR LOIS DE COMPORTEMENT
C IN        VALMOI  VARIABLES EN T-
C IN        DEPDEL  CHAM_NO INCREMENT DE DEPLACEMENT
C IN/JXVAR  VALPLU  VARIABLES EN T+
C                     JXOUT POUR VARPLU (INCLUS DANS VALPLU)
C IN        VARDEP  CHAMP    NON LOCAL EN T+
C IN        LAGDEP  LAGRANGE NON LOCAL (/LC SUR GRAD) EN T+
C IN/JXOUT  PROELE  MATRICES ELEMENTAIRES DU PROJECTEUR
C IN/JXOUT  RESASS  RESIDU ASSEMBLE
C IN/JXOUT  GRADIA  - GRADIENT DE L'ENERGIE ( /LC SUR GRAD )
C                     SIGNE OPPOSE AFIN DE MINIMISER ET NON MAXIMISER
C IN/JXOUT  ENEREL  CHAM_ELEM ENERGIE (OPPOSE DU LAGRANGIEN AUGMENTE)
C ----------------------------------------------------------------------
C -------------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ----------------

      CHARACTER*32 JEXNUM
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)

C -------------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ----------------

      LOGICAL EXIGEO
      INTEGER IRET,JVECR,JVECM,IBID
      INTEGER NCHIN,NCHOUT
      CHARACTER*8 LPAIN(15),LPAOUT(5),K8BID,RESELE,MAILLA
      CHARACTER*16 OPTION
      CHARACTER*19 KBID, COMPNB
      CHARACTER*24 VARMOI,COMMOI,TEMMOI,SIGMOI,DEPMOI
      CHARACTER*24 VARPLU,COMPLU,TEMPLU
      CHARACTER*24 CHGEOM,LIGREL,LCHIN(15),LCHOUT(5)
      CHARACTER*24 K24BID,VARMOJ,VARPLJ,CHTREF
      COMPLEX*16 CBID

      DATA COMPNB/'&&DELALO.COMPNB'/
      DATA VARMOJ/'&&DELALO.VARMOJ'/
      DATA VARPLJ/'&&DELALO.VARPLJ'/


C    INITIALISATION
      CALL JEMARQ()
      RESELE = '&&REDEEL'
      OPTION = 'DECO_LOCA'

      CALL DESAGG(VALMOI,DEPMOI,SIGMOI,VARMOI,COMMOI,
     &                   K24BID,K24BID,K24BID,K24BID)
      CALL DESAGG(VALPLU,K24BID,K24BID,VARPLU,COMPLU,
     &                   K24BID,K24BID,K24BID,K24BID)

      CALL NMVCEX('TEMP',COMMOI,TEMMOI)
      CALL NMVCEX('TEMP',COMPLU,TEMPLU)
      CALL NMVCEX('TEMP',COMREF,CHTREF)


C    CREATION (SI NECESSAIRE) DE LA STRUCTURE RESELE (VECT_ELEM)
C    UN SEUL RESU_ELEM DANS LA LISTE : &&REDEEL.RE0001

      CALL JEEXIN(RESELE // '.LISTE_RESU',IRET)
      IF (IRET.EQ.0) THEN
        CALL MEMARE('V',RESELE,MODELE(1:8),' ',' ',' ')
        CALL WKVECT(RESELE // '.LISTE_RESU','V V K24',1,JVECR)
        ZK24(JVECR) = RESELE // '.RE0001'
      ELSE
        CALL JEVEUO(RESELE // '.LISTE_RESU','E',JVECR)
      END IF
      CALL JEECRA(RESELE // '.LISTE_RESU','LONUTI',1,KBID(1:8))


C    CREATION (SI NECESSAIRE) DE LA STRUCTURE PROELE (MATR_ELEM)
C    UN SEUL RESU_ELEM DANS LA LISTE : &&PRDEEL.ME0001

      CALL JEEXIN(PROELE // '.LISTE_RESU', IRET)
      IF (IRET.EQ.0) THEN
        CALL MEMARE('V',PROELE,MODELE(1:8),' ',' ',' ')
        CALL WKVECT(PROELE // '.LISTE_RESU','V V K24',1,JVECM)
        ZK24(JVECM) = PROELE // '.ME0001'
      ELSE
        CALL JEVEUO(PROELE // '.LISTE_RESU','E',JVECM)
      END IF
      CALL JEECRA(PROELE // '.LISTE_RESU','LONUTI',1,K8BID)


C    DETERMINATION DU LIGREL ET DE LA GEOMETRIE
      LIGREL = MODELE//'.MODELE'
      CALL MEGEOM(MODELE,K8BID,EXIGEO,CHGEOM)
      IF (.NOT.EXIGEO) CALL U2MESS('F','ALGORITH2_71')


C    VARIABLES INTERNES ISSUES DE L'ITERATION PRECEDENTE
      CALL COPISD('CHAMP_GD','V',VARPLU(1:19),VARMOJ(1:19))


C    CALCUL DE L'OPTION
      NCHIN = 15
      LPAIN(1) = 'PGEOMER'
      LCHIN(1) = CHGEOM
      LPAIN(2) = 'PMATERC'
      LCHIN(2) = MATE
      LPAIN(3) = 'PTEREF'
      LCHIN(3) = CHTREF
      LPAIN(4) = 'PTEMPMR'
      LCHIN(4) = TEMMOI
      LPAIN(5) = 'PTEMPPR'
      LCHIN(5) = TEMPLU
      LPAIN(6) = 'PDEPLMR'
      LCHIN(6) = DEPMOI
      LPAIN(7) = 'PCONTMR'
      LCHIN(7) = SIGMOI
      LPAIN(8) = 'PDEPLDR'
      LCHIN(8) = DEPDEL
      LPAIN(9) = 'PCOMPOR'
      LCHIN(9) = COMPOR
      LPAIN(10) = 'PCARCRI'
      LCHIN(10) = CARCRI
      LPAIN(11) = 'PPENCST'
      LCHIN(11) = PENCST
      LPAIN(12) = 'PVARIMR'
      LCHIN(12) = VARMOI
      LPAIN(13) = 'PVARDEP'
      LCHIN(13) = VARDEP
      LPAIN(14) = 'PLAGDEP'
      LCHIN(14) = LAGDEP
      LPAIN(15) = 'PVARIMP'
      LCHIN(15) = VARMOJ

      NCHOUT = 5
      LPAOUT(1) = 'PPROJEC'
      LCHOUT(1) =  ZK24(JVECM)
      LPAOUT(2) = 'PRESIDU'
      LCHOUT(2) =  ZK24(JVECR)
      LPAOUT(3) = 'PGRADIA'
      LCHOUT(3) = GRADIA
      LPAOUT(4) = 'PENEREL'
      LCHOUT(4) = ENEREL
      LPAOUT(5) = 'PVARIPR'
      LCHOUT(5) = VARPLJ


C     CREATION DU CHAM_ELEM_S POUR ETENDRE LE CHAM_ELEM DE VARI_R:
      CALL EXISD('CHAM_ELEM_S',COMPNB,IRET)
      IF (IRET.EQ.0) CALL CESVAR(' ',COMPOR,LIGREL,COMPNB)
      CALL COPISD('CHAM_ELEM_S','V',COMPNB,VARPLJ)


      CALL CALCUL('S',OPTION,LIGREL,NCHIN,LCHIN,LPAIN,NCHOUT,
     &            LCHOUT,LPAOUT,'V')


C    PROJECTION DE VARPLJ SUR LE LIGREL DE MECANIQUE
      CALL COPISD('CHAMP_GD','V',VARPLJ,VARPLU)


C    ASSEMBLAGE DU RESIDU
      CALL ASSVEC('V',RESASS,1,RESELE,1.D0,NUMEDE,' ','ZERO',1)



      CALL JEDEMA()
      END
