      SUBROUTINE EDRONL (NDIM,IMATE,CARCRI,TP, 
     &                    DF,VIM,RLAG,CHAMP,LAGR,
     &                    PONDER,VIP,DVIDA,ENER)
     
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 16/12/2004   AUTEUR VABHHTS J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2003  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE ADBHHVV V.CANO

      IMPLICIT NONE
      INTEGER            NDIM,IMATE
      REAL*8             CARCRI(3)
      REAL*8             TP,DF(3,3),VIM(12),RLAG,CHAMP(0:NDIM)
      REAL*8             LAGR(0: NDIM),PONDER(0:NDIM)
      REAL*8             VIP(12),DVIDA(0:3,0:3),ENER

C.......................................................................
C       INTEGRATION DE LA LOI DE ROUSSELIER EN NON LOCAL
C   EN GRANDES DEFORMATIONS DE TYPE NOUVELLE FORMULATION DE SIMO-MIEHE
C.......................................................................
C IN  NDIM    : DIMENSION DE L'ESPACE
C IN  IMATE   : ADRESSE DU MATERIAU CODE
C IN  T  P    : TEMPERATURE A L'INSTANT DU CALCUL
C IN  DF      : INCREMENT DU GRADIENT DE LA TRANSFORMATION
C IN  VIM     : VARIABLES INTERNES A L INSTANT DU CALCUL PRECEDENT
C                 VIM(1)   = P (DEFORMATION PLASTIQUE CUMULEE)
C                 VIM(2:4) =  GRADIENT DE P
C                 VIM(5)   = POROSITE
C                 VIM(6:11)= DEFORMATION ELASTIQUE EULERIENNE
C		                EE = (ID-BE)/2)
C                 VIM(12) = INDICE DE PLASTICITE
C                         = 0 ELASTIQUE
C                         = 1 PLASTIQUE
C                         = 2 SOLUTION PARTICULIERE
C IN RLAG    : PARAMETRE DE PENALISATION DU LAGRANGIEN AUGMENTE
C IN CHAMP   : CHAMPS AUX NOEUDS
C  P0 = B0*P ET P-GRADIENT=B-GRADIENT*P-P VECTEUR DU CHAMPS AUX NOEUDS
C IN LAGR    : MULTIPLICATEURS DE LAGRANGE : MU-0 ET MU-GRADIENT

C OUT PONDER : COEFFICIENTS DE PONDERATION
C OUT VIP    : VARIABLES INTERNES A L'INSTANT ACTUEL
C OUT DVIDA  : DERIVEE DES VARIABLES INTERNES VIM(1:4) 
C              PAR RAPPORT AU CHAMP
C OUT ENER   : ENERGIE = ENERGIE LIBRE + POTENTIEL DE DISSIPATION
C
C          ATTENTION LES TENSEURS ET MATRICES SONT RANGES DANS
C          L'ORDRE :  XX,YY,ZZ,XY,XZ,YZ
C.......................................................................

      INTEGER ITEMAX, JPROLP, JVALEP, NBVALP
      REAL*8  YOUNG,NU,MU,K,SIGY
      REAL*8  SIG1,D,F0,FCR,ACCE
      REAL*8  FONC,EQETR,PM,RPM,PENTEM,PENTE,PREC
      REAL*8  C1,C2,C3,C4(3),LB,LC
      COMMON /EDROU/ YOUNG,NU,MU,K,SIGY,
     &               SIG1,D,F0,FCR,ACCE,
     &               FONC,EQETR,PM,RPM,PENTEM,PENTE,PREC,
     &               C1,C2,C3,C4,LB,LC,
     &               ITEMAX, JPROLP, JVALEP, NBVALP

      INTEGER        I,INDICE
      CHARACTER*16  OPTION
      REAL*8  INFINI,R8GAEM,PETIT,R8PREM
      REAL*8  R8BID,AIRE
      REAL*8  EM(6),ETR(6),TRETR,DVETR(6),DETRDF(6,3,3),TRE
      REAL*8  DVE(6),NDVE,NPGR
      REAL*8  Y,YM,X,SEUIL,DSEUIL,S,DS,DP
      REAL*8  KR(6),PDTSCA(6)
      REAL*8  EDROY1, EDROY2, EDROYI
      REAL*8  L1VERI
      DATA        KR/1.D0,1.D0,1.D0,0.D0,0.D0,0.D0/
      DATA        PDTSCA/1.D0,1.D0,1.D0,2.D0,2.D0,2.D0/
     
C 1 - RECUPERATION DES CARACTERISTIQUES DU MATERIAU 
C     ET DES CARACTERISTIQUES DE L INTEGRATION

      ITEMAX = NINT(CARCRI(1))
      PREC   = CARCRI(3)
      PM     = VIM(1)
      CALL EDROMA(IMATE,TP)
            
      IF (VIM(5) .LT. F0)  VIM(5) = F0
      
C 2 - CALCUL DE N = DIAG D2PHI/DP      

      PONDER(0)=PENTEM
      DO 10 I=1,NDIM
       PONDER(I)=(LC**2)*PENTEM
  10  CONTINUE

C 3 - CALCUL DES COEFFICIENTS C1 C2 C3 C4 ET RPM=RPM+C1*PM-C2
      
      CALL EDROPC(NDIM,RLAG,CHAMP,LAGR) 
            
C 4 - CALCUL CONCERNANT LA DEFORMATION ELASTIQUE ETR 
C 4.1 - APPEL DE LA ROUTINE CLETDR QUI CALCULE 
C       ETR(IJ)=(ID(IJ)-DF(IK)*DF(JK))/2+DF(IK)*EM(KP)*DF(JP) 
C       ET LA DERIVE DE ETR PAR RAPPORT A DF

      CALL DCOPY(6, VIM(6),1, EM, 1)
      OPTION='RAPH_MECA'
      CALL CLETDR (DF,EM,OPTION,ETR,DETRDF)
                    
C 4.2 - CALCUL DE LA TRACE - DEVIATEUR ET EQUIVALENT DE ETR         
      
      TRETR=ETR(1)+ETR(2)+ETR(3)
      EQETR=0.D0
      DO 20 I=1,6
       DVETR(I)=ETR(I)-KR(I)*TRETR/3.D0
       EQETR=EQETR+PDTSCA(I)*(DVETR(I)**2.D0)
 20   CONTINUE
      EQETR=SQRT(1.5D0*EQETR)

C 6 - CALCUL DE LA FONCTION FONC ET TEST
      
      INFINI = R8GAEM()
      PETIT  = R8PREM()
      FONC = D*VIM(5)*EXP(-K*TRETR/SIG1)
      IF ((FONC.GE.INFINI).OR.(FONC.LE.PETIT)) THEN    
       CALL UTMESS('F','EDRONL',
     &   'LE MODELE EXPLOSE - DECOUPER VOTRE PAS DE TEMPS')
      ENDIF
            
C 7 - INTEGRATION DE LA LOI DE COMPORTEMENT 
C     PAR METHODE DE NEWTON AVEC BORNES CONTROLEES ET DICHOTOMIE
C  RESOLUTION DES EQATIONS:
C  - SI SEUIL(0)<0  => LA SOLUTION EST ELASTIQUE SINON
C  - SI S(0)>0  => LA SOLUTION EST PLASTIQUE ET REGULIERE
C                   ON RESOUD SEUIL(Y)=0 YS=0
C  - SI S(0)<0  => ON RESOUD S(YS)=0 
C                  YS EST SOLUTION SINGULIERE SI DP>2*EQEQETR/3
C  - SINON ON RESOUD SEUIL(Y)=0 POUR Y>YS   
C  AVEC SEUIL(Y)=2*MU*EQETR-S(Y)-3*MU*DP(Y)  
C       Y       = K*X/SIG1
C       X       = TRE-TRETR
C       AGR(I)  =-C3*PGR(I)+C4(I)
C       DP      = (Y*SIG1/K)*EXP(Y)/FONC
C       S(Y)    = -SIG1*FONC*EXP(-Y)+R(PM+DP)+C1*(PM+DP)-C2
     
C 7.1 - SOLUTION ELASTIQUE ?
       
      Y = 0
      CALL EDROFG (Y, DP, S, SEUIL, DSEUIL)
      IF (SEUIL.LT.0.D0) THEN
       INDICE=0
       VIP(12)=0.D0
       GOTO 600
      ENDIF 
                    
C 7.4 - RESOLUTION SEUIL(Y)=0 QUAND S(0)>0
C       CALCUL DE Y PUIS DU DP CORRESPONDANT
      
      IF (S .GT. 0) THEN
       Y = EDROY1()
       CALL EDROFG (Y, DP, S, SEUIL, DSEUIL)
       INDICE = 1
       VIP(12)=1.D0
       GOTO 600
      END IF

C 7.5 - EXAMEN DE LA SOLUTION SINGULIERE ( S(0)<0 )
C 7.5.1 - RESOLUTION S(Y)=0
C         CALCUL DE Y PUIS DU DP CORRESPONDANT     

      Y = EDROYI()
      CALL EDROFG (Y, DP, S, SEUIL, DSEUIL)

C 7.5.2 - CONDITION POUR SOLUTION SINGULIERE        
       
      IF (2.D0*EQETR/3.D0-DP .LE. 0.D0 ) THEN
       INDICE=2
       VIP(12)=2.D0
       GOTO 600
      END IF

      YM = Y

C 7.6 - RESOLUTION SEUIL(Y)=0 QUAND S(0)<0 : ON A S(YM)=0
C       CALCUL DE Y PUIS DU DP CORRESPONDANT

      Y = EDROY2(YM)
      CALL EDROFG (Y, DP, S, SEUIL, DSEUIL)
      INDICE=1
      VIP(12)=1.D0
       
 600  CONTINUE

C FIN DE LA RESOLUTION
             
C 9 - CALCUL DE LA SOLUTION
              
      X      = SIG1*Y/K
      VIP(1) = PM+DP
      DO 30 I=1,3
       VIP(I+1)=C4(I)/C3
 30   CONTINUE      
      VIP(5) = 1.D0-(1.D0-VIM(5))*EXP(-X)
      IF (VIP(5).GE.FCR) THEN
       VIP(5) = 1.D0-(1.D0-VIM(5))*EXP(-ACCE*X)
      ENDIF

C 9.1 - SOLUTION ELASTIQUE OU PLASTIQUE

      IF ((INDICE.EQ.0).OR.(INDICE.EQ.1)) THEN
       DO 40 I=1,6
        VIP(5+I)=DVETR(I)*(1.D0-3.D0*DP/(2.D0*EQETR))
     &         +KR(I)*(X+TRETR)/3.D0
 40    CONTINUE       
      ELSE
      
C 9.2 - SOLUTION SINGULIERE
      
       DO 50 I=1,6
        VIP(5+I)=KR(I)*(X+TRETR)/3.D0
 50    CONTINUE       
      ENDIF
       
C VERIFICATION DE LA BONNE RESOLUTION
C ON COMPARE DP A L

      L1VERI=(2.D0*MU*EQETR-S)/(3.D0*MU)      
      IF (L1VERI.LE.0.D0) L1VERI=0.D0      
      
C 10 - CALCUL DE L ENERGIE

      TRE = X + TRETR
      NDVE = 0.D0
      DO 110 I = 1,6
       DVE(I)=VIP(5+I)-TRE*KR(I)/3.D0
       NDVE = NDVE + PDTSCA(I)*(DVE(I)**2)
 110  CONTINUE
      NPGR=(VIP(2)**2)+(VIP(3)**2)+(VIP(4)**2)
  
      CALL RCFONC('V','TRACTION',JPROLP,JVALEP,NBVALP,R8BID,
     &           R8BID,R8BID,VIP(1),R8BID,PENTE,AIRE,R8BID,R8BID)
      ENER = MU*NDVE+K*(TRE**2)/2.D0+AIRE
     &    +PENTEM*(LC**2)*NPGR/2.D0 - SIGY*VIM(1)
      IF(VIP(12).NE.0.D0) ENER = ENER+SIG1*X*(LOG(X/(D*VIM(5)*DP))-1.D0)
                   
C 11 - CALCUL DE LA MATRICE TANGENTE
      
      CALL EDROTG (VIP,X,RLAG,DVIDA)
           
 999  CONTINUE      
      END
