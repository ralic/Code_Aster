        SUBROUTINE LCMMON( FAMI,KPG,KSP,COMP,NBCOMM,CPMONO,NMAT,NVI,
     &                     VINI,X,  DTIME, PGL,MOD,COEFT,
     &                     NEPS,EPSD, DETOT,COEL,DVIN,
     &                     NFS,NSG,TOUTMS,HSR,ITMAX,TOLER,IRET )
        IMPLICIT NONE
        INTEGER KPG,KSP,NMAT,NBCOMM(NMAT,3),NVI,ITMAX,IRET,NFS,NSG,NEPS
        REAL*8 VINI(*),DVIN(*),X,DTIME,COEFT(NMAT),COEL(NMAT)
        REAL*8 SIGI(6),EPSD(NEPS),DETOT(NEPS),PGL(3,3),TOLER
        REAL*8 HSR(NSG,NSG,1)
        CHARACTER*(*) FAMI
        CHARACTER*16 COMP(*)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 19/12/2012   AUTEUR PELLET J.PELLET 
C RESPONSABLE PROIX J.M.PROIX
C TOLE CRP_21
C TOLE CRS_1404
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C TOLE CRP_21
C ======================================================================
C       IN FAMI     :  FAMILLE DE POINT DE GAUSS (RIGI,MASS,...)
C         KPG,KSP   :  NUMERO DU (SOUS)POINT DE GAUSS
C          COMP     :  NOM DU MODELE DE COMPORTEMENT
C           MOD     :  TYPE DE MODELISATION
C           IMAT    :  ADRESSE DU MATERIAU CODE
C         NBCOMM :  NOMBRE DE COEF MATERIAU PAR FAMILLE
C         CPMONO :  NOMS DES LOIS MATERIAU PAR FAMILLE
C           PGL   : MATRICE DE PASSAGE GLOBAL LOCAL
C           NVI     :  NOMBRE DE VARIABLES INTERNES
C           VINI    :  VARIABLES INTERNES A T
C           X       :  INTERVALE DE TEMPS ADAPTATIF
C           DTIME   :  INTERVALE DE TEMPS
C              :  COEFFICIENTS MATERIAU INELASTIQUE A T
C           EPSD    :  DEFORMATION TOTALE A T
C           DETOT   :  INCREMENT DE DEFORMATION TOTALE
C     OUT:
C           DVIN    :  DERIVEES DES VARIABLES INTERNES A T
C INTEGRATION DES LOIS MONOCRISTALLINES PAR UNE METHODE DE RUNGE KUTTA
C
C     CETTE ROUTINE FOURNIT LA DERIVEE DE L ENSEMBLE DES VARIABLES
C     INTERNES DU MODELE
C
C     ------------------------------------------------------------------
      CHARACTER*8 MOD
      CHARACTER*16 NOMFAM
      CHARACTER*24 CPMONO(5*NMAT+1)
      REAL*8 DDOT,DT,DY(6+NSG),EXPBP(NSG),CRIT,SGNS,Q(3,3),EVI(6)
      REAL*8 DEVI(6),MUS(6),NG(3),TAUS,DGAMMA,DALPHA,DP,RP,YD(6+NSG)
      REAL*8 FKOOH(6,6),MATERF(NMAT*2),MSNS(3,3),GAMSNS(3,3),LG(3)
      REAL*8 TOUTMS(NFS,NSG,6),FP(3,3),FP1(3,3),DEPS(6),DEPSDT
      INTEGER ITENS,NBFSYS,I,NUVI,IFA,NBSYS,IS,NSFA,NSFV
      COMMON /DEPS6/DEPSDT
      INTEGER IRR,DECIRR,NBSYST,DECAL,GDEF
      COMMON/POLYCR/IRR,DECIRR,NBSYST,DECAL,GDEF
C     ------------------------------------------------------------------
C --  VARIABLES INTERNES
C
      CALL R8INIR(9,0.D0,GAMSNS,1)
      CALL R8INIR(NSG,0.D0,DY,1)
      DO 5 ITENS=1,6
        EVI(ITENS) = VINI(ITENS)
        DEVI(ITENS) = 0.D0
    5 CONTINUE
 
      CALL DCOPY(NMAT,COEFT,1,MATERF(NMAT+1),1)
      CALL DCOPY(NMAT,COEL,1,MATERF(1),1)

C     CALCUL DU NOMBRE TOTAL DE SYSTEMES DE GLISSEMENT
      NBFSYS=NBCOMM(NMAT,2)
      NBSYST=0
      DO 10 IFA=1,NBFSYS      
         NOMFAM=CPMONO(5*(IFA-1)+1)
         CALL LCMMSG(NOMFAM,NBSYS,0,PGL,MUS,NG,LG,0,Q)
         NBSYST=NBSYST+NBSYS
   10 CONTINUE

      IF (COEFT(NBCOMM(1,1)).GE.4) THEN
C         KOCKS-RAUCH ET DD_CFC : VARIABLE PRINCIPALE=DENSITE DISLOC
          CALL ASSERT(NBCOMM(NMAT,2).EQ.1)
          DO 102 I=1,NBSYST
             YD(6+I)=VINI(6+3*(I-1)+1)
 102      CONTINUE
      ELSE
C        AUTRES COMPORTEMENTS MONOCRISTALLINS
         DO 103 I=1,NBSYST
            YD(6+I)=VINI(6+3*(I-1)+2)
 103     CONTINUE
      ENDIF
      

C     INVERSE DE L'OPERATEUR D'ELASTICITE DE HOOKE
      IF (COEL(NMAT).EQ.0) THEN
         CALL LCOPIL('ISOTROPE',MOD,COEL,FKOOH)
      ELSEIF (COEL(NMAT).EQ.1) THEN
         CALL LCOPIL('ORTHOTRO',MOD,COEL,FKOOH)
      ENDIF
      
      IF (GDEF.EQ.1) THEN
         CALL LCRKSG(COMP,NVI,VINI,EPSD,DETOT,NMAT,COEL,SIGI)
         CALL LCGRLA (DETOT,DEPS)
         CALL DSCAL(3,SQRT(2.D0),DEPS(4),1)
      ELSE
         CALL CALSIG(FAMI,KPG,KSP,EVI,MOD,COMP,VINI,X,DTIME,EPSD,
     &              DETOT,NMAT,COEL,SIGI)
         CALL DCOPY(6,DETOT,1,DEPS,1)
      ENDIF
      DEPSDT=SQRT(DDOT(6,DEPS,1,DEPS,1)/1.5D0)/DTIME
      NBFSYS=NBCOMM(NMAT,2)

      NUVI=6
C     NSFV : debut de la famille IFA dans les variables internes
      NSFV=6
C     NSFA : debut de la famille IFA dans DY et YD, YF
      NSFA=6

      DO 6 IFA=1,NBFSYS
      
         NOMFAM=CPMONO(5*(IFA-1)+1)

         CALL LCMMSG(NOMFAM,NBSYS,0,PGL,MUS,NG,LG,0,Q)

         DO 7 IS=1,NBSYS

C           CALCUL DE LA SCISSION REDUITE =
C           PROJECTION DE SIG SUR LE SYSTEME DE GLISSEMENT
C           TAU      : SCISSION REDUITE TAU=SIG:MUS

            CALL CALTAU(COMP,IFA,IS,SIGI,FKOOH,NFS,NSG,TOUTMS,
     &                  TAUS,MUS,MSNS)
     
C           CALCUL DE L'ECOULEMENT SUIVANT LE COMPORTEMENT  
C           ECOULEMENT VISCOPLASTIQUE:
C           ROUTINE COMMUNE A L'IMPLICITE (PLASTI-LCPLNL)
C           ET L'EXPLICITE (NMVPRK-GERPAS-RK21CO-RDIF01)
C           CAS IMPLICITE : IL FAUT PRENDRE EN COMPTE DTIME
C           CAS EXPLICITE : IL NE LE FAUT PAS (ON CALCULE DES VITESSES)
C           D'OU :
            DT=-1.D0
          
            CALL LCMMLC(NMAT,NBCOMM,CPMONO,NFS,NSG,HSR,NSFV,NSFA,IFA,
     &      NBSYS,IS,DT,NVI,VINI,YD,DY,ITMAX,TOLER,MATERF,EXPBP,TAUS,
     &          DALPHA,DGAMMA,DP,CRIT,SGNS,RP,IRET)

            IF (IRET.GT.0) THEN
               GOTO 9999
            ENDIF
            
            NUVI=NUVI+3

            DVIN(NUVI-2)=DALPHA
            DVIN(NUVI-1)=DGAMMA
            DVIN(NUVI  )=DP
            
            IF (GDEF.EQ.0) THEN
               CALL DAXPY(6,DGAMMA,MUS,1,DEVI,1)
            ELSE
               CALL DAXPY(9,DGAMMA,MSNS,1,GAMSNS,1)
            ENDIF
  7     CONTINUE

        NSFA=NSFA+NBSYS
        NSFV=NSFV+NBSYS*3

  6   CONTINUE
C
C --    DERIVEES DES VARIABLES INTERNES
C
      DO 30 ITENS=1,6
        DVIN(ITENS)= DEVI(ITENS)
   30 CONTINUE
      IF (GDEF.EQ.1) THEN
         CALL DCOPY(9,VINI(NVI-3-18+1 ),1,FP,1)
         CALL PMAT(3,GAMSNS,FP,FP1)      
         CALL DCOPY(9,FP1,1,DVIN(NVI-3-18+1 ),1)
      ENDIF

 9999 CONTINUE
      END
