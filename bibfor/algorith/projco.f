      SUBROUTINE PROJCO(NOMA,NEWGEO,INST,DEFICO,RESOCO)
C ======================================================================
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 07/10/2004   AUTEUR MABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C
      IMPLICIT     NONE
      CHARACTER*8  NOMA
      REAL*8       INST
      CHARACTER*24 NEWGEO
      CHARACTER*24 DEFICO
      CHARACTER*24 RESOCO
C
C ----------------------------------------------------------------------
C ROUTINE APPELEE PAR : CFGEOM
C ----------------------------------------------------------------------
C
C CALCUL DES COEFFICIENTS DE LA RELATION DE NON-INTERPENETRATION.
C LA PROJECTION N'EST EFFECTUEE QUE SI L'APPARIEMENT NE VIENT PAS
C D'ETRE REACTUALISE (SINON PROJEC A DEJA ETE APPELE PAR RECHCO).
C
C IN  NOMA   : NOM DU MAILLAGE
C IN  NEWGEO : GEOMETRIE ACTUALISEE EN TENANT COMPTE DU CHAMP DE
C              DEPLACEMENTS COURANT
C IN  INST   : VALEUR DE L'INSTANT DE CALCUL
C IN  DEFICO : SD DE DEFINITION DU CONTACT (ISSUE D'AFFE_CHAR_MECA)
C VAR RESOCO : SD DE TRAITEMENT NUMERIQUE DU CONTACT
C
C -------------- DEBUT DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER            ZI
      COMMON  / IVARJE / ZI(1)
      REAL*8             ZR
      COMMON  / RVARJE / ZR(1)
      COMPLEX*16         ZC
      COMMON  / CVARJE / ZC(1)
      LOGICAL            ZL
      COMMON  / LVARJE / ZL(1)
      CHARACTER*8        ZK8
      CHARACTER*16                ZK16
      CHARACTER*24                          ZK24
      CHARACTER*32                                    ZK32
      CHARACTER*80                                              ZK80
      COMMON  / KVARJE / ZK8(1) , ZK16(1) , ZK24(1) , ZK32(1) , ZK80(1)
C
C ---------------- FIN DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER      ZAPPAR
      PARAMETER    (ZAPPAR=3)
      LOGICAL      MULNOR
      INTEGER      REACTU,REAPRO
      INTEGER      IFM,NIV
      INTEGER      IZONE,IESCL,IBID
      INTEGER      NDIM,NESCL,NBNO,NBDDL,POSNO
      INTEGER      POSNOE,POSMA
      INTEGER      JDECAL
      INTEGER      JCOOR
      REAL*8       JEU,CMULT,RBID,R6BID(6),OLDJEU,DIST3
      CHARACTER*8  NOMPAR(3)
      CHARACTER*24 APPARI,APPOIN,APJEU,APCOEF,APDDL
      INTEGER      JAPPAR,JAPPTR,JAPJEU,JAPCOE,JAPDDL
      CHARACTER*24 NOZOCO,COEFCO,CHAMCO,CONTNO,NDIMCO
      INTEGER      JZOCO,JCOEF,JCHAM,JNOCO,JDIM
      CHARACTER*24 JEUSUP,JEUFO1,JEUFO2,JEUFO3,NORMCO
      INTEGER      JJSUP,JJFO1,JJFO2,JJFO3,JNORMO
C
C ----------------------------------------------------------------------
C
      CALL INFNIV (IFM,NIV)
      CALL JEMARQ ()
C
C --- SD POUR LE CONTACT POTENTIEL
C
      CONTNO = DEFICO(1:16)//'.NOEUCO'
      NDIMCO = DEFICO(1:16)//'.NDIMCO'
      NOZOCO = DEFICO(1:16)//'.NOZOCO'
      COEFCO = DEFICO(1:16)//'.COEFCO'
      CHAMCO = DEFICO(1:16)//'.CHAMCO'
      JEUSUP = DEFICO(1:16)//'.JSUPCO'
      JEUFO1 = DEFICO(1:16)//'.JFO1CO'
      JEUFO2 = DEFICO(1:16)//'.JFO2CO'
      JEUFO3 = DEFICO(1:16)//'.JFO3CO'
C
      CALL JEVEUO (CONTNO,'L',JNOCO)
      CALL JEVEUO (NOZOCO,'L',JZOCO)
      CALL JEVEUO (COEFCO,'L',JCOEF)
      CALL JEVEUO (CHAMCO,'L',JCHAM)
      CALL JEVEUO (JEUSUP,'E',JJSUP)
      CALL JEVEUO (JEUFO1,'L',JJFO1)
      CALL JEVEUO (JEUFO2,'L',JJFO2)
      CALL JEVEUO (JEUFO3,'L',JJFO3)
C
C --- SD POUR LE CONTACT EFFECTIF
C
      APPARI = RESOCO(1:14)//'.APPARI'
      APPOIN = RESOCO(1:14)//'.APPOIN'
      APJEU  = RESOCO(1:14)//'.APJEU'
      APCOEF = RESOCO(1:14)//'.APCOEF'
      APDDL  = RESOCO(1:14)//'.APDDL'
      NORMCO = RESOCO(1:14)//'.NORMCO'
C
      CALL JEVEUO (APPARI,'L',JAPPAR)
      CALL JEVEUO (NORMCO,'L',JNORMO)
      CALL JEVEUO (APPOIN,'L',JAPPTR)
      CALL JEVEUO (APJEU, 'E',JAPJEU)
      CALL JEVEUO (APCOEF,'E',JAPCOE)
      CALL JEVEUO (APDDL, 'E',JAPDDL)
C
C --- NOMBRE DE NOEUDS ESCLAVES
C
      NESCL = ZI(JAPPAR)
C
C --- DIMENSION DE L'ESPACE (2 OU 3)
C
      CALL JEVEUO (NDIMCO, 'L',JDIM)
      NDIM = ZI(JDIM)
C
C --- COORDONNEES DES NOEUDS DU MAILLAGE
C
      CALL JEVEUO (NEWGEO(1:19)//'.VALE','L',JCOOR)
C
C --- BOUCLE SUR LES NOEUDS ESCLAVES, PROJECTION SUR LA MAILLE MAITRE
C --- ET CALCUL DU JEU COURANT
C
      DO 10 IESCL = 1,NESCL
C
        POSNOE = ZI(JAPPAR+ZAPPAR*(IESCL-1)+1)
        POSMA  = ZI(JAPPAR+ZAPPAR*(IESCL-1)+2)
        REACTU = ZI(JAPPAR+ZAPPAR*(IESCL-1)+3)
        REAPRO = ABS(REACTU)
        IZONE  = ZI(JZOCO+POSNOE-1)
        CMULT  = ZR(JCOEF+IZONE-1)
        MULNOR = (ABS(ZI(JCHAM+IZONE-1)).EQ.1)
C
C --- CALCUL DU JEU FICTIF DE LA ZONE
C        
        DIST3 = 0.D0
        CALL CFDIST(IZONE,POSNOE,INST,
     &              JNOCO,JCOOR,JJFO1,JJFO2,JJFO3,JJSUP,DIST3)
C
C --- PROJECTION (DETERMINATION DES NOEUDS MAITRES, DES DDLS DE LA
C --- RELATION, CALCUL DES COEFFICIENTS, DE LA NORMALE, DU JEU)
C --- NB : ON AJOUTE AU JEU LE TABLEAU JEUSUP, JEU INITIAL "FICTIF"
C
        IF (REACTU.NE.0) THEN
          JDECAL = ZI(JAPPTR+IESCL-1)
          CALL PROJEC(IZONE,NDIM,NOMA,NEWGEO,RESOCO,DEFICO,
     &                REAPRO,MULNOR,CMULT,1,
     &                POSNOE,POSMA,
     &                ZR(JNORMO+3*(IESCL-1)),R6BID,
     &                ZR(JAPCOE+JDECAL),RBID,RBID,OLDJEU,JEU,IBID,
     &                NBNO,POSNO,NBDDL,ZI(JAPDDL+JDECAL))
          ZR(JAPJEU+IESCL-1) = JEU - ZR(JJSUP+IZONE-1) + DIST3
C
        END IF

 10   CONTINUE
C
C ----------------------------------------------------------------------
C

      CALL JEDEMA ()
      END
