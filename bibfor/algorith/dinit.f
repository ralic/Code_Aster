      SUBROUTINE DINIT(NEQ   ,INSTAM,INSTAP,DEPPLU,POUGD ,
     &                 DEPENT,VITENT,ACCENT,NBPASE,INPSCO,
     &                 DEFICO,DECOL ,VITMOI,ACCMOI,DEPDEL,
     &                 VITPLU,ACCPLU,LSSTRU,SDDYNA)
C     
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 04/04/2007   AUTEUR ABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE MABBAS M.ABBAS
C TOLE CRP_21
C
      IMPLICIT NONE
      LOGICAL      DECOL
      INTEGER      NEQ
      INTEGER      NBPASE
      REAL*8       INSTAM, INSTAP
      CHARACTER*13 INPSCO
      CHARACTER*24 DEPENT,VITENT,ACCENT
      CHARACTER*24 VITMOI,ACCMOI,DEPDEL
      CHARACTER*24 POUGD(8),DEFICO
      CHARACTER*24 DEPPLU,VITPLU,ACCPLU
      CHARACTER*19 SDDYNA
      LOGICAL      LSSTRU
C 
C ----------------------------------------------------------------------
C
C ROUTINE MECA_NON_LINE (DYNAMIQUE - INITIALISATIONS)
C
C INITIALISATIONS EN DYNAMIQUE
C      
C ----------------------------------------------------------------------
C
C      
C IN  NEQ    : NOMBRE D'EQUATIONS
C IN  ALPHA  : PARAMETRE METHODE INTEGRATION
C IN  DELTA  : PARAMETRE METHODE INTEGRATION
C IN  INSTAM : TEMPS INSTANT MOINS
C IN  INSTAP : TEMPS INSTANT PLUS
C IN  COEVIT : PARAMETRE INTEGRATION (METHODE + PAS DE TEMPS)
C IN  COEACC : PARAMETRE INTEGRATION (METHODE + PAS DE TEMPS)
C I/O DEPPLU : DEPLACEMENT INSTANT PLUS
C I/O  POUGD : CHAPEAU POUTRE GRANDES ROTATIONS
C I/O DEPENT : CHAMP MULTIAPPUI
C I/O VITENT : CHAMP MULTIAPPUI
C I/O ACCENT : CHAMP MULTIAPPUI
C IN  NBPASE : NOMBRE PARAMETRES SENSIBLES
C IN  INPSCO : SD CONTENANT LISTE DES NOMS POUR SENSIBILITE
C IN  THETA  : PARAMETRE METHODE INTEGRATION
C IN  DEFICO : SD DEFINITION DU CONTACT
C IN  DECOL  : ?
C IN  VITMOI : VITESSE INSTANT MOINS
C IN  ACCMOI : ACCELERATION INSTANT MOINS
C IN  SDDYNA : SD DEDIEE A LA DYNAMIQUE (CF NDLECT)
C OUT DEPDEL : INCREMENT DEPLACEMENT (PREDICTEUR) EN EXPLICITE
C
C -------------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ----------------
C
      INTEGER            ZI
      COMMON  / IVARJE / ZI(1)
      REAL*8             ZR
      COMMON  / RVARJE / ZR(1)
      COMPLEX*16         ZC
      COMMON  / CVARJE / ZC(1)
      LOGICAL            ZL
      COMMON  / LVARJE / ZL(1)
      CHARACTER*8        ZK8
      CHARACTER*16                ZK16
      CHARACTER*24                          ZK24
      CHARACTER*32                                    ZK32
      CHARACTER*80                                              ZK80
      COMMON  / KVARJE / ZK8(1) , ZK16(1) , ZK24(1) , ZK32(1) , ZK80(1)
C
C -------------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ----------------


      CHARACTER*24  DEPKM1,VITKM1,ACCKM1,ROMKM1
      CHARACTER*24  ROMK,MAESCL,ECPDON,KMUAP
      CHARACTER*24  K24BLA
      INTEGER       NRPASE, IAUX, JAUX, IECPCO
      INTEGER       JECPD,JVITM,JACCM
      INTEGER       JDEPGM,JVITGM,JACCGM,JDEPGP
      INTEGER       JVITP,JACCP,JDEPKM,JVITKM,JACCKM,JROMKM,JROMK
      INTEGER       JDEPP,JDEPEN,JVITEN,JACCEN,JNODEP,JDEPDE
      INTEGER       JNOVIT,JNOACC,JMLTAP,JPSDEL
      INTEGER       JCOSC,JCONI,JTFOR
      CHARACTER*24  TFOR,CONI,CFSC
      INTEGER       IFORSD,NDYNIN,NBEXCI,NBMODE,IFORM
      REAL*8        PAS,V0ACC1,A0VIT1,COEF1,COEF2,COEF3,TETA
      REAL*8        COEVI2,COEAC2
      REAL*8        V0VIT,V0ACC,A0VIT,ALPHA,DELTA,PHI
      REAL*8        COEDEP,COEVIT,COEACC,A0ACC,NDYNRE            
      CHARACTER*16  KFORM
      CHARACTER*24  KBID,NDYNKK,KPRMO
      LOGICAL       NDYNLO
      INTEGER       I,J,IE,IRET,IER

C ----------------------------------------------------------------------
      CALL JEMARQ()

C FORMULATION CONTINUE EN DEPL OU EN VITE (CONTACT ECP)
C MESSAGE D'ERREUR SI [DEPL & TETA_METHODE] OU
C                     [VITE ET (NEWMARK OU HHT)]
      TFOR = SDDYNA(1:15)//'.TYPE_FOR'
      CFSC = SDDYNA(1:15)//'.COEF_SCH'
      CONI = SDDYNA(1:15)//'.INI_CONT'
C      
      CALL JEVEUO(TFOR,'E',JTFOR)
      CALL JEVEUO(CFSC,'E',JCOSC)
      CALL JEVEUO(CONI,'L',JCONI)      

      CALL GETVTX('SCHEMA_TEMPS','FORMULATION',1,1,1,
     &                 KFORM,IRET)
      IF(KFORM(1:11).EQ.'DEPLACEMENT')THEN
         IFORM=1
      ELSEIF(KFORM(1:7).EQ.'VITESSE')THEN
         IFORM=2
      ELSEIF(KFORM(1:12).EQ.'ACCELERATION')THEN
         IFORM=3
      ENDIF

      IF(NDYNLO(SDDYNA,'IMPLICITE'))THEN
        IF(IFORM.NE.1) CALL U2MESS('F','ALGORITH3_9')
        MAESCL = DEFICO(1:16)//'.MAESCL'
        CALL JEEXIN(MAESCL, IECPCO)
        IF (IECPCO .NE. 0) THEN
C         --- FORMULATION POUR LE CONTACT 'CONTINUE'
          ECPDON = DEFICO(1:16)//'.ECPDON'
          CALL JEVEUO(ECPDON,'L',JECPD)
          IFORSD = ZI(JECPD+6)
          ZI(JTFOR+1-1)=IFORM
          ZI(JTFOR+2-1)=IFORSD
        ELSE
C         --- FORMULTATION DYNAMIQUE
          ZI(JTFOR+1-1)=IFORM
          ZI(JTFOR+2-1)=0
        ENDIF

      ELSEIF(NDYNLO(SDDYNA,'EXPLICITE'))THEN
C         --- FORMULTATION DYNAMIQUE
          ZI(JTFOR+1-1)=IFORM
          ZI(JTFOR+2-1)=0
          IF( IFORM.NE.3)THEN
              CALL U2MESS('F','ALGORITH3_9')
          ENDIF   
      ENDIF
      

      V0VIT=ZR(JCONI+1-1)
      V0ACC=ZR(JCONI+2-1)
      A0VIT=ZR(JCONI+3-1)
      A0ACC=ZR(JCONI+4-1)


      PAS = INSTAP - INSTAM
      V0ACC1 = V0ACC*PAS
      A0VIT1 = A0VIT/PAS
      COEDEP = 1.D0

      IF(NDYNLO(SDDYNA,'IMPLICITE'))THEN
        IF(NDYNIN(SDDYNA,'FORMUL_DYNAMIQUE').EQ.1 .AND.
     &     NDYNIN(SDDYNA,'FORMUL_CONTACT').NE.2   )THEN
          ALPHA=NDYNRE(SDDYNA,'ALPHA')
          DELTA=NDYNRE(SDDYNA,'DELTA')
          COEVIT = DELTA/ALPHA/PAS
          COEACC = 1.D0/ALPHA/PAS/PAS
          IF(NDYNLO(SDDYNA,'HHT_COMPLET'))COEDEP = (1.5D0-DELTA)
        ELSEIF(NDYNIN(SDDYNA,'FORMUL_CONTACT').EQ.2)THEN
          IF (DECOL) THEN
            TETA=NDYNRE(SDDYNA,'THETA')
          ELSE
            TETA=1.D0
          ENDIF
          COEVIT = 1.D0/TETA/PAS
          COEACC = 1.D0/TETA/PAS/PAS
        ENDIF
      ELSE
          COEVIT = 0.D0
          COEACC = 0.D0
      ENDIF

      ZR(JCOSC-1+1)=COEDEP 
      ZR(JCOSC-1+2)=COEVIT 
      ZR(JCOSC-1+3)=COEACC

C -- EXTRACTION DES NOMS DES CHAMPS
      CALL DESAGG(POUGD, DEPKM1, VITKM1, ACCKM1, K24BLA,
     &            K24BLA, ROMKM1, ROMK , K24BLA)

      IF(.NOT.NDYNLO(SDDYNA,'STATIQUE'))THEN
       IF(NDYNLO(SDDYNA,'MULTI_APPUI'))THEN
        KMUAP=NDYNKK(SDDYNA,'MULTI_APPUI')
        CALL JEVEUO(KMUAP(1:19)//'.FDEP','L',JNODEP)
        CALL JEVEUO(KMUAP(1:19)//'.FVIT','L',JNOVIT)
        CALL JEVEUO(KMUAP(1:19)//'.FACC','L',JNOACC)
        CALL JEVEUO(KMUAP(1:19)//'.MULA','L',JMLTAP)
        CALL JEVEUO(KMUAP(1:19)//'.PSID','L',JPSDEL)
       ENDIF
      ENDIF

      DO 20 NRPASE = NBPASE,0,-1
        IAUX = NRPASE
        JAUX = 4
        CALL PSNSLE(INPSCO,IAUX,JAUX,DEPPLU)
        JAUX = 14
        CALL PSNSLE(INPSCO,IAUX,JAUX,VITPLU)
        JAUX = 16
        CALL PSNSLE(INPSCO,IAUX,JAUX,ACCPLU)
        JAUX = 18
        CALL PSNSLE(INPSCO,IAUX,JAUX,DEPENT)
        JAUX = 20
        CALL PSNSLE(INPSCO,IAUX,JAUX,VITENT)
        JAUX = 22
        CALL PSNSLE(INPSCO,IAUX,JAUX,ACCENT)

C -- INITIALISATION DES CHAMPS DYNAMIQUES
        CALL JEVEUO(DEPPLU(1:19)//'.VALE','L',JDEPP )
        CALL JEVEUO(VITPLU(1:19)//'.VALE','E',JVITP )
        CALL JEVEUO(ACCPLU(1:19)//'.VALE','E',JACCP )
        CALL JEVEUO(DEPKM1(1:19)//'.VALE','E',JDEPKM)
        CALL JEVEUO(VITKM1(1:19)//'.VALE','E',JVITKM)
        CALL JEVEUO(ACCKM1(1:19)//'.VALE','E',JACCKM)
        CALL JEVEUO(ROMKM1(1:19)//'.VALE','E',JROMKM)
        CALL JEVEUO(ROMK(1:19)  //'.VALE','E',JROMK )
C
        DO 10 I = 1,NEQ
          ZR(JDEPKM+I-1) = ZR(JDEPP+I-1)
          ZR(JVITKM+I-1) = ZR(JVITP+I-1)
          ZR(JACCKM+I-1) = ZR(JACCP+I-1)

          IF(NDYNLO(SDDYNA,'IMPLICITE'))THEN
           IF(NDYNIN(SDDYNA,'FORMUL_DYNAMIQUE').EQ.1 .AND.
     &        NDYNIN(SDDYNA,'FORMUL_CONTACT').NE.2   )THEN
            ZR(JVITP+I-1) = V0VIT*ZR(JVITKM+I-1) + V0ACC1*ZR(JACCKM+I-1)
            ZR(JACCP+I-1) = A0VIT1*ZR(JVITKM+I-1) + A0ACC*ZR(JACCKM+I-1)
           ELSEIF(NDYNIN(SDDYNA,'FORMUL_CONTACT').EQ.2)THEN
            ZR(JVITP+I-1) = ZR(JVITKM+I-1)*(TETA-1)/TETA
            ZR(JACCP+I-1) = -ZR(JVITKM+I-1)/(PAS*TETA)
           ENDIF
          ENDIF

          ZR(JROMKM+I-1) = 0.D0
          ZR(JROMK+I-1)  = 0.D0
   10   CONTINUE

C -- INITIALISATION DES CHAMPS "MULTI-APPUIS"
        IF(.NOT.NDYNLO(SDDYNA,'STATIQUE'))THEN
        IF(NDYNLO(SDDYNA,'MULTI_APPUI'))THEN
          CALL GETFAC('EXCIT',NBEXCI)
          CALL JEVEUO(DEPENT(1:19)//'.VALE','E',JDEPEN)
          CALL JEVEUO(VITENT(1:19)//'.VALE','E',JVITEN)
          CALL JEVEUO(ACCENT(1:19)//'.VALE','E',JACCEN)
          DO 710 IE = 1,NEQ
            ZR(JDEPEN+IE-1) = 0.D0
            ZR(JVITEN+IE-1) = 0.D0
            ZR(JACCEN+IE-1) = 0.D0
  710     CONTINUE
C CE QUI SUIT EST A MODIFIER LORSQUE L ON DERIVERA P/R A UN CHARGEMENT
C DE TYPE DIRICHLET
          IF (NRPASE.EQ.0) THEN
           DO 910 J = 1,NBEXCI
            IF (ZI(JMLTAP+J-1).EQ.1) THEN
             CALL FOINTE('F ',ZK8(JNODEP+J-1),1,'INST',INSTAP,COEF1,
     &                   IER)
             CALL FOINTE('F ',ZK8(JNOVIT+J-1),1,'INST',INSTAP,COEF2,
     &                   IER)
             CALL FOINTE('F ',ZK8(JNOACC+J-1),1,'INST',INSTAP,COEF3,
     &                   IER)
            ELSE
             COEF1 = 0.D0
             COEF2 = 0.D0
             COEF3 = 0.D0
            ENDIF
            DO 810 IE = 1,NEQ
             ZR(JDEPEN+IE-1) = ZR(JDEPEN+IE-1) +
     &                         ZR(JPSDEL+ (J-1)*NEQ+IE-1)*COEF1
             ZR(JVITEN+IE-1) = ZR(JVITEN+IE-1) +
     &                         ZR(JPSDEL+ (J-1)*NEQ+IE-1)*COEF2
             ZR(JACCEN+IE-1) = ZR(JACCEN+IE-1) +
     &                         ZR(JPSDEL+ (J-1)*NEQ+IE-1)*COEF3
  810       CONTINUE
  910      CONTINUE
          ENDIF
        ENDIF
        ENDIF

   20 CONTINUE

C -- SI SCHEMA EXPLICITE, ON CALCULE LES PREDICTEURS
C               {DUn+1}=DT*{Vn}+DT*DT/2*{An}
C               {Un+1}={Un}+{DUn+1}
      IF(NDYNLO(SDDYNA,'EXPLICITE'))THEN
        CALL JEVEUO(VITMOI(1:19)//'.VALE','L',JVITM)
        CALL JEVEUO(ACCMOI(1:19)//'.VALE','L',JACCM)
        CALL JEVEUO(DEPDEL(1:19)//'.VALE','E',JDEPDE)
        CALL JEVEUO(DEPPLU(1:19)//'.VALE','E',JDEPP )
        COEVI2 = PAS
        IF (NDYNLO(SDDYNA,'DIFF_CENT')) THEN
          COEAC2 = COEVI2*COEVI2/2.D0
        ELSEIF (NDYNLO(SDDYNA,'TCHAMWA')) THEN
          PHI=NDYNRE(SDDYNA,'PHI')
          COEAC2 = COEVI2*COEVI2*PHI
        ENDIF
        DO 11 I = 1 , NEQ
          ZR(JDEPDE+I-1) = COEVI2*ZR(JVITM+I-1)+COEAC2*ZR(JACCM+I-1)
          ZR(JDEPP+I-1) = ZR(JDEPP+I-1)+ZR(JDEPDE+I-1)
   11   CONTINUE
        IF (LSSTRU) THEN
          KPRMO=NDYNKK(SDDYNA,'PROJ_MODAL')
          CALL JELIRA(KPRMO(1:19)//'.AGEM','LONMAX',NBMODE,KBID)
          CALL JEVEUO(KPRMO(1:19)//'.AGEM','L',JACCGM)
          CALL JEVEUO(KPRMO(1:19)//'.VGEM','L',JVITGM)
          CALL JEVEUO(KPRMO(1:19)//'.DGEM','L',JDEPGM)
          CALL JEVEUO(KPRMO(1:19)//'.DGEP','E',JDEPGP)
          DO 12 I=1,NBMODE
            ZR(JDEPGP+I-1) = ZR(JDEPGM+I-1) + COEVI2*ZR(JVITGM+I-1)
     &                     + COEAC2*ZR(JACCGM+I-1)
   12     CONTINUE
        ENDIF
      ENDIF

      CALL JEDEMA()

      END
