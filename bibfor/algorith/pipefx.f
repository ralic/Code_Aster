        SUBROUTINE PIPEFX(NNO   ,NNOS  ,NDIM  ,NPGF ,NPTF  ,
     &                    NFH   ,NDDL  ,DDLM  ,SINGU,DDLS  ,
     &                    FPG   ,ELREF ,ELREFC,ELC  ,CFACE ,
     &                    NFACE ,JPTINT,IGEOM ,JLSN ,JLST  ,
     &                    JBASEC,COHES ,MATE  ,DEPLM,DDEPL ,
     &                    DEPL0 ,DEPL1 ,DTAU  ,INDCO,COPILO)

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 13/06/2012   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================

C TOLE CRS_1404 CRP_21   
          
      IMPLICIT NONE
      INCLUDE 'jeveux.h'
      INTEGER       NNO,NNOS,INDCO(60)
      INTEGER       IFA,NPGF,NPTF,NDDL
      INTEGER       CFACE(5,3),NFACE,NDIM,NFH,SINGU,DDLS
      INTEGER       MATE,JPTINT,JBASEC,DDLM
      CHARACTER*8   ELREF,ELREFC,ELC,FPG
      REAL*8        DTAU, COPILO(5,NPGF*NFACE),COHES(60)
      REAL*8        DDEPL(NDDL)
      REAL*8        DEPLM(NDDL),DEPL0(NDDL),DEPL1(NDDL),R8BID
      
C-----------------------------------------------------------------------
C  PILOTAGE PRED_ELAS POUR LES ELEMENTS X_FEM EN CONTACT SOUMIS
C               A LA LOI COHESIVE CZM_EXP_REG
C
C IN  NNO    : NOMBRE DE NOEUDS DU SOUS-ELEMENT DE REFERENCE
C IN  NNOS   : NOMBRE DE NOEUDS SOMMET DU SOUS-ELEMENT DE REFERENCE
C IN  NDIM   : DIMENSION DE L'ESPACE
C IN  NPGF   : NOMBRE DE POINTS DE GAUSS PAR FACETTE
C IN  NPTF   :
C IN  NFH    : NOMBRE DE FONCTIONS HEAVYSIDE (PAR NOEUD)
C IN  NDDL   : NOMBRE TOTAL DE DDL
C IN  DDLM   :
C IN  SINGU  : 
C IN  DDLS   : NOMBRE DE DDL (DEPL+CONTACT) A CHAQUE NOEUD SOMMET
C IN  FPG    : FAMILLE DE POINTS DE GAUSS (SCHEMA D'INTEGRATION)
C IN  ELREF  : ELEMENT DE REFERENCE PRINCIPAL
C IN  ELREFC : ELEMENT DE CONTACT DE REFERENCE PRINCIPAL
C IN  ELC    :
C IN  CFACE  : CONNECTIVITE DES NOEUDS DES FACETTES
C IN  NFACE  :
C IN  JPTINT :
C IN  IGEOM  : COORDONNEES DU POINT DE CONTACT
C IN  JLSN   : ADRESSE LEVEL SET NORMALE
C IN  JBASEC :
C IN  JLST   : ADRESSE LEVEL SET TANGENTE
C IN  COHES  : MEMOIRE DU SAUT DE DEPLACEMENT EQUIVALENT
C IN  MATE   : MATERIAU CODE
C IN  DEPLM  : DDL U EN T-
C IN  DDEPL  : INCREMENT DE DDL U A L'ITERATION NEWTON COURANTE
C IN  DEPL0  : CORRECTION DE DDL U POUR FORCES FIXES
C IN  DEPL1  : CORRECTION DE DDL U POUR FORCES PILOTEES
C IN  DTAU   : 2ND MEMBRE DE L'EQUATION F(ETA)=TAU
C IN  INDCO  : INDICATEUR DE CONTACT EN CHAQUE POINT DE GAUSS
C OUT COPILO : COEFFICIENTS A0 ET A1 POUR CHAQUE POINT DE GAUSS

      
      REAL*8  R8VIDE
      INTEGER I,IGEOM,IPGF,ISSPG
      INTEGER JLSN,JLST
      REAL*8  JAC,FFP(27),FFPC(27),ND(3)
      REAL*8  DFBID(27,3),P(3,3),PP(3,3),G(3)
      REAL*8  TAU1(3),TAU2(3),R,RR,RBID,RB
      REAL*8  SUP(3),SUD(3),SUPP(3),SUDD(3)
      REAL*8  SUP2D(2),SUD2D(2)
      REAL*8  DTANG(3),DNOR(3)
      REAL*8  DSIDEP(6,6),LSN,LST,SIGMA(6)
      CHARACTER*8 JOB
           
C BOUCLE SUR LE NOMBRE DE FACETTES DE CONTACT
      DO 100 IFA=1,NFACE
         DO 110 IPGF=1,NPGF 
C         INDICE DE CE POINT DE GAUSS DANS INDCO
          ISSPG=NPGF*(IFA-1)+IPGF

          IF(INDCO(ISSPG).EQ.0) THEN                      
C           INITIALISATION
            CALL VECINI(2,0.D0,SUP2D)
            CALL VECINI(2,0.D0,SUD2D)
C
C           CALCUL DE JAC (PRODUIT DU JACOBIEN ET DU POIDS)
C           ET DES FF DE L'ELEMENT PARENT AU POINT DE GAUSS
C           ET LA NORMALE ND ORIENTEE DE ESCL -> MAIT

            IF (NDIM.EQ.3) THEN   
               CALL XJACFF(ELREF,ELREFC,ELC,NDIM,FPG,JPTINT,
     &                 IFA,CFACE,IPGF,NNO,IGEOM,JBASEC,G,
     &                 'NON',JAC,FFP,FFPC,DFBID,ND,TAU1,TAU2)        
            ELSEIF (NDIM.EQ.2) THEN
               CALL XJACF2(ELREF,ELREFC,ELC,NDIM,FPG,JPTINT,
     &                 IFA,CFACE,NPTF,IPGF,NNO,IGEOM,JBASEC,
     &                 G,'NON',JAC,FFP,FFPC,DFBID,ND,TAU1)
            ENDIF
C
C           CALCUL DE RR = SQRT(DISTANCE AU FOND DE FISSURE)
C
            IF (SINGU.EQ.1) THEN
               LSN=0.D0
               LST=0.D0
               DO 112 I=1,NNO
                  LSN=LSN+ZR(JLSN-1+I)*FFP(I)
                  LST=LST+ZR(JLST-1+I)*FFP(I)
 112           CONTINUE
C              ERREUR SI LSN NON NUL SUR LA SURFACE
               CALL ASSERT(ABS(LSN).LE.1.D-3)
               R=ABS(LST)
               RR=SQRT(R)
            ENDIF        
C            
C           SAUT PILOTE AU POINT DE GAUSS : SU(ETA) = SUPP + ETA * SUDD
C           COMPOSANTE PILOTEE DU SAUT SUDD
C           SAUT A T- SAUTM
C           COMPOSANTE FIXE DU SAUT A ITERATION N+1 SUPP
C        
            CALL XMMSA4(NDIM,NNO,NNOS,FFP,NDDL,3,
     &                 DEPLM,DDEPL,DEPL0,NFH,
     &                 SINGU,RR,DDLS,DDLM,SUPP)
            CALL XMMSA4(NDIM,NNO,NNOS,FFP,NDDL,1,
     &                 DEPL1,RB,RBID,NFH,
     &                 SINGU,RR,DDLS,DDLM,SUDD)
            JOB='SAUT_EQ'
            CALL XMMSA2(NDIM ,IPGF  ,MATE  ,SUDD ,ND    ,
     &                  TAU1 ,TAU2  ,RB    ,JOB  ,R8BID ,
     &                  RBID ,DSIDEP,SIGMA ,PP   ,DNOR  ,
     &                  DTANG,P     ,SUD)
     
            CALL XMMSA2(NDIM ,IPGF  ,MATE  ,SUPP ,ND    ,
     &                  TAU1 ,TAU2  ,RB    ,JOB  ,R8BID ,
     &                  RBID ,DSIDEP,SIGMA ,PP   ,DNOR  ,
     &                  DTANG,P     ,SUP)
     
            CALL R8INIR(4, 0.D0, COPILO(1,ISSPG),1)
            COPILO(5,ISSPG) = R8VIDE()
C           APPEL DU PILOTAGE PRED_ELAS SPECIFIQUE 
C           A LA LOI DE COMPORTEMENT                         
            IF (NDIM.EQ.2)     THEN
               SUP2D(1)=SUP(1)
               SUP2D(2)=SUP(2)
               SUD2D(1)=SUD(1)
               SUD2D(2)=SUD(2)
               CALL PIPEBA(NDIM,MATE,SUP2D,SUD2D,COHES(ISSPG),
     &                  DTAU,COPILO(1,ISSPG))
            ELSEIF (NDIM.EQ.3) THEN
               CALL PIPEBA(NDIM,MATE,SUP,SUD,COHES(ISSPG),
     &                  DTAU,COPILO(1,ISSPG))
            ENDIF
          ENDIF 
                  
110      CONTINUE
100   CONTINUE              
      END
