      SUBROUTINE CFNORM(NDIM,NNOCO,NOMA,NEWGEO,RESOCO,DEFICO)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 24/09/2007   AUTEUR ABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2004  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT     NONE
      INTEGER      NDIM
      INTEGER      NNOCO
      CHARACTER*8  NOMA
      CHARACTER*24 NEWGEO
      CHARACTER*24 RESOCO
      CHARACTER*24 DEFICO
C      
C ----------------------------------------------------------------------
C
C ROUTINE CONTACT (METHODES DISCRETES - APPARIEMENT)
C
C CALCUL DES NORMALES AUX NOEUDS DE CONTACT (MAITRE ET ESCLAVE)
C
C ----------------------------------------------------------------------
C
C
C IN  NDIM   : DIMENSION DE L'ESPACE (2 OU 3)
C IN  NNOCO  : NOMBRE DE NOEUDS DE CONTACT (MAITRE+ESCLAVE)
C IN  NOMA   : NOM DU MAILLAGE
C IN  NEWGEO : COORDONNEES ACTUALISEES DES NOEUDS DU MAILLAGE
C I/O RESOCO : SD DE TRAITEMENT NUMERIQUE DU CONTACT
C               MISE A JOUR DE NORINI 
C IN  DEFICO : SD DE DEFINITION DU CONTACT (ISSUE D'AFFE_CHAR_MECA)
C
C -------------- DEBUT DECLARATIONS NORMALISEES JEVEUX -----------------
C
      CHARACTER*32 JEXNUM
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C ---------------- FIN DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER      CFMMVD,CFDISI,ZDIRE
      INTEGER      JCOOR,JDES
      INTEGER      POSNO,POSMA,NBNO,JDEB,JFIN
      INTEGER      NUTYP,NUMA
      INTEGER      INO,IMA,IZONE,TANGDF
      REAL*8       NORME,VECSEG(3),COORMA(27),VECNOR(3)
      REAL*8       DTANG(3),NORM(3)
      CHARACTER*8  K8BID
      CHARACTER*24 NOZOCO,CONTMA,MANOCO,PMANO,DIRCO
      INTEGER      JZOCO,JMANO,JMACO,JPOMA,JDIR
      CHARACTER*24 NORINI
      INTEGER      JNRINI
      CHARACTER*4  MATYP
      INTEGER      IFM,NIV      
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()    
      CALL INFDBG('CONTACT',IFM,NIV) 
C
C --- AFFICHAGE
C
      CALL CFIMPE(IFM,NIV,'CFNORM',1)        
C
C --- LECTURE DES SD POUR LE CONTACT POTENTIEL
C
      CONTMA = DEFICO(1:16)//'.MAILCO'
      MANOCO = DEFICO(1:16)//'.MANOCO'
      PMANO  = DEFICO(1:16)//'.PMANOCO'
      DIRCO  = DEFICO(1:16)//'.DIRCO'
      NOZOCO = DEFICO(1:16)//'.NOZOCO'
      NORINI = RESOCO(1:14)//'.NORINI'      
      CALL JEVEUO(NEWGEO(1:19)//'.VALE','L',JCOOR)
      CALL JEVEUO(CONTMA,'L',JMACO)
      CALL JEVEUO(MANOCO,'L',JMANO)
      CALL JEVEUO(PMANO, 'L',JPOMA)
      CALL JEVEUO(NOZOCO,'L',JZOCO)
      CALL JEVEUO(DIRCO, 'L',JDIR )
      CALL JEVEUO(NORINI,'E',JNRINI)  
      ZDIRE = CFMMVD('ZDIRE')    
C
C --- BOUCLE SUR TOUS LES NOEUDS DE CONTACT (MAITRE ET ESCLAVE)
C
      DO 50 POSNO = 1,NNOCO
C
C --- ZONE DE CONTACT CORRESPONDANTE
C
        IZONE = ZI(JZOCO+POSNO-1)
C
C --- INITIALISATION NORMALE SUR NOEUD
C        
        NORM(1) = 0.D0
        NORM(2) = 0.D0
        NORM(3) = 0.D0
C       
        IF (CFDISI(DEFICO,'APPARIEMENT',IZONE).EQ.4) THEN
C        
C --- UTILISATION D'UNE DIRECTION FIXE SI NECESSAIRE VECT_NORM_ESCL 
C
          NORM(1) = ZR(JDIR+ZDIRE*(IZONE-1))
          NORM(2) = ZR(JDIR+ZDIRE*(IZONE-1)+1)
          NORM(3) = ZR(JDIR+ZDIRE*(IZONE-1)+2)
        ELSE
C
C --- SINON CALCUL DES NOUVELLES COMPOSANTES DE LA NORMALE
C
          JDEB = ZI(JPOMA+POSNO-1) + 1
          JFIN = ZI(JPOMA+POSNO)
C
C --- BOUCLE SUR LES MAILLES CONTENANT LE NOEUD
C
          DO 40 IMA = JDEB,JFIN

            POSMA = ZI(JMANO+IMA-1)
            NUMA  = ZI(JMACO+POSMA-1)

            CALL TYPEMA(NOMA,NUMA,NUTYP,MATYP)
            CALL JELIRA(JEXNUM(NOMA(1:8)//'.CONNEX',NUMA),'LONMAX',NBNO,
     &                  K8BID)
            CALL JEVEUO(JEXNUM(NOMA(1:8)//'.CONNEX',NUMA),'L',JDES)
            
            IF (NBNO.GT.9) THEN
              CALL ASSERT(.FALSE.)
            ENDIF
C
C --- COORDONNEES DES NOEUDS DE LA MAILLE COURANTE
C
            DO 20 INO = 1,NBNO
              COORMA(3*(INO-1)+1) = ZR(JCOOR+3*(ZI(JDES+INO-1)-1))
              COORMA(3*(INO-1)+2) = ZR(JCOOR+3*(ZI(JDES+INO-1)-1)+1)
              COORMA(3*(INO-1)+3) = ZR(JCOOR+3*(ZI(JDES+INO-1)-1)+2)
   20       CONTINUE
C
C --- DEFINITION DU REPERE PAR L'UTILISATEUR (VECT_Y/VECT_ORIE_POU)
C
            CALL CFTAND(DEFICO,IZONE,TANGDF,DTANG)
C
C --- CALCUL DE LA NORMALE
C --- VECTEUR NUL POUR LA NORMALE D'UN NOEUD APPARTENANT A UN POI1 EN 3D
C --- CALCUL PAR VECT_Y/VECT_ORIE_POU POUR UN NOEUD DE SEG. EN 3D
C --- CALCUL PAR ROUTINE CANORM POUR LE RESTE
C
            IF ((NDIM.EQ.3) .AND. (MATYP.EQ.'POI1')) THEN
              NORM(1) = 0.D0
              NORM(2) = 0.D0
              NORM(3) = 0.D0
              IF (NBNO.NE.1) THEN
                CALL CFIMPA('CFNORM',1) 
              ENDIF
            ELSE IF ((NDIM.EQ.3) .AND. (MATYP(1:3).EQ.'SEG')) THEN
              IF (TANGDF.EQ.1) THEN
                CALL CANORM(COORMA,VECSEG,NDIM,NUTYP,1)
                NORM(1) = NORM(1) + VECSEG(2)*DTANG(3) -
     &                              VECSEG(3)*DTANG(2)
                NORM(2) = NORM(2) + VECSEG(3)*DTANG(1) -
     &                              VECSEG(1)*DTANG(3)
                NORM(3) = NORM(3) + VECSEG(1)*DTANG(2) -
     &                              VECSEG(2)*DTANG(1)
              ELSE
                CALL U2MESS('F','CONTACT_60')              
              ENDIF 
            ELSE
              CALL CANORM(COORMA,VECNOR,NDIM,NUTYP,1)
              NORM(1) = NORM(1) + VECNOR(1)
              NORM(2) = NORM(2) + VECNOR(2)
              NORM(3) = NORM(3) + VECNOR(3)
            END IF
   40     CONTINUE
        END IF
C
C --- NORMALISATION
C
        CALL NORMEV(NORM,NORME)
C
C --- SAUVEGARDE NORMALE AU NOEUD
C        
        CALL DCOPY(3,NORM,1,ZR(JNRINI+3*(POSNO-1)),1)

   50 CONTINUE
C
      CALL JEDEMA()
      END
