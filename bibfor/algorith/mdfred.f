      SUBROUTINE MDFRED (NBMODE,DEPGEN,FEXGEN,NBREDE,DPLRED,PARRED,
     +                   FONRED,SAURED,SAREDI)
      IMPLICIT REAL*8 (A-H,O-Z)
C ----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 17/02/99   AUTEUR VABHHTS J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C
C CALCUL DU SECOND MEMBRE POUR UNE NON-LINEARITE DE TYPE RELA_EFFO_DEPL
C ----------------------------------------------------------------------
C IN  : NBMODE : NOMBRE DE MODES
C IN  : DEPGEN : DEPLACEMENTS GENERALISES AU PAS COURANT
C VAR : FEXGEN : FORCES GENERALISEES AU PAS COURANT
C IN  : NBREDE : NOMBRE DE POINTS DECRIVANT LA NON-LINEARITE
C IN  : DPLRED : TABLEAU DES DEPLACEMENTS MODAUX AUX NOEUDS DE RED
C IN  : PARRED : PARAMETRES DES NON-LINEARITES
C IN  : FONRED : FONCTIONS DE NON-LINEARITE
C OUT : SAURED : VALEURS SAUVEGARDEES
C OUT : SAREDI : VALEURS SAUVEGARDEES
C ----------------------------------------------------------------------
      INTEGER     IERD,ICOMP,NBMODE,NBREDE,SAREDI(*)
      REAL*8      RESU,SEUIL,FORC,XS,ABSL,PENL,DEPGEN(*),FEXGEN(*),
     &            PARRED(NBREDE,*),SAURED(*),DPLRED(NBREDE,NBMODE,*)
      CHARACTER*8 FONC,COMP,FONRED(NBREDE,*)
C
C     --- BOUCLE SUR LES NOEUDS DE NON-LINEARITE ---
C
      DO 10 I=1,NBREDE
C
        COMP = FONRED(I,2)
        FONC = FONRED(I,3)
        ABSL = PARRED(I,1)
        PENL = PARRED(I,2)
C
        IF (COMP(1:2).EQ.'DX')  ICOMP = 1
        IF (COMP(1:2).EQ.'DY')  ICOMP = 2
        IF (COMP(1:2).EQ.'DZ')  ICOMP = 3
        IF (COMP(1:3).EQ.'DRX') ICOMP = 4
        IF (COMP(1:3).EQ.'DRY') ICOMP = 5
        IF (COMP(1:3).EQ.'DRZ') ICOMP = 6
C
        SEUIL = 0.D0
        DO 20 J=1,NBMODE
          SEUIL = SEUIL + DPLRED(I,J,ICOMP)*DEPGEN(J)
20      CONTINUE
C
        SAURED(I) = SEUIL
        SAREDI(I) = 0
C
        XS = ABS(SEUIL)
        IF ( XS .GT. ABSL ) THEN
          SAREDI(I) = 1
          IF (FONRED(I,4) .EQ. 'TRANSIS ') THEN
             CALL FOINTE('F ',FONC,1,COMP,XS,RESU,IERD)
             IF ( SEUIL .LT. 0.D0 ) RESU = -RESU
             FORC = ( PENL * SEUIL ) - RESU
C
             DO 30 J=1,NBMODE
               FEXGEN(J)=FEXGEN(J)+DPLRED(I,J,ICOMP)*FORC
30           CONTINUE
C
          ENDIF
        ENDIF
C
        IF (FONRED(I,4) .EQ. 'DEPL    ') THEN
          CALL FOINTE('F ',FONC,1,COMP,SEUIL,FORC,IERD)
C
          DO 40 J=1,NBMODE
            FEXGEN(J)=FEXGEN(J)+DPLRED(I,J,ICOMP)*FORC
40        CONTINUE
        ENDIF
C
10    CONTINUE
C
      END
