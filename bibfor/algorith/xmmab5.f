      SUBROUTINE XMMAB5(NDIM  ,NNOL ,NNOF, PLA,
     &                  IPGF,IVFF  ,FFC   ,JAC   ,COEFFR,
     &                  COEFEF,NOEUD ,SEUIL,TAU1,TAU2,MU,IK,
     &                  IFA,CFACE,LACT ,
     &                  LPENAF,MMAT )

      IMPLICIT NONE
      INTEGER     NDIM,NNOL,NNOF,IVFF,IPGF
      INTEGER     CFACE(5,3),IFA
      INTEGER     PLA(27),LACT(8)
      REAL*8      MMAT(216,216)
      REAL*8      FFC(8),JAC,TAU1(3),TAU2(3)
      REAL*8      SEUIL,MU,COEFFR,IK(3,3),COEFEF
      LOGICAL     NOEUD,LPENAF

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 27/06/2011   AUTEUR MASSIN P.MASSIN 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C TOLE CRP_21

C
C ROUTINE CONTACT (METHODE XFEM HPP - CALCUL ELEM.)
C
C --- CALCUL DE F - CAS GLISSANT OU PENALISATION (NON SEULE)
C
C ----------------------------------------------------------------------
C
C IN  NDIM   : DIMENSION DE L'ESPACE
C IN  NNOL   : NOMBRE DE NOEUDS PORTEURS DE DDLC
C IN  NNOF   : NOMBRE DE NOEUDS DE LA FACETTE DE CONTACT
C IN  PLA    : PLACE DES LAMBDAS DANS LA MATRICE
C IN  IPGF   : NUMÉRO DU POINTS DE GAUSS
C IN  IVFF   : ADRESSE DANS ZR DU TABLEAU FF(INO,IPG)
C IN  FFC    : FONCTIONS DE FORME DE L'ELEMENT DE CONTACT
C IN  JAC    : PRODUIT DU JACOBIEN ET DU POIDS
C IN  COEFFR :
C IN  NOEUD  : INDICATEUR FORMULATION (T=NOEUDS , F=ARETE)
C IN  SEUIL  : SEUIL
C IN  TAU1   : TANGENTE A LA FACETTE AU POINT DE GAUSS
C IN  TAU2   : TANGENTE A LA FACETTE AU POINT DE GAUSS
C IN  MU     : COEFFICIENT DE COULOMB
C IN  IK     : MATRICE I-KN
C IN  IFA    : INDICE DE LA FACETTE COURANTE
C IN  CFACE  : CONNECTIVITÉ DES NOEUDS DES FACETTES
C IN  LACT   : LISTE DES LAGRANGES ACTIFS
C IN  LPENAF : INDICATEUR DE PENALISATION DU FROTTEMENT
C I/O MMAT   : MATRICE ELEMENTAITRE DE CONTACT/FROTTEMENT
C
C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX --------------------
C
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)

C --------- FIN  DECLARATIONS  NORMALISEES  JEVEUX --------------------
C
      INTEGER I,J,K,L,NLI,NLJ
      INTEGER PLI,PLJ
      REAL*8  FFI,FFJ,TAIKTA(2,2),ID(3,3)
C
C ----------------------------------------------------------------------

      DO 180 I = 1,NNOL
        PLI=PLA(I)
        IF (NOEUD) THEN
          FFI=FFC(I)
          NLI=LACT(I)
          IF (NLI.EQ.0) GOTO 180
        ELSE
          FFI=ZR(IVFF-1+NNOF*(IPGF-1)+I)
          NLI=CFACE(IFA,I)
        ENDIF
        DO 181 J = 1,NNOL
          PLJ=PLA(J)
          IF (NOEUD) THEN
            FFJ=FFC(J)
            NLJ=LACT(J)
            IF (NLJ.EQ.0) GOTO 181
          ELSE
            FFJ=ZR(IVFF-1+NNOF*(IPGF-1)+J)
            NLJ=CFACE(IFA,J)
          ENDIF

          IF (LPENAF) THEN
C         PENALISATION SEULE, TAIKTA=TAUT.ID.TAU
            CALL MATINI(3,3,0.D0,ID)
            ID(1,1)=1.0D0
            ID(2,2)=1.0D0
            ID(3,3)=1.0D0
            CALL XMAFR2(TAU1,TAU2,ID,TAIKTA)
          ELSE
C         CALCUL DE TAIKTA = TAUT.(ID-KN).TAU
            CALL XMAFR2(TAU1,TAU2,IK,TAIKTA)
          ENDIF

          DO 182 K = 1,NDIM-1
            DO 183 L = 1,NDIM-1
              MMAT(PLI+K,PLJ+L) = MMAT(PLI+K,PLJ+L)
     &        + (MU*SEUIL/COEFFR)*
     &        FFI*FFJ*TAIKTA(K,L)*JAC*COEFEF*COEFEF
 183        CONTINUE
 182      CONTINUE
 181    CONTINUE
 180  CONTINUE

      END
