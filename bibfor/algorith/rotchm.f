      SUBROUTINE ROTCHM(PROFNO,VALE,TETSS,NBSS,INVSK,NBNOT,NBCMP,IAX)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 29/09/2006   AUTEUR VABHHTS J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C***********************************************************************
C    P. RICHARD     DATE 10/02/92
C-----------------------------------------------------------------------
C  BUT: EFFECTUER LA ROTATION DE LA PARTIE DES CHAMNO.VALE CORRESPON-
C  -DANT A CHAQUE SOUS-STRUCTURE A PARTIR DU PROFNO GLOBAL, DU
C  MAILLAGE SQUELETTE GLOBAL, ET DU TABLEAU INV-SKELET ET DU VECTEUR
C  DES ANGLES DE ROTATION DE CHAQUE SOUS-STRUCTURE
      IMPLICIT REAL*8 (A-H,O-Z)
C
C-----------------------------------------------------------------------
C
C PROFNO   /I/: NOM K19 DU PROF_CHNO GLOBAL
C VALE     /M/: VECTEUR CORRESPONDANT AU .VALE DU CHAMNO COURANT
C TESSS    /I/: VECTEUR DES ANGLE DE ROTATION DES SOUS-STRUCTURES
C NBSS     /I/: NOMBRE DE SOUS-STRUCTURES
C INVSK    /I/: TABLEAU INVERSE-SKELETTE
C NBNOT    /I/: NOMBRE DE NOEUDS GLOBAL
C NBCMP    /I/: NOMBRE DE COMPOSANTE MAX DE LA GRANDEUR SOUS-JACENTE
C IAX      /I/: NUMERO DE L'AXE DE ROTATION
C
C-------- DEBUT COMMUNS NORMALISES  JEVEUX  ----------------------------
C
      INTEGER          ZI
      COMMON  /IVARJE/ ZI(1)
      REAL*8           ZR
      COMMON  /RVARJE/ ZR(1)
      COMPLEX*16       ZC
      COMMON  /CVARJE/ ZC(1)
      LOGICAL          ZL
      COMMON  /LVARJE/ ZL(1)
      CHARACTER*8      ZK8
      CHARACTER*16              ZK16
      CHARACTER*24                        ZK24
      CHARACTER*32                                  ZK32
      CHARACTER*80                                            ZK80
      COMMON  /KVARJE/ ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
      CHARACTER*32 JEXNOM,JEXNUM
C
C-----  FIN  COMMUNS NORMALISES  JEVEUX  -------------------------------
C
      PARAMETER(NBCMPM=10)
      CHARACTER*6  PGC
      CHARACTER*8  KBID, NOMG
      CHARACTER*19 PROFNO
      CHARACTER*24 PRNO,NUEQ
      INTEGER INVSK(NBNOT,2),IEQ(NBCMPM)
      REAL*8 VALE(*),TETSS(NBSS),TET0(NBCMPM,NBCMPM),UDEP(NBCMPM)
C
C-----------------------------------------------------------------------
      DATA PGC /'ROTCHM'/
C-----------------------------------------------------------------------
C
C------------------------RECUPERATION DU PRNO DEEQ NUEQ-----------------
C
      CALL JEMARQ()
C
C-----RECUPERATION DU NOMBRE DU NOMBRE D'ENTIERS CODES ASSOCIE A DEPL_R
C
      NOMG = 'DEPL_R'
      CALL DISMOI('F','NB_EC',NOMG,'GRANDEUR',NBEC,KBID,IERD)
      IF (NBEC.GT.10) THEN
         CALL U2MESS('F','MODELISA_94')
      ENDIF
C
      NUEQ=PROFNO//'.NUEQ'
      PRNO=PROFNO//'.PRNO'
C
      CALL JENONU(JEXNOM(PRNO(1:19)//'.LILI','&MAILLA'),IBID)
      CALL JEVEUO(JEXNUM(PRNO,IBID),'L',LLPRNO)
      CALL JEVEUO(NUEQ,'L',LLNUEQ)
C
C----------------------ALLOCATION VECTEUR DECODAGE----------------------
C
      CALL WKVECT('&&'//PGC//'.DECODAGE','V V I',NBCMP,LTIDEC)
C
C---------------------------ROTATION------------------------------------
C
      TETCOU=TETSS(1)
      CALL INTET0(TETCOU,TET0,IAX)
C
C
      DO 10 I=1,NBNOT
C
        NUMSEC=INVSK(I,1)
        TETAC=TETSS(NUMSEC)
        IF(TETAC.NE.TETCOU) THEN
          TETCOU=TETAC
          CALL INTET0(TETCOU,TET0,IAX)
        ENDIF
C
        INUEQ=ZI(LLPRNO+(NBEC+2)*(I-1))
        CALL ISDECO(ZI(LLPRNO+(NBEC+2)*(I-1)+2),ZI(LTIDEC),NBCMP)
        ICOMP=0
C
        DO 20 J=1,NBCMPM
          IF(ZI(LTIDEC+J-1).GT.0) THEN
            ICOMP=ICOMP+1
            IEQ(J)=ZI(LLNUEQ+INUEQ+ICOMP-2)
            UDEP(J)=VALE(IEQ(J))
          ELSE
            IEQ(J)=0
            UDEP(J)=0.D0
          ENDIF
 20     CONTINUE
C
C
        DO 30 J=1,NBCMPM
          IF(IEQ(J).GT.0) THEN
            VALE(IEQ(J))=0.D0
            DO 40 K=1,NBCMPM
              VALE(IEQ(J))=VALE(IEQ(J))+TET0(J,K)*UDEP(K)
 40         CONTINUE
          ENDIF
 30     CONTINUE
C
 10   CONTINUE
C
      CALL JEDETR('&&'//PGC//'.DECODAGE')
C
      CALL JEDEMA()
      END
