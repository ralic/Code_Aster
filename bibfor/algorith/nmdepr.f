      SUBROUTINE NMDEPR(MODELE,LIGREL,CARELE,CHMAT,CHARGE,ICHA,INSTAN,
     &                  RESUFV)
C MODIF ALGORITH  DATE 11/09/2002   AUTEUR VABHHTS J.PELLET 
C ======================================================================
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.

C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.

C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE VABHHTS J.PELLET
      IMPLICIT NONE
      CHARACTER*(*) MODELE,CARELE,CHMAT,RESUFV(3),LIGREL,CHARGE
      REAL*8 INSTAN
      INTEGER ICHA
C ----------------------------------------------------------------------
C   CALCUL DU SECOND MEMBRE ELEMENTAIRE CORRESPONDANT A EVOL_CHAR
C   POUR LA CHARGE CHARGE(ICHA)
C   SI IL N'Y A PAS DE EVOL_CHAR DANS CHARGE(ICHA):
C                             RESUFV N'EST PAS CALCULE

C IN  MODELE      : NOM DU MODELE
C IN  LIGREL      : (SOUS)-LIGREL DU MODELE
C IN  CARELE      : NOM DU CARA_ELEM
C IN  CHMAT       : NOM DU CHAM_MATER_CODE
C IN  CHARGE      : LISTE DES CHARGES
C IN  ICHA        : NUMERO DE LA CHARGE
C IN  INSTAN      : INSTANT DE LA DETERMINATION
C IN/JXOUT RESUFV : RESUELEM CORRESPONDANT AU CALCUL DE EVOL_CHAR
C                       RESULTATS POSSIBLE
C                            1 - VOLUMIQUE
C                            2 - SURFACIQUE
C                            3 - PRESSION

C --- DEBUT DECLARATIONS NORMALISEES JEVEUX ----------------------------
      CHARACTER*32 JEXNUM,JEXNOM,JEXR8,JEXATR
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)

C --- FIN DECLARATIONS NORMALISEES JEVEUX ------------------------------
      LOGICAL EXIGEO,EXICAR
      INTEGER IBID,IER,JCHAR,JFNOE,NBCHAM
      CHARACTER*8 FNOCAL,K8BID,LPAIN(6),PAOUT
      CHARACTER*16 TYSD,OPTION
      CHARACTER*19 CHFNOE,LCHIN(6)
      CHARACTER*24 CHGEOM,NOM24,CHCARA(15)

      CALL JEMARQ()
      CHFNOE = '&&NMDEPR.FNOE_CALC'

C     - 1. DETERMINATION DU CHAMP A L'INSTANT T
C     -----------------------------------------
      CALL JEVEUO(CHARGE,'L',JCHAR)

      NOM24 = ZK24(JCHAR+ICHA-1) (1:8)//'.CHME.EVOL.CHAR'
      CALL JEEXIN(NOM24,IER)
      IF (IER.EQ.0) GO TO 40

C -----------------------------------------------------
      CALL JEVEUO(NOM24,'L',JFNOE)
      FNOCAL = ZK8(JFNOE)

      CALL GETTCO(FNOCAL,TYSD)

      IF (TYSD.NE.'EVOL_CHAR') THEN
        CALL UTMESS('F','MEDEFV_02',' LE CONCEPT EVOL_CHAR : '//FNOCAL//
     &              ' N''EN EST PAS UN !')
        GO TO 40
      END IF

C     ----------------------------------
      CALL DISMOI('F','NB_CHAMP_UTI',FNOCAL,'RESULTAT',NBCHAM,K8BID,IER)

      IF (NBCHAM.LE.0) THEN
        CALL UTMESS('F','MEDEFV_01',' LE CONCEPT EVOL_CHAR : '//FNOCAL//
     &              ' NE CONTIENT AUCUN CHAMP DE TYPE'//' EVOL_CHAR.')
        GO TO 40
      END IF

C 1 - VOLUMIQUE
C     OPTION CHAR_MECA_FR2D2D OU CHAR_MECA_FR3D3D
C     -------------------------------------------
      OPTION = ' '
      CALL RSINCH(FNOCAL,'FVOL_3D','INST',INSTAN,CHFNOE,'EXCLU','EXCLU',
     &            0,'V',IER)
      IF (IER.LE.2) THEN
        OPTION = 'CHAR_MECA_FR3D3D'
        GO TO 10
      ELSE IF (IER.EQ.11 .OR. IER.EQ.12 .OR. IER.EQ.20) THEN
        CALL UTDEBM('F','NMDEPR','PB.INTERPOLATION VOLUMIQUE 3D:')
        CALL UTIMPK('L','EVOL_CHAR  :',1,FNOCAL)
        CALL UTIMPR('S','INSTANT    :',1,INSTAN)
        CALL UTIMPI('L','CODE_RETOUR:',1,IER)
        CALL UTFINM()
      END IF

      CALL RSINCH(FNOCAL,'FVOL_2D','INST',INSTAN,CHFNOE,'EXCLU','EXCLU',
     &            0,'V',IER)
      IF (IER.LE.2) THEN
        OPTION = 'CHAR_MECA_FR2D2D'
        GO TO 10
      ELSE IF (IER.EQ.11 .OR. IER.EQ.12 .OR. IER.EQ.20) THEN
        CALL UTDEBM('F','NMDEPR','PB.INTERPOLATION VOLUMIQUE 2D:')
        CALL UTIMPK('L','EVOL_CHAR  :',1,FNOCAL)
        CALL UTIMPR('S','INSTANT    :',1,INSTAN)
        CALL UTIMPI('L','CODE_RETOUR:',1,IER)
        CALL UTFINM()
      END IF

C     CALCUL DES OPTIONS : CHAR_MECA_FR2D2D OU CHAR_MECA_FR3D3D
C     ---------------------------------------------------------
   10 CONTINUE
      IF (OPTION.EQ.'CHAR_MECA_FR3D3D' .OR.
     &    OPTION.EQ.'CHAR_MECA_FR2D2D') THEN
        CALL MEGEOM(MODELE,' ',EXIGEO,CHGEOM)

        LPAIN(1) = 'PGEOMER'
        LCHIN(1) = CHGEOM
        LPAIN(2) = 'PNFORCER'
        LCHIN(2) = CHFNOE
        PAOUT = 'PVECTUR'

        CALL CORICH('E',RESUFV(1),ICHA,IBID)
        CALL CALCUL('S',OPTION,LIGREL,2,LCHIN,LPAIN,1,RESUFV(1),PAOUT,
     &              'V')
      END IF

C 2 - SURFACIQUE
C     OPTION CHAR_MECA_FR2D3D OU CHAR_MECA_FR1D2D
C     -------------------------------------------
      CALL RSINCH(FNOCAL,'FSUR_3D','INST',INSTAN,CHFNOE,'EXCLU','EXCLU',
     &            0,'V',IER)
      IF (IER.LE.2) THEN
        IF (OPTION.EQ.'CHAR_MECA_FR2D2D') THEN
          CALL UTDEBM('F','NMDEPR','PB.CHARGE VOL2D PUIS SURF3D:')
          CALL UTIMPK('L','EVOL_CHAR:',1,FNOCAL)
          CALL UTIMPR('S','INSTANT  :',1,INSTAN)
          CALL UTFINM()
        END IF
        OPTION = 'CHAR_MECA_FR2D3D'
        GO TO 20
      ELSE IF (IER.EQ.11 .OR. IER.EQ.12 .OR. IER.EQ.20) THEN
        CALL UTDEBM('F','NMDEPR','PB.INTERPOLATION SURFACIQUE 3D:')
        CALL UTIMPK('L','EVOL_CHAR  :',1,FNOCAL)
        CALL UTIMPR('S','INSTANT    :',1,INSTAN)
        CALL UTIMPI('L','CODE_RETOUR:',1,IER)
        CALL UTFINM()
      END IF

      CALL RSINCH(FNOCAL,'FSUR_2D','INST',INSTAN,CHFNOE,'EXCLU','EXCLU',
     &            0,'V',IER)
      IF (IER.LE.2) THEN
        IF (OPTION.EQ.'CHAR_MECA_FR3D3D') THEN
          CALL UTDEBM('F','NMDEPR','PB.CHARGE VOL3D PUIS SURF2D:')
          CALL UTIMPK('L','EVOL_CHAR:',1,FNOCAL)
          CALL UTIMPR('S','INSTANT  :',1,INSTAN)
          CALL UTFINM()
        END IF
        OPTION = 'CHAR_MECA_FR1D2D'
        GO TO 20
      ELSE IF (IER.EQ.11 .OR. IER.EQ.12 .OR. IER.EQ.20) THEN
        CALL UTDEBM('F','NMDEPR','PB.INTERPOLATION SURFACIQUE 2D:')
        CALL UTIMPK('L','EVOL_CHAR  :',1,FNOCAL)
        CALL UTIMPR('S','INSTANT    :',1,INSTAN)
        CALL UTIMPI('L','CODE_RETOUR:',1,IER)
        CALL UTFINM()
      END IF

C     CALCUL DES OPTIONS : CHAR_MECA_FR2D3D OU CHAR_MECA_FR1D2D
C     ---------------------------------------------------------
   20 CONTINUE
      IF (OPTION.EQ.'CHAR_MECA_FR2D3D' .OR.
     &    OPTION.EQ.'CHAR_MECA_FR1D2D') THEN
        CALL MEGEOM(MODELE,' ',EXIGEO,CHGEOM)

        LPAIN(1) = 'PGEOMER'
        LCHIN(1) = CHGEOM
        LPAIN(2) = 'PNFORCER'
        LCHIN(2) = CHFNOE
        PAOUT = 'PVECTUR'

        CALL CORICH('E',RESUFV(2),ICHA,IBID)
        CALL CALCUL('S',OPTION,LIGREL,2,LCHIN,LPAIN,1,RESUFV(2),PAOUT,
     &              'V')
      END IF

C 3 - PRESSION
C     OPTION CHAR_MECA_PRES_R
C     -----------------------
      CALL RSINCH(FNOCAL,'PRES','INST',INSTAN,CHFNOE,'EXCLU','EXCLU',0,
     &            'V',IER)
      IF (IER.LE.2) THEN
        OPTION = 'CHAR_MECA_PRES_R'
        GO TO 30
      ELSE IF (IER.EQ.11 .OR. IER.EQ.12 .OR. IER.EQ.20) THEN
        CALL UTDEBM('F','NMDEPR','PB. INTERPOLATION PRESSION:')
        CALL UTIMPK('L','EVOL_CHAR  :',1,FNOCAL)
        CALL UTIMPR('S','INSTANT    :',1,INSTAN)
        CALL UTIMPI('L','CODE_RETOUR:',1,IER)
        CALL UTFINM()
      END IF

C     CALCUL DE L'OPTION : CHAR_MECA_PRES_R
C     -------------------------------------
   30 CONTINUE
      IF (OPTION.EQ.'CHAR_MECA_PRES_R') THEN
        CALL MEGEOM(MODELE,' ',EXIGEO,CHGEOM)
        CALL MECARA(CARELE,EXICAR,CHCARA)

        LPAIN(1) = 'PGEOMER'
        LCHIN(1) = CHGEOM
        LPAIN(2) = 'PPRESSR'
        LCHIN(2) = CHFNOE
        LPAIN(3) = 'PCACOQU'
        LCHIN(3) = CHCARA(7)
        LPAIN(4) = 'PCAGEPO'
        LCHIN(4) = CHCARA(5)
        LPAIN(5) = 'PCAORIE'
        LCHIN(5) = CHCARA(1)
        LPAIN(6) = 'PNBSP_I'
        LCHIN(6) = CHCARA(1) (1:8)//'.CANBSP'
        PAOUT = 'PVECTUR'

        CALL CORICH('E',RESUFV(3),ICHA,IBID)
        CALL CALCUL('S',OPTION,LIGREL,6,LCHIN,LPAIN,1,RESUFV(3),PAOUT,
     &              'V')
      END IF


   40 CONTINUE
      CALL JEDEMA()
      END
