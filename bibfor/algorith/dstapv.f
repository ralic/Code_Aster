        SUBROUTINE DSTAPV(NBPT,D,T,DMIN,DMAX,
     +                    DMOY,DETYP,DRMS,SD,SDE,SD2)
C***********************************************************************
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 13/06/2012   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C
C       MOYENNAGE STATISTIQUE DES DEPLACEMENTS
C       ALGORITHME CALCUL TEMPOREL A PAS DE TEMPS VARIABLE
C
C
        IMPLICIT REAL*8 (A-H,O-Z)
      INCLUDE 'jeveux.h'
        REAL*8 D(*),T(*),DMOY,DETYP,DRMS,DMAX,DMIN,SD,SDE,SD2
C
C
      CALL JEMARQ()
C
C       ARGUMENTS:
C       ----------------------------------------
C       IN:
C            NBPT         NB DE POINTS DU TABLEAU A ANALYSER
C            D            TABLEAU A ANALYSER
C            T            TABLEAU DES INSTANTS CORRESPONDANTS
C
C       OUT:
C            DMOY        VALEUR MOYENNE ( COMPTAGE AU DESSUS DU SEUIL )
C            DETYP       ECART TYPE
C            DRMS        SQRT DE LA MOYENNE DES CARRES ( RMS )
C            DMAX        VALEUR MAXIMUM ABSOLU DU TABLEAU
C            DMIN        VALEUR MINIMUM ABSOLU DE LA FONCTION
C
C
C
C       VARIABLES UTILISEES
C       ----------------------------------------
C       SD SOMME DES VALEURS
C       SD2 SOMME DES CARRES DES VALEURS
C       SDD SOMME DES CARRES DES DIFFERENCES A LA MOYENNE
C
        CALL WKVECT('&&DSTAPV.TEMP.FCNT','V V R',NBPT,IFT)
C
        SD=0.D0
        SD2=0.D0
        DMOY = 0.D0
        DRMS = 0.D0
        DETYP = 0.D0
        DMAX=-10.D20
        DMIN=-DMAX
C
C --- RECHERCHE DES EXTREMAS ABSOLUS
C
        DO 10 I=1,NBPT
            IF (D(I).GT.DMAX)   DMAX=D(I)
            IF (D(I).LT.DMIN)   DMIN=D(I)
  10    CONTINUE
C
C --- DEPLACEMENT MOYEN
C
        DO 20 I=1,NBPT
            ZR(IFT + I-1) = D(I)
  20    CONTINUE
        CALL TRAPEZ(T,ZR(IFT),NBPT,SD)
        DMOY = SD / (T(NBPT) - T(1))
C
C --- DEPLACEMENT QUADRATIQUE MOYEN
C
        DO 30 I=1,NBPT
            ZR(IFT + I-1) = D(I)*D(I)
  30    CONTINUE
        CALL TRAPEZ(T,ZR(IFT),NBPT,SD2)
        DRMS = SQRT(SD2 / (T(NBPT) - T(1)))
C
C --- DEPLACEMENT QUADRATIQUE MOYEN (MOYENNE NULLE)
C
        DO 40 I=1,NBPT
            ZR(IFT + I-1) = (D(I)-DMOY)*(D(I)-DMOY)
  40    CONTINUE
        CALL TRAPEZ(T,ZR(IFT),NBPT,SDE)
        DETYP = SQRT(SDE / (T(NBPT) - T(1)))
C
C
        CALL JEDETR('&&DSTAPV.TEMP.FCNT')
        CALL JEDEMA()
        END
