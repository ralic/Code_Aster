      SUBROUTINE CONHEX(MACOR,NBCOR,MACOC,NBCOC,
     &                  LFACE,LOMODI,LOCORR,LOREOR,MA)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 08/02/2008   AUTEUR MACOCCO K.MACOCCO 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C
C  ROUTINE CONHEX
C    TRAITEMENT DE KTYC = HEXA20, KTYR = HEXA20, PENTA15, PYRAM13
C            ET DE KTYC = HEXA8 , KTYR = HEXA8 , PENTA6 , PYRAM5
C  DECLARATIONS
C    LOMODI : LOGICAL PRECISANT SI LA MAILLE EST UNE MAILLE MODIFIEE
C    MACOC  : TABLEAU DES NOMS DES NOEUDS   POUR UNE MAILLE FISSURE
C    MACOR  : TABLEAU DES NOMS DES NOEUDS   POUR UNE MAILLE REFERENCE
C    NBCOC  : NOMBRE DE CONNEX              POUR UNE MAILLE FISSURE
C    NBCOR  : NOMBRE DE CONNEX              POUR UNE MAILLE REFERENCE
C    NBLIC  : NOMBRE DE NOEUD TESTES        POUR UNE MAILLE FISSURE
C    NBLIR  : NOMBRE DE NOEUD TESTES        POUR UNE MAILLE REFERENCE
C    NBNOCO : NOMBRE DE NOEUD COMMUNS
C    NOCOC  : TABLEAU DE RANG DES NOEUDS    POUR UNE MAILLE FISSURE
C
C  MOT_CLEF : ORIE_FISSURE
C
C
      IMPLICIT REAL*8 (A-H,O-Z)
C
C     ------------------------------------------------------------------
C
      INTEGER      NBNOCO
      INTEGER      NBLIR,NBCOR
      INTEGER      NBLIC,NBCOC,NOCOC(8)
      INTEGER VALI
C
      CHARACTER*8  MACOR(NBCOR+2),MACOC(NBCOC+2),MA
C
      LOGICAL      LOMODI,LOCORR,LFACE,QUADRA,LOREOR
      LOGICAL      FACE
      FACE(I1,I2,I3,I4)=NOCOC(1).EQ.I1.AND.NOCOC(2).EQ.I2.AND.
     &                  NOCOC(3).EQ.I3.AND.NOCOC(4).EQ.I4
C
C     ------------------------------------------------------------------
C
      QUADRA=NBCOC.EQ.20
      IF (QUADRA) THEN
        IF (NBCOR.EQ.20) NBLIR=8
        IF (NBCOR.EQ.15) NBLIR=6
        IF (NBCOR.EQ.13) NBLIR=5
      ELSE
        NBLIR  = NBCOR
      ENDIF
      NBLIC  = 8
C
C     -----------------------------------------------------------------
C
      CALL CONCOM(MACOR,NBLIR,MACOC,NBLIC,NBNOCO,NOCOC)
C
      IF (NBNOCO.EQ.4) THEN
        IF (FACE(1,2,3,4).OR.FACE(5,6,7,8)) THEN
          LFACE=FACE(1,2,3,4)
          LOCORR=.TRUE.
        ELSE IF (FACE(1,2,5,6).OR.FACE(3,4,7,8)) THEN
C     -------------------------------------------------------------
C     SELON LE MODE 1 (ON TOURNE A 90 DEGRES AUTOUR DE AXE 1)
C     -------------------------------------------------------------
        LOMODI=.TRUE.
          LFACE=FACE(3,4,7,8)
          CALL CONPER(MACOC,1,4,8,5)
          CALL CONPER(MACOC,2,3,7,6)
          IF (QUADRA) THEN
            CALL CONPER(MACOC,10,15,18,14)
            CALL CONPER(MACOC, 9,11,19,17)
            CALL CONPER(MACOC,12,16,20,13)
          ENDIF
        ELSE IF (FACE(1,4,5,8).OR.FACE(2,3,6,7)) THEN
C     -------------------------------------------------------------
C     SELON LE MODE 2 (ON TOURNE A 90 DEGRES AUTOUR DE AXE 2)
C     -------------------------------------------------------------
        LOMODI=.TRUE.
          LFACE=FACE(2,3,6,7)
          CALL CONPER(MACOC,1,2,6,5)
          CALL CONPER(MACOC,4,3,7,8)
          IF (QUADRA) THEN
            CALL CONPER(MACOC, 9,14,17,13)
            CALL CONPER(MACOC,12,10,18,20)
            CALL CONPER(MACOC,11,15,19,16)
          ENDIF
        ELSE
          CALL ASSERT(.FALSE.)
        ENDIF
        IF (LFACE) THEN
          I1=1
          I2=2
          I3=3
        ELSE
          I1=8
          I2=7
          I3=6
        ENDIF
        CALL CONORS(I1,I2,I3,MACOC,NBCOC,MACOR,NBCOR,LOREOR,MA)
        IF (LOREOR) THEN
          CALL CONECH(MACOC,1,5)
          CALL CONECH(MACOC,2,6)
          CALL CONECH(MACOC,3,7)
          CALL CONECH(MACOC,4,8)
          IF (QUADRA) THEN
            CALL CONECH(MACOC, 9,17)
            CALL CONECH(MACOC,10,18)
            CALL CONECH(MACOC,11,19)
            CALL CONECH(MACOC,12,20)
          ENDIF
        ENDIF
        CALL CONJAC(1,2,4,5,MACOC,NBCOC,MA)
        CALL CONJAC(2,1,6,3,MACOC,NBCOC,MA)
        CALL CONJAC(3,2,7,4,MACOC,NBCOC,MA)
        CALL CONJAC(4,1,3,8,MACOC,NBCOC,MA)
        CALL CONJAC(5,1,8,6,MACOC,NBCOC,MA)
        CALL CONJAC(6,2,5,7,MACOC,NBCOC,MA)
        CALL CONJAC(7,3,6,8,MACOC,NBCOC,MA)
        CALL CONJAC(8,4,7,5,MACOC,NBCOC,MA)
C
      ELSE IF (NBNOCO.GT.2) THEN
         VALI = NBNOCO
         CALL U2MESG('E', 'ALGORITH12_59',0,' ',1,VALI,0,0.D0)
      ENDIF
C
      END
