      SUBROUTINE NMASSC (ICODMA,
     &                   TYPPAR,INST,TEMP,BL2,
     &                   COELMA,COELEM,COEVIL,COECYR,COEEPR,COEINT,
     &                   ICAS)
C ----------------------------------------------------------------------
      IMPLICIT NONE
      
      INTEGER     ICODMA
      CHARACTER*2 TYPPAR,BL2
      REAL*8      INST,TEMP
      INTEGER    NBCLMA, NBCLEM, NBCVIL, NBCCYR,NBCEPR,NBCINT
      PARAMETER (NBCLMA=12, NBCLEM=7, NBCVIL=5, NBCCYR=3, NBCEPR=3,
     &           NBCINT=2)
      REAL*8     COELMA(NBCLMA),COELEM(NBCLEM),COEVIL(NBCVIL)
      REAL*8     COECYR(NBCCYR),COEEPR(NBCEPR),COEINT(NBCINT)
      INTEGER    ICAS
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 16/12/2004   AUTEUR VABHHTS J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C
C     RECUPERATION DES CARACTERISTIQUES DU MATERIAU POUR LES ELEMENTS
C     DE POUTRE DANS LE CAS DES COMPORTEMENTS D'ASSEMBLAGE DE
C     COMBUSTIBLE.
C ----------------------------------------------------------------------
C
C IN  ICODMA : ADRESSE DU MATERIAU CODE
C IN  TYPPAR : TYPE DE PARAMETRE SCRUTE
C               '  ' (DEUX BLANCS): PAS DE PARAMETRES
C               'T ' (T/BLANC):     TEMPS MAIS PAS DE TEMPERATURE
C               ' T' (BLANC/T):     TEMPERATURE MAIS PAS DE TEMPS
C               'TT' (T/T):         TEMPERATURE ET TEMPS
C IN  INST   : INSTANT CONSIDERE
C IN  TEMP   : TEMPERATURE CONSIDEREE
C IN  BL2    : CODE POUR RCVALA

C OUT COELMA : COEFFICIENTS POUR LA LOI DE FLUAGE DE LMARC
C OUT COELEM : COEFFICIENTS POUR LA LOI DE FLUAGE DE LEMAITRE
C OUT COEVIL : COEFFICIENTS POUR LA LOI DE VISC_IRRA_LOG
C OUT COECYR : COEFFICIENTS POUR LA LOI DE ZIRC_CYRA2
C OUT COEEPR : COEFFICIENTS POUR LA LOI DE ZIRC_EPRI
C OUT COEINT : COEFFICIENTS POUR LA LOI LEMA_SEUIL
C OUT ICAS   : FLAG MATERIAU
C         1 LOI DE TYPE LMARC
C         2 LOI DE TYPE LEMAITRE AVEC FLUAGE SOUS IRRADIATION
C         3 LOI DE TYPE VISC_IRRA_LOG
C         4 LOI DE TYPE ZIRC_CYRA2
C         5 LOI DE TYPE ZIRC_EPRI
C         6 LOI DE TYPE LEMA_SEUIL
C
C *************** DECLARATION DES VARIABLES LOCALES ********************
C
      INTEGER      NBPAR, NBRES
      REAL*8       VALPAR(2)
      CHARACTER*8  NOMPAR(2)
      REAL*8       VALLMA(NBCLMA),VALLEM(NBCLEM),VALVIL(NBCVIL)
      REAL*8       VALCYR(NBCCYR),VALEPR(NBCEPR),VALINT(NBCINT)
      CHARACTER*2  CODLMA(NBCLMA),CODLEM(NBCLEM),CODVIL(NBCVIL)
      CHARACTER*2  CODCYR(NBCCYR),CODEPR(NBCEPR),CODINT(NBCINT)
      CHARACTER*8  NOMLMA(NBCLMA),NOMLEM(NBCLEM),NOMVIL(NBCVIL) 
      CHARACTER*8  NOMCYR(NBCCYR),NOMEPR(NBCEPR),NOMINT(NBCINT)
C
C *********** FIN DES DECLARATIONS DES VARIABLES LOCALES ***************
C
C ****************************** DATA  *********************************
C
      DATA NOMLMA / 'K','N','DE_0','P','P1','P2','M','RM',
     &              'A_0','Y_0','Y_I','B'/
      DATA NOMLEM / 'N', 'UN_SUR_K', 'UN_SUR_M', 'QSR_K',
     &              'BETA','PHI_ZERO','L'/     
      DATA NOMVIL / 'A', 'B', 'CSTE_TPS', 'ENER_ACT', 'FLUX_PHI'/
      DATA NOMCYR / 'EPSI_FAB', 'TEMP_RECUIT', 'FLUX_PHI'/
      DATA NOMEPR / 'FLUX_PHI', 'R_P', 'THETA_MAX'/
      DATA NOMINT / 'A',         'S'/

     
C
C ********************* DEBUT DE LA SUBROUTINE *************************
C
C --- INITIALISATIONS
C
      ICAS = 0
      CALL R8INIR (NBCLMA,0.D0,COELMA,1)
      CALL R8INIR (NBCLEM,0.D0,COELEM,1)
      CALL R8INIR (NBCVIL,0.D0,COEVIL,1)
      CALL R8INIR (NBCCYR,0.D0,COECYR,1)
      CALL R8INIR (NBCEPR,0.D0,VALEPR,1) 
      CALL R8INIR (NBCINT,0.D0,COEINT,1)

      CALL R8INIR (NBCLMA,0.D0,VALLMA,1)
      CALL R8INIR (NBCLEM,0.D0,VALLEM,1)
      CALL R8INIR (NBCVIL,0.D0,VALVIL,1) 
      CALL R8INIR (NBCCYR,0.D0,VALCYR,1) 
      CALL R8INIR (NBCEPR,0.D0,VALEPR,1) 
      CALL R8INIR (NBCINT,0.D0,VALINT,1) 
    
C INIT DES PARAMETRES
      NBPAR  = 0
      NOMPAR(1) = ' '
      NOMPAR(2) = ' '
      IF (TYPPAR(1:1).EQ.'T') THEN
       NBPAR = NBPAR +1
       NOMPAR(1) = 'INST'
       VALPAR(1) = INST
      ENDIF
      IF (TYPPAR(2:2).EQ.'T') THEN
       NBPAR = NBPAR +1
       NOMPAR(2) = 'TEMP'
       VALPAR(2) = TEMP
      ENDIF
C LECTURE
      NBRES = NBCLMA
      CALL RCVALA(ICODMA,' ','LMARC',NBPAR,NOMPAR,VALPAR,
     &              NBRES,NOMLMA(1),VALLMA(1),CODLMA(1), BL2 )
      NBRES = 3
      CALL RCVALA(ICODMA,' ','LEMAITRE',NBPAR,NOMPAR,VALPAR,
     &              NBRES,NOMLEM(1),VALLEM(1),CODLEM(1), BL2 )
      NBRES = 4
      CALL RCVALA(ICODMA,' ','FLU_IRRA',NBPAR,NOMPAR,VALPAR,
     &              NBRES,NOMLEM(4),VALLEM(4),CODLEM(2), BL2 )
      NBRES = NBCVIL
      CALL RCVALA(ICODMA,' ','VISC_IRRA_',NBPAR,NOMPAR,VALPAR,
     &              NBRES,NOMVIL(1),VALVIL(1),CODVIL(1), BL2 )
      NBRES = NBCCYR
      CALL RCVALA(ICODMA,' ','ZIRC_CYRA2',NBPAR,NOMPAR,VALPAR,
     &              NBRES,NOMCYR(1),VALCYR(1),CODCYR(1), BL2 )
      NBRES = NBCEPR
      CALL RCVALA(ICODMA,' ','ZIRC_EPRI',NBPAR,NOMPAR,VALPAR,
     &              NBRES,NOMEPR(1),VALEPR(1),CODEPR(1), BL2 )
C          
      NBRES = NBCINT
      CALL RCVALA(ICODMA,' ','LEMA_SEUIL',NBPAR,NOMPAR,VALPAR,
     &              NBRES,NOMINT(1),VALINT(1),CODINT(1), BL2 )
C          
      IF (CODLMA(1).EQ.'OK'.AND.
     &    CODLEM(1).NE.'OK'.AND.
     &    CODVIL(1).NE.'OK'.AND.
     &    CODCYR(1).NE.'OK'.AND.
     &    CODEPR(1).NE.'OK'.AND.
     &    CODINT(1).NE.'OK') THEN     
         CALL DCOPY (NBCLMA,VALLMA,1,COELMA,1)
         ICAS = 1
      ELSEIF (CODLMA(1).NE.'OK'.AND.
     &    CODLEM(1).EQ.'OK'.AND.
     &    CODVIL(1).NE.'OK'.AND.
     &    CODCYR(1).NE.'OK'.AND.
     &    CODEPR(1).NE.'OK'.AND.
     &    CODINT(1).NE.'OK') THEN   
         CALL DCOPY (NBCLEM,VALLEM,1,COELEM,1)
         ICAS = 2
      ELSEIF (CODLMA(1).NE.'OK'.AND.
     &    CODLEM(1).NE.'OK'.AND.
     &    CODVIL(1).EQ.'OK'.AND.
     &    CODCYR(1).NE.'OK'.AND.
     &    CODEPR(1).NE.'OK'.AND.
     &    CODINT(1).NE.'OK') THEN      
         CALL DCOPY (NBCVIL,VALVIL,1,COEVIL,1)
         ICAS = 3       
      ELSEIF (CODLMA(1).NE.'OK'.AND.
     &    CODLEM(1).NE.'OK'.AND.
     &    CODVIL(1).NE.'OK'.AND.
     &    CODCYR(1).EQ.'OK'.AND.
     &    CODEPR(1).NE.'OK'.AND.
     &    CODINT(1).NE.'OK') THEN    
         CALL DCOPY (NBCCYR,VALCYR,1,COECYR,1)
         ICAS = 4   
      ELSEIF (CODLMA(1).NE.'OK'.AND.
     &    CODLEM(1).NE.'OK'.AND.
     &    CODVIL(1).NE.'OK'.AND.
     &    CODCYR(1).NE.'OK'.AND.
     &    CODEPR(1).EQ.'OK'.AND.
     &    CODINT(1).NE.'OK') THEN    
         CALL DCOPY (NBCEPR,VALEPR,1,COEEPR,1)
         ICAS = 5       
      ELSEIF (CODLMA(1).NE.'OK'.AND.
     &    CODLEM(1).NE.'OK'.AND.
     &    CODVIL(1).NE.'OK'.AND.
     &    CODCYR(1).NE.'OK'.AND.
     &    CODEPR(1).NE.'OK'.AND.
     &    CODINT(1).EQ.'OK') THEN    
         CALL DCOPY (NBCINT,VALINT,1,COEINT,1)
         ICAS = 6       
      ELSE
         CALL UTMESS ('F','NMASSC',
     &  'DEFINITION MULTIPLE DU COMPORTEMENT POUR UN ELEMENT DE POUTRE')
      ENDIF
      END
