      SUBROUTINE CYNUPL(PROFNO,INDIRF,MODCYC,MAILSK,NBSEC,NBMCAL)
C-----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 24/07/2012   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
      IMPLICIT NONE
C
C***********************************************************************
C    O. NICOLAS     DATE 10/01/06
C-----------------------------------------------------------------------
C  BUT: < CYCLIQUE NUMEROTATION GLOBALE >
C
C    CREER A PARTIR D'UN RESULTAT CYCLIQUE ET UN MAILLAGE GLOBAL
C  SQUELLETTE LE PROFIL CHAMNO ET UNE FAMILLE NUMEROTEE
C ,DONT CHAQUE OBJET CORRESPOND A UN SECTEUR, ET EST DIMENSIONNE
C  A 2*NBDDL ENGENDRE PAR LE SECTEUR:C
C
C            2*(I-1)+1 --> NUMERO EQUATION DANS PFCHNO SECTEUR
C            2*(I-1)+2 --> NUMERO EQUATION DANS PROFNO GLOBAL
C
C-----------------------------------------------------------------------
C
C NOM----- / /:
C
C PROFNO   /I/: NOM K19 DU PROF_CHNO A CREER
C INDIRF   /I/: NOM K24 DE LA FAMILLE DES INDIRECTIONS A CREER
C MODCYC   /I/: NOM DU RESULTAT CYCLIQUE EN AMONT
C MAILSK   /I/: NOM DU MAILLAGE SKELETTE
C NBSEC    /I/: NBRE DE SECTEUR
C
C
C
C
      INCLUDE 'jeveux.h'
C
C
C-----------------------------------------------------------------------
      INTEGER I ,IAD ,IBID ,ICOMP ,IEC ,IEQ ,IER
      INTEGER IPOINT ,IRET ,J ,LDDEEQ ,LDNUEQ ,LDPRNO ,LINUEQ
      INTEGER LLINSK ,LLNUEQ ,LLPRNO ,LTINSE ,LTTDS ,NBCMP ,NBCPMX
      INTEGER NBDDL ,NBMCAL ,NBNOT ,NBSEC ,NDDLT ,NEQSEC ,NSECPR
      INTEGER NTAIL ,NUGD ,NUMNOS ,NUMSEC
C-----------------------------------------------------------------------
      PARAMETER    (NBCPMX=300)
      CHARACTER*6  PGC
      CHARACTER*8  MODCYC,MAILSK,NOMGD
      CHARACTER*8  K8BID
      CHARACTER*19 PFCHNO,PROFNO, CHAMNO
      CHARACTER*24 INDIRF,LILI,PRNO,DEEQ,NUEQ
      INTEGER      IDEC(NBCPMX),NBEC,NEC
C
C-----------------------------------------------------------------------
C
      CALL JEMARQ()
      PGC='CYNUPL'
C
C
C----------------RECUPERATION DU PROF_CHNO:
      CALL RSEXCH('F',MODCYC,'DEPL',1,CHAMNO,IER)
      CALL DISMOI('F','PROF_CHNO',CHAMNO,'CHAM_NO',IBID,PFCHNO,IRET)
C
C---------------RECUPERATION DU NOMBRE DE COMPOSANTES-------------------
C
C     -- QUESTION "POURRIE" :
      CALL DISMOI('F','NOM_GD',PFCHNO,'PROF_CHNO',IBID,NOMGD,IRET)
      CALL DISMOI('F','NB_CMP_MAX',NOMGD,'GRANDEUR',NBCMP,K8BID,IRET)
      CALL JENONU(JEXNOM('&CATA.GD.NOMGD',NOMGD),NUGD)
      NEC  = NBEC(NUGD)
      IF (NEC.GT.10) THEN
        CALL U2MESS('F','MODELISA_94')
      ENDIF

C
C-------------RECUPERATION DIMENSION MAILLAGE SQUELETTE-----------------
C
      CALL DISMOI('F','NB_NO_MAILLA',MAILSK,'MAILLAGE',NBNOT,
     &             K8BID,IRET)
C
C------------RECUPERATION DU .INV.SKELETON------------------------------
C
      CALL JEVEUO(MAILSK//'.INV.SKELETON','L',LLINSK)
C
C--------------RECUPERATION DU PRNO DU SECTEUR--------------------------
C
      CALL JENONU(JEXNOM(PFCHNO//'.LILI','&MAILLA'),IBID)
      CALL JEVEUO(JEXNUM(PFCHNO//'.PRNO',IBID),'L',LLPRNO)
      CALL JEVEUO(PFCHNO//'.NUEQ','L',LLNUEQ)
      CALL DISMOI('F','NB_EQUA',PFCHNO,'PROF_CHNO',NEQSEC,K8BID,IRET)
C
C--------------------ALLOCATION DU VECTEUR DE TRAVAIL-------------------
C     POUR STOCKAGE NOMBRE DE DDL GLOBAUX ENGENDRE PAR SECTEUR
C
      CALL WKVECT('&&'//PGC//'.TAIL.DDL.SECT','V V IS',NBSEC,LTTDS)
C
C--------------BOUCLE DE COMPTAGE DES DDL FINAUX------------------------
C
      NDDLT=0
      DO 10 I=1,NBNOT
        NUMSEC=ZI(LLINSK+I-1)
        NUMNOS=ZI(LLINSK+NBNOT+I-1)
        NDDLT=NDDLT+ZI(LLPRNO+(NUMNOS-1)*(2+NEC)+1)
        ZI(LTTDS+NUMSEC-1)=ZI(LTTDS+NUMSEC-1)+
     &                     ZI(LLPRNO+(NUMNOS-1)*(2+NEC)+1)
 10   CONTINUE
C
C-----------------ALLOCATION DES DIVERS OBJETS--------------------------
C
      LILI=PROFNO//'.LILI'
      PRNO=PROFNO//'.PRNO'
      DEEQ=PROFNO//'.DEEQ'
      NUEQ=PROFNO//'.NUEQ'
C
      CALL JECREO(LILI,'G N K24')
      CALL JEECRA(LILI,'NOMMAX',2,' ')
C
      CALL WKVECT(DEEQ,'G V I',NDDLT*2,LDDEEQ)
C
      CALL WKVECT(NUEQ,'G V I',NDDLT,LDNUEQ)
C
      CALL JECREC(PRNO,'G V I','NU','CONTIG','VARIABLE',2)
C
C
      CALL JECROC(JEXNOM(PRNO(1:19)//'.LILI','&MAILLA'))
      CALL JECROC(JEXNOM(PRNO(1:19)//'.LILI','LIAISONS'))
C
C
      CALL JECREC(INDIRF,'V V I','NU',
     &'DISPERSE','VARIABLE',NBSEC)
C
C
      DO 20 I=1,NBSEC
        CALL JECROC(JEXNUM(INDIRF,I))
        NTAIL=2*ZI(LTTDS+I-1)
        CALL JEECRA(JEXNUM(INDIRF,I),'LONMAX',NTAIL,' ')
        ZI(LTTDS+I-1)=0
 20   CONTINUE
C
C
C---------------REMPLISSAGE DES OBJETS EVIDENTS-------------------------
C
      CALL JEECRA(JEXNUM(PRNO,1),'LONMAX',NBNOT*(2+NEC),K8BID)
      CALL JEECRA(JEXNUM(PRNO,2),'LONMAX',1,K8BID)
      CALL JEECRA(PRNO,'LONT',NBNOT*(2+NEC)+1,' ')
C
C-------------------------REMPLISSAGE DES OBJETS------------------------
C
      CALL JENONU(JEXNOM(PRNO(1:19)//'.LILI','&MAILLA'),IBID)
      CALL JEVEUO(JEXNUM(PRNO,IBID),'E',LDPRNO)
C
      NSECPR=1
      CALL JEVEUO(JEXNUM(INDIRF,NSECPR),'E',LTINSE)
      ICOMP=0
      DO 30 I=1,NBNOT
C
        NUMSEC=ZI(LLINSK+I-1)
        NUMNOS=ZI(LLINSK+NBNOT+I-1)
        IEQ=ZI(LLPRNO+(NUMNOS-1)*(2+NEC))
        NBDDL=ZI(LLPRNO+(NUMNOS-1)*(2+NEC)+1)
        CALL ISDECO(ZI(LLPRNO+(NUMNOS-1)*(2+NEC)+2),IDEC,NBCMP)
C
        ZI(LDPRNO+(I-1)*(2+NEC))=ICOMP+1
        ZI(LDPRNO+(I-1)*(2+NEC)+1)=NBDDL
        DO 40 IEC = 1, NEC
          ZI(LDPRNO+(I-1)*(2+NEC)+1+IEC)=
     &                        ZI(LLPRNO+(NUMNOS-1)*(2+NEC)+1+IEC)
  40    CONTINUE
        IF(NUMSEC.NE.NSECPR) THEN
          CALL JELIBE(JEXNUM(INDIRF,NSECPR))
          NSECPR=NUMSEC
          CALL JEVEUO(JEXNUM(INDIRF,NSECPR),'E',LTINSE)
        ENDIF
        IAD=0
        DO 50 J=1,NBCMP
          IF(IDEC(J).GT.0) THEN
            IAD=IAD+1
            ICOMP=ICOMP+1
            ZI(LDDEEQ+(ICOMP-1)*2)=I
            ZI(LDDEEQ+(ICOMP-1)*2+1)=J
            ZI(LDNUEQ+ICOMP-1)=ICOMP
            LINUEQ=ZI(LLNUEQ+IEQ+IAD-2)
            IPOINT=LTINSE-1+2*ZI(LTTDS+NUMSEC-1)
            ZI(IPOINT+1)=LINUEQ
            ZI(IPOINT+2)=ICOMP
            ZI(LTTDS+NUMSEC-1)=ZI(LTTDS+NUMSEC-1)+1
          ENDIF
 50     CONTINUE
 30   CONTINUE
C
      CALL JELIBE(JEXNUM(INDIRF,NSECPR))
C
C------------------SAUVEGARDE DES OBJETS--------------------------------
C
      CALL JEDETR('&&'//PGC//'.TAIL.DDL.SECT')
C
      CALL JEDEMA()
      END
