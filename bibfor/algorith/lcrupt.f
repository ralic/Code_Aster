      SUBROUTINE LCRUPT (NDIM, TYPMOD, IMATE, TEMP, TREF, EPSM, DEPS,
     &                   VIM, OPTION, SIG, VIP, DSIDEP)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 29/09/2006   AUTEUR VABHHTS J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
      IMPLICIT NONE
      CHARACTER*8        TYPMOD(2)
      CHARACTER*16       OPTION
      INTEGER            NDIM, IMATE
      REAL*8             TEMP, TREF, EPSM(6), DEPS(6), VIM(0:*)
      REAL*8             SIG(6), VIP(0:*), DSIDEP(6,6)
C ----------------------------------------------------------------------
C     LOI DE COMPORTEMENT RUPTURE FRAGILE (EN DELOCALISE)
C
C IN  NDIM    : DIMENSION DE L'ESPACE
C IN  TYPMOD  : TYPE DE MODELISATION
C IN  IMATE   : NATURE DU MATERIAU
C IN  TEMP    : TEMPERATURE EN T+
C IN  TREF    : TEMPERATURE DE REFERENCE
C IN  EPSM    : DEFORMATION EN T-
C IN  DEPS    : INCREMENT DE DEFORMATION
C IN  VIM     : VARIABLES INTERNES EN T-
C IN  OPTION  : OPTION DEMANDEE
C                 RIGI_MECA_TANG ->     DSIDEP
C                 FULL_MECA      -> SIG DSIDEP VIP
C                 RAPH_MECA      -> SIG        VIP
C OUT SIG     : CONTRAINTE
C IN  VIP     : ENDOMMAGEMENT EN T+
C                 0   -> VALEUR DE L'ENDOMMAGEMENT
C                 1:3 -> GRADIENT D'ENDOMMAGEMENT
C OUT DSIDEP  : MATRICE SECANTE
C ----------------------------------------------------------------------
C LES OPTIONS RIGI_MECA_TANG ET FULL_MECA NE SONT PAS ENCORE TRAITEES
C ----------------------------------------------------------------------
C OUT RUFRC0 COMMON CARACTERISTIQUES DU MATERIAU (AFFECTE DANS RUFRMA)

      REAL*8 E, NU, ALPHA, LAMBDA, DEUXMU
      REAL*8 GC
      REAL*8 C, RIGMIN

      COMMON /RUFRC0/
     &         E  , NU , ALPHA , LAMBDA , DEUXMU ,
     &         GC ,
     &         C  , RIGMIN
C ----------------------------------------------------------------------

      LOGICAL     CPLAN, RIGI
      INTEGER     NDIMSI, K
      REAL*8      KRON(6), TREPS
      REAL*8      EPS(6), COPLAN
      REAL*8      VARLOC(0:3), RIG


C    INITIALISATION

      DATA  KRON/1.D0,1.D0,1.D0,0.D0,0.D0,0.D0/
      RIGI  = (OPTION(1:4).EQ.'RIGI' .OR. OPTION(1:4).EQ.'FULL')
      CPLAN = (TYPMOD(1).EQ.'C_PLAN  ')
      NDIMSI = 2*NDIM


C    AFFECTATION DE LA VARIABLE LOCALISEE (VIP CAR RAPH_MECA)

      CALL DCOPY(NDIM+1,VIP,1,VARLOC,1)


C    LECTURE DES CARACTERISTIQUES DU MATERIAU

      CALL RUFRMA(IMATE,TEMP)


C    MISE A JOUR DES DEFORMATIONS MECANIQUES

      DO 10 K = 1, NDIMSI
        EPS(K) = EPSM(K) + DEPS(K)
 10   CONTINUE

      IF (CPLAN) THEN
        COPLAN  = - NU/(1.D0-NU)
        EPS(3) = COPLAN * (EPS(1)+EPS(2))
      END IF


C    CALCUL DE L'ETAT DE RIGIDITE
      RIG = (1 - VARLOC(0))**2 + RIGMIN


C    RIGIDITE

      LAMBDA = RIG * LAMBDA
      DEUXMU = RIG * DEUXMU


C    CALCUL DES CONTRAINTES

      TREPS = EPS(1)+EPS(2)+EPS(3)
        DO 20 K=1, NDIMSI
          SIG(K) = LAMBDA * TREPS * KRON(K) + DEUXMU*EPS(K)
 20     CONTINUE


C - CALCUL DE LA MATRICE DE RIGIDITE SECANTE

      IF (RIGI) THEN
        CALL U2MESS('F','ALGORITH4_93')
      END IF

      END
