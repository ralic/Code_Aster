      SUBROUTINE VETNTH(OPTIOZ,MODELZ,CARELZ,MATCDZ,INSTZ,CHTNZ,COMPOZ,
     &                  TPCHIZ,TPCHFZ,CHHYZ,VECELZ,VECEIZ)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 16/01/2012   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.

C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.

C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C ----------------------------------------------------------------------
C CALCUL DES VECTEURS ELEMENTAIRES DUS AU CHAMP A L'INSTANT PRECEDENT

C IN  OPTIOZ  : OPTION CALCULEE: CHAR_THER_EVOL
C                                CHAR_THER_EVOLNI
C IN  MODELZ : NOM DU MODELE
C IN  CARELZ : CHAMP DE CARA_ELEM
C IN  MATCDZ : MATERIAU CODE
C IN  INSTZ  : CARTE CONTENANT LA VALEUR DU TEMPS
C IN  CHHYZ  : CHAMP D HYDRATATION A L'INSTANT PRECEDENT
C . POUR LE CALCUL DE LA TEMPERATURE :
C IN  CHTNZ  : CHAMP DE TEMPERATURE A L'INSTANT PRECEDENT
C OUT VECEL/VECEI : VECT_ELEM
C   -------------------------------------------------------------------
C     SUBROUTINES APPELLEES:
C       MESSAGE:INFNIV.
C       JEVEUX:JEMARQ,JEDEMA,JEEXIN,WKVECT,JEVEUO,JEECRA.
C       CALCUL: CALCUL.
C       FICH COMM: GETRES.
C       MANIP SD: RSEXCH,MEGEOM,MECARA,EXISD.
C       DIVERS: GCNCO2,CORICH.

C     FONCTIONS INTRINSEQUES:
C       AUCUNE.
C----------------------------------------------------------------------
C CORPS DU PROGRAMME
      IMPLICIT   NONE

C 0.1. ==> ARGUMENTS

      CHARACTER*(*) OPTIOZ,MODELZ,CARELZ,MATCDZ,INSTZ,CHTNZ,VECELZ,
     &              VECEIZ,COMPOZ,TPCHIZ,TPCHFZ,CHHYZ

C 0.2. ==> COMMUNS
C --- DEBUT DECLARATIONS NORMALISEES JEVEUX ----------------------------

      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C --- FIN DECLARATIONS NORMALISEES JEVEUX ------------------------------

C 0.3. ==> VARIABLES LOCALES

      CHARACTER*6 NOMPRO
      PARAMETER (NOMPRO='VETNTH')
      INTEGER NCHINX,NCHOUX
      PARAMETER (NCHINX=15,NCHOUX=2)
      INTEGER IRET,IBID,NCHIN,NCHOUT,I,IFM,NIV
      CHARACTER*8 LPAIN(NCHINX),LPAOUT(NCHOUX),NEWNOM
      CHARACTER*16 OPTION
      CHARACTER*24 MODELE,CARELE,MATCOD,INST,CHTN,VECEL,VECEI,COMPOR,
     &             TMPCHI,TMPCHF,CHHY,LIGRMO,LCHIN(NCHINX),CHGEOM,
     &             LCHOUT(NCHOUX),CHCARA(18)
      LOGICAL EXIGEO,EXICAR,LNLIN

C DEB ------------------------------------------------------------------
C====
C 1.1 PREALABLES LIES AUX OPTIONS
C====
      CALL INFNIV(IFM,NIV)
      DO 10 I = 1,NCHINX
        LPAIN(I) = '        '
        LCHIN(I) = '                        '
   10 CONTINUE
      CALL JEMARQ()
      NEWNOM = '.0000000'
      OPTION = OPTIOZ
      MODELE = MODELZ
      CARELE = CARELZ
      MATCOD = MATCDZ
      INST = INSTZ
      CHTN = CHTNZ
      VECEL = VECELZ
      VECEI = VECEIZ
      COMPOR = COMPOZ
      TMPCHI = TPCHIZ
      TMPCHF = TPCHFZ
      CHHY = CHHYZ
      LNLIN = .FALSE.
      IF (OPTION.EQ.'CHAR_THER_EVOLNI') THEN
        LNLIN = .TRUE.
      END IF
C AFFICHAGE
      IF (NIV.EQ.2) THEN
        WRITE (IFM,*) '*******************************************'
        WRITE (IFM,*) ' CALCUL DE SECOND MEMBRE THERMIQUE: VETNTH'
        WRITE (IFM,*) ' CHAMP MATERIAU CODE  : ',MATCOD
      END IF

C====
C 1.2 PREALABLES LIES AU VECT_ELEM VECEL
C====

C RECHERCHE DU CHAMP DE GEOMETRIE CHGEOM ASSOCIE AU MODELE
      CALL MEGEOM(MODELE,'      ',EXIGEO,CHGEOM)
C RECHERCHE DES NOMS DES CARAELEM CHCARA DANS LA CARTE CARELE
      CALL MECARA(CARELE,EXICAR,CHCARA)
C TEST D'EXISTENCE DE L'OBJET JEVEUX VECEL
      CALL JEEXIN(VECEL,IRET)
      IF (IRET.EQ.0) THEN
C L'OBJET VECEL N'EXISTE PAS
C CREATION DE L'OBJET '.REFE_RESU' DE VECEL ASSOCIE AU MODELE, A
C MATERIAU MATCODE, AU CARAELEM CARELE ET A LA SUR_OPTION 'MASS_THER'
        VECEL = '&&'//NOMPRO//'           .RELR'
        CALL MEMARE('V',VECEL,MODELE(1:8),MATCOD,CARELE,
     &              'MASS_THER')
      ELSE
C L'OBJET EXISTE
        CALL JEDETR(VECEL)
      END IF
      LIGRMO = MODELE(1:8)//'.MODELE'

C====
C 1.3 PREALABLES LIES A L OPTION 'CHAR_THER_EVOLNI'
C====

      IF (LNLIN) THEN
        CALL JEEXIN(VECEI,IRET)
        IF (IRET.EQ.0) THEN
          VECEI = '&&VETNTI           .RELR'
          CALL MEMARE('V',VECEI(1:8),MODELE(1:8),MATCOD,CARELE,
     &                'MASS_THER')
        ELSE
          CALL JEDETR(VECEI)
        END IF
      END IF

C====
C 2. PREPARATION DES CALCULS ELEMENTAIRES (TRONC COMMUN EN IN)
C====

C CHAMP LOCAL CONTENANT LA CARTE DES NOEUDS (X Y Z)
      LPAIN(1) = 'PGEOMER'
      LCHIN(1) = CHGEOM
C ... LE CHAM_NO DE TEMPERATURE OU DE LA DERIVEE A L'INSTANT
C     PRECEDENT (TEMP)
      LPAIN(2) = 'PTEMPER'
      LCHIN(2) = CHTN
C ... LA CARTE MATERIAU (I1) STD
      LPAIN(3) = 'PMATERC'
      LCHIN(3) = MATCOD
C ... LA CARTE DES INSTANTS (INST DELTAT THETA KHI  R RHO)
      LPAIN(4) = 'PTEMPSR'
      LCHIN(4) = INST
C ... CARTE LIEE A DES CARACTERISTIQUES DE COQUE
      LPAIN(5) = 'PCACOQU'
      LCHIN(5) = CHCARA(7)
      NCHIN = 5

C====
C 3. PREPARATION DES CALCULS ELEMENTAIRES (CAS PARTICULIERS EN IN)
C====

      IF (.NOT.LNLIN ) THEN
        NCHIN = NCHIN + 1
C ... CARTE LIEE AUX EF MASSIFS
        LPAIN(NCHIN) = 'PCAMASS'
        LCHIN(NCHIN) = CHCARA(12)
      ELSE
        NCHIN = NCHIN + 1
        LPAIN(NCHIN) = 'PCOMPOR'
        LCHIN(NCHIN) = COMPOR
        NCHIN = NCHIN + 1
        LPAIN(NCHIN) = 'PTMPCHI'
        LCHIN(NCHIN) = TMPCHI
        NCHIN = NCHIN + 1
        LPAIN(NCHIN) = 'PTMPCHF'
        LCHIN(NCHIN) = TMPCHF
      END IF

C ... LE CHAMP RESULTAT
      LPAOUT(1) = 'PVECTTR'
      LCHOUT(1) = '&&'//NOMPRO//'.???????'

C====
C 4. PREPA DES CALCULS ELEM EN OUT
C    PRETRAITEMENTS POUR TENIR COMPTE DE FONC_MULT
C====
      CALL GCNCO2(NEWNOM)
      LCHOUT(1) (10:16) = NEWNOM(2:8)
      CALL CORICH('E',LCHOUT(1),-1,IBID)

      IF (LNLIN) THEN
        LPAOUT(2) = 'PVECTTI'
        LCHOUT(2) = '&&'//NOMPRO//'.???????'
        CALL GCNCO2(NEWNOM)
        LCHOUT(2) (10:16) = NEWNOM(2:8)
        CALL CORICH('E',LCHOUT(2),-1,IBID)
        NCHOUT = 2
        NCHIN = NCHIN + 1
        LPAIN(NCHIN) = 'PHYDRPM'
        LCHIN(NCHIN) = CHHY
      ELSE
        NCHOUT = 1
      END IF

C====
C 5. LANCEMENT DES CALCULS ELEMENTAIRES OPTION
C====
C TEST D'EXISTENCE DU CHAMP DE TEMP A L'INSTANT
C PRECEDENT. SI IL EXISTE ON LANCE LE CALCUL SIN PB.
      CALL EXISD('CHAMP_GD',CHTN(1:19),IRET)
      CALL ASSERT(IRET.GT.0)
      IF (NIV.EQ.2) THEN
        WRITE (IFM,*) '-->  OPTION         :',OPTION
        DO 20 I = 1,NCHIN
          WRITE (IFM,*) '     LPAIN/LCHIN    :',LPAIN(I),' ',LCHIN(I)
   20   CONTINUE
      END IF

CC    PRINT * ,'DANS ',NOMPRO,' OPTION = ',OPTION,' ET STYPSE = ',STYPSE
      CALL CALCUL('S',OPTION,LIGRMO,NCHIN,LCHIN,LPAIN,NCHOUT,LCHOUT,
     &              LPAOUT,'V','OUI')

C SI ON GENERE DE MANIERE EFFECTIVE UN RESULTAT LCHOUT(1) (VOIR (2))
C INCREMENTATION DE LONUTI ET STOCKAGE DU RESULTAT

      CALL REAJRE(VECEL,LCHOUT(1),'V')
      IF (LNLIN) THEN
        CALL REAJRE(VECEI,LCHOUT(2),'V')
      ENDIF

      VECELZ = VECEL
C FIN ------------------------------------------------------------------
      CALL JEDEMA()
      END
