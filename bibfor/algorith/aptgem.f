      SUBROUTINE APTGEM(SDAPPA,NOMA  ,NEWGEO,DEFICO,NDIMG ,
     &                  IZONE ,TYPZON,ITEMAX,EPSMAX,JDECMA,
     &                  NBMA  )
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 21/01/2013   AUTEUR DESOZA T.DESOZA 
C ======================================================================
C COPYRIGHT (C) 1991 - 2013  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT NONE
      INCLUDE 'jeveux.h'

      CHARACTER*32 JEXNUM
      CHARACTER*24 DEFICO
      CHARACTER*19 SDAPPA,NEWGEO
      CHARACTER*8  NOMA
      INTEGER      NDIMG,IZONE,JDECMA,NBMA
      CHARACTER*4  TYPZON
      INTEGER      ITEMAX
      REAL*8       EPSMAX
C
C ----------------------------------------------------------------------
C
C ROUTINE APPARIEMENT - TANGENTES EN CHAQUE NOEUD D'UNE ELEMENT
C
C CALCUL SUR UNE ZONE
C
C ----------------------------------------------------------------------
C
C
C IN  SDAPPA : NOM DE LA SD APPARIEMENT
C IN  NOMA   : SD MAILLAGE
C IN  NEWGEO : CHAMP DE GEOMETRIE ACTUALISE
C IN  DEFICO : SD DEFINITION DU CONTACT
C IN  NMDIMG : DIMENSION DE L'ESPACE
C IN  IZONE  : NUMERO DE LA ZONE
C IN  TYPZON : TYPE DE LA ZONE 'MAIT' OU 'ESCL'
C IN  ITEMAX : NOMBRE MAXI D'ITERATIONS DE NEWTON POUR LA PROJECTION
C IN  EPSMAX : RESIDU POUR CONVERGENCE DE NEWTON POUR LA PROJECTION
C IN  JDECMA : DECALAGE POUR NUMERO DE MAILLE
C IN  NBMA   : NOMBRE DE MAILLES DE LA ZONE
C
C
C
C
      CHARACTER*8  ALIAS,K8BID,NOMMAI,NOMNOE,VALK(2)
      INTEGER      NUMNO(9),LONGC
      INTEGER      NNOSDM,NIVERR
      LOGICAL      LPOUTR,LPOINT
      INTEGER      JTGELN,JDEC
      INTEGER      INO,IMA,NDIM
      INTEGER      POSMAI,NUMMAI
      REAL*8       TAU1(3),TAU2(3),R8MAEM
      CHARACTER*24 APTGEL
      REAL*8       COORMA(27),COORNO(3)
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
C
C --- ACCES SDAPPA
C
      APTGEL = SDAPPA(1:19)//'.TGEL'
C
C --- BOUCLE SUR LES MAILLES
C
      DO 20 IMA = 1,NBMA
C
C ----- MAILLE COURANTE
C
        POSMAI = IMA+JDECMA
C
C ----- NUMERO ABSOLU DE LA MAILLE
C
        CALL APNUMM(SDAPPA,DEFICO,POSMAI,NUMMAI)
C
C ----- NOMBRE DE NOEUDS DE LA MAILLE
C
        CALL APNNDM(SDAPPA,DEFICO,POSMAI,NNOSDM)
C
C ----- CARACTERISTIQUES DE LA MAILLE MAITRE
C
        CALL APTYPM(SDAPPA,NOMA  ,NUMMAI,NDIM  ,NNOSDM,
     &              ALIAS ,NOMMAI)
C
C ----- COORDONNNEES DE LA MAILLE MAITRE
C
        CALL APCOMA(SDAPPA,NOMA  ,NEWGEO,NUMMAI,NNOSDM,
     &              COORMA)
C
C ----- NUMEROS ABSOLUS DES NOEUDS DE LA MAILLE
C
        CALL JEVEUO(JEXNUM(NOMA//'.CONNEX',NUMMAI),'L',JDEC)
        DO 30 INO = 1,NNOSDM
          NUMNO(INO) = ZI(JDEC+INO-1)
 30     CONTINUE
C
C ----- LONGUEUR EFFECTIVE
C
        CALL JELIRA(JEXNUM(APTGEL,POSMAI),'LONMAX',LONGC ,K8BID)
        LONGC = LONGC /6
C
C ----- TYPE DE MAILLE
C
        LPOUTR = (ALIAS(1:2).EQ.'SE').AND.(NDIMG.EQ.3)
        LPOINT = ALIAS.EQ.'PO1'
C
C ----- ACCES MAILLE COURANTE
C
        CALL JEVEUO(JEXNUM(APTGEL,POSMAI),'E',JTGELN)
C
C ----- BOUCLE SUR LES NOEUDS DE LA MAILLE
C
        DO 10 INO = 1,NNOSDM
C
C ------- COORDONNNEES ET NOM DU NOEUD
C
          CALL APCOND(SDAPPA,NEWGEO,NUMNO(INO),COORNO)
          CALL JENUNO(JEXNUM(NOMA//'.NOMNOE',NUMNO(INO)),NOMNOE)
          VALK(1) = NOMMAI
          VALK(2) = NOMNOE
C
C ------- INITIALISATIONS
C
          TAU1(1) = R8MAEM()
          TAU1(2) = R8MAEM()
          TAU1(3) = R8MAEM()
          TAU2(1) = R8MAEM()
          TAU2(2) = R8MAEM()
          TAU2(3) = R8MAEM()
C
C ------- CALCUL DES TANGENTES EN CE NOEUD
C
          IF (LPOINT) THEN
C
C --------- ELEMENT POINT
C
            CALL APCPOI(SDAPPA,NDIMG ,IZONE ,NOMMAI,TYPZON,
     &                  TAU1  ,TAU2  )
          ELSE
C
C --------- AUTRES ELEMENTS
C
            CALL MMCTAN(NOMMAI,ALIAS ,NNOSDM,NDIM  ,COORMA,
     &                  COORNO,ITEMAX,EPSMAX,TAU1  ,TAU2  )
C
            IF (LPOUTR) THEN
C
C --------- CAS PARTICULIER : ELEMENT POUTRE
C
              CALL APCPOU(SDAPPA,IZONE ,NOMMAI,TYPZON,TAU1  ,
     &                    TAU2  )
            ENDIF
C
          ENDIF
C
C ------- NORMALISATION DES TANGENTES
C
          CALL MMTANN(NDIMG ,TAU1  ,TAU2  ,NIVERR)

          IF (NIVERR.EQ.1) THEN
            CALL U2MESK('F','APPARIEMENT_14',2,VALK)
          ENDIF
C
C ------- STOCKAGE DES TANGENTES
C
          IF (INO.LE.LONGC) THEN
C --------- CE TEST PROTEGE CONTRE LES QUAD8 QUI N'ONT QUE 4 NOEUDS !
            ZR(JTGELN+6*(INO-1)+1 -1) = TAU1(1)
            ZR(JTGELN+6*(INO-1)+2 -1) = TAU1(2)
            ZR(JTGELN+6*(INO-1)+3 -1) = TAU1(3)
            ZR(JTGELN+6*(INO-1)+4 -1) = TAU2(1)
            ZR(JTGELN+6*(INO-1)+5 -1) = TAU2(2)
            ZR(JTGELN+6*(INO-1)+6 -1) = TAU2(3)
          ENDIF

 10     CONTINUE
 20   CONTINUE
C
      CALL JEDEMA()
      END
