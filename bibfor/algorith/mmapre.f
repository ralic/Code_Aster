      SUBROUTINE MMAPRE(PREMIE,NOMA  ,NUMEDD,DEFICO,RESOCO,
     &                  SDAPPA)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 23/01/2012   AUTEUR ABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT NONE
      LOGICAL      PREMIE
      CHARACTER*8  NOMA
      CHARACTER*24 NUMEDD,DEFICO,RESOCO
      CHARACTER*19 SDAPPA
C
C ----------------------------------------------------------------------
C
C ROUTINE CONTACT (METHODES CONTINUES - APPARIEMENT)
C
C RECOPIE DE LA SD APPARIEMENT DEDIEE POUR LE CONTACT
C
C ----------------------------------------------------------------------
C
C
C IN  PREMIE : VAUT .TRUE. SI PREMIER INSTANT DE STAT/DYNA_NON_LINE
C IN  NOMA   : NOM DU MAILLAGE
C IN  SDAPPA : NOM DE LA SD APPARIEMENT
C IN  DEFICO : SD POUR LA DEFINITION DE CONTACT
C IN  RESOCO : SD DE TRAITEMENT NUMERIQUE DU CONTACT
C
C -------------- DEBUT DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER ZI
      COMMON /IVARJE/ ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C ---------------- FIN DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER      IFM,NIV
      INTEGER      CFDISI,NZOCO,NDIMG
      CHARACTER*24 TABFIN,CRNUDD
      INTEGER      JTABF,JCRNUD
      INTEGER      CFMMVD,ZTABF
      INTEGER      IZONE,IP,IMAE,IPTM
      INTEGER      IPTC
      INTEGER      NTPC,NBPT,NBMAE,NPTM,NEQ,NNOMAE
      REAL*8       TAU1M(3),TAU2M(3),NORM(3)
      REAL*8       KSIPR1,KSIPR2
      REAL*8       VECTPM(3),JEUSGN,DDOT
      REAL*8       MMINFR,SEUILI,EPSINT
      REAL*8       ARMINI,ARMIN
      CHARACTER*8  ALIASE,NOMMAM,K8BID
      LOGICAL      MMINFL,LVERI
      INTEGER      MMINFI
      INTEGER      IBID,IRET
      INTEGER      JDECME
      INTEGER      CTCINI,TYPINT,TYPAPP,ENTAPP
      INTEGER      NDEXCL(9)
      INTEGER      POSMAE,NUMMAE,POSMAM,NUMMAM,OLDMAM
      LOGICAL      LAPPAR,LPIVOT,LGLISS,LEXFRO
      INTEGER      TYPRAC,TYPBAR,NDEXFR
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
      CALL INFDBG('CONTACT',IFM,NIV)
C
C --- AFFICHAGE
C
      IF (NIV.GE.2) THEN
        WRITE (IFM,*) '<CONTACT> ...... RECOPIE DE L''APPARIEMENT'
      ENDIF
C
C --- INITIALISATIONS
C
      IPTC   = 1
      NTPC   = 0
      LAPPAR = .FALSE.
      CALL DISMOI('F','NB_EQUA',NUMEDD,'NUME_DDL',NEQ,K8BID,IRET)
C
C --- ACCES SD CONTACT
C
      TABFIN = RESOCO(1:14)//'.TABFIN'
      CRNUDD = RESOCO(1:14)//'.NUDD'
      CALL JEVEUO(TABFIN,'E',JTABF )
      CALL JEVEUO(CRNUDD,'E',JCRNUD)
      ZTABF = CFMMVD('ZTABF')
C
C --- NOMBRE TOTAL DE ZONES ET DIMENSION DU PROBLEME
C
      NZOCO  = CFDISI(DEFICO,'NZOCO' )
      NDIMG  = CFDISI(DEFICO,'NDIM'  )
C
C --- TOLERANCE POUR LA DETECTION DU CONTACT INITIAL
C
      ARMINI = ARMIN(NOMA)
      EPSINT = 1.D-6*ARMINI
C
C --- BOUCLE SUR LES ZONES
C
      IP     = 1
      DO 10 IZONE = 1,NZOCO
C
C ----- INFORMATION SUR LA ZONE
C
        JDECME = MMINFI(DEFICO,'JDECME',IZONE )
        NBMAE  = MMINFI(DEFICO,'NBMAE' ,IZONE )
        LPIVOT = MMINFL(DEFICO,'EXCLUSION_PIV_NUL',IZONE )
        LGLISS = MMINFL(DEFICO,'GLISSIERE_ZONE'   ,IZONE )
        SEUILI = MMINFR(DEFICO,'SEUIL_INIT'       ,IZONE )
        SEUILI = -ABS(SEUILI)
        CTCINI = MMINFI(DEFICO,'CONTACT_INIT'     ,IZONE )
        TYPINT = MMINFI(DEFICO,'INTEGRATION'      ,IZONE )
C
C ----- MODE VERIF: ON SAUTE LES POINTS
C
        LVERI  = MMINFL(DEFICO,'VERIF' ,IZONE )
        IF (LVERI) THEN
          NBPT   = MMINFI(DEFICO,'NBPT'  ,IZONE )
          IP     = IP + NBPT
          GOTO 25
        ENDIF
C
C ----- BOUCLE SUR LES MAILLES ESCLAVES
C
        DO 20 IMAE = 1,NBMAE
C
C ------- NUMERO ABSOLU DE LA MAILLE ESCLAVE
C
          POSMAE = JDECME + IMAE
          CALL CFNUMM(DEFICO,1   ,POSMAE,NUMMAE)
C
C ------- NOMBRE DE POINTS SUR LA MAILLE ESCLAVE
C
          CALL MMINFM(POSMAE,DEFICO,'NPTM',NPTM  )
C
C ------- INFOS SUR LA MAILLE ESCLAVE
C
          CALL MMELTY(NOMA  ,NUMMAE,ALIASE,NNOMAE,IBID  )
C
C ------- EXCLUE LES NOEUDS SUIVANT OPTION EXCL_PIVOT_NUL
C
          CALL MMEXCR(NOMA  ,NEQ   ,DEFICO,RESOCO,LPIVOT,
     &                POSMAE,NDEXCL,TYPRAC,TYPBAR,NDEXFR)
C
C ------- BOUCLE SUR LES POINTS
C
          DO 30 IPTM = 1,NPTM
C
C --------- INFOS APPARIEMENT
C
            CALL APINFI(SDAPPA,'APPARI_TYPE'     ,IP    ,TYPAPP)
            CALL APINFI(SDAPPA,'APPARI_ENTITE'   ,IP    ,ENTAPP)
            CALL APINFR(SDAPPA,'APPARI_PROJ_KSI1',IP    ,KSIPR1)
            CALL APINFR(SDAPPA,'APPARI_PROJ_KSI2',IP    ,KSIPR2)
            CALL APVECT(SDAPPA,'APPARI_TAU1'     ,IP    ,TAU1M )
            CALL APVECT(SDAPPA,'APPARI_TAU2'     ,IP    ,TAU2M )
            CALL APVECT(SDAPPA,'APPARI_VECTPM'   ,IP    ,VECTPM)
C
C --------- TRAITEMENT DES NOEUDS EXCLUS
C
            CALL MMEXCL(RESOCO,TYPINT,IPTC  ,IPTM  ,NDEXCL,
     &                  NDEXFR,TYPBAR,TYPRAC,TYPAPP,LEXFRO)
C
C --------- APPARIEMENT NODAL INTERDIT !
C
            IF (TYPAPP.EQ.1) THEN
              CALL ASSERT(.FALSE.)
            ENDIF
C
C --------- NUMEROS DE LA MAILLE MAITRE
C
            POSMAM = ENTAPP
            CALL CFNUMM(DEFICO,1   ,POSMAM,NUMMAM)
C
C --------- INDICATEUR NOUVEL APPARIEMENT
C
            OLDMAM = NINT(ZR(JTABF+ZTABF*(IPTC-1)+2))
            IF (OLDMAM.NE.NUMMAM ) THEN
              LAPPAR = .TRUE.
            ENDIF
C
C --------- SAUVEGARDE APPARIEMENT
C
            CALL MMAPMA(NOMA  ,DEFICO,RESOCO,NDIMG ,IZONE ,
     &                  LEXFRO,TYPINT,ALIASE,POSMAE,NUMMAE,
     &                  NNOMAE,POSMAM,NUMMAM,KSIPR1,KSIPR2,
     &                  TAU1M ,TAU2M ,IPTM  ,IPTC  ,NORM  ,
     &                  NOMMAM)
C
C --------- JEU SIGNE
C
            JEUSGN = DDOT(NDIMG ,NORM  ,1     ,VECTPM,1     )
C
C --------- TRAITEMENT DES OPTIONS
C
            CALL MMOPTI(PREMIE,RESOCO,SEUILI,CTCINI,LGLISS,
     &                  IPTC  ,EPSINT,JEUSGN)
C
C --------- LIAISON DE CONTACT EFFECTIVE
C
            IPTC   = IPTC + 1
            NTPC   = NTPC + 1
C
C --------- POINT SUIVANT
C
            IP     = IP + 1
C
  30      CONTINUE
  20    CONTINUE
  25    CONTINUE
  10  CONTINUE
C
C --- NOMBRE TOTAL DE NOEUDS EN CONTACT
C
      ZR(JTABF-1+1)  = NTPC
      CALL ASSERT(NTPC.EQ.CFDISI(DEFICO,'NTPC'))
C
C --- INDICATEUR DE REAPPARIEMENT (OU PAS)
C
      IF (PREMIE) THEN
        LAPPAR = .TRUE.
      ENDIF
      ZL(JCRNUD-1+1) = LAPPAR
C
C --- AFFICHAGE
C
      IF (NIV.GE.2) THEN
        CALL MMIMP1(IFM   ,NOMA  ,DEFICO,RESOCO)
      ENDIF
C
      CALL JEDEMA()
      END
