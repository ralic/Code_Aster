      SUBROUTINE NMHOFF (NDIM,IMATE,INST,EPSM,DEPS,OPTION,SIGP,DSIDEP)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 29/04/2004   AUTEUR JMBHH01 J.M.PROIX 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C
      IMPLICIT NONE
      INTEGER        NDIM,IMATE
      REAL*8         INST,DEPS(6),EPSM(6),SIGP(6),DSIDEP(6,6)
      CHARACTER*16   OPTION
C -------------------------------------------------------------------
C     REALISE LA LOI DE NORTON-HOFF POUR LES ELEMENTS INCOMPRESSIBLES
C
C IN  NDIM    : DIMENSION DE L'ESPACE
C IN  IMATE   : ADRESSE DU MATERIAU CODE
C IN  INST    : INSTANT DU CALCUL
C IN  EPSM    : DEFORMATIONS A L'INSTANT PRECEDENT
C IN  DEPS    : INCREMENT DE DEFORMATIONS
C IN  OPTION  : OPTION DEMANDEE : RIGI_MECA_TANG , FULL_MECA , RAPH_MECA
C                                 RIGI_MECA_ELAS
C OUT SIGP    : CONTRAINTES A L'INSTANT ACTUEL
C OUT DSIDEP  : MATRICE CARREE (INUTILISE POUR RAPH_MECA)
C
C               ATTENTION LES TENSEURS ET MATRICES SONT RANGES DANS
C               L'ORDRE :  XX,YY,ZZ,SQRT(2)*XY,SQRT(2)*XZ,SQRT(2)*YZ
C
C --------------------------------------------------------------------
      LOGICAL      RESI, RIGI, ELAS, LINE
      INTEGER      K,L,NDIMSI
      CHARACTER*2  COD
      REAL*8       SY, M, AM
      REAL*8       EPS(6), EPSNO
      REAL*8       COEF, RAC23, R8NRM2
C -----------------------------------------------------------------


C -- INITIALISATION

      NDIMSI = 2*NDIM
      RAC23  = SQRT(2.D0/3.D0)

      RESI = OPTION.EQ.'RAPH_MECA'      .OR. OPTION.EQ.'FULL_MECA'
      RIGI = OPTION.EQ.'RIGI_MECA_TANG' .OR. OPTION.EQ.'FULL_MECA'
      ELAS = OPTION.EQ.'RIGI_MECA_ELAS'

      CALL R8COPY(NDIMSI, EPSM,1, EPS,1)
      IF (RESI) CALL R8AXPY(NDIMSI, 1.D0, DEPS,1, EPS,1)
      EPSNO = R8NRM2(NDIMSI, EPS,1)

      LINE = INST.EQ.1 .OR. EPSNO.EQ.0.D0


C -- CARACTERISTIQUES MATERIAU (SY) ET RIGIDITE

      CALL RCVALA(IMATE,' ','ECRO_LINE',0,' ',0.D0,1,'SY',SY,COD,'F ')
      M    = 1 + 10**(1-INST)
      AM   = SY * RAC23**M
      IF (LINE) THEN
        COEF = AM
      ELSE
        COEF = AM * EPSNO**(M-2)
      ENDIF


C -- CALCUL DE SIGP

      IF (RESI) THEN
        DO 100 K = 1,NDIMSI
          SIGP(K)  = COEF*EPS(K)
100     CONTINUE
      ENDIF


C -- CALCUL DE DSIDEP(6,6)

      IF  (RIGI .OR. ELAS) THEN

        CALL R8INIR(36, 0.D0, DSIDEP,1)

        DO 210 K = 1, NDIMSI
         DSIDEP(K,K) = COEF
 210    CONTINUE

        IF (RIGI .AND. .NOT. LINE) THEN
          COEF = COEF * (M-2) / EPSNO**2
          DO 220 K = 1, NDIMSI
          DO 221 L = 1, NDIMSI
            DSIDEP(K,L) = DSIDEP(K,L) + COEF*EPS(K)*EPS(L)
 221      CONTINUE
 220      CONTINUE
        END IF
      END IF

      END
