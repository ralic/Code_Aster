      SUBROUTINE REGEGL ( NOMRES, RESGEN, MAILSK, PROFNO )
      IMPLICIT  NONE
      CHARACTER*8         NOMRES, RESGEN, MAILSK
      CHARACTER*19                                PROFNO
C-----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 15/06/2005   AUTEUR VABHHTS J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C
C  BUT : < RESTITUTION GENERALISEE GLOBALE >
C
C  RESTITUER EN BASE PHYSIQUE SUR UN MAILLAGE SQUELETTE LES RESULTATS
C  ISSUS DE LA SOUS-STRUCTURATION GENERALE.
C  LE CONCEPT RESULTAT EST UN RESULTAT COMPOSE "MODE_MECA"
C
C-----------------------------------------------------------------------
C
C NOMRES  /I/ : NOM K8 DU CONCEPT MODE_MECA RESULTAT
C RESGEN  /I/ : NOM K8 DU RESULTAT GENERALISE AMONT
C MAILSK  /I/ : NOM K8 DU MAILLAGE SQUELETTE SUPPORT
C PROFNO  /I/ : NOM K19 DU PROF_CHNO DU SQUELETTE
C
C-------- DEBUT COMMUNS NORMALISES  JEVEUX  ----------------------------
C
      INTEGER          ZI
      COMMON  /IVARJE/ ZI(1)
      REAL*8           ZR
      COMMON  /RVARJE/ ZR(1)
      COMPLEX*16       ZC
      COMMON  /CVARJE/ ZC(1)
      LOGICAL          ZL
      COMMON  /LVARJE/ ZL(1)
      CHARACTER*8      ZK8
      CHARACTER*16              ZK16
      CHARACTER*24                        ZK24
      CHARACTER*32                                  ZK32
      CHARACTER*80                                            ZK80
      COMMON  /KVARJE/ ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
      CHARACTER*32 JEXNUM,JEXNOM
C
C----------  FIN  COMMUNS NORMALISES  JEVEUX  --------------------------
C
      INTEGER      I,IAD,IAR,IBID,IDEP,IEQ,IER,IORD,IRET,J,JBID,K,L,
     +             LDNEW,LLCHAB,LLCHOL,LLIND,LLINER,LLINSK,LLMASS,
     +             LLNUEQ,LLORS,LLPRS,LLREF,LLROT,LREFE,LTROTX,
     +             LTROTY,LTROTZ,LTVEC,LTYPE,NBBAS,NBCMP,NBCOU,NBMAS,
     +             NBMAX,NBMOD,NBNOT,NBSST,NEQ, NEQS,NNO,NUMO,NUTARS
      INTEGER      IADPAR(8)
      REAL*8       COMPX,COMPY,COMPZ,EFMASX,EFMASY,EFMASZ,FREQ,GENEK,
     +             GENEM,MAT(3,3),OMEG2,RBID
      CHARACTER*8  BASMOD,MACREL,MODGEN,SOUTR,KBID
      CHARACTER*16 DEPL,NOMPAR(8)
      CHARACTER*19 RAID,NUMDDL,NUMGEN,CHAMNE
      CHARACTER*24 CREFE(2),CHAMOL,CHAMBA,INDIRF
      COMPLEX*16   CBID
      CHARACTER*1 K1BID
C
C-----------------------------------------------------------------------
      DATA DEPL   /'DEPL            '/
      DATA SOUTR  /'&SOUSSTR'/
      DATA NOMPAR /'FREQ','RIGI_GENE','MASS_GENE','OMEGA2','NUME_MODE',
     &             'MASS_EFFE_DX','MASS_EFFE_DY','MASS_EFFE_DZ'/
C-----------------------------------------------------------------------
C
      CALL JEMARQ()
      INDIRF='&&REGEGL.INDIR.SST'
C
C-----ECRITURE DU TITRE-------------------------------------------------
C
      CALL TITRE
C
C-----VERIF SQUELETTE---------------------------------------------------
C
      CALL JEEXIN(MAILSK//'.INV.SKELETON',IRET)
      IF(IRET.EQ.0) THEN
        CALL UTDEBM('F','REGEGL',
     &              'LE MAILLAGE N''EST PAS UN MAILLAGE SQUELETTE')
        CALL UTIMPK('L','MAILLAGE',1,MAILSK)
        CALL UTFINM
      ENDIF
      CALL JEVEUO(MAILSK//'.INV.SKELETON','L',LLINSK)
C
C-----RECUPERATION DU MODELE GENERALISE--------------------------------
C
      CALL JEVEUO(RESGEN//'           .REFD','L',LLREF)
      RAID=ZK24(LLREF+2)
      CALL JELIBE(RESGEN//'           .REFD')
      CALL JEVEUO(RAID//'.REFA','L',LLREF)
      NUMGEN(1:14)=ZK24(LLREF+1)
      NUMGEN(15:19)='.NUME'
      CALL JELIBE(RAID//'.REFA')
      CALL JEVEUO(NUMGEN//'.REFN','L',LLREF)
      MODGEN=ZK24(LLREF)
      CALL JELIRA(MODGEN//'      .MODG.SSNO','NOMMAX',NBSST,K1BID)
      KBID='  '
      CALL MGUTDM(MODGEN,KBID,1,'NB_CMP_MAX',NBCMP,KBID)
C
C-----RECUPERATION DES ROTATIONS----------------------------------------
C
      CALL WKVECT('&&REGEGL.ROTX','V V R',NBSST,LTROTX)
      CALL WKVECT('&&REGEGL.ROTY','V V R',NBSST,LTROTY)
      CALL WKVECT('&&REGEGL.ROTZ','V V R',NBSST,LTROTZ)
      DO 15 I=1,NBSST
        CALL JEVEUO(JEXNUM(MODGEN//'      .MODG.SSOR',I),'L',LLROT)
        ZR(LTROTZ+I-1)=ZR(LLROT)
        ZR(LTROTY+I-1)=ZR(LLROT+1)
        ZR(LTROTX+I-1)=ZR(LLROT+2)
15    CONTINUE
C
C-----CREATION DU PROF-CHAMNO-------------------------------------------
C
      CALL GENUGL(PROFNO,INDIRF,MODGEN,MAILSK)
      CALL JELIRA(PROFNO//'.NUEQ','LONMAX',NEQ,K1BID)
C
C-----RECUPERATION DU NOMBRE DE NOEUDS----------------------------------
C
      CALL DISMOI('F','NB_NO_MAILLA',MAILSK,'MAILLAGE',NBNOT,KBID,IRET)
C
C-----RECUPERATION DE LA BASE MODALE------------------------------------
C
      CREFE(1)=MAILSK
      CREFE(2)=PROFNO
C
C-----RECUPERATION NOMBRE DE MODES PROPRES CALCULES---------------------
C
      CALL RSORAC(RESGEN,'LONUTI',IBID,RBID,KBID,CBID,RBID,
     &            KBID,NBMOD,1,IBID)
C
C --- ON RESTITUE SUR TOUS LES MODES OU SUR QUELQUES MODES:
C
      CALL GETVIS ( ' ', 'NUME_ORDRE', 1,1,0, IBID, NNO )
      IF ( NNO .NE. 0 ) THEN
        NBMOD = -NNO
        CALL WKVECT ( '&&REGEGL.NUME', 'V V I', NBMOD, JBID )
        CALL GETVIS ( ' ', 'NUME_ORDRE', 1,1,NBMOD, ZI(JBID), NNO )
      ELSE
        CALL WKVECT ( '&&REGEGL.NUME', 'V V I', NBMOD, JBID )
        DO 2 I = 1 , NBMOD
           ZI(JBID+I-1) = I
 2      CONTINUE
      ENDIF
C
C-----ALLOCATION STRUCTURE DE DONNEES RESULTAT--------------------------
C
      CALL RSCRSD(NOMRES,'MODE_MECA',NBMOD)
C
CC
CCC---RESTITUTION PROPREMENT DITE---------------------------------------
CC
C
      CALL JEVEUO(NUMGEN//'.NUEQ','L',LLNUEQ)
      CALL JENONU(JEXNOM(NUMGEN//'.LILI',SOUTR),IBID)
      CALL JEVEUO(JEXNUM(NUMGEN//'.ORIG',IBID),'L',LLORS)
      CALL JENONU(JEXNOM(NUMGEN//'.LILI',SOUTR),IBID)
      CALL JEVEUO(JEXNUM(NUMGEN//'.PRNO',IBID),'L',LLPRS)
C
C  BOUCLE SUR LES MODES A RESTITUER
C
      DO 20 I = 1,NBMOD
        IORD = ZI(JBID+I-1)
C
C  REQUETTE NOM ET ADRESSE CHAMNO GENERALISE
C
        CALL DCAPNO ( RESGEN, DEPL, IORD, CHAMOL )
        CALL JEVEUO ( CHAMOL, 'L', LLCHOL )
C
C  REQUETTE NOM ET ADRESSE NOUVEAU CHAMNO
C
        CALL RSEXCH ( NOMRES, DEPL, I, CHAMNE, IER )
        CALL VTCREA ( CHAMNE, CREFE, 'G', 'R', NEQ )
        CALL JEVEUO ( CHAMNE//'.VALE', 'E', LDNEW )
C
        CALL RSADPA ( RESGEN, 'L', 8,NOMPAR, IORD,0,IADPAR,KBID)
        FREQ   = ZR(IADPAR(1))
        GENEK  = ZR(IADPAR(2))
        GENEM  = ZR(IADPAR(3))
        OMEG2  = ZR(IADPAR(4))
        NUMO   = ZI(IADPAR(5))
        EFMASX = 0.D0
        EFMASY = 0.D0
        EFMASZ = 0.D0
C
C  BOUCLE SUR LES SOUS-STRUCTURES
C
        DO 30 K = 1,NBSST
          CALL JEEXIN(JEXNUM(INDIRF,K),IRET)
C
C  TEST SI LA SST GENERE DES DDL GLOBAUX
C
          IF(IRET.NE.0) THEN
            KBID='  '
            CALL MGUTDM(MODGEN,KBID,K,'NOM_BASE_MODALE',IBID,BASMOD)
            CALL JEVEUO(BASMOD//'           .REFD','L',LREFE)
            CALL JEVEUO(ZK24(LREFE)(1:8)//'      .INTD.TYPE','L',
     &                  LTYPE)
            IF (ZK8(LTYPE).EQ.'AUCUN') THEN
             CALL UTDEBM('A','REGEGL',' AUCUN TYPE D''INTERFACE DEFINI')
               CALL UTIMPI('S',' POUR LA SOUS STRUCTURE : ',1,K)
               CALL UTIMPI('L',' PAS DE MODE RIGIDE D''INTERFACE'//
     &           ' LE CALCUL DE MASSES EFFECTIVES RISQUE D''ETRE'//
     &           ' IMPRECIS',0,K)
               CALL UTFINM
            ENDIF
            CALL MGUTDM(MODGEN,KBID,K,'NOM_MACR_ELEM',IBID,MACREL)
            CALL JEVEUO(MACREL//'.MAEL.MASS .VALE','L',LLMASS)
            CALL JELIRA(MACREL//'.MAEL.MASS .VALE','LONMAX',NBMAX,
     &                  K1BID)
            CALL JELIRA(MACREL//'.MAEL.MASS .VALE','LONUTI',NBMAS,
     &                  K1BID)
            CALL JEVEUO(MACREL//'.MAEL.INER .VALE','L',LLINER)
C
C           --- CALCUL DE LA MATRICE DE ROTAION ---
            CALL JEVEUO(JEXNUM(MODGEN//'      .MODG.SSOR',K),'L',LLROT)
            CALL MATROT(ZR(LLROT),MAT)
C
            CALL BMNBMD(BASMOD,'TOUT',NBBAS)
            KBID='  '
            CALL MGUTDM(MODGEN,KBID,K,'NOM_NUME_DDL',IBID,NUMDDL)
            CALL DISMOI('F','NB_EQUA',NUMDDL,'NUME_DDL',NEQS,KBID,IRET)
            CALL WKVECT('&&REGEGL.TRAV','V V R',NEQS,LTVEC)
C
C  RECUPERATION DU NUMERO TARDIF DE LA SST
C
            DO 40 J=1,NBSST
              IF(ZI(LLORS+J-1).EQ.K) NUTARS=J
40          CONTINUE
            IEQ=ZI(LLPRS+(NUTARS-1)*2)
C
C  BOUCLE SUR LES MODES PROPRES DE LA BASE
C
            DO 50 J=1,NBBAS
              CALL DCAPNO(BASMOD,DEPL,J,CHAMBA)
              CALL JEVEUO(CHAMBA,'L',LLCHAB)
C
C  BOUCLE SUR LES EQUATIONS PHYSIQUES
C
              IAD=LLCHOL+ZI(LLNUEQ+IEQ+J-2)-1
C           --- CALCUL DES MASSES EFFECTIVES ---
              COMPX = ZR(LLINER+J-1)
              COMPY = ZR(LLINER+NBBAS+J-1)
              COMPZ = ZR(LLINER+2*NBBAS+J-1)
C             --- UTILISATION DE MAT TRANSPOSEE (TRANSFORMATION INVERSE)
              EFMASX = EFMASX + ZR(IAD)*(COMPX*MAT(1,1) + COMPY*MAT(2,1)
     +                                   + COMPZ*MAT(3,1))
              EFMASY = EFMASY + ZR(IAD)*(COMPX*MAT(1,2) + COMPY*MAT(2,2)
     +                                   + COMPZ*MAT(3,2))
              EFMASZ = EFMASZ + ZR(IAD)*(COMPX*MAT(1,3) + COMPY*MAT(2,3)
     +                                   + COMPZ*MAT(3,3))
C
C  BOUCLE SUR LES DDL DE LA BASE
C
              DO 60 L=1,NEQS
                ZR(LTVEC+L-1)=ZR(LTVEC+L-1)+ZR(LLCHAB+L-1)*
     &                        ZR(IAD)
60            CONTINUE
              CALL JELIBE(CHAMBA)
50          CONTINUE
            CALL JEVEUO(JEXNUM(INDIRF,K),'L',LLIND)
            CALL JELIRA(JEXNUM(INDIRF,K),'LONMAX',NBCOU,K1BID)
            NBCOU=NBCOU/2
            DO 70 L=1,NBCOU
              IDEP=ZI(LLIND+(L-1)*2)
              IAR=ZI(LLIND+(L-1)*2+1)
              ZR(LDNEW+IAR-1)=ZR(LTVEC+IDEP-1)
70          CONTINUE
            CALL JELIBE(JEXNUM(INDIRF,K))
            CALL JEDETR('&&REGEGL.TRAV')
          ENDIF
30      CONTINUE
C
        EFMASX = EFMASX*EFMASX/GENEM
        EFMASY = EFMASY*EFMASY/GENEM
        EFMASZ = EFMASZ*EFMASZ/GENEM
        CALL RSNOCH ( NOMRES, DEPL, I, ' ' )
        CALL RSADPA ( NOMRES,'E',8,NOMPAR,I,0,IADPAR,KBID)
        ZR(IADPAR(1)) = FREQ
        ZR(IADPAR(2)) = GENEK
        ZR(IADPAR(3)) = GENEM
        ZR(IADPAR(4)) = OMEG2
        ZI(IADPAR(5)) = NUMO
        ZR(IADPAR(6)) = EFMASX
        ZR(IADPAR(7)) = EFMASY
        ZR(IADPAR(8)) = EFMASZ
C
        CALL JELIBE(CHAMOL)
C
C  ROTATION DU CHAMPS AUX NOEUDS
C
        CALL ROTCHM(PROFNO,ZR(LDNEW),ZR(LTROTZ),NBSST,ZI(LLINSK),NBNOT,
     &              NBCMP,3)
        CALL ROTCHM(PROFNO,ZR(LDNEW),ZR(LTROTY),NBSST,ZI(LLINSK),NBNOT,
     &              NBCMP,2)
        CALL ROTCHM(PROFNO,ZR(LDNEW),ZR(LTROTX),NBSST,ZI(LLINSK),NBNOT,
     &              NBCMP,1)
20    CONTINUE
C
      CALL JEDETC('V','&&REGEGL',1)
C
      CALL JEDETR ( INDIRF )
      CALL JELIBE ( NUMGEN//'.NUEQ' )
      CALL JEDETR ( '&&REGEGL.NUME' )
C
 9999 CONTINUE
      CALL JEDEMA()
      END
