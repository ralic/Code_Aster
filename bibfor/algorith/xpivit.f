      SUBROUTINE XPIVIT(CHSCF ,CHSAI ,CNCTE ,NDIM  ,NUMMAE,
     &                  IFACE ,XPC   ,YPC   ,NVIT  ,GROUP ,
     &                  NARET )
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 22/12/2009   AUTEUR ABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2009  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C
      IMPLICIT NONE
      CHARACTER*19 CHSCF,CHSAI
      CHARACTER*24 CNCTE
      INTEGER      NDIM,NUMMAE,IFACE
      REAL*8       XPC,YPC
      INTEGER      NVIT,GROUP,NARET
C
C ----------------------------------------------------------------------
C
C ROUTINE XFEM (CONTACT - GRANDS GLISSEMENTS)
C
C SERT À DEFINIR SI LE POINT D'INTEGRATION EST VITAL OU NON
C
C TRAVAIL EFFECTUE EN COLLABORATION AVEC L'I.F.P.
C
C ----------------------------------------------------------------------
C
C IN  CHSCF : CHAMP CONTENANT LES NUMEROS LOCAUX 
C             DES POINTS D'INTERSECTIONS DES FACETTES DE CONTACT
C IN  CHSAI : CHAMP CONTENANT LES INFOS SUR LES ARETES COUPEES
C IN  NDIM  : DIMENSION DU MODELE
C IN NUMMAE : POSITION DE LA MAILLE ESCLAVE
C IN IFACE  : NUMERO LOCAL DE LA FACETTE ESCLAVE
C IN XPC    : COORDONNEE X DU POINT D'INTEGRATION DE CONTACT SUR 
C             LA MAILLE ESCLAVE
C IN YPC    : COORDONNEE Y DU POINT D'INTEGRATION DE CONTACT SUR
C             LA MAILLE ESCLAVE
C
C OUT NVIT  : VAUT 1 SI LE POINT D'INTEGRATION EST SUR UNE ARETE
C             VITALE (0 SINON)
C OUT GROUP : NUMERO DE GROUPE SI LE POINT EST SUR UNE ARETE
C             APPARTENANT À UN GROUPE D'ARETES CONNECTÉES (0 SINON)
C OUT NARET : NUMERO D'ARETE DU GROUPE SI LE POINT DE CONTACT EST
C             SUR UNE ARETE APPARTENANT À UN GROUPE (0 SINON)
C  
C -------------- DEBUT DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
C
C ---------------- FIN DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER   JCSL1,JCSD1,JCSV1,JCSL2,JCSD2,JCSV2,IAD
      INTEGER   ZXAIN,XXMMVD,PINT,ARET,IER,JCNTE,NCTE,NARCON
      CHARACTER*8  K8BID
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
C
C --- INITIALISATIONS
C
      ZXAIN = XXMMVD('ZXAIN')
      PINT = 0
      ARET = 0
      NVIT = 0
      GROUP = 0
      NARET = 0
C
C --- RECUPERATION DE QUELQUES DONNEES
C
      CALL JEEXIN(CNCTE,IER)
      IF (IER.NE.0) THEN               
        CALL JEVEUO(CNCTE,'L',JCNTE)
        CALL JELIRA(CNCTE,'LONMAX',NARCON,K8BID)
      ELSE
        NARCON = 0
      ENDIF
C
C --- ON REGARDE SI LE POINT D'INTEGRATION EST SUR UNE ARETE
C
      IF (NDIM.EQ.2) THEN
        IF (XPC.EQ.-1) THEN
          PINT = 1
        ELSEIF (XPC.EQ.1) THEN
          PINT = 2
        ENDIF
      ELSEIF (NDIM.EQ.3) THEN
        IF ((XPC.EQ.0).AND.(YPC.EQ.0)) THEN
          PINT = 1
        ELSEIF ((XPC.EQ.1).AND.(YPC.EQ.0)) THEN
          PINT = 2
        ELSEIF ((XPC.EQ.0).AND.(YPC.EQ.1)) THEN
          PINT = 3
        ENDIF
      ENDIF
C
C --- SI IL EST SUR UNE ARETE
C
      IF (PINT.NE.0) THEN
C
C --- ON RECUPERE LE NUMERO DU POINT D'INTERSECTION COUPEE CORESPONDANT
C
        CALL JEVEUO(CHSCF//'.CESD','L',JCSD1)
        CALL JEVEUO(CHSCF//'.CESV','L',JCSV1)
        CALL JEVEUO(CHSCF//'.CESL','L',JCSL1)
        CALL CESEXI('S',JCSD1,JCSL1,NUMMAE,1,1,NDIM*(IFACE-1)+PINT,IAD)
        CALL ASSERT(IAD.GT.0)
        PINT = ZI(JCSV1-1+IAD)
C
C --- ON RECUPERE LE NUMERO D'ARETE COUPÉE ET L'INFO VITAL OU PAS DE
C --- CETTE ARETE COUPEE
C
        CALL JEVEUO(CHSAI//'.CESD','L',JCSD2)
        CALL JEVEUO(CHSAI//'.CESV','L',JCSV2)
        CALL JEVEUO(CHSAI//'.CESL','L',JCSL2)
        CALL CESEXI('S',JCSD2,JCSL2,NUMMAE,1,1,ZXAIN*(PINT-1)+1,IAD)
        CALL ASSERT(IAD.GT.0)
        ARET = NINT(ZR(JCSV2-1+IAD))
        CALL CESEXI('S',JCSD2,JCSL2,NUMMAE,1,1,ZXAIN*(PINT-1)+5,IAD)
        CALL ASSERT(IAD.GT.0)
        NVIT = NINT(ZR(JCSV2-1+IAD))
C
        IF ((NVIT.EQ.1).AND.(ARET.NE.0)) THEN
C
C --- ON REGARDE SI LE PT DE CONTACT EST SUR UNE ARETE CONNECTEE
C
          DO 10 NCTE=1,NARCON/4
            IF ((ZI(JCNTE-1+4*(NCTE-1)+3).EQ.NUMMAE).AND.
     &          (ZI(JCNTE-1+4*(NCTE-1)+4).EQ.ARET)) THEN
C
C --- ON NOTE SON NUMERO DE GROUPE ET SON NUMERO D'ARETE DANS LE GROUPE
C
              GROUP = ZI(JCNTE-1+4*(NCTE-1)+2)
              NARET = ZI(JCNTE-1+4*(NCTE-1)+1)
              IF (NARET.NE.1) THEN
                NVIT = 0
                GOTO 10
              ENDIF
            ENDIF
 10       CONTINUE
        ENDIF
      ENDIF
C
      CALL JEDEMA()
      END
