      SUBROUTINE NMADEV(SDDISC,SDERRO,ITERAT)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 02/04/2012   AUTEUR ABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE GENIAUT S.GENIAUT
C
      IMPLICIT     NONE
      CHARACTER*19 SDDISC
      CHARACTER*24 SDERRO
      INTEGER      ITERAT
C
C ----------------------------------------------------------------------
C
C ROUTINE MECA_NON_LINE (ALGORITHME)
C
C MISE A JOUR DE L'INDICATEUR DE SUCCES DES ITERATIONS DE NEWTON
C
C ----------------------------------------------------------------------
C
C
C IN  SDDISC : SD DISCRETISATION TEMPORELLE
C IN  SDERRO : GESTION DES ERREURS
C IN  ITERAT : NUMERO DE L'ITERATION DE NEWTON
C
C ----------------------------------------------------------------------
C
      INTEGER      IBID,VALI,NBOK
      INTEGER      IADAPT,NADAPT
      REAL*8       R8BID,VALE
      CHARACTER*8  K8BID,CRICOM,METLIS
      CHARACTER*16 NOPARA
      CHARACTER*19 EVEN
      LOGICAL      ITEMAX,LERRIT,DIVRES,CVNEWT
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
C
C --- LA MISE A JOUR DE L'INDICATEUR DE SUCCES DES ITERATIONS DE NEWTON
C --- N'EST FAITE QU'EN GESTION AUTO DU PAS DE TEMPS
C
      CALL UTDIDT('L'   ,SDDISC,'LIST',IBID  ,'METHODE',
     &            R8BID ,IBID  ,METLIS)
      IF (METLIS.NE.'AUTO') GOTO 9999
C
C --- EVENEMENTS
C
      CALL NMERGE(SDERRO,'ITER_MAXI',ITEMAX)
      CALL NMLTEV(SDERRO,'ERRI','NEWT',LERRIT)
      CALL NMERGE(SDERRO,'DIVE_RESI',DIVRES)
C
C --- NEWTON A CONVERGE ?
C
      CALL NMLECV(SDERRO,'NEWT',CVNEWT)
C
C --- BOUCLE SUR LES OCCURENCES D'ADAPTATION
C
      CALL UTDIDT('L'   ,SDDISC,'LIST',IBID  ,'NADAPT',
     &            R8BID ,NADAPT,K8BID )
C
      DO 10 IADAPT=1,NADAPT
C
C ----- NOM DE L'EVENEMENT
C
        CALL UTDIDT('L'   ,SDDISC,'ADAP',IADAPT,'NOM_EVEN',
     &              R8BID ,IBID  ,EVEN  )

        IF (EVEN.EQ.'SEUIL_SANS_FORMULE') THEN
C
C ------- PARAMETRES DU SEUIL
C
          CALL UTDIDT('L'   ,SDDISC,'ADAP',IADAPT,'NOM_PARA',
     &                R8BID ,IBID  ,NOPARA)
          CALL UTDIDT('L'   ,SDDISC,'ADAP',IADAPT,'CRIT_COMP',
     &                R8BID ,IBID  ,CRICOM)
          CALL UTDIDT('L'   ,SDDISC,'ADAP',IADAPT,'VALE',
     &                VALE  ,VALI  ,K8BID )
C
          CALL ASSERT(NOPARA.EQ.'NB_ITER_NEWT')
C
C ------- RECUP DU NB DE SUCCES CONSECUTIFS : NBOK
C
          CALL UTDIDT('L'   ,SDDISC,'ADAP',IADAPT,'NB_EVEN_OK',
     &                R8BID ,NBOK  ,K8BID )
C
C ------- EN CAS DE NOUVEAU SUCCES A CONVERGENCE
C
          IF (CVNEWT) THEN
            IF (CRICOM.EQ.'LT'.AND.ITERAT.LT.VALI.OR.
     &          CRICOM.EQ.'GT'.AND.ITERAT.GT.VALI.OR.
     &          CRICOM.EQ.'LE'.AND.ITERAT.LE.VALI.OR.
     &          CRICOM.EQ.'GE'.AND.ITERAT.GE.VALI) THEN
              NBOK = NBOK+1
            ENDIF
          ENDIF
C
C ------- EN CAS D'ECHEC: ON REMET A ZERO
C
          IF (LERRIT.OR.ITEMAX.OR.DIVRES) NBOK=0
C
C ------- ENREGISTREMENT DU NB DE SUCCES CONSECUTIFS
C
          CALL UTDIDT('E'   ,SDDISC,'ADAP',IADAPT,'NB_EVEN_OK',
     &                R8BID ,NBOK  ,K8BID )

        ENDIF
 10   CONTINUE
C
 9999 CONTINUE
C
      CALL JEDEMA()
      END
