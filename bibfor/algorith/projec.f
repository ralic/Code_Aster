      SUBROUTINE PROJEC (IZONE,NDIM,NOMA,NEWGEO,RESOCO,DEFICO,MULNOR,
     &               POSNOE,POSMA,REAPRO,CMULT,NORM,POSNO,COEF,OLDJEU,
     &                   JEU,DDL,NBNO,JDECAL,TANG,JEUFX,COFX,COFY)
C ======================================================================
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 05/04/2004   AUTEUR MABBAS M.ABBAS 
C TOLE CRP_21
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C
      IMPLICIT NONE
C
      LOGICAL       MULNOR
      INTEGER       NDIM,POSNOE,POSMA,REAPRO,POSNO(10),IZONE
      INTEGER       DDL(30),NBNO,JDECAL
      REAL*8        NORM(3),COEF(30),OLDJEU,JEU,CMULT,TANG(6),JEUFX
      REAL*8        COFX(30),COFY(30)
      CHARACTER*8   NOMA
      CHARACTER*24  NEWGEO,RESOCO,DEFICO
C
C ----------------------------------------------------------------------
C ROUTINE APPELEE PAR : CHMANO / PROJCO
C ----------------------------------------------------------------------
C
C "PROJECTION" D'UN NOEUD ESCLAVE SUR UNE MAILLE MAITRE.
C CALCUL DES COEFFICIENTS A UTILISER POUR REPRESENTER LE DEPLACEMENT
C DE LA PROJECTION EN FONCTION DES DEPLACEMENTS DES NOEUDS DE LA MAILLE.
C CALCUL DU JEU COURANT. MEMORISATION DES NUMEROS DES DDLS MAITRES.
C
C IN  IZONE  : NUMERO DE LA ZONE DE CONTACT
C IN  NDIM   : DIMENSION DE L'ESPACE (2 OU 3)
C IN  NOMA   : NOM DU MAILLAGE
C IN  DDLCO  : NUMEROS DES DDLS DES NOEUDS DE CONTACT POTENTIELS
C IN  PDDL   : POINTEUR DES DDLS DES NOEUDS DE CONTACT POTENTIELS
C IN  NEWGEO : COORDONNEES REACTUALISEES DES NOEUDS DU MAILLAGE
C IN  CONTNO : LISTE DES NUMEROS DES NOEUDS POTENTIELS DE CONTACT
C IN  CONTMA : LISTE DES NUMEROS DES MAILLES POTENTIELLES DE CONTACT
C IN  NOMACO : TABLEAU DONNANT POUR CHAQUE MAILLE DE CONTACT LA LISTE
C              DES NOEUDS DE CONTACT DE LA MEME SURFACE QU'ELLE CONTIENT
C IN  PNOMA  : POINTEUR DE CE TABLEAU (DIM NMACO+1)
C IN  NORINI : COMPOSANTES DES NORMALES AUX NOEUDS DE CONTACT
C IN  MULNOR : LOGIQUE QUI VAUT 1 LORSQU'ON MULTIPLIE LES COEFFICIENTS
C              DE LA RELATION UNILATERALE PAR LES COMPOSANTES DES
C              NORMALES (I.E. : ON EST DANS UNE ZONE OU LA RELATION
C              UNILATERALE PORTE SUR LE DEPLACEMENT)
C IN  POSNOE : INDICE DU NOEUD ESCLAVE DANS CONTNO
C IN  POSMA  : INDICE DE LA MAILLE MAITRE (OU DU NOEUD MAITRE SI < 0)
C IN  REAPRO : 0 -> INTERDIT POUR L'INSTANT (SEUL JEU RECALCULE)
C              1 -> PROJECTION LINEAIRE
C              2 -> PROJECTION QUADRATIQUE
C IN  CMULT  : COEFFICIENT MULTIPLICATEUR DE LA RELATION UNILATERALE
C VAR NORM   : DIRECTION DE PROJECTION (DIRECTION PM NORMEE)
C OUT POSNO  : INDICES DANS CONTNO DES NOEUDS ESCLAVE ET MAITRES
C OUT COEF   : COEFFICIENTS LIES AU NOEUD ESCLAVE ET AUX NOEUDS MAITRES
C OUT OLDJEU : JEU OBTENU AVANT CORRECTION
C OUT JEU    : VALEUR DU JEU APRES CORRECTION
C OUT DDL    : NUMEROS DES DDLS ESCLAVE ET MAITRES CONCERNES
C OUT NBNO   : NOMBRE DE NOEUDS MAITRES CONCERNES
C OUT JDECAL : NOMBRE DE DDLS CONCERNES (ESCLAVES + MAITRES)
C
C -------------- DEBUT DECLARATIONS NORMALISEES JEVEUX -----------------
C
      CHARACTER*32       JEXNUM , JEXNOM
      INTEGER            ZI
      COMMON  / IVARJE / ZI(1)
      REAL*8             ZR
      COMMON  / RVARJE / ZR(1)
      COMPLEX*16         ZC
      COMMON  / CVARJE / ZC(1)
      LOGICAL            ZL
      COMMON  / LVARJE / ZL(1)
      CHARACTER*8        ZK8
      CHARACTER*16                ZK16
      CHARACTER*24                          ZK24
      CHARACTER*32                                    ZK32
      CHARACTER*80                                              ZK80
      COMMON  / KVARJE / ZK8(1) , ZK16(1) , ZK24(1) , ZK32(1) , ZK80(1)
C
C ---------------- FIN DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER      NUMNO,NUMA,ITYP,NUTYP,K,INO,NO(9),JDECDL
      INTEGER      JCOOR,JDDL,JPDDL,JNOCO,JMACO,JNOMA,JPONO,JDEC,IATYMA
      INTEGER      NBDDL1,NBDDL2,NTPOI1,NTSEG2,NTSEG3,NTTRI3,NTTRI6
      INTEGER      NTQUA4, NTQUA8, NTQUA9,JTGDEF,JMETH,PRONOR,JLISSA
      INTEGER      JNORLI,JNRINI,JVERIP
      REAL*8       COOR(27),COORDP(3),COORDM(3),R8DOT,COEFNO(9),PM(3)
      REAL*8       XNORM(3),XTANG(6)
      CHARACTER*19 NLISSA
      CHARACTER*24 CONTNO,DDLCO,PDDL,CONTMA,NOMACO,PNOMA,TANDEF
      CHARACTER*24 METHCO,NORLIS,NORINI,VERIPE
      REAL*8       TOLE
      INTEGER      ARETE(3),NOEUD(3)
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ ()
C
      CONTNO = DEFICO(1:16)//'.NOEUCO'
      CONTMA = DEFICO(1:16)//'.MAILCO'
      NOMACO = DEFICO(1:16)//'.NOMACO'
      PNOMA  = DEFICO(1:16)//'.PNOMACO'
      DDLCO  = DEFICO(1:16)//'.DDLCO'
      PDDL   = DEFICO(1:16)//'.PDDLCO'
      TANDEF = DEFICO(1:16)//'.TANDEF'
      METHCO = DEFICO(1:16)//'.METHCO'
      NORLIS = DEFICO(1:16)//'.NORLIS'
      NORINI = RESOCO(1:14)//'.NORINI'
      VERIPE = DEFICO(1:16)//'.VERIPE'       
C
      CALL JEVEUO (NEWGEO(1:19)//'.VALE','L',JCOOR)
      CALL JEVEUO (DDLCO, 'L',JDDL)
      CALL JEVEUO (PDDL,  'L',JPDDL)
      CALL JEVEUO (CONTNO,'L',JNOCO)
      CALL JEVEUO (CONTMA,'L',JMACO)
      CALL JEVEUO (NOMACO,'L',JNOMA)
      CALL JEVEUO (PNOMA, 'L',JPONO)
      CALL JEVEUO (METHCO,'L',JMETH)
      CALL JEVEUO (NORLIS,'L',JNORLI)
      CALL JEVEUO (NORINI,'L',JNRINI)
      CALL JEVEUO (VERIPE,'L',JVERIP)
C
C ----------------------------------------------------------------------
C     PRONOR EST UN INDICATEUR DES NORMALES D'APPARIEMMENT ET
C     DU LISSAGE
C     PRONOR = 0 SI NORMALE = 'MAIT'       ET LISSAGE = 'NON'
C            = 1 SI NORMALE = 'MAIT_ESCL'  ET LISSAGE = 'NON'
C            = 2 SI NORMALE = 'MAIT'       ET LISSAGE = 'OUI'
C            = 3 SI NORMALE = 'MAIT_ESCL'  ET LISSAGE = 'OUI'
C ----------------------------------------------------------------------
C
      PRONOR = ZI(JMETH+8)
      PRONOR = PRONOR + 2 * ZI(JNORLI+1)

      IF (ZI(JMETH+10*(IZONE-1)+2).EQ.1) THEN
         CALL JEVEUO (TANDEF,'L',JTGDEF)
         XTANG(1) = ZR(JTGDEF+3*(IZONE-1))
         XTANG(2) = ZR(JTGDEF+3*(IZONE-1)+1)
         XTANG(3) = ZR(JTGDEF+3*(IZONE-1)+2)
      ELSE
         XTANG(1) = 0.0D0
         XTANG(2) = 0.0D0
         XTANG(3) = 0.0D0
      ENDIF
C
C --- CARACTERISTIQUES DU NOEUD ESCLAVE
C
      POSNO(1) = POSNOE
      NUMNO = ZI(JNOCO+POSNOE-1)
      NBDDL1 = ZI(JPDDL+POSNO(1)) - ZI(JPDDL+POSNO(1)-1)
      IF (NBDDL1.GT.3) THEN
        CALL UTMESS ('F','PROJEC_01','ON NE PEUT PAS AVOIR PLUS'
     &               //' DE 3 DDLS IMPLIQUES DANS LA MEME RELATION'
     &               //' UNILATERALE')
      END IF
      JDECAL = 0
      JDECDL = ZI(JPDDL+POSNO(1)-1)
      DO 3 K = 1,3
        XNORM(K) = ZR(JNRINI+3*(POSNO(1)-1)+K-1)
        NORM(K)  = XNORM(K)
3     CONTINUE
      CALL CATANG(NDIM,XNORM,XTANG,ZI(JMETH+10*(IZONE-1)+2))
C
C --- COORDONNEES DU NOEUD ESCLAVE
C
      COORDP(1) = ZR(JCOOR+3*(NUMNO-1))
      COORDP(2) = ZR(JCOOR+3*(NUMNO-1)+1)
      COORDP(3) = ZR(JCOOR+3*(NUMNO-1)+2)
C
C ======================================================================
C APPARIEMENT NODAL : POSMA < 0 ET NUMA EST LE NUMERO DU NOEUD MAITRE
C SI PAS D'APPARIEMENT (POSNO(2) = 0) ON NE FAIT RIEN
C ======================================================================
C CETTE PARTIE DU CODE N'EST ATTEINTE QU'A PARTIR DE PROJCO (PAS CHMANO)
C ======================================================================
C
      IF (POSMA.LE.0) THEN
        POSNO(2) = ABS(POSMA)
        IF (POSNO(2).EQ.0) THEN
          NBNO = 0
          JEU = 0.D0
        ELSE
          NBNO = 1
          NO(1) = ZI(JNOCO+POSNO(2)-1)
          COORDM(1) = ZR(JCOOR+3*(NO(1)-1))
          COORDM(2) = ZR(JCOOR+3*(NO(1)-1)+1)
          COORDM(3) = ZR(JCOOR+3*(NO(1)-1)+2)
          DO 10 K = 1,3
            PM(K) = COORDM(K)-COORDP(K)
10        CONTINUE
          JEU = R8DOT(3,NORM,1,PM,1)
          NBDDL2 = ZI(JPDDL+POSNO(2)) - ZI(JPDDL+POSNO(2)-1)
          IF (NBDDL2.GT.3) THEN
            CALL UTMESS ('F','PROJEC_02','ON NE PEUT PAS AVOIR PLUS'
     &                 //' DE 3 DDLS IMPLIQUES DANS LA MEME RELATION'
     &                 //' UNILATERALE')
          END IF
          JDECDL = ZI(JPDDL+POSNO(2)-1)
          DO 25 K = 1,NBDDL2
            COEF(JDECAL+K) = -1.D0 * CMULT
            IF (MULNOR) COEF(JDECAL+K) = COEF(JDECAL+K) * NORM(K)
            DDL(JDECAL+K) = ZI(JDDL+JDECDL+K-1)
25        CONTINUE
          JDECAL = JDECAL + NBDDL2
        END IF
        GO TO 999
      END IF
C
C ======================================================================
C APPARIEMENT NOEUD-FACETTE : NUMA EST LE NUMERO DE LA MAILLE MAITRE
C ======================================================================
C
C --- NUMERO DE LA MAILLE MAITRE ET NOMBRE DE NOEUDS
C
      NUMA = ZI(JMACO+POSMA-1)
      NBNO = ZI(JPONO+POSMA) - ZI(JPONO+POSMA-1)
      IF (NBNO.GT.9) THEN
        CALL UTMESS ('F','PROJEC_03','ERREUR DE DIMENSIONNEMENT : '
     &               //'LE NOMBRE DE NOEUDS EST SUPERIEUR A 9')
      END IF
C
C --- TYPE DE LA MAILLE MAITRE DE NUMERO ABSOLU NUMA
C SEG2=2  SEG3=4  TRIA3=7  TRIA6=9  QUAD4=12  QUAD8=14  QUAD9=16
C
      CALL JENONU(JEXNOM('&CATA.TM.NOMTM','POI1')  , NTPOI1 )
      CALL JENONU(JEXNOM('&CATA.TM.NOMTM','SEG2')  , NTSEG2 )
      CALL JENONU(JEXNOM('&CATA.TM.NOMTM','SEG3')  , NTSEG3 )
      CALL JENONU(JEXNOM('&CATA.TM.NOMTM','TRIA3') , NTTRI3 )
      CALL JENONU(JEXNOM('&CATA.TM.NOMTM','TRIA6') , NTTRI6 )
      CALL JENONU(JEXNOM('&CATA.TM.NOMTM','QUAD4') , NTQUA4 )
      CALL JENONU(JEXNOM('&CATA.TM.NOMTM','QUAD8') , NTQUA8 )
      CALL JENONU(JEXNOM('&CATA.TM.NOMTM','QUAD9') , NTQUA9 )
C
      CALL JEVEUO (NOMA//'.TYPMAIL','L',IATYMA)
      ITYP  = IATYMA-1+NUMA
      NUTYP = ZI(ITYP)
      IF ( NUTYP .EQ. NTPOI1 ) THEN
        CALL UTMESS ('F','PROJEC_07','UN POI1 NE PEUT PAS ETRE '
     &               //'UNE MAILLE MAITRE')
      END IF
C      IF (NDIM.EQ.3) THEN
C        IF ((NUTYP.EQ.NTSEG2).OR.(NUTYP.EQ.NTSEG3)) THEN
C          CALL UTMESS ('F','PROJEC_04','UN SEGMENT NE PEUT PAS ETRE '
C     &                 //'UNE MAILLE MAITRE EN 3D')
C        END IF
C      END IF
C
C --- INDICE DANS CONTMA ET NUMERO ABSOLU DES NOEUDS DE LA MAILLE MAITRE
C
      JDEC = ZI(JPONO+POSMA-1)
      DO 60 INO = 1,NBNO
        POSNO(INO+1) = ZI(JNOMA+JDEC+INO-1)
        NO(INO)      = ZI(JNOCO+POSNO(INO+1)-1)
 60   CONTINUE
      IF (PRONOR.GE.2) THEN
         NLISSA = '&&PROJEC.LISSAGE'
         CALL WKVECT(NLISSA,'V V R',NBNO*3,JLISSA)
         DO 61 INO = 1, NBNO
            DO 62 K = 1,3
              ZR(JLISSA+(INO-1)*3-1+K)=ZR(JNRINI+3*(POSNO(INO+1)-1)-1+K)
 62         CONTINUE
 61      CONTINUE
      ENDIF
C
C --- COORDONNEES DES NOEUDS DE LA MAILLE MAITRE
C
      DO 70 INO = 1,NBNO
        COOR(3*(INO-1)+1) = ZR(JCOOR+3*(NO(INO)-1))
        COOR(3*(INO-1)+2) = ZR(JCOOR+3*(NO(INO)-1)+1)
        COOR(3*(INO-1)+3) = ZR(JCOOR+3*(NO(INO)-1)+2)
  70  CONTINUE
      
C
C --- CALCUL DE LA "PROJECTION" M DU NOEUD ESCLAVE SUR LA MAILLE MAITRE.
C --- COEFNO CONTIENT 1 COEFFICIENT PAR NOEUD MAITRE : ON RANGERA CE
C --- COEFFICIENT APRES DANS LE TABLEAU COEF POUR TOUS LES DDLS DE
C --- CE NOEUD MAITRE.
C
C --- AFFECTATION DE LA TANGENTE DEFINIE PAR L'UTILISATEUR
C
      IF(ZI(JMETH+10*(IZONE-1)+2).EQ.1) THEN
         TANG(1) = ZR(JTGDEF+3*(IZONE-1))
         TANG(2) = ZR(JTGDEF+3*(IZONE-1)+1)
         TANG(3) = ZR(JTGDEF+3*(IZONE-1)+2)
      ENDIF
      IF ((NUTYP.EQ.NTSEG2).OR.(NUTYP.EQ.NTSEG3)) THEN
        CALL PROJSE (NUTYP,NDIM,REAPRO,COOR(1),COOR(4),COOR(7),
     &           COORDP,NORM,COORDM,COEFNO,
     &           OLDJEU,JEU,TANG,JEUFX,PRONOR,ZI(JMETH+10*(IZONE-1)+2),
     &           ZR(JLISSA),
     &           ZR(JVERIP+(IZONE-1)))
      ELSE IF ((NUTYP.EQ.NTTRI3).OR.(NUTYP.EQ.NTTRI6)) THEN
        TOLE=1D-2
        CALL PROJTR (NUTYP,NDIM,NBNO,REAPRO,COOR(1),COOR(4),
     &               COOR(7),COORDP,NORM,COORDM,COEFNO,
     &               OLDJEU,JEU,TANG,PRONOR,ZI(JMETH+10*(IZONE-1)+2),
     &               ZR(JLISSA),
     &               ZR(JVERIP+(IZONE-1)),
     &               TOLE,ARETE,NOEUD)
      ELSE IF ((NUTYP.EQ.NTQUA4).OR.(NUTYP.EQ.NTQUA8).OR.
     &                              (NUTYP.EQ.NTQUA9)) THEN
        CALL PROJQU (NUTYP,NDIM,NBNO,REAPRO,COOR(1),COOR(4),
     &               COOR(7),COOR(10),COORDP,NORM,COORDM,COEFNO,
     &               OLDJEU,JEU,TANG,PRONOR,ZI(JMETH+10*(IZONE-1)+2),
     &               ZR(JLISSA),
     &               ZR(JVERIP+(IZONE-1)))
      ELSE
        CALL UTMESS ('F','PROJEC_05','ON NE SAIT PAS TRAITER CE '
     &               //'TYPE DE MAILLE')
      END IF

C
C --- NUMEROS DES DEGRES DE LIBERTE DES NOEUDS MAITRES ET COEFFICIENTS
C
      DO 5 K = 1,NBDDL1
        COEF(JDECAL+K) = 1.D0 * CMULT
        COFX(JDECAL+K) = 1.D0 * CMULT
        COFY(JDECAL+K) = 1.D0 * CMULT
        IF (MULNOR) THEN
           COEF(JDECAL+K) = COEF(JDECAL+K) * NORM(K)
           COFX(JDECAL+K) = COFX(JDECAL+K) * TANG(K)
           IF(NDIM.EQ.3) COFY(JDECAL+K)=COFY(JDECAL+K) * TANG(K+3)
        ENDIF
        DDL(JDECAL+K) = ZI(JDDL+JDECDL+K-1)
5     CONTINUE
      JDECAL = JDECAL + NBDDL1

      DO 80 INO = 1,NBNO
        NBDDL2 = ZI(JPDDL+POSNO(INO+1)) - ZI(JPDDL+POSNO(INO+1)-1)
        IF (NBDDL2.GT.3) THEN
          CALL UTMESS ('F','PROJEC_06','ON NE PEUT PAS AVOIR PLUS'
     &               //' DE 3 DDLS IMPLIQUES DANS LA MEME RELATION'
     &               //' UNILATERALE')
        END IF
        JDECDL = ZI(JPDDL+POSNO(INO+1)-1)
        DO 85 K = 1,NBDDL2
          COEF(JDECAL+K) = COEFNO(INO) * CMULT
          COFX(JDECAL+K) = COEFNO(INO) * CMULT
          COFY(JDECAL+K) = COEFNO(INO) * CMULT
          IF (MULNOR) THEN
             COEF(JDECAL+K) = COEF(JDECAL+K) * NORM(K)
             COFX(JDECAL+K) = COFX(JDECAL+K) * TANG(K)
             IF(NDIM.EQ.3) COFY(JDECAL+K) = COFY(JDECAL+K) * TANG(K+3)
          ENDIF
          DDL(JDECAL+K) = ZI(JDDL+JDECDL+K-1)
 85     CONTINUE
        JDECAL = JDECAL + NBDDL2
 80   CONTINUE
C
C ======================================================================
C
      IF (PRONOR.GE.2) CALL JEDETR(NLISSA)
 999  CONTINUE
C
C ----------------------------------------------------------------------
C
      CALL JEDEMA()
      
      END
