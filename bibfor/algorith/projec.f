      SUBROUTINE PROJEC(IZONE,NDIM,NOMA,NEWGEO,RESOCO,DEFICO,
     &                  PROJ,ZONESY,
     &                  POSNOE,POSMA,
     &                  NORM,TANG,
     &                  COEF,COFX,COFY,OLDJEU,JEU,PROYES,
     &                  NBNO,POSNO,NBDDL,DDL)
C ======================================================================
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 26/06/2006   AUTEUR MABBAS M.ABBAS 
C TOLE CRP_21
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C
      IMPLICIT     NONE
      INTEGER      IZONE
      INTEGER      NDIM
      CHARACTER*8  NOMA
      CHARACTER*24 NEWGEO
      CHARACTER*24 RESOCO
      CHARACTER*24 DEFICO
      INTEGER      PROJ
      INTEGER      ZONESY
      INTEGER      POSNOE
      INTEGER      POSMA
      REAL*8       NORM(3)
      REAL*8       TANG(6)
      REAL*8       COEF(30)
      REAL*8       COFX(30)
      REAL*8       COFY(30) 
      REAL*8       OLDJEU
      REAL*8       JEU
      INTEGER      PROYES
      INTEGER      NBNO
      INTEGER      POSNO(10)
      INTEGER      NBDDL
      INTEGER      DDL(30)    
C
C ----------------------------------------------------------------------
C ROUTINE APPELEE PAR : CHMANO / PROJCO
C ----------------------------------------------------------------------
C
C "PROJECTION" D'UN NOEUD ESCLAVE SUR UNE MAILLE MAITRE.
C CALCUL DES COEFFICIENTS A UTILISER POUR REPRESENTER LE DEPLACEMENT
C DE LA PROJECTION EN FONCTION DES DEPLACEMENTS DES NOEUDS DE LA MAILLE.
C CALCUL DU JEU COURANT. MEMORISATION DES NUMEROS DES DDLS MAITRES.
C
C * ROUTINE APPELE PAR CHMANO: LA PROJECTION EST UN PREALABLE A LA
C     RECHERCHE DE LA MAILLE LA PLUS PROCHE CONNAISSANT LE NOEUD LE
C     PLUS PROCHE.
C * ROUTINE APPELE PAR PROJCO: LA PROJECTION PROPREMENT DITE
C
C
C IN  IZONE  : NUMERO DE LA ZONE DE CONTACT
C IN  NDIM   : DIMENSION DE L'ESPACE (2 OU 3)
C IN  NOMA   : NOM DU MAILLAGE
C IN  NEWGEO : GEOMETRIE ACTUALISEE EN TENANT COMPTE DU CHAMP DE
C              DEPLACEMENTS COURANT
C VAR RESOCO : SD DE TRAITEMENT NUMERIQUE DU CONTACT
C IN  DEFICO : SD DE DEFINITION DU CONTACT (ISSUE D'AFFE_CHAR_MECA)
C IN  PROJ   : 0 -> INTERDIT POUR L'INSTANT (SEUL JEU RECALCULE)
C              1 -> PROJECTION LINEAIRE
C              2 -> PROJECTION QUADRATIQUE
C IN  ZONESY : 0 -> ZONE DE CONTACT NORMAL
C              1 -> ZONE DE CONTACT AVEC APAPRIEMENT SYMETRIQUE
C IN  POSNOE : INDICE DU NOEUD ESCLAVE DANS CONTNO
C IN  POSMA  : INDICE DE LA MAILLE MAITRE (OU DU NOEUD MAITRE SI < 0)
C I/O NORM   : DIRECTION DE PROJECTION (DIRECTION PM NORMEE)
C I/O TANG   : LES DEUX VECTEURS TANGENTS
C OUT COEF   : COEFFICIENTS LIES AU NOEUD ESCLAVE ET AUX NOEUDS MAITRES
C              (DIRECTION NORMALE)
C OUT COEFX  : COEFFICIENTS LIES AU NOEUD ESCLAVE ET AUX NOEUDS MAITRES
C              (PROJECTION SUR LA PREMIERE TANGENTE)
C OUT COEFY  : COEFFICIENTS LIES AU NOEUD ESCLAVE ET AUX NOEUDS MAITRES
C              (PROJECTION SUR LA SECONDE TANGENTE)
C OUT OLDJEU : JEU OBTENU AVANT CORRECTION
C OUT JEU    : VALEUR DU JEU APRES CORRECTION
C OUT PROYES : TYPE DE PROJECTION 
C              -1000  PROJECTION EN DEHORS
C                   DE LA MAILLE MAITRE (TOLERANCE TOLEOU ET DEBORD) 
C              -30X   PROJECTION SUR
C                   ENTITE GEOMETRIQUE NOEUD
C                   DE LA MAILLE MAITRE (TOLERANCE TOLEIN ET NOEUD)
C                      X EST LE NUMERO DE NOEUD (1 A 4)
C              -20X   PROJECTION SUR
C                   ENTITE GEOMETRIQUE ARETE
C                   DE LA MAILLE MAITRE (TOLERANCE TOLEIN ET ARETE)
C                      X EST LE NUMERO D'ARETE (1 A 4)
C              -10X   PROJECTION SUR
C                   ENTITE GEOMETRIQUE DIAG (PSEUDO SUR QUADRANGLE)
C                   DE LA MAILLE MAITRE (TOLERANCE TOLEIN ET DIAG)
C                      X EST LE NUMERO DE DIAGONALE (1 A 2)
C               0     PROJECTION NORMALE
C OUT NBNO   : NOMBRE DE NOEUDS MAITRES CONCERNES
C OUT POSNO  : INDICES DANS CONTNO DES NOEUDS ESCLAVE ET MAITRES
C OUT NBDDL  : NOMBRE DE DDL NOEUD ESCLAVE+NOEUDS MAITRES
C OUT DDL    : NUMEROS DES DDLS ESCLAVE ET MAITRES CONCERNES
C
C -------------- DEBUT DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER            ZI
      COMMON  / IVARJE / ZI(1)
      REAL*8             ZR
      COMMON  / RVARJE / ZR(1)
      COMPLEX*16         ZC
      COMMON  / CVARJE / ZC(1)
      LOGICAL            ZL
      COMMON  / LVARJE / ZL(1)
      CHARACTER*8        ZK8
      CHARACTER*16                ZK16
      CHARACTER*24                          ZK24
      CHARACTER*32                                    ZK32
      CHARACTER*80                                              ZK80
      COMMON  / KVARJE / ZK8(1) , ZK16(1) , ZK24(1) , ZK32(1) , ZK80(1)
C
C ---------------- FIN DECLARATIONS NORMALISEES JEVEUX -----------------
C
      REAL*8       COORDP(3),COOR(27),COORDM(3)
      CHARACTER*24 NORINI
      INTEGER      JNRINI
      CHARACTER*19 NLISSA
      INTEGER      JLISSA
      INTEGER      MOYEN,LISSA,TANGDF,DIAGNO
      CHARACTER*4  MATYP     
      REAL*8       TOLEIN,TOLEOU
      INTEGER      INO,K,NBDDLE,POSNOM
      REAL*8       COEFNO(9)     
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ ()
C
      NORINI = RESOCO(1:14)//'.NORINI'  
C
      CALL JEVEUO (NORINI,'L',JNRINI)
C
C --- CARACTERISTIQUES DU NOEUD ESCLAVE
C

      CALL CARANO (IZONE,DEFICO,RESOCO,NEWGEO,NDIM,POSNOE,
     &             COORDP,NORM,TANG,NBDDLE)

      POSNO(1) = POSNOE
C
C --- APPARIEMENT
C
      IF (POSMA.LE.0) THEN
C --- APPARIEMENT NODAL : POSMA < 0 
C --- SI PAS D'APPARIEMENT (POSNO(2) = 0) ON NE FAIT RIEN
C --- CETTE PARTIE DU CODE N'EST ATTEINTE QU'A PARTIR DE PROJCO 
C --- (PAS CHMANO)
         POSNO(2) = ABS(POSMA)
         POSNOM   = POSNO(2)
         IF (POSNO(2).EQ.0) THEN
           NBNO = 0
           JEU  = 0.D0
         ELSE
           CALL PROJNO(NEWGEO,DEFICO,NORM,COORDP,POSNOM,
     &                 NBNO,JEU)
         ENDIF
      ELSE
C --- APPARIEMENT NOEUD-FACETTE 
C
C --- CARACTERISTIQUES DE LA MAILLE MAITRE
C
         CALL CARAMA(NOMA,DEFICO,NEWGEO,POSMA,IZONE,
     &               POSNO,
     &               MATYP,COORDP,NBNO,COOR,
     &               MOYEN,LISSA,TANGDF,TOLEIN,TOLEOU,DIAGNO)
C
C --- SI APPARIEMENT SYMETRIQUE: ACTIVATION DU DIAGNOSTIC FIN POUR 
C --- LA DETECTION DE LA PROJECTION SUR LES NOEUDS
C
         IF (ZONESY.NE.0) THEN 
           DIAGNO = 1
         ENDIF
C
C --- LISSAGE DES NORMALES
C
         IF (LISSA.EQ.1) THEN
            NLISSA = '&&PROJEC.LISSAGE'
            CALL WKVECT(NLISSA,'V V R',NBNO*3,JLISSA)
            DO 61 INO = 1, NBNO
               DO 62 K = 1,3
                 ZR(JLISSA+(INO-1)*3-1+K) = 
     &              ZR(JNRINI+3*(POSNO(INO+1)-1)-1+K)
 62            CONTINUE
 61         CONTINUE
         ENDIF

         CALL PROJMA(MATYP,NBNO,NDIM,
     &               COOR,COORDP,
     &               PROJ,MOYEN,LISSA,TANGDF,JLISSA,DIAGNO,
     &               TOLEIN,TOLEOU,
     &               NORM,TANG,
     &               COORDM,COEFNO,OLDJEU,JEU,PROYES)
      ENDIF
C
C --- NUMEROS DES DEGRES DE LIBERTE DES NOEUDS MAITRES ET COEFFICIENTS
C
      CALL CFCOEF(NDIM,DEFICO,RESOCO,NEWGEO,
     &            NBDDLE,NBNO,POSNO,POSMA,COEFNO,
     &            NORM,TANG,
     &            COEF,COFX,COFY,
     &            NBDDL,DDL)
C
C ----------------------------------------------------------------------
C
      IF (LISSA.EQ.1) THEN 
        CALL JEDETR(NLISSA)
      ENDIF
C
      CALL JEDEMA()
      
      END
