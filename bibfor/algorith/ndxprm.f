      SUBROUTINE NDXPRM(MODELZ,MATE  ,CARELE,COMPOR,CARCRI,
     &                  METHOD,LISCHA,NUMEDD,NUMFIX,SOLVEU,
     &                  COMREF,SDDISC,SDDYNA,SDTIME,NUMINS,
     &                  FONACT,VALINC,SOLALG,VEELEM,MEELEM,
     &                  MEASSE,MAPREC,MATASS,CODERE,FACCVG,
     &                  LDCCVG)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 24/05/2011   AUTEUR ABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C TOLE CRP_21
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT NONE
      CHARACTER*16  METHOD(*)
      INTEGER       FONACT(*)
      CHARACTER*(*) MODELZ
      CHARACTER*24  MATE,CARELE
      CHARACTER*24  COMPOR,CARCRI,SDTIME,NUMEDD,NUMFIX
      CHARACTER*19  SDDISC,SDDYNA,LISCHA,SOLVEU
      CHARACTER*24  COMREF,CODERE
      CHARACTER*19  SOLALG(*),VALINC(*)
      CHARACTER*19  VEELEM(*),MEELEM(*),MEASSE(*)
      INTEGER       NUMINS
      CHARACTER*19  MAPREC,MATASS
      INTEGER       FACCVG,LDCCVG
C
C ----------------------------------------------------------------------
C
C ROUTINE MECA_NON_LINE (CALCUL - UTILITAIRE)
C
C CALCUL DE LA MATRICE GLOBALE EN PREDICTION - CAS EXPLICITE
C
C ----------------------------------------------------------------------
C
C
C IN  MODELE : MODELE
C IN  NUMEDD : NUME_DDL (VARIABLE AU COURS DU CALCUL)
C IN  NUMFIX : NUME_DDL (FIXE AU COURS DU CALCUL)
C IN  MATE   : CHAMP MATERIAU
C IN  CARELE : CARACTERISTIQUES DES ELEMENTS DE STRUCTURE
C IN  COMREF : VARI_COM DE REFERENCE
C IN  COMPOR : COMPORTEMENT
C IN  LISCHA : LISTE DES CHARGES
C IN  SDDYNA : SD POUR LA DYNAMIQUE
C IN  SDTIME : SD TIMER
C IN  METHOD : INFORMATIONS SUR LES METHODES DE RESOLUTION (VOIR NMLECT)
C IN  SOLVEU : SOLVEUR
C IN  CARCRI : PARAMETRES METHODES D'INTEGRATION LOCALES (VOIR NMLECT)
C IN  SDDISC : SD DISC_INST
C IN  PREMIE : SI PREMIER INSTANT DE CALCUL
C IN  NUMINS : NUMERO D'INSTANT
C IN  ITERAT : NUMERO D'ITERATION
C IN  VALINC : VARIABLE CHAPEAU POUR INCREMENTS VARIABLES
C IN  SOLALG : VARIABLE CHAPEAU POUR INCREMENTS SOLUTIONS
C IN  MEASSE : VARIABLE CHAPEAU POUR NOM DES MATR_ASSE
C IN  MEELEM : VARIABLE CHAPEAU POUR NOM DES MATR_ELEM
C IN  MEELEM : VARIABLE CHAPEAU POUR NOM DES VECT_ELEM
C OUT MATASS : MATRICE DE RESOLUTION ASSEMBLEE
C OUT MAPREC : MATRICE DE RESOLUTION ASSEMBLEE - PRECONDITIONNEMENT
C OUT FACCVG : CODE RETOUR FACTORISATION MATRICE GLOBALE
C                0 - OK
C                1 - MATRICE SINGULIERE
C                3 - ON NE SAIT PAS SI SINGULIERE
C OUT LDCCVG : CODE RETOUR INTEGRATION DU COMPORTEMENT
C                0 - OK
C                1 - ECHEC DANS L'INTEGRATION : PAS DE RESULTATS
C                2 - ERREUR DANS LES LDC SUR LA NON VERIFICATION DE
C                    CRITERES PHYSIQUES 
C                3 - SIZZ NON NUL (DEBORST) ON CONTINUE A ITERER
C OUT CODERE : CHAM_ELEM CODE RETOUR INTEGRATION LDC
C
C -------------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ----------------
C
      INTEGER ZI
      COMMON /IVARJE/ ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C -------------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ----------------
C
      LOGICAL      REASMA
      LOGICAL      LCRIGI,LCFINT,LCAMOR,LARIGI,LPREM
      
      LOGICAL      LAMOR,LSUIV,LSHIMA,LPRMO
      LOGICAL      NDYNLO,ISFONC
      LOGICAL      LBID
      REAL*8       R8BID
      CHARACTER*16 METPRE
      CHARACTER*16 OPTRIG,OPTAMO
      INTEGER      IFM,NIV,IBID,IRET
      INTEGER      ITERAT
      INTEGER      NBMATR
      CHARACTER*24 K24BLA
      CHARACTER*6  LTYPMA(20)
      CHARACTER*16 LOPTME(20),LOPTMA(20)
      LOGICAL      LASSME(20),LCALME(20)
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
      CALL INFDBG('MECA_NON_LINE',IFM,NIV)
C
C --- AFFICHAGE
C
      IF (NIV.GE.2) THEN
        WRITE (IFM,*) '<MECANONLINE> ... CALCUL MATRICE'
      ENDIF
C
C --- FONCTIONNALITES ACTIVEES
C
      LAMOR  = NDYNLO(SDDYNA,'MAT_AMORT')
      LPRMO  = NDYNLO(SDDYNA,'PROJ_MODAL') 
      LSUIV  = ISFONC(FONACT,'FORCE_SUIVEUSE')
      LSHIMA = NDYNLO(SDDYNA,'COEF_MASS_SHIFT')
C
C --- PREMIER PAS DE TEMPS ?
C
      CALL DIBCLE(SDDISC,'PREMIE','L',IRET  )
      LPREM  = IRET.EQ.0 
C
C --- PARAMETRES
C
      METPRE = METHOD(5)      
      IF (METPRE.NE.'TANGENTE') THEN
        CALL U2MESS('F','MECANONLINE5_1')
      ENDIF
C
C --- INITIALISATIONS
C
      CALL NMCMAT('INIT',' ',' ',' ',.FALSE.,
     &            .FALSE.,NBMATR,LTYPMA,LOPTME,LOPTMA,
     &            LCALME,LASSME)
      FACCVG = 0
      LDCCVG = 0
      ITERAT = 0
      LCAMOR = .FALSE.
      K24BLA = ' '
C
C --- CHOIX DE REASSEMBLAGE DE LA MATRICE GLOBALE
C    
      REASMA =  .FALSE.
      IF (LPREM) THEN
        IF (LPRMO) THEN
          REASMA  = .FALSE.
        ELSE  
          REASMA  = .TRUE.
        ENDIF
      ENDIF
C
C --- CHOIX DE REASSEMBLAGE DE L'AMORTISSEMENT
C
      IF (LAMOR) THEN
        CALL NMCHRA(SDDYNA,OPTAMO,LCAMOR)
      ENDIF
C
C --- OPTION DE CALCUL POUR MERIMO
C
      OPTRIG = 'RIGI_MECA_TANG'
      LCFINT = .FALSE.
      LCRIGI = .FALSE.
      LARIGI = .FALSE.
C
C --- SI ON DOIT RECALCULER L'AMORTISSEMENT DE RAYLEIGH
C
      IF (LCAMOR) THEN
        LCRIGI = .TRUE.
      ENDIF
C
C --- DECALAGE COEF_MASS_SHIFT AU PREMIER PAS DE TEMPS -> ON A BESOIN
C --- DE LA MATRICE DE RIGIDITE
C
      IF (LSHIMA.AND.LPREM) THEN
        LCRIGI = .TRUE.
        LARIGI = .TRUE.
      ENDIF
C
C --- CALCUL DES MATR-ELEM DE RIGIDITE
C
      IF (LCRIGI) THEN
        CALL NMCMAT('AJOU','MERIGI',OPTRIG,' ',.TRUE.,
     &              LARIGI,NBMATR,LTYPMA,LOPTME,LOPTMA,
     &              LCALME,LASSME)
      ENDIF
C
C --- CALCUL ET ASSEMBLAGE DES MATR-ELEM D'AMORTISSEMENT DE RAYLEIGH
C
      IF (LCAMOR) THEN
        CALL NMCMAT('AJOU','MEAMOR',OPTAMO,' ',.TRUE.,
     &             .TRUE.,NBMATR,LTYPMA,LOPTME,LOPTMA,
     &             LCALME,LASSME)
      ENDIF
C
C --- CALCUL DES MATR-ELEM DES CHARGEMENTS
C
      IF (LSUIV) THEN
        CALL NMCMAT('AJOU','MESUIV',' ',' ',.TRUE.,
     &              .FALSE.,NBMATR,LTYPMA,LOPTME,LOPTMA,
     &              LCALME,LASSME)
      ENDIF
C
C --- CALCUL ET ASSEMBLAGE DES MATR_ELEM DE LA LISTE
C
      IF (NBMATR.GT.0) THEN
        CALL NMXMAT(MODELZ,MATE  ,CARELE,COMPOR,CARCRI,
     &              SDDISC,SDDYNA,FONACT,NUMINS,ITERAT,
     &              VALINC,SOLALG,LISCHA,COMREF,K24BLA,
     &              K24BLA,SOLVEU,NUMEDD,NUMFIX,SDTIME,
     &              NBMATR,LTYPMA,LOPTME,LOPTMA,LCALME,
     &              LASSME,LCFINT,MEELEM,MEASSE,VEELEM,
     &              LDCCVG,CODERE)
      ENDIF
C
C --- CALCUL DE LA MATRICE ASSEMBLEE GLOBALE
C
      IF (REASMA) THEN
        CALL NDXMAT(FONACT,LISCHA,SOLVEU,NUMEDD,SDDYNA,
     &              SDDISC,MEELEM,MEASSE,MATASS)
      ENDIF
C
C --- FACTORISATION DE LA MATRICE ASSEMBLEE GLOBALE
C
      IF (REASMA) THEN
        CALL NMTIME('INIT' ,'TMP',SDTIME,LBID  ,R8BID )
        CALL NMTIME('DEBUT','TMP',SDTIME,LBID  ,R8BID )
        CALL PRERES(SOLVEU,'V',FACCVG,MAPREC,MATASS,IBID,-9999)
        CALL NMTIME('FIN'      ,'TMP',SDTIME,LBID  ,R8BID )
        CALL NMTIME('FACT_NUME','TMP',SDTIME,LBID  ,R8BID )
      ENDIF
C
      CALL JEDEMA()
C
      END
