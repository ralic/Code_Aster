      SUBROUTINE SQU110 ( NOMRES,NOMA,NBMAIL,NOMAIL,TRANS,ANGL)
      IMPLICIT NONE
      INTEGER             NBMAIL
      REAL*8              TRANS(3),ANGL(3)
      CHARACTER*8         NOMRES, NOMA, NOMAIL(*)
C-----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 26/04/2011   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C TOLE CRP_6
C
C  CREER LE MAILLAGE SQUELETTE CORRESPONDANT A UN MAILLAGE
C
C-----------------------------------------------------------------------
C
C OUT : NOMRES : NOM DU MAILLAGE A CREER
C IN  : NOMA   : NOM DU MAILLAGE EXISTANT
C IN  : NBMAIL : NOMBRE DE MAILLES A TRAITER
C IN  : NOMAIL : LISTE DES MAILLES A TRAITER (NOM)
C IN  : TRANS  : VECTEUR TRANSLATION A APPLIQUER AUX NOEUDS
C IN  : ANGL   : VECTEUR ROTATION    A APPLIQUER AUX NOEUDS
C
C-------- DEBUT COMMUNS NORMALISES  JEVEUX  ----------------------------
C
      INTEGER          ZI
      COMMON  /IVARJE/ ZI(1)
      REAL*8           ZR
      COMMON  /RVARJE/ ZR(1)
      COMPLEX*16       ZC
      COMMON  /CVARJE/ ZC(1)
      LOGICAL          ZL
      COMMON  /LVARJE/ ZL(1)
      CHARACTER*8      ZK8
      CHARACTER*16            ZK16
      CHARACTER*24                    ZK24
      CHARACTER*32                            ZK32
      CHARACTER*80                                    ZK80
      COMMON  /KVARJE/ ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
      CHARACTER*32 JEXNOM,JEXNUM
C
C----------  FIN  COMMUNS NORMALISES  JEVEUX  --------------------------
C
      INTEGER       NBCMPM
      PARAMETER    (NBCMPM=10)
      INTEGER       NBNOMX, KVALE, JNOE, JTOM, JOPT , K , L
      INTEGER       JDIME, JREFE, JVALE, JTNM, JNPT, NBPT, I , JTRANS
      INTEGER IBID, NBNOEU, INO, IMA, NBNOEV, JANGL,IATYMA
      CHARACTER*4   CDIM
      CHARACTER*8   K8B, MAILLE, NOEUD
      CHARACTER*24  NOMNOV, TYPMAV, CONNEV, NODIMV,
     +              COOVAV, COODSV, COOREV
      CHARACTER*24  NOMMAI, NOMNOE, TYPMAI, CONNEX, NODIME,
     +              COOVAL, COODSC, COOREF
      CHARACTER*24  NOMTRA, NOMANG
C
      REAL*8        MAT1(NBCMPM,NBCMPM),MAT2(NBCMPM,NBCMPM)
      REAL*8        MAT3(NBCMPM,NBCMPM),ZERO,MATTMP(NBCMPM,NBCMPM)
      REAL*8        MATROT(NBCMPM,NBCMPM),X1(3),XR1(3)
C
      REAL*8        PI
C
C-----------------------------------------------------------------------
      DATA ZERO /0.0D+00/
C-----------------------------------------------------------------------
C
      PI = 4.0D0 * ATAN(1.0D0)
C
      CALL JEMARQ ( )
C
      NOMNOV  = NOMA//'.NOMNOE         '
      TYPMAV  = NOMA//'.TYPMAIL        '
      CONNEV  = NOMA//'.CONNEX         '
      NODIMV  = NOMA//'.DIME           '
      COOVAV  = NOMA//'.COORDO    .VALE'
      COODSV  = NOMA//'.COORDO    .DESC'
      COOREV  = NOMA//'.COORDO    .REFE'
C
      NOMMAI  = NOMRES//'.NOMMAI         '
      NOMNOE  = NOMRES//'.NOMNOE         '
      TYPMAI  = NOMRES//'.TYPMAIL        '
      CONNEX  = NOMRES//'.CONNEX         '
      NODIME  = NOMRES//'.DIME           '
      COOVAL  = NOMRES//'.COORDO    .VALE'
      COODSC  = NOMRES//'.COORDO    .DESC'
      COOREF  = NOMRES//'.COORDO    .REFE'
C
      NOMTRA  = NOMRES//'.TRANS          '
      NOMANG  = NOMRES//'.ANGL_NAUT      '
C
C
C     --- ON RECUPERE LES NOEUDS ---
C
      CALL JEVEUO ( NODIMV , 'L' , JDIME )
      NBNOEV = ZI(JDIME)
      CALL WKVECT ('&&SQU110.TRAV','V V I',NBNOEV,JNOE)
      NBNOEU = 1
      DO 40 IMA = 1 , NBMAIL
         MAILLE = NOMAIL(IMA)
         CALL JENONU ( JEXNOM(NOMA//'.NOMMAI',MAILLE),IBID)
         CALL JELIRA ( JEXNUM(CONNEV,IBID),'LONMAX',NBPT,K8B)
         CALL JEVEUO ( JEXNUM(CONNEV,IBID),'L',JOPT)
         DO 42 INO = 0 , NBPT-1
            CALL I2RDLI ( ZI(JOPT+INO) , ZI(JNOE) , NBNOEU )
 42      CONTINUE
 40   CONTINUE
      NBNOEU = NBNOEU - 1
C
C --- CALCUL DE LA MATRICE DE ROTATION A PARTIR DE ANGL_NAUT
C --- CONVERSION DES ANGLES NAUTIQUES  DEGRE ---> RADIAN
C
      ANGL(1) = ANGL(1) * PI / 180.0D0
      ANGL(2) = ANGL(2) * PI / 180.0D0
      ANGL(3) = ANGL(3) * PI / 180.0D0
C
      CALL INTET0(ANGL(1),MAT1,3)
      CALL INTET0(ANGL(2),MAT2,2)
      CALL INTET0(ANGL(3),MAT3,1)
      CALL R8INIR(NBCMPM*NBCMPM,ZERO,MATTMP,1)
      CALL PMPPR(MAT3,NBCMPM,NBCMPM,1,MAT2,NBCMPM,NBCMPM,1,
     &           MATTMP,NBCMPM,NBCMPM)
      CALL R8INIR(NBCMPM*NBCMPM,ZERO,MATROT,1)
      CALL PMPPR(MATTMP,NBCMPM,NBCMPM,1,MAT1,NBCMPM,NBCMPM,1,
     &           MATROT,NBCMPM,NBCMPM)
C
C                     --- ON CREE LE SQUELETTE ---
C
C     --- LE '.TRANS' ---
C
      CALL WKVECT ( NOMTRA, 'G V R8', 3, JTRANS )
      ZR(JTRANS-1+1) = TRANS(1)
      ZR(JTRANS-1+2) = TRANS(2)
      ZR(JTRANS-1+3) = TRANS(3)
C
C     --- LE '.ANGL_NAUT' ---
C
      CALL WKVECT ( NOMANG, 'G V R8', 3, JANGL )
      ZR(JANGL-1+1) = ANGL(1)
      ZR(JANGL-1+2) = ANGL(2)
      ZR(JANGL-1+3) = ANGL(3)
C
C     --- LE '.DIME' ---
C
      CALL WKVECT ( NODIME, 'G V I', 6, JDIME )
      ZI(JDIME-1+1) = NBNOEU
      ZI(JDIME-1+3) = NBMAIL
      ZI(JDIME-1+6) = 3
C
C     --- LE '.NOMNOE' ---
C
      CALL JECREO ( NOMNOE, 'G N K8' )
      CALL JEECRA ( NOMNOE, 'NOMMAX', NBNOEU, ' ' )
      DO 10 INO = 0 , NBNOEU-1
         CALL JENUNO ( JEXNUM(NOMNOV,ZI(JNOE+INO)) , NOEUD )
         CALL JECROC ( JEXNOM(NOMNOE,NOEUD) )
 10   CONTINUE
C
C     --- LE '.COORDO' ---
C
      CALL JEDUPO (COODSV, 'G', COODSC, .FALSE. )
      CALL JEDUPO (COOREV, 'G', COOREF, .FALSE. )
      CALL JEVEUO ( COOREF , 'E' , JREFE )
      ZK24(JREFE) = NOMRES
      CALL JEVEUO ( COOVAV, 'L', KVALE )
      CALL WKVECT ( COOVAL, 'G V R8', 3*NBNOEU, JVALE)
      CALL JELIRA ( COOVAV, 'DOCU', IBID, CDIM )
      CALL JEECRA ( COOVAL, 'DOCU', IBID, CDIM )
      DO 20 I = 0 , NBNOEU-1
         CALL JENUNO ( JEXNUM(NOMNOV,ZI(JNOE+I)) , NOEUD )
         CALL JENONU ( JEXNOM(NOMNOV,NOEUD) , INO )
         DO 35 K=1,3
            X1(K)=ZR(KVALE+3*(INO-1)+K-1)
35       CONTINUE
         DO 36 K=1,3
            XR1(K)=0.D0
            DO 50 L=1,3
               XR1(K)=XR1(K)+MATROT(K,L)*X1(L)
50          CONTINUE
            XR1(K)=XR1(K)+TRANS(K)
36       CONTINUE
         ZR(JVALE+3*I  ) = XR1(1)
         ZR(JVALE+3*I+1) = XR1(2)
         ZR(JVALE+3*I+2) = XR1(3)
20    CONTINUE
C
C     --- LE '.NOMMAI' ET LE '.CONNEX' ---
C
      NBNOMX = 0
      DO 30 IMA = 1 , NBMAIL
         CALL JENONU ( JEXNOM(NOMA//'.NOMMAI',NOMAIL(IMA)),IBID)
         CALL JELIRA(JEXNUM(CONNEV,IBID),'LONMAX',NBPT,K8B)
         NBNOMX = MAX( NBNOMX , NBPT )
 30   CONTINUE
C
      CALL JECREO(NOMMAI,'G N K8')
      CALL JEECRA(NOMMAI,'NOMMAX',NBMAIL,' ')
C
      CALL WKVECT(TYPMAI,'G V I',NBMAIL,IBID)
C
      CALL JECREC(CONNEX,'G V I','NU','CONTIG','VARIABLE',NBMAIL)
      CALL JEECRA(CONNEX,'LONT',NBNOMX*NBMAIL,' ')
C
      DO 32 IMA = 1 , NBMAIL
         MAILLE = NOMAIL(IMA)
         CALL JECROC ( JEXNOM(NOMMAI,MAILLE) )
C
         CALL JENONU ( JEXNOM(NOMA//'.NOMMAI',MAILLE), IBID)
         CALL JEVEUO(TYPMAV,'L',IATYMA)
         JTOM=IATYMA-1+IBID
         CALL JENONU ( JEXNOM(NOMMAI,MAILLE), IBID )
         CALL JEVEUO(TYPMAI,'E',IATYMA)
         JTNM=IATYMA-1+IBID
         ZI(JTNM) = ZI(JTOM)
C
         CALL JENONU ( JEXNOM(NOMA//'.NOMMAI',MAILLE),IBID)
         CALL JELIRA ( JEXNUM(CONNEV,IBID),'LONMAX',NBPT,K8B)
         CALL JEVEUO ( JEXNUM(CONNEV,IBID),'L',JOPT)
         CALL JENONU ( JEXNOM(NOMMAI,MAILLE),IBID)
         CALL JEECRA ( JEXNUM(CONNEX,IBID),'LONMAX',NBPT,K8B)
         CALL JEVEUO ( JEXNUM(CONNEX,IBID),'E',JNPT)
         DO 34 INO = 0 , NBPT-1
            CALL JENUNO ( JEXNUM(NOMNOV,ZI(JOPT+INO)) , NOEUD )
            CALL JENONU ( JEXNOM(NOMNOE,NOEUD) , ZI(JNPT+INO) )
 34      CONTINUE
 32   CONTINUE
C
      CALL JEDETR ( '&&SQU110.TRAV' )
      CALL JEDEMA ( )
      END
