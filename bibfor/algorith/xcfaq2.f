      SUBROUTINE XCFAQ2(JLSN,JLST,JGRLSN,IGEOM,PINTER,NINTER,
     &                  AINTER,NFACE,NPTF,CFACE,NBTOT)
      IMPLICIT NONE

      INTEGER       JGRLSN,IGEOM,NINTER,NFACE,CFACE(5,3),JLSN,JLST
      INTEGER       NPTF,NBTOT
      CHARACTER*24  PINTER,AINTER

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 26/04/2011   AUTEUR DELMAS J.DELMAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C                     TROUVER LES PTS D'INTERSECTION ENTRE LES ARETES,
C                     ET LE PLAN DE FISSURE, DÉCOUPAGE EN FACETTES,
C                     POINT MILIEU DE FISSURE (UNIQUEMENT 2D)
C     ENTREE
C       LSN      : VALEURS DE LA LEVEL SET NORMALE
C       LST      : VALEURS DE LA LEVEL SET TANGENTE
C       JGRLSN   : ADRESSE DU GRADIENT DE LA LEVEL SET NORMALE
C       IGEOM    : ADRESSE DES COORDONNEES DES NOEUDS DE L'ELT PARENT
C
C     SORTIE
C       PINTER  : COORDONNEES DES POINTS D'INTERSECTION
C       NINTER  : NOMBRE DE POINTS D'INTERSECTION
C       AINTER  : INFOS ARETE ASSOCIEE AU POINTS D'INTERSECTION
C       NFACE   : NOMBRE DE FACETTES
C       NPTF    : NOMBRE DE POINTS PAR FACETTE
C       CFACE   : CONNECTIVITE DES NOEUDS DES FACETTES
C
C     ----------------------------------------------------------------
C     ----- DEBUT COMMUNS NORMALISES  JEVEUX  ------------------------
      INTEGER          ZI
      COMMON  /IVARJE/ ZI(1)
      REAL*8           ZR
      COMMON  /RVARJE/ ZR(1)
      COMPLEX*16       ZC
      COMMON  /CVARJE/ ZC(1)
      LOGICAL          ZL
      COMMON  /LVARJE/ ZL(1)
      CHARACTER*8      ZK8
      CHARACTER*16             ZK16
      CHARACTER*24                      ZK24
      CHARACTER*32                               ZK32
      CHARACTER*80                                        ZK80
      COMMON  /KVARJE/ ZK8(1), ZK16(1), ZK24(1), ZK32(1), ZK80(1)
C     -----  FIN  COMMUNS NORMALISES  JEVEUX  ------------------------
C
      REAL*8          A(3),B(3),C(3),LSNA,LSNB,PADIST,LONGAR,TAMPOR(4)
      REAL*8          ALPHA,ND(3)
      REAL*8          DDOT,AB(2),LSTA,LSTB,LSTC,ABPRIM(2),PREC
      REAL*8          R8PREM,FF(27),KSIC(3),SC
      REAL*8          M(3),LSNM,LSTM,KSI,MILFI(3),SMILFI
      INTEGER         J,AR(12,3),NBAR,NA,NB,JPTINT,INS,JAINT
      INTEGER         IA,I,IPT,IBID,NNO,K
      INTEGER         IADZI,IAZK24,NDIM,PTMAX
      INTEGER         ZXAIN,XXMMVD
      INTEGER         INM,INC,NM,JCOR2D,JTABAR,NBNOMX
      LOGICAL         LLIN,ISMALI,CUT
      CHARACTER*8     TYPMA,ELP,ELC
      CHARACTER*24    COOR2D,TABAR

      PARAMETER       (PTMAX=3, ELC='SE3',NBNOMX=27)
C --------------------------------------------------------------------

      CALL JEMARQ()

C     PREC PERMET D'EVITER LES ERREURS DE PRECISION CONDUISANT
C     A IA=IN=0 POUR LES MAILLES DU FRONT
      PREC = 1000*R8PREM()

      ZXAIN = XXMMVD('ZXAIN')
      CALL ELREF1(ELP)
      CALL ELREF4(' ','RIGI',NDIM,NNO,IBID,IBID,IBID,IBID,IBID,IBID)
      CALL ASSERT(NDIM.EQ.2)


C     1) RECHERCHE DES POINTS D'INTERSECTION
C     --------------------------------------

C     CREATION DU VECTEUR DES COORDONNEES DES POINTS D'INTERSECTION
      CALL WKVECT(PINTER,'V V R',PTMAX*NDIM,JPTINT)

C     VECTEUR REEL À ZXAIN COMPOSANTES, POUR CHAQUE PT D'INTER :
C     - NUMÉRO ARETE CORRESPONDANTE         (0 SI C'EST UN NOEUD SOMMET)
C     - NUMÉRO NOEUD SI NOEUD SOMMET        (0 SINON)
C     - LONGUEUR DE L'ARETE
C     - POSITION DU PT SUR L'ARETE          (0 SI C'EST UN NOEUD SOMMET)
C     - ARETE VITALE                        (0 SI NON)
      CALL WKVECT(AINTER,'V V R',PTMAX*ZXAIN,JAINT)

      CALL TECAEL(IADZI,IAZK24)
      TYPMA=ZK24(IAZK24-1+3+ZI(IADZI-1+2)+3)
      IF (ISMALI(TYPMA)) THEN
        LLIN = .TRUE.
      ELSE
        IF (ISMALI(ELP)) THEN
          LLIN = .FALSE.
        ELSEIF (.NOT.ISMALI(ELP)) THEN
          LLIN = .TRUE.
        ENDIF
      ENDIF

C     ATTENTION : POUR LES ELEMENTS QUADRATIQUES 3D
C     PAS DE CONTACT CAR CES ELEMENTS NE PORTENT PAS DE DDLS LSGS_C...
C     ILS FAUT DONC LES REPERER, ALORS NINTER = -1 (CA SERT DANS XDELCO)
      IF (ELP.EQ.'H20'.OR.ELP.EQ.'P15'.OR.ELP.EQ.'T10') THEN
        NINTER=-1
        NFACE=0
        GOTO 999
      ENDIF


C     L'ELEMENT EST IL TRAVERSE PAR LA FISSURE?
      CUT=.FALSE.
      I=1
 1    CONTINUE
C     (1) RECHERCHE D'UN NOEUD PIVOT (LSN NON NULLE)
      IF(ZR(JLSN-1+I).NE.0.AND.I.LT.NNO)THEN
        DO 30 K=I+1,NNO
C     (2) PRODUIT DE CE PIVOT PAR LES AUTRES LSN
          IF(ZR(JLSN-1+I)*ZR(JLSN-1+K).LT.0.D0) CUT=.TRUE.
 30     CONTINUE
      ELSE
        I=I+1
        GO TO 1
      END IF

      IPT=0
C     COMPTEUR DE POINT INTERSECTION = NOEUD SOMMET
      INS=0
C     COMPTEUR DE POINT INTERSECTION = POINT ARETE
      INC=0
C     COMPTEUR DE POINT INTERSECTION = NOEUD MILIEU
      INM=0
      CALL CONARE(TYPMA,AR,NBAR)

C     BOUCLE SUR LES ARETES POUR DETERMINER LES POINTS D'INTERSECTION
      DO 100 IA=1,NBAR

C       NUM NO DE L'ELEMENT
        NA=AR(IA,1)
        NB=AR(IA,2)
        NM=AR(IA,3)
        LSNA=ZR(JLSN-1+NA)
        LSNB=ZR(JLSN-1+NB)
        LSNM=ZR(JLSN-1+NM)
        LSTA=ZR(JLST-1+NA)
        LSTB=ZR(JLST-1+NB)
        LSTM=ZR(JLST-1+NM)

        DO 110 I=1,NDIM
          A(I)=ZR(IGEOM-1+NDIM*(NA-1)+I)
          B(I)=ZR(IGEOM-1+NDIM*(NB-1)+I)
          M(I)=ZR(IGEOM-1+NDIM*(NM-1)+I)
 110    CONTINUE
        IF (NDIM.LT.3) THEN
              A(3)=0.D0
              B(3)=0.D0
              C(3)=0.D0
              M(3)=0.D0
        ENDIF

        KSI=1.D0
        LONGAR=PADIST(NDIM,A,B)

        IF ((LSNA*LSNB).LE.0.D0) THEN
          IF ((LSNA.EQ.0.D0).AND.(LSTA.LE.PREC)) THEN
C           ON AJOUTE A LA LISTE LE POINT A
            IF (LSTA.GE.0.D0.AND.LLIN) THEN
             CALL XAJPIN(JPTINT,PTMAX,IPT,INS,A,LONGAR,JAINT,0,0,0.D0)
            ELSE
             CALL XAJPIN(JPTINT,PTMAX,IPT,INS,A,LONGAR,JAINT,0,NA,0.D0)
            ENDIF
          ENDIF
          IF (LSNB.EQ.0.D0.AND.LSTB.LE.PREC) THEN
C           ON AJOUTE A LA LISTE LE POINT B
            IF (LSTB.GE.0.D0.AND.LLIN) THEN
             CALL XAJPIN(JPTINT,PTMAX,IPT,INS,B,LONGAR,JAINT,0,0,0.D0)
            ELSE
             CALL XAJPIN(JPTINT,PTMAX,IPT,INS,B,LONGAR,JAINT,0,NB,0.D0)
            ENDIF
          ENDIF

          IF (LSNM.EQ.0.D0.AND.LSTM.LE.PREC) THEN
C           ON AJOUTE A LA LISTE LE POINT M
            ALPHA=PADIST(NDIM,A,M)
            IF (LSTM.GE.0.D0.AND.LLIN) THEN
             CALL XAJPIN(JPTINT,PTMAX,IPT,INM,M,LONGAR,JAINT,0,0,0.D0)
            ELSE
              IF (CUT) THEN
                CALL XAJPIN(JPTINT,PTMAX,IPT,INC,M,LONGAR,JAINT,
     &                                                  IA,0,ALPHA)
              ELSEIF (.NOT.CUT) THEN
                CALL XAJPIN(JPTINT,PTMAX,IPT,INM,M,LONGAR,JAINT,
     &                                                  0,NM,ALPHA)
              ENDIF
            ENDIF
          ENDIF

          IF (LSNA.NE.0.D0.AND.LSNB.NE.0.D0.AND.LSNM.NE.0) THEN
C           INTERPOLATION DES COORDONNÉES DE C
            CALL XINTAR(ELP,NDIM,IA,IGEOM,JLSN,C)
C           POSITION DU PT D'INTERSECTION SUR L'ARETE
            ALPHA=PADIST(NDIM,A,C)
            TABAR='&&XDECQU.TABAR'
            CALL WKVECT(TABAR,'V V R',3*NDIM,JTABAR)
            DO 307 I=1,NDIM
              ZR(JTABAR-1+I)=B(I)
              ZR(JTABAR-1+NDIM+I)=A(I)
              ZR(JTABAR-1+2*NDIM+I)=M(I)
 307        CONTINUE
C          CALCUL DES FF DU SE3 (REEREF N'ACCEPTE PAS NDIM=2 & NNO=3)
            CALL ABSCVL(NDIM,JTABAR,C,SC)
            CALL XINVAC(ELP,NDIM,JTABAR,SC,KSIC)
            CALL ASSERT(KSIC(1).GE.-1 .AND. KSIC(1).LE.1)
            CALL ELRFVF(ELC,KSIC(1),NBNOMX,FF,IBID)
            LSTC=FF(1)*LSTB+FF(2)*LSTA+FF(3)*LSTM
            IF (LSTC.LE.PREC) THEN
              IF (LSTC.GE.0.D0.AND.LLIN) THEN
                CALL XAJPIN(JPTINT,PTMAX,IPT,INC,C,LONGAR,JAINT,0,0,
     &                       0.D0)
              ELSE
                CALL XAJPIN(JPTINT,PTMAX,IPT,INC,C,LONGAR,JAINT,IA,0,
     &                     ALPHA)
              ENDIF
            ENDIF
          CALL JEDETR(TABAR)
          ENDIF

        ENDIF

 100  CONTINUE

C     RECHERCHE SPECIFIQUE POUR LES ELEMENTS EN FOND DE FISSURE
      CALL XCFACF(JPTINT,PTMAX,IPT,JAINT,ZR(JLSN),ZR(JLST),IGEOM,
     &                                       NNO,NDIM,LLIN,TYPMA)

      NINTER=INS+INC
      NBTOT=NINTER+INM

      IF(CUT .AND. NINTER.EQ.2 .AND. INM.NE.1)THEN
C       RECHERCHE POINT MILIEU FISSURE
        CALL XMILFI(ELP,NDIM,NNO,JPTINT,IGEOM,JLSN,
     &                                 1,NINTER,MILFI)
C       TABLEAU DES COORDONNEES DES NOEUDS DE L'ARETE AB : B,A,E
        COOR2D='&&XCFAQ2.COOR2D'
        CALL WKVECT(COOR2D,'V V R',3*NDIM,JCOR2D)
        DO 10 J=1,NDIM
          ZR(JCOR2D-1+J)=ZR(JPTINT-1+J)
          ZR(JCOR2D-1+NDIM+J)=ZR(JPTINT-1+NDIM+J)
          ZR(JCOR2D-1+2*NDIM+J)=MILFI(J)
 10     CONTINUE
        KSI=0.D0
        CALL ABSCVF(NDIM,JCOR2D,KSI,SMILFI)
C       ON AJOUTE A LA LISTE LE POINT MILFI
        CALL XAJPIN(JPTINT,PTMAX,IPT,NBTOT,MILFI,SMILFI*2,
     &                                         JAINT,5,0,SMILFI)
        CALL JEDETR(COOR2D)
      ENDIF


      IF (0.EQ.1) THEN
        WRITE(6,*)'POINTS D''INTERSECTION NON TRIES'
        DO 150 I=1,NINTER
          DO 151 J=1,NDIM
            WRITE(6,*)' ',ZR(JPTINT-1+(I-1)*NDIM+J)
 151      CONTINUE
 150    CONTINUE
      ENDIF

C     2) DECOUPAGE EN FACETTES TRIANGULAIRES DE LA SURFACE DEFINIE
C     ------------------------------------------------------------

C                  (BOOK IV 09/09/04)

C     CAS 2D
        DO 800 I=1,5
         DO 801 J=1,3
           CFACE(I,J)=0
 801     CONTINUE
 800    CONTINUE

        IF (NINTER .EQ. 2) THEN
C       NORMALE A LA FISSURE (MOYENNE DE LA NORMALE AUX NOEUDS)
          CALL VECINI(2,0.D0,ND)
          DO 810 I=1,NNO
            DO 811 J=1,2
              ND(J)=ND(J)+ZR(JGRLSN-1+2*(I-1)+J)/NNO
 811        CONTINUE
 810      CONTINUE

          DO 841 J=1,2
            A(J)=ZR(JPTINT-1+J)
            B(J)=ZR(JPTINT-1+2+J)
            AB(J)=B(J)-A(J)
 841      CONTINUE

          ABPRIM(1)=-AB(2)
          ABPRIM(2)=AB(1)

          IF (DDOT(2,ABPRIM,1,ND,1) .LT. 0.D0) THEN
            DO 852 K=1,2
              TAMPOR(K)=ZR(JPTINT-1+K)
              ZR(JPTINT-1+K)=ZR(JPTINT-1+2+K)
              ZR(JPTINT-1+2+K)=TAMPOR(K)
 852        CONTINUE
            DO 853 K=1,4
              TAMPOR(K)=ZR(JAINT-1+K)
              ZR(JAINT-1+K)=ZR(JAINT-1+ZXAIN+K)
              ZR(JAINT-1+ZXAIN+K)=TAMPOR(K)
 853        CONTINUE
          ENDIF
          NFACE=1
          NPTF=3
          CFACE(1,1)=1
          CFACE(1,2)=2
          CFACE(1,3)=3
        ELSE
          NFACE=0
        ENDIF


      IF (0.EQ.1) THEN
        WRITE(6,*)'CFACE '
        DO 300 I=1,NFACE
          DO 301 J=1,NPTF
            WRITE(6,*)' ',CFACE(I,J)
 301      CONTINUE
 300    CONTINUE
      ENDIF

 999  CONTINUE

      CALL JEDEMA()
      END
