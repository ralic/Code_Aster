      SUBROUTINE CFFPM2(RESOCO,RESIGR,NBLIAI,NBLIAC,NDIM  )
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 09/11/2012   AUTEUR DELMAS J.DELMAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C
      IMPLICIT     NONE
      INCLUDE 'jeveux.h'

      CHARACTER*32 JEXNUM
      CHARACTER*24 RESOCO
      REAL*8       RESIGR
      INTEGER      NBLIAI,NBLIAC,NDIM
C
C ----------------------------------------------------------------------
C
C ROUTINE CONTACT (RESOLUTION - PENALISATION)
C
C CALCUL DE LA MATRICE FRO2 = E_T*AT
C
C ----------------------------------------------------------------------
C
C
C IN  RESOCO : SD DE TRAITEMENT NUMERIQUE DU CONTACT
C IN  RESIGR : RESI_GLOB_RELA
C IN  NBLIAI : NOMBRE DE LIAISONS DE CONTACT POSSIBLES
C IN  NBLIAC : NOMBRE DE LIAISONS ACTIVES
C IN  NDIM   : DIMENSION DU PROBLEME
C
C
C
C
      INTEGER      NDLMAX
      PARAMETER   (NDLMAX = 30)
      INTEGER      JDECAL,NBDDL
      REAL*8       JEUINI,GLIS
      REAL*8       COEFPT,COEFFF,COEFTE,BETA
      REAL*8       LAMBDC,LAMBDF
      INTEGER      ILIAC,ILIAI,ILIAI2
      CHARACTER*19 MU,LIAC,AFMU
      INTEGER      JMU,JLIAC,JAFMU
      CHARACTER*24 APPOIN,APDDL
      INTEGER      JAPPTR,JAPDDL
      CHARACTER*24 JEUX
      INTEGER      JJEUX
      CHARACTER*24 TACFIN
      INTEGER      JTACF
      INTEGER      CFMMVD,ZTACF
      CHARACTER*19 FRO2
      INTEGER      JFRO2
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
C
C --- LECTURE DES STRUCTURES DE DONNEES DE CONTACT
C
      APPOIN = RESOCO(1:14)//'.APPOIN'
      APDDL  = RESOCO(1:14)//'.APDDL'
      LIAC   = RESOCO(1:14)//'.LIAC'
      TACFIN = RESOCO(1:14)//'.TACFIN'
      AFMU   = RESOCO(1:14)//'.AFMU'
      MU     = RESOCO(1:14)//'.MU'
      FRO2   = RESOCO(1:14)//'.FRO2'
      CALL JEVEUO(APPOIN,'L',JAPPTR)
      CALL JEVEUO(APDDL, 'L',JAPDDL)
      CALL JEVEUO(LIAC,  'L',JLIAC )
      CALL JEVEUO(TACFIN,'L',JTACF )
      CALL JEVEUO(AFMU , 'L',JAFMU )
      CALL JEVEUO(MU,    'E',JMU   )
      ZTACF  = CFMMVD('ZTACF')
C
      JEUX   = RESOCO(1:14)//'.JEUX'
      CALL JEVEUO(JEUX  ,'L',JJEUX )
C
C --- CALCUL DE LA MATRICE E_T*AaT
C
      DO 100 ILIAI = 1,NBLIAI
C
C ----- INITIALISATION DE LA COLONNE
C
        CALL JEVEUO(JEXNUM(FRO2,ILIAI), 'E', JFRO2 )
        CALL R8INIR(NDLMAX,0.D0,ZR(JFRO2),1)
C
C ----- LA LIAISON EST-ELLE ACTIVE ?
C
        JEUINI = ZR(JJEUX+3*(ILIAI-1)+1-1)
C
C ----- CALCUL
C
        IF (JEUINI.LT.0.D0) THEN
C
C ------- REPERAGE DE LA LIAISON
C
          JDECAL = ZI(JAPPTR+ILIAI-1)
          NBDDL  = ZI(JAPPTR+ILIAI) - ZI(JAPPTR+ILIAI-1)
C
C ------- CALCUL DU JEU TANGENT
C
          CALL CFCGLT(RESOCO,ILIAI ,GLIS  )
C
C ------- PARAMETRES
C
          COEFFF = ZR(JTACF+ZTACF*(ILIAI-1)+1-1)
          COEFPT = ZR(JTACF+ZTACF*(ILIAI-1)+3-1)
          COEFTE = ZR(JTACF+ZTACF*(ILIAI-1)+4-1)
C
C ------- LAMBDA DE FROTTEMENT
C
          DO 320 ILIAC = 1,NBLIAC
            ILIAI2 = ZI(JLIAC+ILIAC-1)
            LAMBDC = ZR(JMU+ILIAC-1)
            IF (ILIAI2.EQ.ILIAI) THEN
              IF ( LAMBDC .GT. 0.D0 ) THEN
                LAMBDF = COEFFF*LAMBDC
              ELSE
                LAMBDF = 0.D0
              ENDIF
            ENDIF
 320      CONTINUE
C
C ------- ACTIVATION GLISSEMENT/ADHERENCE
C
          IF (LAMBDF.EQ.0.D0) THEN
            BETA   = 0.D0
          ELSE
            IF ( ZR(JMU+2*NBLIAI+ILIAI-1).NE.0.D0) THEN
              IF ( GLIS .LE. LAMBDF/COEFPT ) THEN
                BETA   = 0.D0
              ELSE
                BETA   = SQRT(1.D0/(LAMBDF*GLIS))
              ENDIF
            ELSE
              BETA   = 0.D0
            ENDIF
            IF ( RESIGR .GE. 1.D-03 ) THEN
              BETA   = SQRT(COEFTE) * BETA
            ENDIF
            CALL CALAPR(NBDDL,BETA,ZR(JAFMU),
     &                  ZI(JAPDDL+JDECAL),ZR(JFRO2))
            ZR(JMU+2*NBLIAI+ILIAI-1) = 1.D0
          ENDIF
        ELSE
          ZR(JMU+2*NBLIAI+ILIAI-1) = 0.D0
        ENDIF
        CALL JELIBE(JEXNUM(FRO2,ILIAI))
 100  CONTINUE
C
      CALL JEDEMA()
C
      END
