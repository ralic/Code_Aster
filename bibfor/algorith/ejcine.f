      SUBROUTINE EJCINE(NDIM,AXI,NNO1,NNO2,VFF1,VFF2,WREF,DFFR2,
     &                  GEOM,WG,KPG,IPG,IDF2,ROT,B)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 26/04/2011   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE LAVERNE J.LAVERNE
C TOLE CRS_1404
      IMPLICIT NONE
      LOGICAL AXI
      INTEGER NDIM,NNO1,NNO2,KPG,IPG,IDF2
      REAL*8  WREF,VFF1(NNO1),VFF2(NNO2),GEOM(NDIM,NNO2),ROT(NDIM,NDIM)
      REAL*8  DFFR2(NDIM-1,NNO2),WG,B(2*NDIM-1,NDIM+1,2*NNO1+NNO2)

C-----------------------------------------------------------------------
C  MATRICE CINEMATIQUE POUR LES ELEMENTS DE JOINT HM (EN UN PG DONNE)
C-----------------------------------------------------------------------
C IN  NDIM   DIMENSION DE L'ESPACE
C IN  AXI    .TRUE. SI AXISYMETRIQUE
C IN  NNO1   NB DE NOEUDS DE LA FACE POUR LES DEPLACEMENTS
C IN  NNO2   NB DE NOEUDS DE LA FACE POUR LES PRESSIONS P ET LA GEOM X
C IN  VFF1   VALEUR DES FONCTIONS DE FORME (DE LA FACE) POUR U
C IN  VFF2   VALEUR DES FONCTIONS DE FORME (DE LA FACE) POUR P ET X
C IN  WREF   POIDS DE REFERENCE DU POINT DE GAUSS
C IN  DFFR2  DERIVEE DES FONCTIONS DE FORME DE REFERENCE DE P ET X EN G
C IN  GEOM   COORDONNEES DES NOEUDS (X)
C OUT WG     POIDS REEL DU POINT DE GAUSS (AVEC DISTORSION)
C OUT ROT    MATRICE DE ROTATION DU LOCAL AU GLOBAL
C OUT B      MATRICE DE PASSAGE UNODAL -> SAUT DE U LOCAL
C-----------------------------------------------------------------------
      INTEGER N,I,J
      REAL*8 COVA(3,3),METR(2,2),COUR,JAC,COSA,SINA,NOA1
      REAL*8 RAY
      REAL*8 GEOLOC(NDIM,NNO2),GEOTAN(NDIM-1,NNO2)
      REAL*8 DDOT,DFDIS(NNO2,NDIM-1),WG2
C-----------------------------------------------------------------------

      IF (NDIM.EQ.3) THEN

        CALL SUBACO(NNO2,DFFR2,GEOM,COVA)
        CALL SUMETR(COVA,METR,JAC)
        WG = WREF*JAC

C       MATRICE DE ROTATION
        NOA1 = SQRT(COVA(1,1)**2 + COVA(2,1)**2 + COVA(3,1)**2)
        ROT(1,1) = COVA(1,3)
        ROT(1,2) = COVA(2,3)
        ROT(1,3) = COVA(3,3)
        ROT(2,1) = COVA(1,1)/NOA1
        ROT(2,2) = COVA(2,1)/NOA1
        ROT(2,3) = COVA(3,1)/NOA1
        ROT(3,1) = ROT(1,2)*ROT(2,3) - ROT(1,3)*ROT(2,2)
        ROT(3,2) = ROT(1,3)*ROT(2,1) - ROT(1,1)*ROT(2,3)
        ROT(3,3) = ROT(1,1)*ROT(2,2) - ROT(1,2)*ROT(2,1)

C       CALCUL DE LA GEOMETRIE DANS LE REPERE LOCAL
        CALL R8INIR(NDIM*NNO2, 0.D0, GEOLOC ,1)

        DO 10 N=1,NNO2
          DO 11 I=1,NDIM
            DO 12 J=1,NDIM
              GEOLOC(I,N) = GEOLOC(I,N) + ROT(I,J)*GEOM(J,N)
   12       CONTINUE
   11     CONTINUE
   10   CONTINUE

        DO 13 N=1,NNO2
          DO 14 I=2,NDIM
            GEOTAN(I-1,N)=GEOLOC(I,N)
   14     CONTINUE
   13   CONTINUE

C       CALCUL DES DERIVEE DES FF DANS LE PLAN TANGENTIEL
        CALL DFDM2D(NNO2,KPG,IPG,IDF2,GEOTAN,DFDIS(1,1),DFDIS(1,2),WG2)

      ELSEIF (NDIM.EQ.2) THEN

C       CALCUL DES DERIVEE DES FF DANS LE PLAN TANGENTIEL
        CALL DFDM1D(NNO2,WREF,DFFR2,GEOM,DFDIS(1,1),COUR,WG,COSA,SINA)

C       CALCUL DE LA DISTANCE A L'AXE EN AXI, R=RAYON DU PG COURANT
        IF (AXI) THEN
          RAY = DDOT(NNO2,GEOM,2,VFF2,1)
          WG = RAY*WG
        ENDIF

C       MATRICE DE ROTATION
        ROT(1,1) = -COSA
        ROT(1,2) = -SINA
        ROT(2,1) =  SINA
        ROT(2,2) = -COSA

      ENDIF

C     CONSTRUCTION DE LA MATRICE B
      CALL R8INIR((2*NDIM-1)*(NDIM+1)*(2*NNO1+NNO2), 0.D0, B ,1)

      DO 20 I = 1,NDIM
        DO 30 J = 1,NDIM

          DO 40 N = 1, NNO1
            B(I,J,N)      = - ROT(I,J)*VFF1(N)
            B(I,J,N+NNO1) =   ROT(I,J)*VFF1(N)
 40       CONTINUE

 30     CONTINUE
 20   CONTINUE

      DO 21 I = 1,NDIM-1
        DO 41 N = 1, NNO2
          B(NDIM+I,NDIM+1,2*NNO1+N) = DFDIS(N,I)
 41     CONTINUE
 21   CONTINUE

      END
