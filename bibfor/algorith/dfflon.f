      SUBROUTINE DFFLON (GEOM,NONOFF,NOMNOE,INOFF,NBNOFF,TYPFON,D)

      IMPLICIT NONE

      REAL*8       GEOM(*),D
      INTEGER      INOFF,NBNOFF
      CHARACTER*6  TYPFON
      CHARACTER*8  NONOFF(*)
      CHARACTER*24 NOMNOE

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 11/04/2012   AUTEUR LADIER A.LADIER 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C
C FONCTION REALISEE (OPERATEURS DEFI_FOND_FISS) :
C
C      RETOURNE UNE ESTIMATION DE LA LONGUEUR DES SEGMENTS DU FOND DE
C      FISSURE EN UN NOEUD DU FOND
C      (UNIQUEMENT EN 3D)
C
C IN 
C   GEOM    : VALEURS DES COORDONNEES DU MAILLAGE
C   NONOFO  : NOMS DES NOEUDS DU FOND
C   NOMNOE  : OBJET '.NOMNOE' DU MAILLAGE
C   INOFF   : INCIDE LOCAL DU NOEUD DE LA BASE DEMANDE
C   NBNOFF  : NOMBRE DE NOEUDS DU FOND DE FISSURE
C   TYPFON  : TYPE DE FOND (OUVERT/FERME/INF/SUP)

C OUT
C   D   : LONGUEUR CARACTERISTIQUES DES SEGMENTS DU FOND AUTOUT DU NOEUD
C
C-----------------------------------------------------------------------
C     ----- DEBUT COMMUNS NORMALISES  JEVEUX  --------------------------
      INTEGER          ZI
      COMMON  /IVARJE/ ZI(1)
      REAL*8           ZR
      COMMON  /RVARJE/ ZR(1)
      COMPLEX*16       ZC
      COMMON  /CVARJE/ ZC(1)
      LOGICAL          ZL
      COMMON  /LVARJE/ ZL(1)
      CHARACTER*8      ZK8
      CHARACTER*16             ZK16
      CHARACTER*24                      ZK24
      CHARACTER*32                               ZK32
      CHARACTER*80                                        ZK80
      COMMON  /KVARJE/ ZK8(1), ZK16(1), ZK24(1), ZK32(1), ZK80(1)
      CHARACTER*32     JEXNOM, JEXNUM
C     ----- FIN COMMUNS NORMALISES  JEVEUX  ----------------------------

C
      REAL*8  DIS2NO,DIJ,DIH
      INTEGER NUNOI,NUNOJ,NUNOH
C
C-----------------------------------------------------------------------
C
      CALL JEMARQ ( )
C
C     REMARQUE GENERALE : ON POURRAIT TROUVER UN ALGO PLUS ELEGANT,
C                         MAIS ON PERDRAIT EN LISIBILITE

C     NUNOI : NUMERO DU NOEUD COURANT
C     NUNOJ : NUMERO DU NOEUD APRES
C     NUNOH : NUMERO DU NOEUD AVANT

C     CAS PARTICULIER DU PREMIER NOEUD DU FOND DE FISSURE
      IF (INOFF.EQ.1) THEN
      
        CALL JENONU(JEXNOM(NOMNOE,NONOFF(INOFF  )),NUNOI)
        CALL JENONU(JEXNOM(NOMNOE,NONOFF(INOFF+1)),NUNOJ)

        DIJ = DIS2NO(GEOM,NUNOI,NUNOJ)
        D = DIJ

        IF (TYPFON.EQ.'FERME') THEN
C         ATTENTION, LE DERNIER NOEUD (EN POSITION NBNOFF) = LE 1ER
          CALL JENONU(JEXNOM(NOMNOE,NONOFF(NBNOFF  )),NUNOI)
          CALL JENONU(JEXNOM(NOMNOE,NONOFF(NBNOFF-1)),NUNOH)
          DIH = DIS2NO(GEOM,NUNOI,NUNOH)
          D = MIN(DIJ,DIH)
        ENDIF
        
C     CAS PARTICULIER DU DENIER NOEUD DU FOND DE FISSURE
      ELSEIF (INOFF.EQ.NBNOFF) THEN

        CALL JENONU(JEXNOM(NOMNOE,NONOFF(INOFF  )),NUNOI)
        CALL JENONU(JEXNOM(NOMNOE,NONOFF(INOFF-1)),NUNOH)

        DIH = DIS2NO(GEOM,NUNOI,NUNOH)
        D = DIH

        IF (TYPFON.EQ.'FERME') THEN
C         ATTENTION, LE PREMIER NOEUD = LE DERNIER
          CALL JENONU(JEXNOM(NOMNOE,NONOFF(1)),NUNOI)
          CALL JENONU(JEXNOM(NOMNOE,NONOFF(2)),NUNOJ)
          DIJ = DIS2NO(GEOM,NUNOI,NUNOJ)
          D = MIN(DIJ,DIH)
        ENDIF
        
C     CAS GENERAL
      ELSE

        CALL JENONU(JEXNOM(NOMNOE,NONOFF(INOFF-1)),NUNOH)
        CALL JENONU(JEXNOM(NOMNOE,NONOFF(INOFF  )),NUNOI)
        CALL JENONU(JEXNOM(NOMNOE,NONOFF(INOFF+1)),NUNOJ)

        DIH = DIS2NO(GEOM,NUNOI,NUNOH)
        DIJ = DIS2NO(GEOM,NUNOI,NUNOJ)
        D = MIN(DIJ,DIH)
      
      ENDIF

      CALL JEDEMA()
      END
