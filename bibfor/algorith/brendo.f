      SUBROUTINE BRENDO(SIGE6,BT6,SUT,BC1,SUC,LOCAL,
     #T33,N33,LCT,BW,PW,BCH,PCH,DELTA,LCC,MT,MC,
     #SIGET6,SIGEC6,NU,DT66,DC0,SUT6,SUC1,SIGA6,DT6)

C    ROUTINE ANCIENNEMENT NOMMEE ENDO_B3D

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 26/04/2011   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================

C      CALCUL DES ENDOMMAGEMENTS DE TRACTION ET DE COMPRESSION

C      ATTENTION : UTILISATION DES SUBROUTINES
C     ZERO,MATMAT...
C TOLE CRP_20
C TOLE CRP_21

      IMPLICIT REAL*8(A-Z)
      INTEGER I,J,K,L
      REAL*8 SIGE6(6),SIGE33(3,3),SIGE3(3),VSE33(3,3)
      REAL*8 VSE33T(3,3)
      REAL*8 BT6(6),BT33(3,3),SUT33(3,3),SUT3(3)
      REAL*8 BT3(3),VBT33(3,3),VBT33T(3,3)
      REAL*8 SGET33(3,3),SGEC33(3,3)
      LOGICAL LOCAL,LCOMP,ENDOC,ASTER
      REAL*8 X33(3,3),Y33(3,3),Z33(3,3),Y133(3,3)
      REAL*8 T33(3,3)
      REAL*8 N33(3,3),L3(3)
      REAL*8 SGET3(3),SGEC3(3)
      REAL*8 SIGET6(6),SIGEC6(6)
      REAL*8 NU
      REAL*8 BT033(3,3),BT033T(3,3)
      REAL*8 DT66(6,6),DT6(6)
      REAL*8 S33(3,3),TRAV(3,3)
      REAL*8 SUT6(6),SIGA6(6),SIGA33(3,3)
      REAL*8 SIGAT6(6),DT33(3,3),DT33F(3,3)

      ZZ = 0.D0

C  *INITIALISATION LOCALISATION ET ENDOMMAGEMENT TRAITES EN COMPRESSION*
      ENDOC=.TRUE.
      LCOMP=.TRUE.

C     *****INITIALISATION DE L INDICATEUR DE LOCALISATION *******
C     INDIC=.FALSE.

C     ***** ACTIVATION DES CALCULS COMPLEMENTAIRES POUR ASTER ***
      ASTER=.TRUE.

C     T33 ET N33 SONT LES TENSEURS CARACTÈRISANT LA TAILLE DES ELEMENTS
C     MASSIFS UTILISÈS

C      SI LOCAL=.TRUE. ON EFFECTUE UNE HOMOTHETIE POUR CONTROLER
C      L ENERGIE DISSIPEE
C     CETTE HOMOTHETIE NE CONCERNE QUE LA PART DE CONTRAINTE DUE AU
C     CHARGEMENT EXTERNE
C     LA PROCEDURE EST DONC A REVOIR EXTERIEUR

C     **** HYPOTHESE D ENDOMMAGEMENT DE TRACTION ANISOTROPE **********

C      RANGEMENT DES CONTRAINTES EFFECTIVES EN TABLEAU 3*3
       SIGE33(1,1)=SIGE6(1)
       SIGE33(2,2)=SIGE6(2)
       SIGE33(3,3)=SIGE6(3)
       SIGE33(1,2)=SIGE6(4)
       SIGE33(1,3)=SIGE6(5)
       SIGE33(2,3)=SIGE6(6)
       SIGE33(2,1)=SIGE33(1,2)
       SIGE33(3,1)=SIGE33(1,3)
       SIGE33(3,2)=SIGE33(2,3)


C      RANGEMENT DES INDICES ADIMENSIONNELS EN TABLEAU 3*3
       BT33(1,1)=BT6(1)
       BT33(2,2)=BT6(2)
       BT33(3,3)=BT6(3)
       BT33(1,2)=BT6(4)
       BT33(1,3)=BT6(5)
       BT33(2,3)=BT6(6)
       BT33(2,1)=BT33(1,2)
       BT33(3,1)=BT33(1,3)
       BT33(3,2)=BT33(2,3)

C     CALCUL DES CONTRAINTES SEUILS DE TRACTION EN DEBUT DE
C       PAS A PARTIR DES INDICES
      CALL BRVP33(BT33,BT3,BT033)
      CALL TRANSP(BT033,3,3,3,BT033T,3)

C     *** ACTUALISATION DES CONTRAINTES SEUILS DE FISSURATION ********

C     PRINT*,'INDICE INITIAL EN BASE FIXE'
C      CALL AFFICHE33(BT33)
C      PRINT*,'MATRICE DE PASSAGE ‡ LA BASE D ENDO'
C      CALL AFFICHE33 (VBT33)

      CALL MATINI(3,3,ZZ,X33)
      E1=1.D0/MT
      DO 10 I=1,3
       IF(BT3(I).GT.0.D0)THEN
        X33(I,I)=((MT*BT3(I))**E1)*SUT
       ELSE
        X33(I,I)=0.D0
       END IF
C      SEUIL3(I)=X33(I,I)
10    CONTINUE
C     PASSAGE DES CONTRAINTES SEUILS EN BASE FIXE
      CALL UTBTAB('ZERO',3,3,X33,BT033T,TRAV,SUT33)
C      PRINT*,'SEUIL INITIAL EN BASE FIXE'
C      CALL AFFICHE33(SUT33)
C     CONTRAINTE SEUIL DE COMPRESSION EN DEBUT DE PAS
      IF(BC1.GT.0.D0)THEN
       SUC1=((MC*BC1)**(1.D0/MC))*SUC
      ELSE
       SUC1=0.D0
      END IF

C     CONTRAINTES EFFECTIVES PRINCIPALES ET MATRICE DE
C       PASSAGE ‡ LA BASE PRINCIPALE
      DO 20 I=1,3
       DO 30 J=1,3
        X33(I,J)=SIGE33(I,J)
30     CONTINUE
20    CONTINUE
C      DIAGONALISATION ET VALEURS PROPRES PAR LA METHODE DE JACOBI
      CALL BRVP33(X33,SIGE3,VSE33)

C      CALL AFFICHE33(X33)

C      TRANSPOSITION DE DE LA MATRICE DE PASSAGE
        CALL TRANSP(VSE33,3,3,3,VSE33T,3)
C      PRINT*,'PASSAGE A BASE PRIN SIGMA'
C      CALL AFFICHE33(VSE33)

C      DECOMPOSITION DES CONTRAINTES
C        PRINCIPALES EN PARTIE POSITIVE ET NÈGATIVE
      DO 40 I=1,3
       SGET3(I)=0.5D0*(SIGE3(I)+ABS(SIGE3(I)))
       SGEC3(I)=0.5D0*(SIGE3(I)-ABS(SIGE3(I)))
40    CONTINUE

C     MISE A JOUR DES INDICES DE FISSURATION DE TRACTION
C     LA MISE A JOUR SE FAIT SUR LES CONTRAINTES SEUILS
C     VALEUR PRINCIPALES DES SEUILS ACTUELS
      DO 50 I=1,3
       DO 60 J=1,3
        X33(I,J)=SUT33(I,J)
60     CONTINUE
50    CONTINUE
C     EXPRESSION DES CONTRAINTES SEUILS DE TRACTION DANS
C       LA BASE PRINCIPALE DES CONTRAINTES EFFECTIVES ACTUELLES
      CALL UTBTAB('ZERO',3,3,X33,VSE33,TRAV,Y133)
C      PRINT*,'SEUIL INITIAUX EN BASE PRINCIPALE'
C      CALL AFFICHE33(Y133)

C     CALCUL DE LA TAILLE DES ELEMENTS FINIS DANS LES DIRECTIONS
C       PRINCIPALES DES CONTRAINTES
      IF(LOCAL)THEN
       CALL BRTAIL(L3,T33,N33,VSE33)
      ENDIF

C    ACTUALISATION DES CONTRAINTES SEUILS(ECOULEMENT/CRITERE DE RANKINE)
      DO 70 I=1,3
       IF(LOCAL)THEN
C        ON TESTE LA LOCALISATION SUR LES CONTRAINTES EFFECTIVES ISSUES
C        DU CHARGEMENT EXTÈRIEUR (SEULE CAUSE POSSIBLE DE LOCALISATION)
C       PRINT*,'SGET3(',I,')',SGET3(I),'BW,PW,BCH,PCH',BW,PW,BCH,PCH
        SGET3(I)=SGET3(I)-BW*PW-BCH*PCH
        IF(SGET3(I).GT.SUT)THEN
C        LE CHARGEMENT ACTUEL PROVOQUE LA LOCALISATION :
C          ON EFFECTUE L HOMOTHETIE
C        INDIC=.TRUE.
         SGET3(I)=(SGET3(I)-SUT)*L3(I)/LCT+SUT
C         SGET3(I)=(SGET3(I)-SUT)*2.D0+SUT
        END IF
C       ON REPASSE EN CONTRAINTE EFFECTIVE INTÈRIEURE POUR
C       LA CONFORMITÈ AVEC LE CALCUL DES CONTRAINTES APPARENTES
        SGET3(I)=SGET3(I)+BW*PW+BCH*PCH
       END IF
       DO 80 J=1,3
        IF (I.EQ.J) THEN
         Y33(I,I)=MAX(Y133(I,I),SGET3(I))
        ELSE
         Y33(I,J)=Y133(I,J)
        END IF
80     CONTINUE
70    CONTINUE
C      PRINT*,'CONTRAINTES SEUILS EN BASE PRINCIPALE'
C      CALL AFFICHE33(Y33)

C     RETOUR DES CONTRAINTES SEUILS DANS LA BASE FIXE
      CALL UTBTAB('ZERO',3,3,Y33,VSE33T,TRAV,Z33)
C      PRINT*,'CONTRAINTES SEUILS EN BASE FIXE'
C      CALL AFFICHE33(Z33)

C     STOCKAGE DES INDICES DE FISSURATION APRES ECOULEMENT
      CALL BRVP33(Z33,SUT3,VBT33)
      CALL TRANSP(VBT33,3,3,3,VBT33T,3)
      CALL MATINI(3,3,ZZ,X33)
      DO 90 I=1,3
       IF(SUT3(I).GT.0.D0)THEN
        BT3(I)=E1*((SUT3(I)/SUT)**MT)
       ELSE
        BT3(I)=0.D0
       END IF
C       PRINT*,'BT3(',I,')=',BT3(I),'DS ENDO'
90    CONTINUE

C     RETOUR DES INDICES DE FISSURATION EN BASE FIXE POUR LE
C       STOCKAGE EN VECTEUR6
      CALL MATINI(3,3,ZZ,X33)
      CALL MATINI(3,3,ZZ,DT33)
      DO 100 I=1,3
       X33(I,I)=BT3(I)
       DT33(I,I)=1.D0-EXP(-BT3(I))
100   CONTINUE
      CALL UTBTAB('ZERO',3,3,X33,VBT33T,TRAV,BT33)
      CALL UTBTAB('ZERO',3,3,DT33,VBT33T,TRAV,DT33F)
      DO 110 I=1,3
       BT6(I)=BT33(I,I)
       DT6(I)=DT33F(I,I)
110   CONTINUE
      BT6(4)=BT33(1,2)
      BT6(5)=BT33(1,3)
      BT6(6)=BT33(2,3)
      DT6(4)=DT33F(1,2)
      DT6(5)=DT33F(1,3)
      DT6(6)=DT33F(2,3)


C     RETOUR DE CONTRAINTES SEUILS EN
C       BASE FIXE SI STOCKAGE NECESSAIRE (VERSION ASTER)
      IF(ASTER)THEN
       CALL MATINI(3,3,ZZ,X33)
       DO 120 I=1,3
        X33(I,I)=SUT3(I)
120    CONTINUE
       CALL UTBTAB('ZERO',3,3,X33,VBT33T,TRAV,SUT33)
       DO 130 I=1,3
        SUT6(I)=SUT33(I,I)
130    CONTINUE
       SUT6(4)=SUT33(1,2)
       SUT6(5)=SUT33(1,3)
       SUT6(6)=SUT33(2,3)
      END IF

C     *** ACTUALISATION DU TENSEUR DES ENDOMMAGEMENTS DE TRACTION ***

C     HYPOTHESE DU MATERIAU ORTHOTROPE DANS LES
C       DIRECTIONS PRINCIPALES DE FISSURATION

C     CALCUL DE LA MATRICE CARRÈE S33=S0/S1 POUR LES TRACTION EFFECTIVES
      S1=EXP(BT3(1))
      S2=EXP(BT3(2))
      S3=EXP(BT3(3))
      T1 = S2 * S3
      T2 = NU ** 2
      T3 = T2 * S3
      T5 = 2.D0 * T2 * NU
      T6 = T2 * S2
      T8 = S1 * S2
      T10 = S1 * T2
      T12 = 1.D0 / (-T8 * S3 + T10 + T3 + T5 + T6)
      T14 = NU * S2
      T18 = NU * S3
      T22 = S1 * S3
      T23 = S1 * NU
      S33(1,1) = (-T1 + T2 + T3 + T5 + T6) * T12
      S33(1,2) = NU * (T1 - S3 - NU + T14) * T12
      S33(1,3) = NU * (T1 + T18 - NU - S2) * T12
      S33(2,1) = NU * (-S3 - NU + T22 + T23) * T12
      S33(2,2) = (T3 + T5 - T22 + T2 + T10) * T12
      S33(2,3) = NU * (T18 + T22 - S1 - NU) * T12
      S33(3,1) = NU * (-NU - S2 + T23 + T8) * T12
      S33(3,2) = NU * (T14 - S1 - NU + T8) * T12
      S33(3,3) = (T5 + T6 + T10 - T8 + T2) * T12

C     CALCUL DE LA MATRICE D ENDOMMAGEMENT DT66
      DO 140 I=1,6
       DO 150 J=1,6
        DT66(I,J)=0.D0
150    CONTINUE
140   CONTINUE
      DO 160 I=1,3
       DO 170 J=1,3
        IF(I.EQ.J)THEN
         DT66(I,J)=1.D0-S33(I,J)
        ELSE
         DT66(I,J)=-S33(I,J)
        END IF
170    CONTINUE
160   CONTINUE
      DO 180 I=4,6
       CALL BRINDZ(I,K,L)
       DT66(I,I)=1.D0-EXP(-BT3(K)-BT3(L))
180   CONTINUE


C     **** CAS DE L ENDOMMAGEMENT ISOTROPE DE COMPRESSION ***********

      IF(ENDOC)THEN
C      MISE A JOUR DE L'ENDOMMAGEMENT ISOTROPE DE
C        COMPRESSION (DRUCKER PRAGGER)
C      CALCUL DES INVARIANTS DU TENSEUR DES CONTRAINTES EFFECTIVES
       XJ2=(SGEC3(1)-SGEC3(2))**2+(SGEC3(1)-SGEC3(3))**2
     # +(SGEC3(2)-SGEC3(3))**2
       RJ2=SQRT(XJ2/6.D0)
       XI1=(SGEC3(1)+SGEC3(2)+SGEC3(3))/3.D0
C      CONTRAINTE EQUIVALENTE
       XC=(RJ2+DELTA*XI1)
       XC=0.5D0*(XC+ABS(XC))

C      SEUIL ACTUEL
       XS=SUC1
C      TRAITEMENT DE LA LOCALISATION EN COMPRESSION : TEST SUR
C      LES CONTRAINTES EFFECTIVES ISSUES DU CHARGEMENT EXTÈRIEUR
       IF(LOCAL.AND.LCOMP)THEN
        IF(XC.GT.SUC)THEN
C         LA LOCALISATION EN COMPRESSION EST EN COURS
C         INDIC=.TRUE.
C         LONGUEUR EQUIVALENTE
          LM=(L3(1)*L3(2)*L3(3))**(1.D0/3.D0)
C         HOMOTHETIE SUR LES CONTRAINTES DEVIATORIQUES NEGATIVES
          YC=(XC-SUC)*LM/LCC+SUC
C      RAPPORT D HOMOTHETIE ISOTROPES DES CONTRAINTES EXTERNES NEGATIVES
          RH=YC/XC
          XC=YC
C         ACTUALISATION DES CONTRAINTES EFFECTIVES EN ZONE DE
C           LOCALISATION SUIVANT LE RAPPORT D HOMOTHETIE
          DO 190 I=1,3
C          CAS DES CONTRAINTES DE COMPRESSION
C            (LOCALISATION EN COMPRESSION ISOTROPE)
           SGEC3(I)=RH*(SGEC3(I)-BW*PW-BCH*PCH)+(BW*PW+BCH*PCH)
190       CONTINUE
C         ESTIMATION DE L ENDOMMAGEMENT DE COMPRESSION APRES HOMOTHETIE
C         DES CONTRAINTES DUES AU CHARGEMENT EXT
          XJ2=(SGEC3(1)-SGEC3(2))**2+(SGEC3(1)-SGEC3(3))**2
     #    +(SGEC3(2)-SGEC3(3))**2
          RJ2=SQRT(XJ2/6.D0)
          XI1=(SGEC3(1)+SGEC3(2)+SGEC3(3))/3.D0
C         NOUVELLE CONTRAINTE EQUIVALENTE APRES HOMOTHETIE
          XC=(RJ2+DELTA*XI1)
          XC=0.5D0*(XC+ABS(XC))
        END IF
       END IF
C      ACTUALISATION DU SEUIL DE COMPRESSION
       XC=MAX(XC,XS)
       SUC1=XC
      END IF
C     ACTUALISATION DE L'INDICE DE FISSURATION DE DRUCKER PRAGER
      IF(SUC1.GT.0.D0)THEN
       BC1=((SUC1/SUC)**MC)/MC
      ELSE
       BC1=0.D0
      END IF
C     ENDOMMAGEMENT DE COMPRESSION
      DC0=1.D0-EXP(-BC1)

C      PRINT*,'BT33 EN BASE FIXE :'
C      CALL AFFICHE33(BT33)
C      PRINT*,'BC1:',BC1
C      READ*


C     CALCUL DES CONTRAINTES EFFECTIVES EN BASE PRINCIPALE D ENDOMMAGEMT



C     PASSAGE DES CONTRAINTES PRINCIPALES DE
C       TRACTION ACTUALISEES EN BASE ENDO TRACT
      CALL MATINI(3,3,ZZ,X33)
      DO 200 I=1,3
       X33(I,I)=SGET3(I)
200   CONTINUE
      CALL UTBTAB('ZERO',3,3,X33,VSE33T,TRAV,SGET33)
C     PASSAGE EN BASE PRIN ENDO TRAC
      CALL MATINI(3,3,ZZ,X33)
      CALL UTBTAB('ZERO',3,3,SGET33,VBT33,TRAV,X33)
      DO 210 I=1,6
       CALL BRINDZ(I,J,K)
       SIGET6(I)=X33(J,K)
210   CONTINUE

C     PASSAGE DES CONTRAINTES PRINCIPALES DE COMPRESSION
C       ACTUALISEES EN BASE ENDO TRACT
      CALL MATINI(3,3,ZZ,X33)
      DO 220 I=1,3
       X33(I,I)=SGEC3(I)
220   CONTINUE
      CALL UTBTAB('ZERO',3,3,X33,VSE33T,TRAV,SGEC33)
C     PASSAGE EN BASE PRIN ENDO TRAC
      CALL MATINI(3,3,ZZ,X33)
      CALL UTBTAB('ZERO',3,3,SGEC33,VBT33,TRAV,X33)
      DO 230 I=1,6
       CALL BRINDZ(I,J,K)
       SIGEC6(I)=X33(J,K)
230   CONTINUE


C     *** CONTRAINTES APPARENTES ********************************

C      DO I=1,6
C       SIGA6(I)=0.D0
C       DO J=1,6
C        IF((I.LE.3).AND.(J.EQ.I))THEN
C        ON EST SUR LA DIAGONALE DU TENSEUR DES CONTRAINTES
C         IF(PW.GT.0.)THEN
C         CAS DE LA SURPRESSION HYDRIQUE APPLIQUÈE DANS LES FISSURES
C          SIGA6(I)=SIGA6(I)+(1.-DC0)*
C     #    ((1.-DT66(I,I))*SIGET6(I)+SIGEC6(I))-BW*PW
C         ELSE
C         LA DEPRESSION NE S APPLIQUE PAS DANS LES FISSURES
C          BWEQ=BW*(1.-DC0)*(1.-DT66(I,I))
C          SIGA6(I)=SIGA6(I)+(1.-DC0)*
C     #    ((1.-DT66(I,I))*SIGET6(I)+SIGEC6(I))-BWEQ*PW
C         END IF
C        ELSE
C         IF(J.EQ.I) THEN
C          SIGA6(I)=SIGA6(I)+(1.-DC0)*
C     #    ((1.-DT66(I,I))*SIGET6(I)+SIGEC6(I))
C         ELSE
C          SIGA6(I)=SIGA6(I)-DT66(I,J)*(1.-DC0)*SIGET6(J)
C         END IF
C        END IF
C       END DO
C      END DO

C     CALCUL DES CONTRAINTES APPARENTES DE TRACTION EN
C       BASE PRINCIPALE D ENDOMMAGEMENT
      DO 240 I=1,6
       SIGAT6(I)=0.D0
       DO 250 J=1,6
        IF(J.EQ.I)THEN
C        ON EST SUR LA DIAGONALE
         IF(I.LE.3)THEN
C         ON EST SUR LA CONTRAINTE NORMALE : ON RAJOUTE
C           LA PRESSION AUX CONTRAINTES DE TRACTION UNIQUEMENT
          IF(SIGEC6(I).LT.0.D0)THEN
           BWPW=0.D0
          ELSE
           BWPW=BW*PW
          END IF
C        CHARGEMENT PAR LES PRESSIONS
          IF(BWPW.LT.0.D0) THEN
C        CAS DE LA DEPRESSION CAPILLAIRE APPLIQUÈE SUR LE SQUELETTE SAIN
           SIGAT6(I)=SIGAT6(I)+(1.D0-DT66(I,J))*
     #     (SIGET6(J)-BWPW-BCH*PCH)
          ELSE
C        LA SURPRESSION  D EAU EXISTE AUSSI DANS
C          LES FISSURES (PAS CELLE DE RAG)
           SIGAT6(I)=SIGAT6(I)+(1.D0-DT66(I,J))*
     #     (SIGET6(J)-BCH*PCH)-BWPW
          END IF
         ELSE
C         ON EST PAS LES CONTRAINTES NORMALES MAIS SUR LES CISAILLEMENTS
          SIGAT6(I)=SIGAT6(I)+(1.D0-DT66(I,J))*SIGET6(J)
         END IF
        ELSE
C        ON PREND EN COMPTE LES COUPLAGES AVEC
C          LES CONTRAINTES HOR DIAGONALE
         SIGAT6(I)=SIGAT6(I)-DT66(I,J)*SIGET6(J)
        END IF
250    CONTINUE
C      PASSAGE A LA CONTRAINTE SUIVANTE
240   CONTINUE

C      CALCUL DES CONTRAINTES APPARENTES  TOTALES EN
C        BASE PRINCIPALE D ENDOMMAGEMENT
C      (COUPLAGE AVEC L ENDOMMAGEMENT DE COMPRESSION ET
C        PRISE EN COMPTE DES PRESSIONS
C        HYDRIQUES POUR LES CONTRAINTES NORMALES EFFECTIVES NEGATIVE)
      DO 260 I=1,6
        IF((SIGEC6(I).LT.0.D0).AND.(I.LE.3))THEN
C        LA CONTRAINTE EFFECTIVE NORMALE DANS LA
C          DIRECTION I EST NEGATIVE : ON RAJOUTE LA PRESSION
         IF(PW.LT.0.D0) THEN
C         DANS LE SQUELETTE SAIN SI DEPRESSION
          SIGA6(I)=(1.D0-DC0)*(SIGAT6(I)+SIGEC6(I)-BW*PW)
         ELSE
C         PARTOUT SI SURPRESSION
          SIGA6(I)=(1.D0-DC0)*(SIGAT6(I)+SIGEC6(I))-BW*PW
         END IF
        ELSE
C        ON EST PAS SUR LES CONTRAINTES NORMALES : PAS DE PRESSION
C          ET ENDO DE COMP ISOTROPE
         SIGA6(I)=(1.D0-DC0)*(SIGAT6(I)+SIGEC6(I))
        END IF
260   CONTINUE

C     RETOUR DES CONTRAINTES APPARENTES EN BASE FIXE
      CALL MATINI(3,3,ZZ,X33)
      DO 270 I=1,6
       CALL BRINDZ(I,J,K)
       X33(J,K)=SIGA6(I)
       IF(J.NE.K)THEN
        X33(K,J)=X33(J,K)
       END IF
270   CONTINUE
      CALL UTBTAB('ZERO',3,3,X33,VBT33T,TRAV,SIGA33)
      DO 280 I=1,6
       CALL BRINDZ(I,J,K)
       SIGA6(I)=SIGA33(J,K)
280   CONTINUE


      END
