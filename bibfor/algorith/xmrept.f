      SUBROUTINE XMREPT(NDIM  ,DEFICO,CHSGM ,CHSLO ,CHSAI ,
     &                  GEOM  ,STATUE,MMAIT ,AMAIT ,NMAIT )
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 22/12/2009   AUTEUR ABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2006  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C
      IMPLICIT NONE
      CHARACTER*24 DEFICO
      CHARACTER*19 CHSGM,CHSLO,CHSAI
      REAL*8       GEOM(3)
      INTEGER      NDIM,MMAIT,AMAIT,NMAIT,STATUE
C      
C ----------------------------------------------------------------------
C
C ROUTINE XFEM (CONTACT - GRANDS GLISSEMENTS)
C
C ON CHERCHE LE POINT D'INTERSECTION MAITRE LE PLUS PROCHE DE 
C POINT DE CONTACT
C
C TRAVAIL EFFECTUE EN COLLABORATION AVEC I.F.P.
C
C ----------------------------------------------------------------------
C
C
C IN  DEFICO : SD POUR LA DEFINITION DE CONTACT
C IN  CHSGM  : CHAMP CONTENANT LA GEOMETRIE DE LA FACETTE MAITRE
C IN  CHSLO  : CHAMP CONTENANT L'INFO SUR LES NBRE DE PT D'INTER 
C              ET DE FACETTE DES MAILLES
C IN  CHSAI  : CHAMP CONTENANT LES INFOS SUR LES ARETES COUPEES
C IN  NDIM   : DIMENSION DU MODELE
C IN  STATUE : STATUT DE LA MAILLE ESCLAVE
C IN  GEOM   : COORDONNEES DU POINT DE CONTACT ESCLAVE
C OUT MMAIT  : LE NUMÉRO GLOBAL DE LA MAILLE MAÎTRE CONTENANT LE POINT 
C              D'INTERSECTION DE PLUS PROCHE
C OUT AMAIT  : LE NUMÉRO LOCAL DE L'ARRETE CONTENANT LE POINT 
C              D'INTERSECTION DE PLUS PROCHE
C OUT NMAIT  : LE NUMÉRO LOCAL DU NOEUD CONTENANT LE POINT 
C              D'INTERSECTION DE PLUS PROCHE
C
C
C -------------- DEBUT DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C ---------------- FIN DECLARATIONS NORMALISEES JEVEUX ---------------
C
      INTEGER      NUMMAI,STATUT,NTMAE,IMA,IAD,NBPT
      INTEGER      CFDISI,CFMMVD,ZMESX
      INTEGER      JCSD1,JCSV1,JCSL1
      INTEGER      JCSD2,JCSV2,JCSL2
      INTEGER      JCSD3,JCSV3,JCSL3      
      INTEGER      INI,J,I
      REAL*8       COORD(3),DMIN,DIST,R8GAEM
      CHARACTER*24 MAESCX
      INTEGER      JMAESX
      INTEGER      ZXAIN,XXMMVD      
C
C --------------------------------------------------------------------
C
      CALL JEMARQ()
C
C --- SI LA MAILLE ESCLAVE EST CRACK-TIP, ON SORT
C
      IF (STATUE.EQ.-2) GOTO 999            
C
C --- INITIALISATIONS
C          
      DMIN   = R8GAEM()       
      DO 10 I=1,3
        COORD(I)=0.D0
  10  CONTINUE
      NTMAE  = CFDISI(DEFICO,'NTMAE')
C      
C --- RECUPERATION DE QUELQUES DONNEES      
C
      MAESCX = DEFICO(1:16)//'.MAESCX' 
      CALL JEVEUO(MAESCX,'L',JMAESX)      
      ZMESX  = CFMMVD('ZMESX')  
      ZXAIN = XXMMVD('ZXAIN')  
C      
C --- ON RECUPERE LA GEOMETRIE DES FACETTES MAITRES
C
      CALL JEVEUO(CHSGM//'.CESD','L',JCSD1)
      CALL JEVEUO(CHSGM//'.CESV','L',JCSV1)
      CALL JEVEUO(CHSGM//'.CESL','L',JCSL1)
C      
C --- ON RECUPERE LES INFOS SUR LES ARETES COUPEES
C
      CALL JEVEUO(CHSAI//'.CESD','L',JCSD2)
      CALL JEVEUO(CHSAI//'.CESV','L',JCSV2)
      CALL JEVEUO(CHSAI//'.CESL','L',JCSL2)
C      
C --- ON RECUPERE LES INFOS SUR LES NBRES DE PT
C --- ET DE FACETTES DE CONTACT
C
      CALL JEVEUO(CHSLO//'.CESD','L',JCSD3)
      CALL JEVEUO(CHSLO//'.CESV','L',JCSV3)
      CALL JEVEUO(CHSLO//'.CESL','L',JCSL3)
C
C --- BOUCLE SUR LES MAILLES FISSURÉES
C
      DO 100 IMA=1,NTMAE
        NUMMAI = ZI(JMAESX+ZMESX*(IMA-1)+1-1)
        STATUT = ZI(JMAESX+ZMESX*(IMA-1)+4-1)

        IF (STATUT.LT.0) GO TO 100

C ----- RECUPERATION DU NOMBRE DE POINTS D'INTERSECTION DE LA MAILLE

        CALL CESEXI('C',JCSD3,JCSL3,NUMMAI,1,1,1,IAD)
        CALL ASSERT(IAD.GT.0)
        NBPT   = ZI(JCSV3-1+IAD)        
C ----- BOUCLE SUR LES POINTS D'INTERSECTION

        DO 110 INI=1,NBPT
C ------- COORDONNEES DE GEOMETRIQUES DU POINT D'INTERSECTION   
          DO 120 J=1,NDIM
            CALL CESEXI('S',JCSD1,JCSL1,NUMMAI,1,1,
     &              NDIM*(INI-1)+J,IAD)
            CALL ASSERT(IAD.GT.0)
            COORD(J) = ZR(JCSV1-1+IAD)
  120     CONTINUE
C ------- CALCUL DE LA DISTANCE
          DIST = SQRT((COORD(1)-GEOM(1))**2+ (COORD(2)-GEOM(2))**2+
     &           (COORD(3)-GEOM(3))**2)
          IF (DIST.LT.DMIN) THEN
            DMIN   = DIST
            MMAIT  = NUMMAI
            CALL CESEXI('S',JCSD2,JCSL2,NUMMAI,1,
     &                  1,ZXAIN*(INI-1)+1,IAD)
            CALL ASSERT(IAD.GT.0)
            AMAIT = NINT(ZR(JCSV2-1+IAD))
            IF (AMAIT.EQ.0) THEN
              CALL CESEXI('S',JCSD2,JCSL2,NUMMAI,1,
     &                     1,ZXAIN*(INI-1)+2,IAD)
              CALL ASSERT(IAD.GT.0)
              NMAIT  = NINT(ZR(JCSV2-1+IAD))
            ELSE
              NMAIT  = 0
            ENDIF
          ENDIF
  110   CONTINUE
  100 CONTINUE
C
  999 CONTINUE
C  
      CALL JEDEMA()
      END
