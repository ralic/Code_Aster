      SUBROUTINE XMREPT(CHSGM,CHSLO,CHSAI,NDIM,GEOM,JMAESC,STATUE,
     &                 MMAIT,AMAIT,NMAIT)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 08/12/2009   AUTEUR PROIX J-M.PROIX 
C ======================================================================
C COPYRIGHT (C) 1991 - 2006  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
      IMPLICIT NONE
      CHARACTER*19  CHSGM,CHSLO,CHSAI
      REAL*8       GEOM(3)
      INTEGER      NDIM,JMAESC,MMAIT,AMAIT,NMAIT,STATUE
C
C ----------------------------------------------------------------------
C ROUTINE APPELEE PAR : XAPPAR
C ----------------------------------------------------------------------
C ROUTINE SPECIFIQUE A L'APPROCHE <<GRANDS GLISSEMENTS AVEC XFEM>>,
C TRAVAIL EFFECTUE EN COLLABORATION AVEC I.F.P.
C ----------------------------------------------------------------------
C ON CHERCHE LE POINT D'INTERSECTION MAITRE LE PLUS PROCHE DE 
C POINT DE CONTACT
C
C IN  CHSGM  : CHAMP CONTENANT LA GEOMETRIE DE LA FACETTE MAITRE
C IN  CHSLO  : CHAMP CONTENANT L'INFO SUR LES NBRE DE PT D'INTER 
C              ET DE FACETTE DES MAILLES
C IN  CHSAI  : CHAMP CONTENANT LES INFOS SUR LES ARETES COUPEES
C IN  NDIM   : DIMENSION DU MODELE
C IN  GEOM   : COORDONNEES DU POINT DE CONTACT ESCLAVE
C IN  JMAESC : POINTEUR VERS LA LISTE DE MAILLES FISSURES
C IN  STATUE : STATUM DE LA MAILLE ESCLAVE
C OUT MMAIT  : LE NUMÉRO GLOBAL DE LA MAILLE MAÎTRE CONTENANT LE POINT 
C              D'INTERSECTION DE PLUS PROCHE
C OUT AMAIT  : LE NUMÉRO LOCAL DE L'ARRETE CONTENANT LE POINT 
C              D'INTERSECTION DE PLUS PROCHE
C OUT NMAIT  : LE NUMÉRO LOCAL DU NOEUD CONTENANT LE POINT 
C              D'INTERSECTION DE PLUS PROCHE
C
C -------------- DEBUT DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C ---------------- FIN DECLARATIONS NORMALISEES JEVEUX ---------------
C
      INTEGER POSMA,NBN,NTMA,IMA,IFACE,IAD,NFIS
      INTEGER JNFIS,JMOFIS,CFMMVD,ZMESX
      INTEGER JCSD1,JCSV1,JCSL1,JCSD2,JCSV2,JCSL2,JCSD3,JCSV3,JCSL3
      INTEGER JCOOR,INI,J,I,STATUM,NBPT
      INTEGER ZXAIN,XXMMVD
      REAL*8 COORD(3),DMIN,DIST,NORMD,NORMV,R8GAEM
      CHARACTER*8 NOMFIS
      CHARACTER*24 PZONE,CONTNO,PSURNO
C
C --------------------------------------------------------------------
C
      CALL JEMARQ()
C
C --- SI LA MAILLE ESCLAVE EST CRACK-TIP, ON SORT
      IF (STATUE.EQ.-2) GOTO 999
C
      DMIN = R8GAEM()
      NTMA = ZI(JMAESC)
      ZMESX = CFMMVD('ZMESX')
      ZXAIN = XXMMVD('ZXAIN')
      DO 10 I=1,3
        COORD(I)=0.D0
  10  CONTINUE
C
C-----ON RECUPERE LA GEOMETRIE DES FACETTES MAITRES
C
      CALL JEVEUO(CHSGM//'.CESD','L',JCSD1)
      CALL JEVEUO(CHSGM//'.CESV','L',JCSV1)
      CALL JEVEUO(CHSGM//'.CESL','L',JCSL1)
C
C-----ON RECUPERE LES INFOS SUR LES ARETES COUPEES
C
      CALL JEVEUO(CHSAI//'.CESD','L',JCSD2)
      CALL JEVEUO(CHSAI//'.CESV','L',JCSV2)
      CALL JEVEUO(CHSAI//'.CESL','L',JCSL2)
C
C-----ON RECUPERE LES INFOS SUR LES NBRES DE PT
C     ET DE FACETTES DE CONTACT
C
      CALL JEVEUO(CHSLO//'.CESD','L',JCSD3)
      CALL JEVEUO(CHSLO//'.CESV','L',JCSV3)
      CALL JEVEUO(CHSLO//'.CESL','L',JCSL3) 
C
C-----BOUCLE SUR LES MAILLES FISSURÉES---------------------------------
C
      DO 100 IMA=1,NTMA
        POSMA  = ZI(JMAESC+ZMESX*(IMA-1)+1)
        STATUM = ZI(JMAESC+ZMESX*(IMA-1)+4)
C        NBN    = ZI(JMAESC+ZMESX*(IMA-1)+3)

        IF (STATUM.LT.0) GO TO 100
C
C --- RECUPERATION DU NOMBRES DE POINTS D'INTERSECTION DE LA MAILLE
C
        CALL CESEXI('C',JCSD3,JCSL3,POSMA,1,1,1,IAD)
        CALL ASSERT(IAD.GT.0)
        NBPT = ZI(JCSV3-1+IAD)
C
C-------BOUCLE SUR LES POINT D'INTERSECTION---------------------------
        DO 110 INI=1,NBPT
C---------COORDONNEES GEOMETRIQUES DU POINT D'INTERSECTION      
          DO 120 J=1,NDIM
            CALL CESEXI('S',JCSD1,JCSL1,POSMA,1,1,NDIM*(INI-1)+J,IAD)
            CALL ASSERT(IAD.GT.0)
            COORD(J)=ZR(JCSV1-1+IAD)
  120     CONTINUE
C---------CALCUL DE LA DISTANCE--------------------------------------
          DIST = SQRT((COORD(1)-GEOM(1))**2+ (COORD(2)-GEOM(2))**2+
     &           (COORD(3)-GEOM(3))**2)
          IF (DIST.LT.DMIN) THEN
            DMIN=DIST
            MMAIT=POSMA
            CALL CESEXI('S',JCSD2,JCSL2,POSMA,1,1,ZXAIN*(INI-1)+1,IAD)
            CALL ASSERT(IAD.GT.0)
            AMAIT=NINT(ZR(JCSV2-1+IAD))
            IF (AMAIT.EQ.0) THEN
              CALL CESEXI('S',JCSD2,JCSL2,POSMA,1,1,ZXAIN*(INI-1)+2,IAD)
              CALL ASSERT(IAD.GT.0)
              NMAIT=NINT(ZR(JCSV2-1+IAD))
            ELSE
              NMAIT=0
            ENDIF
          ENDIF
  110   CONTINUE
  100 CONTINUE
C
  999 CONTINUE
C
      CALL JEDEMA()
      END
