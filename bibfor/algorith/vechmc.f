      SUBROUTINE VECHMC(MODELZ,CARELZ,MATE,CHARGZ,INFCHZ,INSTAP,VECELZ)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 11/09/2002   AUTEUR VABHHTS J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.

C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.

C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
      IMPLICIT REAL*8 (A-H,O-Z)
      CHARACTER*24 MODELE,CARELE,CHARGE,INFCHA
      CHARACTER*(*) MODELZ,CARELZ,MATE,CHARGZ,INFCHZ,VECELZ
      REAL*8 INSTAP
C ----------------------------------------------------------------------
C     CALCUL DES VECTEURS ELEMENTAIRES DES CHARGEMENTS MECANIQUES
C     COMPLEXES.
C     ( COPIE DE LA ROUTINE VECHME )

C IN  MODELZ  : NOM DU MODELE
C IN  CARELZ  : CARACTERISTIQUES DES POUTRES ET COQUES
C IN  MATE    : MATERIAU
C IN  CHARGZ  : LISTE DES CHARGES
C IN  INFCHZ  : INFORMATIONS SUR LES CHARGEMENTS
C IN  INSTAP  : INSTANT DU CALCUL
C OUT/JXOUT VECELZ  : VECT_ELEM

C --- DEBUT DECLARATIONS NORMALISEES JEVEUX ----------------------------

      CHARACTER*32 JEXNUM,JEXNOM,JEXR8,JEXATR
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)

C --- FIN DECLARATIONS NORMALISEES JEVEUX ------------------------------
      CHARACTER*8 NOMCHA,VECELE,LPAIN(12),PAOUT,K8BID,LCMP(2),NEWNOM
      CHARACTER*16 OPTION
      CHARACTER*24 CHGEOM,CHCARA(15),CHTIME,LIGREL
      CHARACTER*24 LIGRMO,LIGRCH,LCHIN(12),RESUEL
      INTEGER IBID,IRET,NCHAR,ILVE,I,K,ICHA,NUMCHM
      INTEGER NBCHMX,IER,INUM,JCHAR,JINF,JLVE,LONLIS,NBCHME
      LOGICAL EXIGEO,EXICAR,BIDON
      COMPLEX*16 CBID
      PARAMETER (NBCHMX=14)
      CHARACTER*6 NOMLIG(NBCHMX),NOMPAF(NBCHMX),NOMPAR(NBCHMX)
      CHARACTER*6 NOMOPF(NBCHMX),NOMOPR(NBCHMX)
      DATA NOMLIG/'.FORNO','.F3D3D','.F2D3D','.F1D3D','.F2D2D','.F1D2D',
     &     '.F1D1D','.PESAN','.ROTAT','.PRESS','.FELEC','.FCO3D',
     &     '.FCO2D','.EPSIN'/
      DATA NOMOPR/'FORC_C','FC3D3D','FC2D3D','FC1D3D','FC2D2D','FC1D2D',
     &     'FC1D1D','PESA_C','ROTA_C','PRES_C','FCELEC','FCCO3D',
     &     'FCCO2D','EPSI_C'/
      DATA NOMOPF/'FORC_C','FF3D3D','FF2D3D','FF1D3D','FF2D2D','FF1D2D',
     &     'FF1D1D','PESA_C','ROTA_C','PRES_F','FRELEC','FFCO3D',
     &     'FFCO2D','EPSI_F'/
      DATA NOMPAR/'FORNOC','FC3D3D','FC2D3D','FC1D3D','FC2D2D','FC1D2D',
     &     'FC1D1D','PESANC','ROTATC','PRESSC','FCELEC','FCCO3D',
     &     'FCCO2D','EPSINC'/
      DATA NOMPAF/'FORNOF','FF3D3D','FF2D3D','FF1D3D','FF2D2D','FF1D2D',
     &     'FF1D1D','PESANR','ROTATR','PRESSF','FRELEC','FFCO3D',
     &     'FFCO2D','EPSINF'/

      CALL JEMARQ()
      NEWNOM = '.0000000'
      MODELE = MODELZ
      CARELE = CARELZ
      CHARGE = CHARGZ
      INFCHA = INFCHZ

      BIDON = .TRUE.
      CALL JEEXIN(CHARGE,IRET)
      IF (IRET.NE.0) THEN
        CALL JELIRA(CHARGE,'LONMAX',NCHAR,K8BID)
        IF (NCHAR.NE.0) THEN
          BIDON = .FALSE.
          CALL JEVEUO(CHARGE,'L',JCHAR)
          CALL JEVEUO(INFCHA,'L',JINF)
        END IF
      END IF

      VECELE = VECELZ
      IF (VECELE.EQ.' ') THEN
        VECELE = '&&VECHMC'
      END IF
      RESUEL = '&&VECHMC.???????'

      CALL DETRSD('VECT_ELEM',VECELE)
      CALL MEMARE('V',VECELE,MODELE(1:8),MATE,CARELE,'CHAR_MECA')
      IF (BIDON) THEN
        CALL WKVECT(VECELE//'.LISTE_RESU','V V K24',1,JLVE)
        CALL JEECRA(VECELE//'.LISTE_RESU','LONUTI',0,K8BID)
        GO TO 30
      ELSE
        LONLIS = NBCHMX*NCHAR
        CALL WKVECT(VECELE//'.LISTE_RESU','V V K24',LONLIS,JLVE)
        CALL JEECRA(VECELE//'.LISTE_RESU','LONUTI',0,K8BID)
      END IF


      LIGRMO = MODELE(1:8)//'.MODELE'
      CALL MEGEOM(MODELE(1:8),ZK24(JCHAR) (1:8),EXIGEO,CHGEOM)
      CALL MECARA(CARELE(1:8),EXICAR,CHCARA)

      CHTIME = '&&VECHMC.CH_INST_R'
      CALL MECACT('V',CHTIME,'MODELE',LIGRMO,'INST_R  ',1,'INST   ',
     &            IBID,INSTAP,CBID,K8BID)
      LPAIN(2) = 'PGEOMER'
      LCHIN(2) = CHGEOM
      LPAIN(3) = 'PTEMPSR'
      LCHIN(3) = CHTIME
      LPAIN(4) = 'PMATERC'
      LCHIN(4) = MATE
      LPAIN(5) = 'PCACOQU'
      LCHIN(5) = CHCARA(7)
      LPAIN(6) = 'PCAGNPO'
      LCHIN(6) = CHCARA(6)
      LPAIN(7) = 'PCADISM'
      LCHIN(7) = CHCARA(3)
      LPAIN(8) = 'PCAORIE'
      LCHIN(8) = CHCARA(1)
      LPAIN(9) = 'PCACABL'
      LCHIN(9) = CHCARA(10)
      LPAIN(10) = 'PCAARPO'
      LCHIN(10) = CHCARA(9)
      LPAIN(11) = 'PCAGNBA'
      LCHIN(11) = CHCARA(11)
      LPAIN(12) = 'PCAMASS'
      LCHIN(12) = CHCARA(12)
      PAOUT = 'PVECTUC'

      ILVE = 0
      DO 20 ICHA = 1,NCHAR
        NOMCHA = ZK24(JCHAR+ICHA-1) (1:8)
        LIGRCH = NOMCHA//'.CHME.LIGRE'
        NUMCHM = ZI(JINF+NCHAR+ICHA)
        IF (NUMCHM.GT.0) THEN
          LIGREL = LIGRCH
          DO 10 K = 1,NBCHMX
            LCHIN(1) = LIGRCH(1:13)//NOMLIG(K)//'.DESC'
            CALL EXISD('CHAMP_GD',LCHIN(1),IRET)
            IF (IRET.NE.0) THEN
              IF (NUMCHM.EQ.1) THEN
                OPTION = 'CHAR_MECA_'//NOMOPR(K)
                LPAIN(1) = 'P'//NOMPAR(K)
              ELSE IF (NUMCHM.EQ.2) THEN
                OPTION = 'CHAR_MECA_'//NOMOPF(K)
                LPAIN(1) = 'P'//NOMPAF(K)
              ELSE IF (NUMCHM.EQ.3) THEN
                OPTION = 'CHAR_MECA_'//NOMOPF(K)
                LPAIN(1) = 'P'//NOMPAF(K)
              ELSE IF (NUMCHM.EQ.4) THEN
                GO TO 10
              END IF
              CALL GCNCO2(NEWNOM)
              RESUEL(10:16) = NEWNOM(2:8)
              CALL CORICH('E',RESUEL,ICHA,IBID)
              CALL CALCUL('S',OPTION,LIGREL,12,LCHIN,LPAIN,1,RESUEL,
     &                    PAOUT,'V')
              ILVE = ILVE + 1
              ZK24(JLVE-1+ILVE) = RESUEL
            END IF
            LIGREL = LIGRMO
   10     CONTINUE
        END IF

   20 CONTINUE
      CALL JEECRA(VECELE//'.LISTE_RESU','LONUTI',ILVE,K8BID)

   30 CONTINUE
      VECELZ = VECELE//'.LISTE_RESU'
      CALL JEDEMA()
      END
