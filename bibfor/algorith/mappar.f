      SUBROUTINE MAPPAR(PREMIE,NOMA  ,NUMEDD,DEFICO,RESOCO,
     &                  NEWGEO)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 27/07/2010   AUTEUR DESOZA T.DESOZA 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT NONE
      LOGICAL      PREMIE
      CHARACTER*8  NOMA
      CHARACTER*24 DEFICO,RESOCO
      CHARACTER*24 NUMEDD
      CHARACTER*24 NEWGEO
C
C ----------------------------------------------------------------------
C
C ROUTINE CONTACT (METHODE CONTINUE)
C
C REALISE L'APPARIEMENT ENTRE SURFACE ESCLAVE ET SURFACE MAITRE POUR
C LE CONTACT METHODE CONTINUE
C
C ----------------------------------------------------------------------
C
C
C METHODE : POUR CHAQUE POINT DE CONTACT (SUR UNE MAILLE ESCLAVE ET
C AVEC UN SCHEMA D'INTEGRATION DONNE), ON RECHERCHE LE NOEUD MAITRE LE
C PLUS PROCHE ET ON PROJETTE SUR LES MAILLES QUI L'ENTOURE
C
C STOCKAGE DES POINTS  DE CONTACT DES SURFACES  ESCLAVES ET APPARIEMENT
C
C IN  NOMA   : NOM DU MAILLAGE
C IN  PREMIE : VAUT .TRUE. SI PREMIER INSTANT DE STAT/DYNA_NON_LINE
C IN  DEFICO : SD POUR LA DEFINITION DE CONTACT
C IN  NUMEDD : NOM DE LA NUMEROTATION MECANIQUE
C IN  RESOCO : SD POUR LA RESOLUTION DE CONTACT
C IN  NEWGEO : GEOMETRIE ACTUALISEE
C
C -------------- DEBUT DECLARATIONS NORMALISEES JEVEUX -----------------
C
      CHARACTER*32 JEXNUM
      INTEGER ZI
      COMMON /IVARJE/ ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C ---------------- FIN DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER      IFM,NIV
      INTEGER      CFMMVD,ZTABF,ZMAES
      INTEGER      IMAE,IPC,ISURFM
      INTEGER      NTMAE,NDIM,NTPC,NBPC,NZOCO,NNE
      INTEGER      IZONE,TYCO,IRET,NEQ
      INTEGER      POSMAE,POSNOM,POSMAM
      INTEGER      NUMMAE,NUMMAM,OLDMAM
      INTEGER      ITEMAX
      REAL*8       KSI1,KSI2
      REAL*8       TAU1M(3),TAU2M(3),TAU1(3),TAU2(3),NORM(3)
      REAL*8       COORME(27),COORPC(3),DISTME(3)
      REAL*8       JEU,JEUSGN,NOOR
      REAL*8       KSIPC1,KSIPC2,WPC
      REAL*8       MMINFR,DIR(3),TOLEOU,SEUILI
      REAL*8       CFDISR,EPSMAX,TOLEAP
      REAL*8       ARMINI,ARMIN ,EPSINT,DDOT
      CHARACTER*8  ALIAS,K8BID,NOMMAE
      CHARACTER*24 MAESCL,TABFIN,CRNUDD
      INTEGER      JMAESC,JTABF,JCRNUD
      LOGICAL      PROJIN,LSSFRO,LGLISS,LAPPAR,PRTOLE
      LOGICAL      COMPLI,CTCINI,DIRAPP,LPIVOT,LSSCON
      LOGICAL      LDETEC
      LOGICAL      MMINFL
      INTEGER      CFDISI,MMINFI
      INTEGER      TYPBAR,TYPRAC,CODRET
      INTEGER      NDEXFR,NDEXCL(9),NDFOND(9),NDRACC(9)
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
      CALL INFDBG('CONTACT',IFM,NIV)
C
C --- ACCES SD CONTACT
C
      MAESCL = DEFICO(1:16)//'.MAESCL'
      TABFIN = RESOCO(1:14)//'.TABFIN'
      CRNUDD = RESOCO(1:14)//'.NUDD'
      CALL JEVEUO(MAESCL,'L',JMAESC)
      CALL JEVEUO(TABFIN,'E',JTABF)
      CALL JEVEUO(CRNUDD,'E',JCRNUD)
C
      ZTABF = CFMMVD('ZTABF')
      ZMAES = CFMMVD('ZMAES')
C
C --- INITIALISATIONS
C
      LAPPAR = .FALSE.
      LDETEC = .FALSE.
      NTMAE  = CFDISI(DEFICO,'NTMAE')
      NDIM   = CFDISI(DEFICO,'NDIM' )
      NZOCO  = CFDISI(DEFICO,'NZOCO')
      NTPC   = 0
      DIR(1) = 0.D0
      DIR(2) = 0.D0
      DIR(3) = 0.D0
      DISTME(1) = 0.D0
      DISTME(2) = 0.D0
      DISTME(3) = 0.D0
      CALL DISMOI('F','NB_EQUA',NUMEDD,'NUME_DDL',NEQ,K8BID,IRET)
C
C --- INFOS GENERIQUES POUR L'ALGORITHME D'APPARIEMENT
C
      EPSMAX = CFDISR(DEFICO,'PROJ_NEWT_RESI'   )
      ITEMAX = CFDISI(DEFICO,'PROJ_NEWT_ITER'   )
C
C --- TOLERANCE POUR LA DETECTION DU CONTACT INITIAL
C
      ARMINI = ARMIN(NOMA)
      EPSINT = 1.D-6*ARMINI
C
C --- GESTION AUTOMATIQUE DES RELATIONS REDONDANTES
C
      DO 11 IZONE = 1,NZOCO
        LPIVOT = MMINFL(DEFICO,'EXCLUSION_PIV_NUL',IZONE )
        IF (LPIVOT) THEN
          LDETEC = .TRUE.
        ENDIF
   11 CONTINUE
      IF (LDETEC) THEN
        CALL REDNEX(NUMEDD,NEQ   ,RESOCO)
      ENDIF
C
C --- CALCUL DES NORMALES/TANGENTES EN CHAQUE NOEUD (SI NECESSAIRE)
C
      CALL CFCALN(NOMA  ,DEFICO,RESOCO,NEWGEO,ITEMAX,
     &            EPSMAX)
C
C --- AFFICHAGE
C
      IF (NIV.GE.2) THEN
        WRITE (IFM,*) '<CONTACT> APPARIEMENT EFFECTIF'
      ENDIF
C
C --- BOUCLE SUR LES MAILLES ESCLAVES
C
      DO 20 IMAE = 1,NTMAE
C
C --- ZONE DE CONTACT
C
        IZONE  = ZI(JMAESC+ZMAES*(IMAE-1)+2-1)
        CALL CFZONE(DEFICO,IZONE ,'MAIT',ISURFM)
C
C --- OPTIONS SUR LA ZONE DE CONTACT
C
        TOLEOU = MMINFR(DEFICO,'TOLE_PROJ_EXT'    ,IZONE )
        LPIVOT = MMINFL(DEFICO,'EXCLUSION_PIV_NUL',IZONE )
        SEUILI = MMINFR(DEFICO,'SEUIL_INIT'       ,IZONE )
        SEUILI = -ABS(SEUILI)
        TYCO   = MMINFI(DEFICO,'INTEGRATION'      ,IZONE )
        DIRAPP = MMINFL(DEFICO,'TYPE_APPA_FIXE'   ,IZONE )
        IF (DIRAPP) THEN
          DIR(1) = MMINFR(DEFICO,'TYPE_APPA_DIRX',IZONE )
          DIR(2) = MMINFR(DEFICO,'TYPE_APPA_DIRY',IZONE )
          DIR(3) = MMINFR(DEFICO,'TYPE_APPA_DIRZ',IZONE )
        ENDIF
        TOLEAP = MMINFR(DEFICO,'TOLE_APPA'        ,IZONE )
        COMPLI = MMINFL(DEFICO,'COMPLIANCE'       ,IZONE )
        LSSFRO = MMINFL(DEFICO,'SANS_GROUP_NO_FR' ,IZONE )
        LSSCON = MMINFL(DEFICO,'SANS_GROUP_NO'    ,IZONE )
        LGLISS = MMINFL(DEFICO,'GLISSIERE_ZONE'   ,IZONE )
        CTCINI = MMINFL(DEFICO,'CONTACT_INIT'     ,IZONE )
C
C --- MAILLE ESCLAVE COURANTE
C
        POSMAE = ZI(JMAESC+ZMAES*(IMAE-1)+1-1)
C
C --- INFOS SUR LA MAILLE ESCLAVE COURANTE
C
        NBPC   = ZI(JMAESC+ZMAES*(IMAE-1)+3-1)
        CALL CFPOSM(NOMA  ,DEFICO,'MAIL',1     ,POSMAE,
     &              NUMMAE,CODRET)
        CALL JENUNO(JEXNUM(NOMA//'.NOMMAI',NUMMAE),NOMMAE)
        IF (CODRET.GE.0) THEN
          CALL MCOMCE(NOMA  ,NEWGEO(1:19),NUMMAE,COORME,ALIAS ,
     &                NNE   )
        ELSE
          CALL ASSERT(.FALSE.)
        ENDIF
C
C --- TYPE DU NOEUD EXCLU PAR GROUP_NO_RACC
C
        TYPRAC = ZI(JMAESC+ZMAES*(IMAE-1)+6-1)
C
C --- TYPE DE LA MAILLE SI BARSOUM
C
        TYPBAR = ZI(JMAESC+ZMAES*(IMAE-1)+4-1)
C
C --- ENTIER CODE POUR LES NOEUDS INTERDITS DANS
C --- SANS_GROUP_NO_FR OU SANS_NOEUD_FR
C
        NDEXFR = ZI(JMAESC+ZMAES*(IMAE-1)+5-1)
C
C --- EXCLUE LES NOEUDS SUIVANT OPTIONS  EXCL_PIVOT_NUL ET SANS_NOEUD
C
        CALL MMEXCR(NOMA  ,NEQ   ,DEFICO,RESOCO,LPIVOT,
     &              LSSCON,IZONE ,POSMAE,NDEXCL)
C
C --- APPARIEMENT - BOUCLE SUR LES POINTS DE CONTACT
C
        DO 10 IPC = 1,NBPC
C
C --- COORDONNEES DANS ELEMENT DE REFERENCE ET POIDS DU POINT DE CONTACT
C
          CALL MMGAUS(ALIAS ,TYCO  ,IPC   ,KSIPC1,KSIPC2,
     &                WPC)
C
C --- CALCUL DES COORDONNEES REELLES DU POINT DE CONTACT
C
          CALL MCOPCE(NDIM  ,ALIAS ,NNE   ,KSIPC1,KSIPC2,
     &                COORME,COORPC)
C
C --- RECHERCHE DU NOEUD MAITRE LE PLUS PROCHE DU POINT DE CONTACT
C
          CALL MMREND(DEFICO,NEWGEO,ISURFM,COORPC,TOLEAP,
     &                DIRAPP,DIR   ,POSNOM,PRTOLE)
C
C --- PROJECTION DU POINT DE CONTACT SUR LA MAILLE MAITRE
C
          CALL MMREMA(NOMA  ,DEFICO,NEWGEO,COORPC,POSNOM,
     &                ITEMAX,EPSMAX,TOLEOU,DIRAPP,DIR   ,
     &                POSMAM,NUMMAM,JEU   ,TAU1M ,TAU2M ,
     &                KSI1  ,KSI2  ,PROJIN,DISTME)
C
C --- RE-DEFINITION BASE TANGENTE SUIVANT OPTIONS
C
          CALL MMTANR(NOMA  ,NDIM  ,DEFICO,RESOCO,NEWGEO,
     &                IZONE ,LSSFRO,POSMAE,POSMAM,NUMMAM,
     &                TYCO  ,IPC   ,KSI1  ,KSI2  ,TAU1M ,
     &                TAU2M ,TAU1  ,TAU2  ,ALIAS )
C
C --- INDICATEUR NOUVEL APPARIEMENT
C
          OLDMAM = NINT(ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+2))
          IF (OLDMAM.NE.NUMMAM ) THEN
            LAPPAR = .TRUE.
          ENDIF
C
C --- STOCKAGE DES VALEURS POUR LE CHAM_ELEM (VOIR MMCHML)
C
          ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+1)  = NUMMAE
          ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+2)  = NUMMAM
          ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+3)  = KSIPC1
          ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+4)  = KSIPC2
          ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+5)  = KSI1
          ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+6)  = KSI2
          ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+7)  = TAU1(1)
          ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+8)  = TAU1(2)
          ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+9)  = TAU1(3)
          ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+10) = TAU2(1)
          ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+11) = TAU2(2)
          ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+12) = TAU2(3)
          ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+13) = IZONE
          ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+14) = WPC
          ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+15) = IPC
C
C --- SEUIL_INIT
C
          IF (PREMIE) THEN
            ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+16) = SEUILI
          END IF
C
C --- CONTACT_INIT
C
          IF (PREMIE) THEN
            IF (CTCINI) THEN
              CALL MMNORM(NDIM  ,TAU1  ,TAU2  ,NORM  ,NOOR  )
              JEUSGN=DDOT(NDIM  ,NORM  ,1     ,DISTME,1     )
              IF (JEUSGN.LE.EPSINT) THEN
                ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+22) = 1.D0
                IF (LGLISS) THEN
                  ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+17) = 1.D0
                END IF
              ENDIF
            ELSE
              ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+22) = 0.D0
            END IF
          ENDIF
C
C --- INDICATEUR DE COMPLIANCE
C
          IF (PREMIE) THEN
            ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+23) = 0.D0
          END IF
          IF (.NOT.COMPLI) THEN
            ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+23) = 1.D0
          END IF
C
C --- NOEUDS EXCLUS
C
          ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+18) = 0.D0
C
C --- POINTS DE CONTACT EXCLUS PAR TOLE_APPA
C
          IF (.NOT.PRTOLE) THEN
            ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+18) = 1.D0
          ENDIF
C
C --- POINTS DE CONTACT EXCLUS PAR PROJECTION HORS ZONE
C
          IF (.NOT. PROJIN) THEN
            ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+18) = 1.D0
            IF (LGLISS) THEN
              ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+17) = 1.D0
            END IF
          ENDIF
C
C --- NOEUDS EXCLUS PAR SANS_GROUP_NO OU EXCL_PIV_NUL
C
          IF (TYCO.EQ.1) THEN
            IF (NDEXCL(IPC).EQ.1) THEN
              ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+18) = 1.D0
            ENDIF
          ENDIF
C
C --- NOEUDS EXCLUS: PAS EN CONTACT
C
          IF (ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+18) .EQ. 1.D0) THEN
            ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+22) = 0.D0
          END IF
C
C --- NOEUDS EXCLUS PAR SANS_GROUP_NO_FR
C
          ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+19) = NDEXFR
C
C --- NOEUDS EXCLUS PAR GROUP_NO_FOND
C
          CALL MMEXNO(TYPBAR,TYPRAC,NDFOND)
          IF (NDFOND(IPC).EQ.1) THEN
            ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+20) = TYPBAR
          ELSE
            ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+20) = 0
          ENDIF
C
C --- NOEUDS EXCLUS PAR GROUP_NO_RACC
C
          CALL MMEXNO(TYPBAR,TYPRAC,NDRACC)
          IF (NDRACC(IPC).EQ.1) THEN
            ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+21) = TYPRAC
          ELSE
            ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+21) = 0
          ENDIF

 10     CONTINUE
        NTPC = NTPC + NBPC
 20   CONTINUE
C
      ZR(JTABF-1+1)  = NTPC
      CALL ASSERT(NTPC.EQ.CFDISI(DEFICO,'NTPC'))
C
C --- INDICATEUR DE REAPPARIEMENT (OU PAS)
C
      IF (PREMIE) THEN
        LAPPAR = .TRUE.
      ENDIF
      ZL(JCRNUD-1+1) = LAPPAR
C
C --- AFFICHAGE
C
      IF (NIV.GE.2) THEN
        CALL MMIMP1(IFM   ,NOMA  ,DEFICO,RESOCO)
      ENDIF
      CALL JEDETR(NEWGEO)
C
      CALL JEDEMA()
      END
