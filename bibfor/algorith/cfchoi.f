      SUBROUTINE CFCHOI(METHOZ,PROJMI,JEU   ,JEUMIN,POSMAM,
     &                  POSMIN,TAU1  ,T1MIN ,TAU2  ,T2MIN ,
     &                  KSI1  ,XIMIN ,KSI2  ,YIMIN ,LDIST ,
     &                  LDMIN )
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 13/10/2008   AUTEUR ABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2007  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT     NONE
      CHARACTER*(*) METHOZ
      CHARACTER*3   PROJMI
      LOGICAL       LDIST,LDMIN      
      REAL*8        JEU,JEUMIN
      INTEGER       POSMAM,POSMIN
      REAL*8        TAU1(3),T1MIN(3)
      REAL*8        TAU2(3),T2MIN(3)
      REAL*8        KSI1,XIMIN
      REAL*8        KSI2,YIMIN            
C      
C ----------------------------------------------------------------------
C
C ROUTINE CONTACT (METHODES DISCRETES - APPARIEMENT - MAIT/ESCL)
C
C CHOIX DE LA MAILLE APPARIEE AU NOEUD ESCLAVE COURANT
C
C ----------------------------------------------------------------------
C
C
C IN  METHOD : TYPE DE METHODE DE CONTACT (CONTINUE OU DISCRET)
C I/O PROJMI : OPERATION DE PROJECTION 
C              'NOP' ON NE FAIT RIEN
C              'ADD' ON AJOUTE L'APPARIEMENT DE CE NOEUD ESCLAVE
C              'SUP' ON SUPPRIME L'APPARIEMENT DE CE NOEUD ESCLAVE
C IN  JEU    : JEU CALCULE SUR LE VECTEUR PM
C I/O JEUMIN : VALEUR DU JEU MINIMUM
C IN  POSMAM : MAILLE MAITRE APPARIEE
C OUT POSMIN : MAILLE MAITRE CORRESPONDANT AU JEU MINIMUM 
C IN  LDIST  : TYPE DE PROJECTION
C              VAUT .FALSE. SI POINT RAMENE DANS ELEMENT DE REFERENCE
C              VAUT .TRUE. SI LA PROJECTION EST BIEN DANS L'ELEMENT
C OUT LDMIN  : TYPE DE PROJECTION CORRESPONDANT AU JEU MINIMUM
C              VAUT .FALSE. SI POINT RAMENE DANS ELEMENT DE REFERENCE
C              VAUT .TRUE. SI LA PROJECTION EST BIEN DANS L'ELEMENT
C IN  TAU1   : PREMIER VECTEUR TANGENT
C IN  TAU2   : SECOND VECTEUR TANGENT
C OUT T1MIN  : PREMIER VECTEUR TANGENT CORRESPONDANT AU JEU MINIMUM
C OUT T2MIN  : SECOND VECTEUR TANGENT CORRESPONDANT AU JEU MINIMUM
C IN  KSI1   : COORD. PARAM. 1 DE LA PROJECTION SUR MAILLE MAITRE
C IN  KSI2   : COORD. PARAM. 2 DE LA PROJECTION SUR MAILLE MAITRE
C OUT XIMIN  : COORD. PARAM. 1 DE LA PROJECTION SUR MAILLE MAITRE 
C               CORRESPONDANT AU JEU MINIMUM
C OUT YIMIN  : COORD. PARAM. 2 DE LA PROJECTION SUR MAILLE MAITRE 
C               CORRESPONDANT AU JEU MINIMUM
C
C -------------- DEBUT DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C ---------------- FIN DECLARATIONS NORMALISEES JEVEUX -----------------
C
      REAL*8      ECAN
      CHARACTER*8 METHOD
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
C
C --- INITIALISATIONS
C
      METHOD = METHOZ           
C
C --- ECART ANCIEN/NOUVEAU
C
      IF (JEUMIN.EQ.0.D0) THEN
        ECAN = 1.D0
      ELSE
        ECAN = ABS((JEU -JEUMIN)/JEUMIN)
      ENDIF           
C
C --- PROJECTION SUR UNE MAILLE
C ---      -> ON APPARIE
C --- PROJECTION HORS MAILLE
C ---      -> SI NVELLE PROJECTION "LOIN" DE L'ANCIENNE (ECAN.GT.1D-8)
C ---         ON SUPPRIME L'APPARIEMENT
C
      IF (METHOD.EQ.'DISCRETE') THEN
        IF ((JEU .LE.JEUMIN).AND.
     &      (ECAN.GT.1D-15)) THEN
          JEUMIN = JEU 
          POSMIN = POSMAM
          LDMIN  = LDIST
          CALL DCOPY(3,TAU1,1,T1MIN,1)
          CALL DCOPY(3,TAU2,1,T2MIN,1)
          XIMIN = KSI1
          YIMIN = KSI2
          IF (LDIST) THEN
            PROJMI = 'ADD'   
          ELSE
            IF (PROJMI.EQ.'ADD') THEN
              IF (ECAN.GE.1.D-8) THEN
                PROJMI = 'SUP'
              ENDIF
            ELSE
              PROJMI = 'SUP'
            ENDIF
          ENDIF
        ELSE
          IF (PROJMI.EQ.'ADD') THEN
            PROJMI =  'NOP'
          ENDIF
        ENDIF
      ELSEIF (METHOD.EQ.'CONTINUE') THEN
        IF (JEU.LT.JEUMIN) THEN
          PROJMI = 'ADD'
          POSMIN = POSMAM
          JEUMIN = JEU 
          LDMIN  = LDIST
          CALL DCOPY(3,TAU1,1,T1MIN,1)
          CALL DCOPY(3,TAU2,1,T2MIN,1)
          XIMIN = KSI1
          YIMIN = KSI2
        END IF           
      ELSE
        CALL ASSERT(.FALSE.)
      ENDIF 
C 
      CALL JEDEMA()
      END
