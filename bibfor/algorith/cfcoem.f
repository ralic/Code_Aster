      SUBROUTINE CFCOEM(RESOCO,NTNOE ,LCTFD ,LCTF3D,POSNOE,
     &                  ILIAI ,NBDDLT,NBNOM ,POSNSM,DDL   ,
     &                  COEF  ,COFX  ,COFY)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 22/12/2009   AUTEUR ABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2007  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT     NONE
      CHARACTER*24 RESOCO
      INTEGER      NTNOE      
      INTEGER      POSNOE
      INTEGER      ILIAI
      INTEGER      NBDDLT
      INTEGER      NBNOM
      INTEGER      POSNSM(9)
      INTEGER      DDL(30)  
      REAL*8       COEF(30),COFX(30),COFY(30)
      LOGICAL      LCTFD,LCTF3D         
C      
C ----------------------------------------------------------------------
C
C ROUTINE CONTACT (METHODES DISCRETES - APPARIEMENT - MAIT/ESCL)
C
C COEFFICIENTS RELATIONS LINEAIRES APPARIEMENT
C
C ----------------------------------------------------------------------
C
C
C IN  RESOCO : SD DE TRAITEMENT NUMERIQUE DU CONTACT
C IN  LCTFD  : FROTTEMENT
C IN  LCTF3D : FROTTEMENT EN 3D
C IN  POSNSM : INDICES DANS CONTNO DES NOEUDS MAITRES
C IN  POSNOE : INDICES DANS CONTNO DU NOEUD ESCLAVE
C IN  ILIAI  : INDICE DE LA LIAISON COURANTE
C IN  NTNOE  : NOMBRE TOTAL DE NOEUDS ESCLAVES
C IN  COEF   : VALEURS EN M DES FONCTIONS DE FORME ASSOCIEES AUX NOEUDS
C IN  COFX   : VALEURS EN M DES FONCTIONS DE FORME ASSOCIEES AUX NOEUDS
C                POUR LA PREMIERE DIRECTION TANGENTE
C IN  COFY   : VALEURS EN M DES FONCTIONS DE FORME ASSOCIEES AUX NOEUDS
C                POUR LA SECONDE DIRECTION TANGENTE
C IN  NBNOM  : NOMBRE DE NOEUDS MAITRES CONCERNES (MAX: 9)
C IN  POSNO  : INDICES DANS CONTNO DU NOEUD ESCLAVE ET DES NOEUDS 
C               MAITRES (MAX: 1+9=10)
C IN  NBDDLT : NOMBRE DE DDL NOEUD ESCLAVE+NOEUDS MAITRES
C IN  DDL    : NUMEROS DES DDLS ESCLAVE ET MAITRES CONCERNES
C
C -------------- DEBUT DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C ---------------- FIN DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER NBDDLE,NBDDLM
      INTEGER JDECAL,JDECDL,K,INO
      INTEGER      JAPPTR,JPDDL,JAPCOE,JAPDDL,JAPCOF
      CHARACTER*24 APPOIN,PDDL ,APCOEF,APDDL,APCOFR
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
C
C --- LECTURE DES STRUCTURES DE DONNEES DE CONTACT
C
      APCOEF = RESOCO(1:14)//'.APCOEF'
      APCOFR = RESOCO(1:14)//'.APCOFR'
      APDDL  = RESOCO(1:14)//'.APDDL'
      APPOIN = RESOCO(1:14)//'.APPOIN'
      PDDL   = RESOCO(1:14)//'.PDDLCO'
C      
      CALL JEVEUO(APPOIN,'E',JAPPTR)      
      CALL JEVEUO(PDDL,  'E',JPDDL )   
      CALL JEVEUO(APCOEF,'E',JAPCOE)      
      CALL JEVEUO(APDDL, 'E',JAPDDL)      
      IF (LCTFD) THEN
        CALL JEVEUO(APCOFR,'E',JAPCOF)
      ENDIF        
C
C --- NOMBRE DE DDL POUR LE NOEUD ESCLAVE
C      
      NBDDLE = ZI(JPDDL+POSNOE) - ZI(JPDDL+POSNOE-1)
C
C --- NOMBRE TOTAL RELATIONS LINEAIRES
C
      ZI(JAPPTR+ILIAI) = ZI(JAPPTR+ILIAI-1) + NBDDLT
C 
C --- RELATION DE CONTACT POUR LE NOEUD ESCLAVE
C
      JDECAL = ZI(JAPPTR+ILIAI-1)
      DO 5 K = 1,NBDDLE
        ZR(JAPCOE+JDECAL+K-1) = COEF(K)
        ZI(JAPDDL+JDECAL+K-1) = DDL(K)
   5  CONTINUE
C 
C --- RELATION DE FROTTEMENT POUR LE NOEUD ESCLAVE
C
      IF (LCTFD) THEN
        DO 10 K = 1,NBDDLE
          ZR(JAPCOF+JDECAL+K-1) = COFX(K)
          IF (LCTF3D) THEN
            ZR(JAPCOF+JDECAL+30*NTNOE+K-1) = COFY(K)
          END IF
   10   CONTINUE
      ENDIF
C 
C --- RELATION DE CONTACT POUR LE NOEUD MAITRE
C
      JDECAL = JDECAL + NBDDLE
      JDECDL = NBDDLE 
      DO 50 INO = 1,NBNOM
        
        NBDDLM = ZI(JPDDL+POSNSM(INO)) - ZI(JPDDL+POSNSM(INO)-1)
        DO 40 K = 1,NBDDLM
          ZR(JAPCOE+JDECAL+K-1) = COEF(JDECDL+K)
          ZI(JAPDDL+JDECAL+K-1) = DDL(JDECDL+K)       
   40   CONTINUE
C 
C --- RELATION DE FROTTEMENT POUR LE NOEUD MAITRE
C        
        IF (LCTFD) THEN
          DO 41 K = 1,NBDDLM
            ZR(JAPCOF+JDECAL+K-1) = COFX(JDECDL+K)
            IF (LCTF3D) THEN
              ZR(JAPCOF+JDECAL+30*NTNOE+K-1) = COFY(JDECDL+K)
            ENDIF
   41     CONTINUE
        ENDIF
        JDECAL = JDECAL + NBDDLM
        JDECDL = JDECDL + NBDDLM
   50 CONTINUE
C
      CALL JEDEMA()
      END
