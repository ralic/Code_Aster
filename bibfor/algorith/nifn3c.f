      SUBROUTINE NIFN3C(OPTION, NNO1, NNO2, NPG, IPOIDS, IVF1, IVF2,
     &                  IDFDE1, DFDI, GEOM,  SIG, IMATE, COMPOR, DEPLM,
     &                  GONFLM, FINTU, FINTA)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 09/05/2011   AUTEUR SFAYOLLE S.FAYOLLE 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE SFAYOLLE S.FAYOLLE
      IMPLICIT NONE

      INTEGER NNO1,NNO2, NPG,IPOIDS,IVF1,IVF2,IDFDE1, IMATE

      REAL*8 GEOM(3,NNO1),DFDI(NNO1,3), SIG(7,NPG), FINTU(3,20)
      REAL*8 FINTA(1,8), DEPLM(3,NNO1), GONFLM(1,NNO2)

      CHARACTER*16 OPTION, COMPOR(4)

C......................................................................
C
C     BUT:  CALCUL  DE L'OPTION FORC_NODA EN QUASI INCOMPRESSIBLE
C                  DEFORMATION = 'PETIT'
C......................................................................
C IN  NNO1    : NOMBRE DE NOEUDS DE L'ELEMENT LIES AUX DEPLACEMENTS
C IN  NNO2    : NOMBRE DE NOEUDS DE L'ELEMENT LIES A LA PRESSION
C IN  NPG     : NOMBRE DE POINTS DE GAUSS
C IN  POIDSG  : POIDS DES POINTS DE GAUSS
C IN  VFF1    : VALEUR  DES FONCTIONS DE FORME LIES AUX DEPLACEMENTS
C IN  VFF2    : VALEUR  DES FONCTIONS DE FORME LIES A LA PRESSION
C IN  DFDE1   : DERIVEE DES FONCTIONS DE FORME ELEMENT DE REFERENCE
C IN  DFDN1   : DERIVEE DES FONCTIONS DE FORME ELEMENT DE REFERENCE
C IN  DFDK1   : DERIVEE DES FONCTIONS DE FORME ELEMENT DE REFERENCE
C IN  GEOM    : COORDONEES DES NOEUDS
C IN  SIG     : CONTRAINTES A L'INSTANT PRECEDENT
C IN  DEPLM   : DEPLACEMENTS A L'INSTANT PRECEDENT
C IN  GONFLM  : VARIABLES LIEES AU GONFLEMENT A L'INSTANT PRECEDENT
C OUT DFDI    : DERIVEE DES FONCTIONS DE FORME  AU DERNIER PT DE GAUSS
C OUT FINTU   : FORCES INTERIEURES ASSOCIEES AU DEPLACEMENT
C OUT FINTA   : FORCES INTERIEURES ASSOCIEES A LA PRESSION ET GONFLEMENT
C......................................................................
C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ---------------------
      INTEGER            ZI
      COMMON  / IVARJE / ZI(1)
      REAL*8             ZR
      COMMON  / RVARJE / ZR(1)
      COMPLEX*16         ZC
      COMMON  / CVARJE / ZC(1)
      LOGICAL            ZL
      COMMON  / LVARJE / ZL(1)
      CHARACTER*8        ZK8
      CHARACTER*16                ZK16
      CHARACTER*24                          ZK24
      CHARACTER*32                                    ZK32
      CHARACTER*80                                              ZK80
      COMMON  / KVARJE / ZK8(1) , ZK16(1) , ZK24(1) , ZK32(1) , ZK80(1)
C --------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ---------------------
      LOGICAL GRAND, AXI

      INTEGER KPG, N, I

      REAL*8 TMP, RAC2, RBID, DEF(6,20,3), ALPHA, F(3,3),POIDS, EPSM(6)
      REAL*8 SIGMA(6), PM, DIVUM, DDOT, DSBDEP(6,6), TREPST

C --------------------------------------------------------------------

C - PRE REQUIS
      IF (NNO1 .GT. 20) CALL U2MESS('F','ALGORITH6_54')

C - INITIALISATION
      CALL R8INIR(60, 0.D0, FINTU,1)
      CALL R8INIR(8, 0.D0, FINTA,1)
      RAC2  = SQRT(2.D0)
      GRAND = .FALSE.
      AXI   = .FALSE.

C - CALCUL POUR CHAQUE POINT DE GAUSS
      DO 800 KPG = 1,NPG

C - CALCUL DU GONFLEMENT
        PM = 0.D0
        DO 1 N = 1, NNO2
          PM = PM + ZR(IVF2+N+(KPG-1)*NNO2-1)*GONFLM(1,N)
 1      CONTINUE

C - CALCUL DES ELEMENTS GEOMETRIQUES
        CALL R8INIR(6, 0.D0, EPSM,1)
        CALL NMGEOM(3,NNO1,AXI,GRAND,GEOM,KPG,IPOIDS,
     &              IVF1,IDFDE1,DEPLM,.TRUE.,POIDS,DFDI,
     &                     F,EPSM,RBID)
        DIVUM = EPSM(1) + EPSM(2) + EPSM(3)

C - CALCUL DE LA MATRICE B
        DO 35 N=1,NNO1
          DO 30 I=1,3
            DEF(1,N,I) =  F(I,1)*DFDI(N,1)
            DEF(2,N,I) =  F(I,2)*DFDI(N,2)
            DEF(3,N,I) =  F(I,3)*DFDI(N,3)
            DEF(4,N,I) = (F(I,1)*DFDI(N,2) + F(I,2)*DFDI(N,1))/RAC2
            DEF(5,N,I) = (F(I,1)*DFDI(N,3) + F(I,3)*DFDI(N,1))/RAC2
            DEF(6,N,I) = (F(I,2)*DFDI(N,3) + F(I,3)*DFDI(N,2))/RAC2
 30       CONTINUE
 35     CONTINUE

C - CALCUL DES CONTRAINTES MECANIQUES A L'EQUILIBRE
        DO 40 I=1,3
          SIGMA(I) = SIG(I,KPG)
 40     CONTINUE
        DO 65 I=4,6
          SIGMA(I) = SIG(I,KPG)*RAC2
 65     CONTINUE

C - CALCUL DE LA MATRICE D'ELASTICITE BULLE
        CALL TANBUL(OPTION, 3, KPG, IMATE, COMPOR, ALPHA, DSBDEP,
     &              TREPST)

C - CALCUL DE FINT_U
        DO 3 N=1,NNO1
          DO 2 I=1,3
            TMP = DDOT(6, SIGMA,1, DEF(1,N,I),1)
            FINTU(I,N) = FINTU(I,N) + TMP*POIDS
 2        CONTINUE
 3      CONTINUE

C - CALCUL DE FINT_P ET FINT_G
        DO  4 N = 1, NNO2
          TMP = (DIVUM-PM*ALPHA)*ZR(IVF2+N+(KPG-1)*NNO2-1)
          FINTA(1,N) = FINTA(1,N) + TMP*POIDS
 4      CONTINUE
 800  CONTINUE

      END
