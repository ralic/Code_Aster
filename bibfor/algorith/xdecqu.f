      SUBROUTINE XDECQU(IT,NDIM,CONNEC,LSN,JGRLSN,IGEOM,PINTER,
     &               NINTER,NPTS,AINTER,PMILIE,NMILIE,MFIS)
      IMPLICIT NONE 

      REAL*8        LSN(*)
      INTEGER       IT,NDIM,CONNEC(6,6),NINTER,IGEOM,NPTS,NMILIE,MFIS
      INTEGER       JGRLSN
      CHARACTER*24  PINTER,AINTER,PMILIE

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 16/06/2010   AUTEUR CARON A.CARON 
C ======================================================================
C COPYRIGHT (C) 1991 - 2010  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C                      TROUVER LES PTS D'INTERSECTION ENTRE LES ARETES
C                      ET LE PLAN DE FISSURE 
C                    
C     ENTREE
C       IT       : INDICE DU TETRA EN COURS
C       CONNEC   : CONNECTIVITÉ DES NOEUDS DU TETRA
C       LSN      : VALEURS DE LA LEVEL SET NORMALE
C       IGEOM    : ADRESSE DES COORDONNÉES DES NOEUDS DE L'ELT PARENT
C       JGRLSN   : VALEURS DU GRADIENT DE LA LEVEL SET NORMALE
C
C     SORTIE
C       PINTER   : COORDONNÉES DES POINTS D'INTERSECTION
C       NINTER   : NB DE POINTS D'INTERSECTION
C       NPTS     : NB DE PTS D'INTERSECTION COINCIDANT AVEC UN 
C                  NOEUD SOMMET
C       AINTER   : INFOS ARETE ASSOCIÉE AU POINTS D'INTERSECTION
C       PMILIE   : COORDONNEES DES POINTS MILIEUX
C       NMILIE   : NB DE POINTS MILIEUX
C       MFIS     : NB DE POINTS MILIEUX SUR LA FISSURE
C     ----------------------------------------------------------------
C     ----- DEBUT COMMUNS NORMALISES  JEVEUX  ------------------------
      INTEGER          ZI
      COMMON  /IVARJE/ ZI(1)
      REAL*8           ZR
      COMMON  /RVARJE/ ZR(1)
      COMPLEX*16       ZC
      COMMON  /CVARJE/ ZC(1)
      LOGICAL          ZL
      COMMON  /LVARJE/ ZL(1)
      CHARACTER*8      ZK8
      CHARACTER*16             ZK16
      CHARACTER*24                      ZK24
      CHARACTER*32                               ZK32
      CHARACTER*80                                        ZK80
      COMMON  /KVARJE/ ZK8(1), ZK16(1), ZK24(1), ZK32(1), ZK80(1)
C     -----  FIN  COMMUNS NORMALISES  JEVEUX  ------------------------
C
      REAL*8          A(NDIM),B(NDIM),C(NDIM),M(NDIM),LSNA,LSNB,LSNM
      REAL*8          ALPHA,PADIST,LONGAR,LONREF,TAMPOR(4)
      REAL*8          MILFI(NDIM),MILAC(NDIM),MILARA(NDIM),MILARB(NDIM)
      REAL*8          TEMP(NDIM),X(NDIM),XLSN,D,CRILSN,R8PREM,VAL,RBID
      INTEGER         AR(12,3),NBAR,NTA,NTB,NA,NB,JPTINT,INS,JAINT
      INTEGER         IA,I,IPI,IPM,IBID,PP,PD,K,TABDIR(4)
      INTEGER         NDIME
      INTEGER         J,R,IP,IPP,NNO,A1,A2,NX
      INTEGER         PTMAX,PMMAX(2),NNOMAX(2)
      INTEGER         NTM,NM,INM,NPTM,NNOP   
      INTEGER         JPTMIL,JTABCO,JTABLS,JTABAR
      INTEGER         ZXAIN,XXMMVD
      CHARACTER*8     TYPMA,ELRESE(2)
      LOGICAL         CUT
      CHARACTER*24    TABCO,TABLS,TABAR

      PARAMETER       (CRILSN=1.D-6)
      PARAMETER       (PTMAX=3)
      DATA            ELRESE /'SE3','TR6'/
      DATA            NNOMAX / 3 , 6 /
      DATA            PMMAX  / 2,  6 /
C --------------------------------------------------------------------

      CALL JEMARQ()

      TABCO='&&XDECQU.TABCO'
      TABLS='&&XDECQU.TABLS'

      ZXAIN = XXMMVD('ZXAIN')      
      CALL ELREF4(' ','RIGI',NDIME,NNOP,IBID,IBID,IBID,IBID,IBID,IBID)

C     CRÉATION DU VECTEUR DES COORDONNÉES DES POINTS D'INTERSECTION
      CALL WKVECT(PINTER,'V V R',NDIM*PTMAX,JPTINT)

C     VECTEUR REEL A 4 COMPOSANTES, POUR CHAQUE PT D'INTER : 
C     - NUMERO ARETE CORRESPONDANTE (0 SI C'EST UN NOEUD SOMMET)
C     - VRAI NUMERO NOEUD CORRESPONDANT (SERT QUE POUR NOEUD SOMMET)
C     - LONGUEUR DE L'ARETE
C     - POSITION DU PT SUR L'ARETE
      CALL WKVECT(AINTER,'V V R',PTMAX*ZXAIN,JAINT)

C     CRÉATION DU VECTEUR DES COORDONNÉES DES POINTS MILIEUX
      CALL WKVECT(PMILIE,'V V R',NDIM*PMMAX(NDIM),JPTMIL)
  
      TYPMA=ELRESE(NDIME)
      NNO = NNOMAX(NDIME)

C     CALCUL DES COORDONNEES ET LSN DU NOEUD 9
      IF (NNOP.EQ.8) CALL NDCENT(IGEOM,LSN,X,XLSN)

      NX=0
C     TABLEAU DES COORDONNEES DES NOEUDS DE L'ELEMENT ENFANT
      CALL WKVECT(TABCO,'V V R',NNO*NDIM,JTABCO)
      DO 50 J=1,NNO
        DO 51 I=1,NDIM
          IF(CONNEC(IT,J).EQ.9)THEN
            NX=J
            VAL=X(I)
          ELSE
            VAL=ZR(IGEOM-1+NDIM*(CONNEC(IT,J)-1)+I)
          ENDIF
          ZR(JTABCO-1+NDIM*(J-1)+I)=VAL
 51     CONTINUE
 50   CONTINUE     

C     TABLEAU DES LSN DES NOEUDS DE L'ELEMENT ENFANT
      CALL WKVECT(TABLS,'V V R',NNO,JTABLS)
      DO 40 J=1,NNO
        IF(CONNEC(IT,J).EQ.9) THEN
          VAL=XLSN
        ELSE
          VAL=LSN(CONNEC(IT,J))
        ENDIF
        ZR(JTABLS-1+J)=VAL
 40   CONTINUE

C     L'ELEMENT EST IL TRAVERSE PAR LA FISSURE?
      CUT=.FALSE.
      I=1
 1    CONTINUE
C     (1) RECHERCHE D'UN NOEUD PIVOT (LSN NON NULLE)
      IF(I.NE.NX.AND.ZR(JTABLS-1+I).NE.0.AND.I.LT.NNO)THEN
        DO 30 K=I+1,NNO
C     (2) PRODUIT DE CE PIVOT PAR LES AUTRES LSN
          IF(ZR(JTABLS-1+I)*ZR(JTABLS-1+K).LT.0.D0) CUT=.TRUE.
 30     CONTINUE
      ELSE 
        I=I+1
        GO TO 1
      END IF

      CALL CONARE(TYPMA,AR,NBAR)
C     COMPTEUR DE POINT INTERSECTION = NOEUD SOMMET
      INS=0
C     COMPTEUR DE POINT INTERSECTION = NOEUD MILIEU
      INM=0
C     COMPTEUR DE POINT INTERSECTION = TOUS TYPES CONFONDUS
      IPI=0
C     LONGUEUR D'ARETE MAXIMALE DE L'ELEMENT (DU SE3 OU TR6)
      LONREF=0.D0

C     BOUCLE SUR LES ARETES POUR DETERMINER LES POINTS D'INTERSECTION
      DO 100 IA=1,NBAR

C       RECUPERATION NUM ENFANT DES NOEUDS DE L'ARETE
        NTA=AR(IA,1)
        NTB=AR(IA,2)                
        NTM=AR(IA,3)

C       RECUPERATION NUM PARENT DES NOEUDS DE L'ARETE
        NA=CONNEC(IT,NTA)
        NB=CONNEC(IT,NTB)
        NM=CONNEC(IT,NTM)

C       RECUPERATION LSN DES NOEUDS EXTREMITE DE L'ARETE
        LSNA=ZR(JTABLS-1+NTA)
        LSNB=ZR(JTABLS-1+NTB)
        LSNM=ZR(JTABLS-1+NTM)

        CALL VECINI(3,0.D0,A)
        CALL VECINI(3,0.D0,B)
        CALL VECINI(3,0.D0,M)

C       RECUPERATION COORDONNEES DES NOEUDS EXTREMITE DE L'ARETE
        DO 101 I=1,NDIM  
          A(I)=ZR(JTABCO-1+NDIM*(NTA-1)+I)
          B(I)=ZR(JTABCO-1+NDIM*(NTB-1)+I)  
          M(I)=ZR(JTABCO-1+NDIM*(NTM-1)+I)  
 101    CONTINUE

C     BLINDAGE PARTIEL : FISSURE RENTRANTE SUR UNE ARETE
        IF (LSNA*LSNM.LT.0 .AND. LSNB*LSNM.LT.0) THEN
          CALL U2MESS('F','XFEM_63')
        ENDIF

C       LONGUEUR DE L'ARETE
        LONGAR=PADIST(NDIM,A,B)

C DEBUT RECHERCHE COORDONNEES DES POINTS D'INTERSECTION
C UN SEUL POINT INTER NON NOEUD PAR ARETE!

C       SI LA FISSURE COUPE L'ARETE
        IF ((LSNA*LSNB).LE.0) THEN

C         SI LA FISSURE COUPE L'EXTREMITE A
          IF (LSNA.EQ.0) THEN
C           ON AJOUTE A LA LISTE LE POINT A
            CALL XAJPIN(JPTINT,PTMAX,IPI,INS,A,LONGAR,
     &                                      JAINT,0,NA,0.D0)
          END IF

C         SI LA FISSURE COUPE L'EXTREMITE B
          IF (LSNB.EQ.0) THEN
C           ON AJOUTE A LA LISTE LE POINT B
            CALL XAJPIN(JPTINT,PTMAX,IPI,INS,B,LONGAR,
     &                                    JAINT,0,NB,LONGAR)
          END IF

C         SI LA FISSURE COUPE LE MILIEU M
C         PETITE TOLERANCE SUR LSNM CAR VALEUR CALCULEE
          D=999.D0
          IF (ABS(LSNM-LSNB).GT.R8PREM() ) D=LSNM/(LSNM-LSNB)

          IF (ABS(LSNM-LSNB).LE.R8PREM().OR.ABS(D).LE.CRILSN) THEN
            ALPHA=PADIST(NDIM,A,M)

            IF (CUT) THEN
              CALL XAJPIN(JPTINT,PTMAX,IPI,INM,M,LONGAR,
     &                                     JAINT,IA,0,ALPHA)
            ELSEIF (.NOT.CUT) THEN
              CALL XAJPIN(JPTINT,PTMAX,IPI,INM,M,LONGAR,
     &                                     JAINT,0,NM,ALPHA)
            ENDIF
          END IF

C         SI LA FISSURE COUPE AILLEURS
          IF (LSNA.NE.0.AND.LSNB.NE.0.AND.LSNM.NE.0) THEN 
C           INTERPOLATION DES COORDONNEES DE C
            CALL XINTAR(TYPMA,NDIM,IA,JTABCO,JTABLS,C)
C           POSITION DU PT D'INTERSECTION SUR L'ARETE
            ALPHA=PADIST(NDIM,A,C)
C           ON AJOUTE A LA LISTE LE POINT C
            CALL XAJPIN(JPTINT,PTMAX,IPI,IBID,C,LONGAR,
     &                                      JAINT,IA,0,ALPHA)
          END IF  

        END IF

 100  CONTINUE

C       NB DE POINTS D'INTERSECTION
        NINTER=IPI
C       NB DE POINTS D'INTERSECTION = NOEUD SOMMET
        NPTS  =INS
C       NB DE POINTS D'INTERSECTION = NOEUD MILIEU
        NPTM  =INM

C     TRI DES POINTS D'INTERSECTION PAR ORDRE CROISSANT DES ARETES 
      DO 200 PD=1,NINTER-1
         PP=PD
         DO 201 I=PP,NINTER
           IF (ZR(JAINT-1+ZXAIN*(I-1)+1).LT.ZR(JAINT-1+ZXAIN*(PP-1)+1))
     &       PP=I
 201     CONTINUE
         DO 202 K=1,4
          TAMPOR(K)=ZR(JAINT-1+ZXAIN*(PP-1)+K)
          ZR(JAINT-1+ZXAIN*(PP-1)+K)=ZR(JAINT-1+ZXAIN*(PD-1)+K)
          ZR(JAINT-1+ZXAIN*(PD-1)+K)=TAMPOR(K)
 202     CONTINUE
         DO 203 K=1,NDIM
           TAMPOR(K)=ZR(JPTINT-1+NDIM*(PP-1)+K)
           ZR(JPTINT-1+NDIM*(PP-1)+K)=ZR(JPTINT-1+NDIM*(PD-1)+K)
           ZR(JPTINT-1+NDIM*(PD-1)+K)=TAMPOR(K)
 203     CONTINUE
 200   CONTINUE

C DEBUT RECHERCHE COORDONNEES DES POINTS MILIEUX
      IF (.NOT.CUT) GOTO 999

C ON NE DEVRAIT AVOIR QUE
C       TR6    NINTER=2 NPTS=0         ---> NSE=3
C       TR6    NINTER=2 NPTS=1         ---> NSE=2
C       TR6    NINTER=3 NPTS=1 NPTM=1  ---> NSE=3
C       SE3    NINTER=1 NPTS=0         ---> NSE=2
      CALL XERFIS(NDIME,NINTER,NPTS,NPTM)

C     COMPTEUR DES POINTS INTERSECTION NON CONFONDUS AVEC ND SOMMET
      IP=0
C     COMPTEUR DE POINTS MILIEUX
      IPM=0
      INM=0
C     CALCUL D'UNE LONGUEUR CARACTERISTIQUE DE L'ELEMENT
      CALL LONCAR(NDIM,TYPMA,ZR(JTABCO),LONREF)

      DO 300 R = 1, NINTER
        A2=NINT(ZR(JAINT-1+ZXAIN*(R-1)+1))
        IF (A2.NE.0) THEN
          IP = IP+1
          NM=AR(A2,3)
          IF (IP.EQ.1) THEN
            CALL INFOAR(NDIM,AR,A2,1,JTABCO,JTABLS,A,B,M,
     &                                             RBID,RBID,RBID)
          ELSEIF(IP.GT.1)THEN
            DO 301 I=1,2
              DO 302 J=1,2
                IF (AR(A2,I).EQ.AR(A1,J)) THEN
                  CALL INFOAR(NDIM,AR,A2,I,JTABCO,JTABLS,A,B,M,
     &                                            RBID,RBID,RBID)
                  TABDIR(1)=AR(A2,I)
                  TABDIR(2)=AR(A1,3-J)
                  TABDIR(3)=AR(A2,3-I)
                  TABDIR(4)=AR(6-(A1+A2),3)
                  IF (J.EQ.2) THEN
                    DO 304 K=1,NDIM
                      TEMP(K)=ZR(JPTMIL-1+(IP-2)*NDIM+K)
                      ZR(JPTMIL-1+(IP-2)*NDIM+K)=
     &                                  ZR(JPTMIL-1+(IP-1)*NDIM+K)
                      ZR(JPTMIL-1+(IP-1)*NDIM+K)=TEMP(K) 
 304                CONTINUE
                  END IF
                END IF
 302          CONTINUE
 301        CONTINUE
          END IF

          CALL VECINI(NDIM,0.D0,MILARA)
          CALL VECINI(NDIM,0.D0,MILARB)
          IF(NM.EQ.NX) THEN
            DO 306 K=1,NDIM  
              MILARA(K)=(ZR(JPTINT-1+NDIM*(R-1)+K)+B(K))/2
              MILARB(K)=(ZR(JPTINT-1+NDIM*(R-1)+K)+A(K))/2
 306        CONTINUE
          ELSE
          TABAR='&&XDECQU.TABAR'
C         TABLEAU DES COORDONNEES DES NOEUDS DE L'ARETE AB : B,A,E
            CALL WKVECT(TABAR,'V V R',3*NDIM,JTABAR)
            DO 307 I=1,NDIM
              ZR(JTABAR-1+I)=B(I)
              ZR(JTABAR-1+NDIM+I)=A(I)
              ZR(JTABAR-1+2*NDIM+I)=M(I)
 307        CONTINUE
C           INTERPOLATION DES COORDONNEES DES POINTS MILIEUX MA ET MB
            CALL XMILAR(NDIM,JPTINT,JTABAR,R,MILARA,MILARB)    
            CALL JEDETR(TABAR)
          END IF

C         STOCKAGE PMILIE
          CALL XAJPMI(JPTMIL,PMMAX(NDIM),IPM,INM,MILARA,LONREF)
          CALL XAJPMI(JPTMIL,PMMAX(NDIM),IPM,INM,MILARB,LONREF)

          IF (R.EQ.NINTER .AND. R.GT.1) THEN
            IPP=R-1
C           CALCUL DU POINT MILIEU DE 101-102
            CALL XMILFI(TYPMA,NDIM,NNO,JPTINT,JTABCO,JTABLS,
     &                                              IPP,R,MILFI)
            MFIS=MFIS+1
C           STOCKAGE PMILIE
            CALL XAJPMI(JPTMIL,PMMAX(NDIM),IPM,INM,MILFI,LONREF)
          END IF
        
          IF (IP.EQ.2) THEN
C           CALCUL DU POINT MILIEU DE 101-C
            CALL XMILAC(NDIM,IGEOM,JPTINT,
     &               JTABCO,TABDIR,JGRLSN,IP,R,JPTMIL,MILAC)

C           STOCKAGE PMILIE
            CALL XAJPMI(JPTMIL,PMMAX(NDIM),IPM,INM,MILAC,LONREF)
          END IF

          A1=A2

        END IF
 300  CONTINUE

C     NB DE POINTS MILIEUX
      NMILIE = IPM

 999  CONTINUE

      CALL JEDETR(TABCO)
      CALL JEDETR(TABLS)

      CALL JEDEMA()
      END
