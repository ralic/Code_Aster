      SUBROUTINE CFPPPG(RESOCO,NDIM  ,NEQ   ,NESMAX,NBLIAC,
     &                  GLIMIN,GLIMAX)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 09/11/2012   AUTEUR DELMAS J.DELMAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C
      IMPLICIT NONE
      INCLUDE 'jeveux.h'

      CHARACTER*24 RESOCO
      REAL*8       GLIMIN,GLIMAX
      INTEGER      NBLIAC,NEQ,NESMAX,NDIM
C
C ----------------------------------------------------------------------
C
C ROUTINE CONTACT (METHODES DISCRETES - RESOLUTION)
C
C DETERMINATION DU PLUS GRAND ET DU PLUS PETIT GLISSEMENT TANGENT
C
C ----------------------------------------------------------------------
C
C
C IN  RESOCO : SD DE TRAITEMENT NUMERIQUE DU CONTACT
C IN  NEQ    : NOMBRE D'EQUATIONS
C IN  NDIM   : DIMENSION DU PROBLEME
C IN  NESMAX : NOMBRE MAX DE NOEUDS ESCLAVES
C IN  NBLIAC : NOMBRE DE LIAISONS ACTIVES
C OUT GLIMIN : GLISSEMENT MINIMUM
C OUT GLIMAX : GLISSEMENT MAXIMUM
C
C
C
C
      REAL*8       R8MAEM,R8PREM
      INTEGER      ILIAI,ILIAC
      REAL*8       JEXOLD,JEYOLD,JEXNEW,JEYNEW,JEXINC,JEYINC
      CHARACTER*24 APDDL,APPOIN,APCOFR
      INTEGER      JAPDDL,JAPPTR,JAPCOF
      CHARACTER*19 LIAC
      INTEGER      JLIAC
      CHARACTER*24 JEUITE
      INTEGER      JJEUIT
      CHARACTER*19 DEPLC
      INTEGER      JDEPC
      INTEGER      NBDDL,JDECAL
      REAL*8       GLIS
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
C
C --- INITIALISATIONS
C
      GLIMAX = 0.D0
      GLIMIN = R8MAEM()
C
C --- LECTURE DES STRUCTURES DE DONNEES DE CONTACT
C
      APPOIN = RESOCO(1:14)//'.APPOIN'
      APCOFR = RESOCO(1:14)//'.APCOFR'
      APDDL  = RESOCO(1:14)//'.APDDL'
      LIAC   = RESOCO(1:14)//'.LIAC'
      CALL JEVEUO(APPOIN,'L',JAPPTR)
      CALL JEVEUO(APCOFR,'L',JAPCOF)
      CALL JEVEUO(APDDL ,'L',JAPDDL)
      CALL JEVEUO(LIAC  ,'L',JLIAC )
      JEUITE = RESOCO(1:14)//'.JEUITE'
      CALL JEVEUO(JEUITE,'L',JJEUIT)
C
C --- ACCES AUX CHAMPS DE TRAVAIL
C --- DEPLC : INCREMENT DE DEPLACEMENT CUMULE DEPUIS DEBUT
C ---         DU PAS DE TEMPS AVEC CORRECTION DU CONTACT
C
      DEPLC  = RESOCO(1:14)//'.DEPC'
      CALL JEVEUO(DEPLC (1:19)//'.VALE','L',JDEPC)
C
C --- CALCUL DES GLISSEMENTS
C
      DO 10 ILIAC = 1,NBLIAC
C
C ----- REPERAGE DE LA LIAISON
C
        ILIAI  = ZI(JLIAC-1+ILIAC)
        JDECAL = ZI(JAPPTR+ILIAI-1)
        NBDDL  = ZI(JAPPTR+ILIAI) - ZI(JAPPTR+ILIAI-1)
C
C ----- JEUX TANGENTS AVANT ITERATION DE NEWTON
C
        JEXOLD = ZR(JJEUIT+3*(ILIAI-1)+2-1)
        JEYOLD = ZR(JJEUIT+3*(ILIAI-1)+3-1)
C
C ----- INCREMENT DES JEUX TANGENTS
C
        JEXINC = 0.D0
        JEYINC = 0.D0
        CALL CALADU(NEQ,NBDDL,ZR(JAPCOF+JDECAL),
     &              ZI(JAPDDL+JDECAL),ZR(JDEPC),JEXINC)
        IF (NDIM.EQ.3) THEN
          CALL CALADU(NEQ,NBDDL,ZR(JAPCOF+JDECAL+30*NESMAX),
     &                ZI(JAPDDL+JDECAL),ZR(JDEPC),JEYINC)
        END IF
C
C ----- CALCUL DES JEUX TANGENTS
C
        JEXNEW = JEXOLD - JEXINC
        JEYNEW = JEYOLD - JEYINC
        GLIS   = SQRT(JEXNEW**2+JEYNEW**2)
        GLIMAX = MAX(GLIMAX,GLIS)
        GLIMIN = MIN(GLIMIN,GLIS)
  10  CONTINUE
      GLIMIN = MAX(GLIMIN,R8PREM())
C
      CALL JEDEMA()
C
      END
