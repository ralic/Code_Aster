      REAL*8 FUNCTION LCROY2 (YMIN)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 29/09/2006   AUTEUR VABHHTS J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE ADBHHVV V.CANO
      IMPLICIT NONE
      REAL*8   YMIN

C *********************************************************************
C *        INTEGRATION DE LA LOI DE ROUSSELIER LOCAL                  *
C *    RESOLUTION DE SEUIL(Y)=0 QUAND S(0)<0 AVEC S(YMIN)=0           *
C *    PAR UNE METHODE DE NEWTON AVEC BORNES CONTROLEES ET DICHOTOMIE *
C *     - LA BORNE INFERIEURE EST YINF=YMIN                           *
C *     - LA BORNE SUPERIEURE EST TELLE QUE:                          *
C *       YSUP*EXP(YSUP) =2*K*FONC*EQTR/3*SIG1                        *
C *********************************************************************

C IN  YMIN   : MINORANT DE LA SOLUTION ( S(YM)=0 )
C ----------------------------------------------------------------------
C  COMMON SIMO - MIEHE

      INTEGER IND1(6),IND2(6)
      REAL*8  KR(6),RAC2,RC(6)
      REAL*8  LAMBDA,MU,DEUXMU,UNK,TROISK,COTHER
      REAL*8  JM,DJ,JP,DJDF(3,3)
      REAL*8  BEM(6),ETR(6),DVETR(6),EQETR,TRETR,DETRDF(6,3,3)
      REAL*8  SIGMA(6),DSIGDE(6,6),DSIGDJ(6)
      COMMON /LCSMC/
     &          IND1,IND2,KR,RAC2,RC,
     &          LAMBDA,MU,DEUXMU,UNK,TROISK,COTHER,
     &          JM,DJ,JP,DJDF,
     &          BEM,ETR,DVETR,EQETR,TRETR,DETRDF,
     &          SIGMA,DSIGDE,DSIGDJ
C ----------------------------------------------------------------------
C  COMMON LOI DE COMPORTEMENT ROUSSELIER

      INTEGER ITEMAX, JPROLP, JVALEP, NBVALP
      REAL*8  PREC,YOUNG,NU,ALPHA,SIGY,SIG1,ROUSD,F0,FCR,ACCE
      REAL*8  PM,RPM,FONC,FCD,DFCDDJ
      COMMON /LCROU/ PREC,YOUNG,NU,ALPHA,SIGY,SIG1,ROUSD,F0,FCR,ACCE,
     &               PM,RPM,FONC,FCD,DFCDDJ,
     &               ITEMAX, JPROLP, JVALEP, NBVALP
C ----------------------------------------------------------------------
      INTEGER ITER
      REAL*8  S,SEUIL, DSEUIL,G,DG
      REAL*8  Y, DP, YINF, YSUP
      REAL*8  LCROTY

C 1 - CALCUL DES BORNES
C     LCROTY RESOUD UNE EQUATION DU TYPE Y*EXP(Y)=CONSTANTE

      YINF = YMIN
      YSUP = LCROTY(2*UNK*FONC*EQETR/(3.D0*SIG1), PREC, ITEMAX)

C 2 - RESOLUTION PAR UNE METHODE DE NEWTON ENTRE LES BORNES

      Y = YSUP
      DO 10 ITER = 1, ITEMAX

        CALL LCROFG(Y, DP, S, SEUIL, DSEUIL)
        G = SEUIL
        DG = DSEUIL
        IF (G.GE.0) YINF = Y
        IF (G.LE.0) YSUP = Y

        IF ( ABS(SEUIL)/SIGY .LE. PREC ) GOTO 100

        Y = Y - G/DG
        IF (Y.LE.YINF .OR. Y.GE.YSUP)  Y=(YINF+YSUP)/2

 10   CONTINUE
      CALL U2MESS('F','ALGORITH3_55')

 100  CONTINUE
      LCROY2 = Y
      END
