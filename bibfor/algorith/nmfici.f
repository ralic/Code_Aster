      SUBROUTINE NMFICI(NNO,NDDL,WREF,VFF,DFDE,GEOM,POIDS,B)

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 26/04/2011   AUTEUR COURTOIS M.COURTOIS 
C TOLE CRS_1404
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C ======================================================================
C COPYRIGHT (C) 2007 NECS - BRUNO ZUBER   WWW.NECS.FR
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================

      IMPLICIT NONE
      INTEGER NNO,NDDL
      REAL*8  WREF,VFF(NNO),DFDE(2,NNO),GEOM(3,NDDL/3)
      REAL*8  POIDS,B(3,3,NDDL/3)
C-----------------------------------------------------------------------
C  MATRICE CINEMATIQUE POUR LES JOINTS 3D (EN UN POINT DE GAUSS DONNE)
C  ATTENTION : ON UTILISE LES FONCTIONS DE FORME DES FACE DU JOINT 3D,
C              PAR EXEMPLE FONCTION DE FORME DU QUAD8 POUR LES HEXA20.
C-----------------------------------------------------------------------
C IN  NNO  NOMBRE DE NOEUDS DE LA FACE (2*NNO POUR TOUT L'ELEM LINEAIRE)
C IN  NDDL   NOMBRE DE DEGRES DE LIBERTE EN DEPL TOTAL (3 PAR NOEUDS)
C IN  WREF   POIDS DE REFERENCE DU POINT DE GAUSS
C IN  VFF    VALEUR DES FONCTIONS DE FORME (DE LA FACE)
C            VVF A POUR DIM NNO, Y COMPRIS EN QUADRATIQUE
C IN  DFDE   DERIVEE DES FONCTIONS DE FORME (DE LA FACE)
C IN  GEOM   COORDONNEES DES NOEUDS
C OUT POIDS  POIDS REEL DU POINT DE GAUSS (AVEC DISTORSION)
C OUT B      MATRICE DE PASSAGE UNODAL -> SAUT DE U LOCAL
C-----------------------------------------------------------------------
      INTEGER N
      REAL*8 COVA(3,3),METR(2,2),JAC,R(3,3),NOA1
C-----------------------------------------------------------------------


C    CALCUL DE LA BASE COVARIANTE LOCALE ET JACOBIEN
      CALL SUBACO(NNO,DFDE,GEOM,COVA)
      CALL SUMETR(COVA,METR,JAC)
      POIDS = WREF*JAC

C    CALCUL DE LA BASE ORTHONORMEE LOCALE
      NOA1 = SQRT(COVA(1,1)**2 + COVA(2,1)**2 + COVA(3,1)**2)
      R(1,1) = COVA(1,3)
      R(1,2) = COVA(2,3)
      R(1,3) = COVA(3,3)
      R(2,1) = COVA(1,1)/NOA1
      R(2,2) = COVA(2,1)/NOA1
      R(2,3) = COVA(3,1)/NOA1
      R(3,1) = R(1,2)*R(2,3) - R(1,3)*R(2,2)
      R(3,2) = R(1,3)*R(2,1) - R(1,1)*R(2,3)
      R(3,3) = R(1,1)*R(2,2) - R(1,2)*R(2,1)


C    CONSTRUCTION DE LA MATRICE B

        DO 10 N = 1,NNO
          CALL DCOPY(9,R,1,B(1,1,N),1)
          CALL DSCAL(9,-VFF(N),B(1,1,N),1)
          CALL DCOPY(9,R,1,B(1,1,N+NNO),1)
          CALL DSCAL(9,VFF(N),B(1,1,N+NNO),1)
 10     CONTINUE

      END
