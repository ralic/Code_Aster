      SUBROUTINE CALCDP(CRIT,SEUIL,DT,RPRIM,MUTRBE,
     &                  SIGM0,EPSI0,COEFM,DP,IRET)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 26/04/2011   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
      IMPLICIT NONE
      REAL*8       SEUIL,DT,RPRIM,MUTRBE,CRIT(3)
      REAL*8       SIGM0,EPSI0,COEFM,DP
C-------------------------------------------------------------
C CALCUL DE DP PAR RESOLUTION DE L'EQUATION SCALAIRE FPRIM=0
C FPRIM=F1-F(I) TEL QUE F1 : DROITE DECROISSANTE
C                       F(I): SIGM0*ARGSH((DP/DT/EPSI0)^(1/M))
C ------------------------------------------------------------
C IN CRIT  : CRITERES DE CONVERGENCE LOCAUX
C IN SEUIL : VALEUR DE LA FONCTION SEUIL F
C IN DT    : PAS DE TEMPS
C IN RPRIM : ECROUISSAGE
C IN MUTRBE : MU * TR (Bel)
C IN SIGM0,EPSI0,COEFM : PARAMETRE LOI VISQUEUSE ROUSS_VISC
C OUT DP    : INCREMENT VISCOPLASTIQUE
C OUT IRET    : CODE RETOUR DE LA DETERMINATION DE DP
C               IRET=0 => PAS DE PROBLEME
C               IRET=1 => ABSENCE DE CONVERGENCE
C-------------------------------------------------------------
      REAL*8     FPRIM, FPLAS, DFPRIM,DPNEW,FPNEW,TEST,ARG
      REAL*8      R0, DPPLAS,DPMIN,DPMAX ,X(4),Y(4)
      INTEGER    ITER , IRET

C INITIALISATION
      R0  = MUTRBE + RPRIM

C ESSAI PLASTIQUE PUR (DP = DPMAX)

      DPPLAS = SEUIL/R0
      DP     = DPPLAS
C    CALCUL DU SEUIL
      CALL CALCFP(MUTRBE,RPRIM,SEUIL,DT,DP,SIGM0,EPSI0,COEFM,
     &            FPLAS,FPRIM,DFPRIM)
      IF (ABS(FPRIM) / (1.D0+SEUIL) .LT. CRIT(3) ) GOTO 9999

      DPMAX = DPPLAS
      X(1) = DP
      Y(1) = FPRIM

C  RESOLUTION DE FPRIM=0 - METHODE SECANTE AVEC NEWTON SI BESOIN

C------EXAMEN DE LA SOLUTION DP=DPMIN
      DPMIN = 0.D0
      DP =DPMIN
      CALL CALCFP(MUTRBE,RPRIM,SEUIL,DT,DP,SIGM0,EPSI0,COEFM,
     &              FPLAS,FPRIM,DFPRIM)
      X(2) = DP
      Y(2) = FPRIM

C     AMELIORATION DES BORNES PAR ESTMATION DE DP^VP
      ARG = (DPMAX/DT/EPSI0)**(1.D0/COEFM)
      TEST = SIGM0 * LOG(ARG+SQRT(ARG**2+1))

      IF (FPLAS.LT.TEST) THEN
        DP = DT*EPSI0* (SINH(FPLAS/SIGM0) )**COEFM
        IF ((DP .GE. DPMIN) .AND. (DP .LE. DPMAX)) THEN
          CALL CALCFP(MUTRBE,RPRIM,SEUIL,DT,DP,SIGM0,EPSI0,COEFM,
     &                FPLAS,FPRIM,DFPRIM)
          IF (ABS(FPRIM)/(1.D0+SEUIL) .LT. CRIT(3) )  GOTO 9999
          IF (FPRIM.LT.0) THEN
           X(1) = DP
           Y(1) = FPRIM
          ELSE
            X(2) = DP
            Y(2) = FPRIM
          ENDIF
        ENDIF
      ENDIF

C------CALCUL DE DP : EQUATION SCALAIRE FPRIM = 0 AVEC DPMIN< DP < DPMAX
      X(3) = X(1)
      Y(3) = Y(1)
      X(4) = X(2)
      Y(4) = Y(2)
      DO 100 ITER = 1,INT(CRIT(1))

      CALL ZEROCO(X,Y)
      DP = X(4)
      CALL CALCFP(MUTRBE,RPRIM,SEUIL,DT,DP,SIGM0,EPSI0,COEFM,
     &              FPLAS,FPRIM,DFPRIM)
      IF (ABS(FPRIM)/(1.D0+SEUIL) .LT. CRIT(3) ) GOTO 9999

      DPNEW=DP-FPRIM/DFPRIM
      IF ((DPNEW .GE. DPMIN) .AND. (DPNEW .LE. DPMAX)) THEN
         CALL CALCFP(MUTRBE,RPRIM,SEUIL,DT,DPNEW,SIGM0,EPSI0,COEFM,
     &                FPLAS,FPNEW,DFPRIM)

          IF (ABS(FPNEW)/(1.D0+SEUIL) .LT. CRIT(3) ) THEN
            DP=DPNEW
            GOTO 9999
          ENDIF
          IF (ABS(FPNEW)/(1.D0+SEUIL) .LT. ABS(FPRIM)/(1.D0+SEUIL))
     &      THEN
            DP=DPNEW
            FPRIM = FPNEW
          ENDIF

        ENDIF
        Y(4)=FPRIM
        X(4)=DP
  100   CONTINUE

        IRET = 1

 9999 CONTINUE

       END
