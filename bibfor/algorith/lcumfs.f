      SUBROUTINE LCUMFS(VARI,NVARI,CMAT,NMAT,IFLU,
     &                   ISPH,TDT,HINI,HFIN,
     &                   AFPS,BFPS,CFPS)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 08/03/2004   AUTEUR REZETTE C.REZETTE 
C ======================================================================
C COPYRIGHT (C) 1991 - 2002  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE YLEPAPE Y.LEPAPE
C
C ROUTINE APPELE DANS LCUMSD
C LCUMFS     SOURCE    BENBOU   01/03/26 
C
C_______________________________________________________________________
C
C ROUTINE QUI CALCUL LES MATRICES DE DEFORMATION 
C   DE FLUAGE PROPRE SPHERIQUE
C   D APRES LE MODELE UMLV
C
C     IFLU = O => SORTIE DES VALEURS TOTALES
C     IFLU = 1 => SORTIE DES VALEURS REVERSIBLES
C     IFLU = 2 => SORTIE DES VALEURS IRREVERSIBLES
C
C
C
C   DFPROSPH(N+1) = AFPS + BFPS * SIGMA(N) + CFPS * SIGMA(N+1)
C
C IN  VARI     : VARIABLES INTERNES INITIALES
C IN  NVARI    : DIMENSION DES VECTEURS VARIABLES INTERNES
C IN  CMAT     : VECTEUR DE PARAMETRES (MATERIAU ET AUTRE)
C IN  NMAT     : DIMENSION DE CMAT
C IN  IFLU     : CF COMMENTAIRES CI-DESSUS
C IN  ISPH     : MODE DE CALCUL DE LA PARTIE SPHERIQUE
C IN  TDT      : PAS DE TEMPS
C IN  HINI     : HUMIDITE INITIALE
C IN  HFIN     : HUMIDITE FINALE
C OUT AFPS     : CF. EQUATION CI-DESSUS
C OUT BFPS     : IDEM
C OUT CFPS     : IDEM
C_______________________________________________________________________
C
      IMPLICIT REAL*8(A-H,O-Z)
      INTEGER          NVARI,NMAT
      INTEGER          IFLU,IFPO,ISPH
      REAL*8 AFPS, BFPS, CFPS
      REAL*8 VARI(NVARI),CMAT(NMAT)
      REAL*8 AMBDA1,AMBDA2
      REAL*8 ASI,ASR,BSI,BSR,CSI,CSR,DENO,DH,EISP,ERSP
      REAL*8 HFIN,HINI,HVFIN,HVINI         
      REAL*8 RISP,RRSP,TDT,TSEXP,TSPH,VAR1
      REAL*8 VAR2,VRSP,X1,X2
C
C RECUPERATION DES VALEURS DES PARAMETRES MATERIAU
C
      RRSP = CMAT(3)
      VRSP = CMAT(4)
      RISP = CMAT(5)
C     VISP = CMAT(6)
      IFPO = CMAT(13)
C
      HVINI = HINI
      HVFIN = HFIN
C
C TEST SI L HUMIDITE RELATIVE DOIT ETRE PRISE EN COMPTE OU NON
C
      IF (IFPO.EQ.1) THEN
        HINI = 1.D0
        HFIN = 1.D0
      ENDIF 
C
      DH = HFIN - HINI
C
C TEST SI TDT = 0
C
        AFPS = 0.D0
        BFPS = 0.D0
        CFPS = 0.D0
      IF (TDT.EQ.0.D0) THEN
        AFPS = 0.D0
        BFPS = 0.D0
        CFPS = 0.D0
        GOTO 10
      ENDIF
C
C RECUPERATION DES VARIABLES INTERNES INITIALES
C
      ERSP = VARI(1)
      EISP = VARI(2)
C
      IF (ISPH.EQ.0) THEN
C 
C LA DEFORMATION DE FLUAGE PROPRE IRREVERSIBLE N EST PAS PRIS EN COMPTE
C
C
C    CONSTRUCTION DE LA MATRICE SPHERIQUE REVERSIBLE
C
C          => EQUATION (3.6-4) ET (3.6-5)
C
        TSPH = VRSP / RRSP
        TSEXP = EXP(-TDT/TSPH)
        ASR = (TSEXP - 1.D0) * ERSP
        BSR = 1.D0/RRSP*( TSEXP*(-HINI*(2*TSPH/TDT+1.D0)
     $        + HFIN*TSPH/TDT)
     $        + HINI*(2.D0*(TSPH-TDT)/TDT+1.D0) - HFIN*(TSPH-TDT)/TDT )
        CSR = HINI/(TDT*RRSP)*( TSPH*TSEXP - (TSPH - TDT) )
C
C    CONSTRUCTION DE LA MATRICE SPHERIQUE IRREVERSIBLE
C
C          => EQUATION (3.6-6)
C
        ASI = 0.D0
        BSI = 0.D0
        CSI = 0.D0
C
      ELSE
C
C LA DEFORMATION DE FLUAGE PROPRE IRREVERSIBLE EST PRISE EN COMPTE
C
C    CONSTRUCTION DE LA MATRICE SPHERIQUE REVERSIBLE
C
C          => EQUATION (3.6-8) ET (3.6-9)
C
C      CALCUL DE PARAMETRES MATERIAUX
C
        CALL LCUMP1(CMAT,NMAT,AMBDA1,AMBDA2,X1,X2)
        DENO = X1 * X2 - 1.D0
C
        IF (DENO.EQ.0.D0) THEN
          CALL UTMESS ('F','LCUMFP','DIVISION PAR ZERO DANS LCUMFS')
        ENDIF
C
        VAR1 = (X1*X2*EXP(AMBDA1*TDT) - EXP(AMBDA2*TDT))/DENO - 1.D0
        VAR2 = (X1*( EXP(AMBDA1*TDT) - EXP(AMBDA2*TDT) ))/DENO
C
        ASR = VAR1 * ERSP - VAR2 * EISP
        BSR = DH* ( -VAR1/RRSP + VAR2/RISP )
        CSR = HINI* ( -VAR1/RRSP + VAR2/RISP )
C
C    CONSTRUCTION DE LA MATRICE SPHERIQUE IRREVERSIBLE
C
C          => EQUATION (3.6-8) ET (3.6-10)
C
C
        VAR1 = X2 * ( EXP(AMBDA1*TDT) - EXP(AMBDA2*TDT) )/DENO
        VAR2 = ( EXP(AMBDA1*TDT) - X1*X2*EXP(AMBDA2*TDT))/DENO + 1.D0
C
        ASI = VAR1 * ERSP - VAR2 * EISP
        BSI = DH* ( -VAR1/RRSP + VAR2/RISP )
        CSI = HINI* ( -VAR1/RRSP + VAR2/RISP )
      ENDIF
C
C  CONSTRUCTION DE LA MATRICE TOTAL :
C
C  * PARTIE REVERSIBLE   : ASR  BSR  CSR  (IFLU = 0)
C        => EQUATION (3.6-11)
C             +
C  * PARTIE IRREVERSIBLE : ASI  BSI  CSI  (IFLU = 1)
C        => EQUATION (3.6-12)
C             =
C  * TOTAL               : AFPS BFPS CFPS (IFLU = 2)
C        => EQUATION (3.6-13)
C
      IF (IFLU .EQ. 0) THEN
        AFPS = ASR + ASI
        BFPS = BSR + BSI
        CFPS = CSR + CSI
      ELSEIF (IFLU.EQ.1) THEN
        AFPS = ASR
        BFPS = BSR
        CFPS = CSR
      ELSEIF (IFLU.EQ.2) THEN
        AFPS = ASI
        BFPS = BSI
        CFPS = CSI
      ENDIF
C
10    CONTINUE
C
      HINI = HVINI
      HFIN = HVFIN
C
      END
