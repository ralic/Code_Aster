      SUBROUTINE MMMREG(NOMA  ,DEFICO,RESOCO,DEPCN ,NDD1  ,
     &                  GLIE  ,GLIM  )
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 14/09/2010   AUTEUR ABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2010  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT     NONE
      CHARACTER*8  NOMA
      CHARACTER*24 DEFICO,RESOCO
      CHARACTER*24 GLIE,GLIM
      INTEGER      NDD1
C
C ----------------------------------------------------------------------
C
C ROUTINE CONTACT (METHODE CONTINUE - POST-TRAITEMENT)
C
C CALCULER LES GLISSEMENTS
C
C ----------------------------------------------------------------------
C
C
C IN  NOMA   : NOM DU MAILLAGE
C IN  DEFICO : SD DE DEFINITION DU CONTACT
C IN  RESOCO : SD DE TRAITEMENT NUMERIQUE DU CONTACT
C IN  DEPCN  : CHAM_NO_S REDUIT DE L'INCREMENT DE DEPLACEMENT CUMULE
C IN  NDD1   : NOMBRE DE DDL/NOEUD
C OUT GLIE   : GLISSEMENT ESCLAVE
C OUT GLIM   : GLISSEMENT MAITRE
C
C ------------- DEBUT DECLARATIONS NORMALISEES JEVEUX -----------------
C
      CHARACTER*32 JEXATR
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C --------------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ---------------
C      
      CHARACTER*24 TABFIN
      INTEGER      JTABF 
      INTEGER      MMINFI,CFDISI,NDIMG,NZOCO,NTPC
      INTEGER      NNE,NNM,NBMAE,NPTM
      INTEGER      IZONE,IMAE,IPTC,IPTM,I
      INTEGER      NUNO,IBID,JDECME
      INTEGER      POSMAE,NUMMAE,NUMMAM
      INTEGER      ICONEX,ILONG
      REAL*8       KSIPC1,KSIPC2,KSIPR1,KSIPR2
      INTEGER      JGLIE,JGLIM
      INTEGER      CFMMVD,ZTABF
      CHARACTER*19 DEPCN
      INTEGER      JDEPDL,JDEPDE
      REAL*8       DEPLPM(3),DEPLPE(3)
      REAL*8       TAU1(3),TAU2(3),FF(9)
      CHARACTER*8  ALIASE,ALIASM
      LOGICAL      MMINFL,LVERI
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
C
C --- ACCES AU MAILLAGE
C
      CALL JEVEUO(JEXATR(NOMA(1:8)//'.CONNEX','LONCUM'),'L',ILONG)
      CALL JEVEUO(NOMA(1:8)//'.CONNEX','L',ICONEX)
C
C --- LECTURE DES STRUCTURES DE DONNEES DE CONTACT
C
      TABFIN = RESOCO(1:14)//'.TABFIN'
      CALL JEVEUO(TABFIN,'L',JTABF)     
C
      ZTABF  = CFMMVD('ZTABF')          
C
C --- INITIALISATIONS
C
      NDIMG  = CFDISI(DEFICO,'NDIM'  )
      NZOCO  = CFDISI(DEFICO,'NZOCO' ) 
      NTPC   = CFDISI(DEFICO,'NTPC'  ) 
C
C --- ACCES AU CHAM_NO_S POUR LES DEPLACEMENTS/LAGRANGES
C
      CALL JEVEUO(DEPCN(1:19)//'.CNSV','L',JDEPDE)
      CALL JEVEUO(DEPCN(1:19)//'.CNSL','L',JDEPDL)      
C
C --- CREATION D OBJETS DE TRAVAIL
C      * VECTEUR DE GLISSEMENT DU NOEUD ESCLAVE
C      * VECTEUR DE GLISSEMENT DU NOEUD MAITRE
C
      CALL WKVECT(GLIE  ,'V V R',2*NTPC,JGLIE )
      CALL WKVECT(GLIM  ,'V V R',2*NTPC,JGLIM )
C
C --- CALCUL DU JEU AUX NOEUDS EN PRENANT LE MIN DES JEUX AUX NOEUDS,
C --- DU GLISSEMENT EN PRENANT LE MAX DU GLISSEMENT AUX NOEUDS
C --- DE L INDICATEUR DE CONTACT EN CONSIDERANT QU UN NOEUD EST EN
C --- CONTACT A PARTIR DU MOMENT OU IL L EST SUR AU MOINS UNE
C --- DES MAILLES
C
      IPTC   = 1
      DO 10 IZONE = 1,NZOCO
C
C --- OPTIONS SUR LA ZONE DE CONTACT
C       
        LVERI  = MMINFL(DEFICO,'VERIF' ,IZONE )
        NBMAE  = MMINFI(DEFICO,'NBMAE' ,IZONE )
        JDECME = MMINFI(DEFICO,'JDECME',IZONE )
C 
C ----- MODE VERIF: ON SAUTE LES POINTS
C  
        IF (LVERI) THEN
          GOTO 25
        ENDIF
C
C ----- BOUCLE SUR LES MAILLES ESCLAVES
C 
        DO 20 IMAE = 1,NBMAE
C
C ------- POSITION DE LA MAILLE ESCLAVE
C
          POSMAE = JDECME + IMAE                
C
C ------- NOMBRE DE POINTS SUR LA MAILLE ESCLAVE
C            
          CALL MMINFM(POSMAE,DEFICO,'NPTM',NPTM  )        
C
C ------- BOUCLE SUR LES POINTS
C      
          DO 30 IPTM = 1,NPTM  
C
C --------- INFOS
C  
            NUMMAE  = NINT(ZR(JTABF+ZTABF*(IPTC-1)+1)) 
            NUMMAM  = NINT(ZR(JTABF+ZTABF*(IPTC-1)+2)) 
            KSIPR1  = ZR(JTABF+ZTABF*(IPTC-1)+5)
            KSIPR2  = ZR(JTABF+ZTABF*(IPTC-1)+6)
            KSIPC1  = ZR(JTABF+ZTABF*(IPTC-1)+3)
            KSIPC2  = ZR(JTABF+ZTABF*(IPTC-1)+4)            
C
C --------- VECTEURS DIRECTEURS DU PLAN DE CONTACT
C
            TAU1(1) = ZR(JTABF+ZTABF*(IPTC-1)+7)
            TAU1(2) = ZR(JTABF+ZTABF*(IPTC-1)+8)
            TAU1(3) = ZR(JTABF+ZTABF*(IPTC-1)+9)
            TAU2(1) = ZR(JTABF+ZTABF*(IPTC-1)+10)
            TAU2(2) = ZR(JTABF+ZTABF*(IPTC-1)+11)
            TAU2(3) = ZR(JTABF+ZTABF*(IPTC-1)+12) 
C
C --------- DEPLACEMENT DU NOEUD ESCLAVE DE LA MAILLE ESCLAVE
C
            CALL MMELTY(NOMA  ,NUMMAE,ALIASE,NNE   ,IBID  )
            CALL MMNONF(NDIMG ,NNE   ,ALIASE,KSIPC1,KSIPC2,
     &                  FF    )
            DEPLPE(1) = 0.D0
            DEPLPE(2) = 0.D0
            DEPLPE(3) = 0.D0
C
C --------- DEPLACEMENT DE LA PROJECTION DU NOEUD ESCLAVE 
C --------- SUR LA MAILLE MAITRE POUR LE CALCUL DU GLISSEMENT
C
            DO 33 I = 1,NNE
              NUNO = ZI(ICONEX+ZI(ILONG-1+NUMMAE)+I-2)
              DEPLPE(1) = DEPLPE(1)+ZR(JDEPDE-1+NDD1*(NUNO-1)+1)*FF(I)
              DEPLPE(2) = DEPLPE(2)+ZR(JDEPDE-1+NDD1*(NUNO-1)+2)*FF(I)
              DEPLPE(3) = DEPLPE(3)+ZR(JDEPDE-1+NDD1*(NUNO-1)+3)*FF(I) 
   33       CONTINUE
C
C --------- DEPLACEMENT DU NOEUD MAITRE,
C --------- PROJETE DU NOEUD ESCLAVE SUR LA MAILLE MAITRE
C
            CALL MMELTY(NOMA  ,NUMMAM,ALIASM,NNM   ,IBID  )
            CALL MMNONF(NDIMG ,NNM   ,ALIASM,KSIPR1,KSIPR2,
     &                  FF    )
            DEPLPM(1) = 0.D0
            DEPLPM(2) = 0.D0
            DEPLPM(3) = 0.D0
C
C --------- DEPLACEMENT DE LA PROJECTION DU NOEUD ESCLAVE 
C --------- SUR LA MAILLE MAITRE
C
            DO 40 I = 1,NNM
              NUNO = ZI(ICONEX+ZI(ILONG-1+NUMMAM)+I-2)
              CALL ASSERT(ZL(JDEPDL-1+NDD1*(NUNO-1)+1))
              CALL ASSERT(ZL(JDEPDL-1+NDD1*(NUNO-1)+2))
              DEPLPM(1) = DEPLPM(1)+ZR(JDEPDE-1+NDD1*(NUNO-1)+1)*FF(I)
              DEPLPM(2) = DEPLPM(2)+ZR(JDEPDE-1+NDD1*(NUNO-1)+2)*FF(I)
              IF (NDIMG.EQ.3) THEN
                CALL ASSERT(ZL(JDEPDL-1+NDD1*(NUNO-1)+3))
                DEPLPM(3) = DEPLPM(3)+ZR(JDEPDE-1+NDD1*(NUNO-1)+3)*FF(I)
              ENDIF  
   40       CONTINUE   
C
C --------- ECRITURE DES GLISSEMENTS
C
            IF (NDIMG.EQ.3) THEN
              ZR(JGLIE+2*(IPTC-1))   = DEPLPE(1)*TAU1(1) +
     &                                 DEPLPE(2)*TAU1(2) +
     &                                 DEPLPE(3)*TAU1(3)
              ZR(JGLIE+2*(IPTC-1)+1) = DEPLPE(1)*TAU2(1) +
     &                                 DEPLPE(2)*TAU2(2) +
     &                                 DEPLPE(3)*TAU2(3)
              ZR(JGLIM+2*(IPTC-1))   = DEPLPM(1)*TAU1(1) +
     &                                 DEPLPM(2)*TAU1(2) +
     &                                 DEPLPM(3)*TAU1(3)
              ZR(JGLIM+2*(IPTC-1)+1) = DEPLPM(1)*TAU2(1) +
     &                                 DEPLPM(2)*TAU2(2) +
     &                                 DEPLPM(3)*TAU2(3)
            ELSEIF (NDIMG.EQ.2) THEN
              ZR(JGLIE+IPTC-1) = DEPLPE(1)*TAU1(1) +
     &                           DEPLPE(2)*TAU1(2)
              ZR(JGLIM+IPTC-1) = DEPLPM(1)*TAU1(1) +
     &                           DEPLPM(2)*TAU1(2)            
            ELSE
              CALL ASSERT(.FALSE.)
            ENDIF
C
C --------- LIAISON DE CONTACT SUIVANTE
C
            IPTC   = IPTC + 1 
  30      CONTINUE
  20    CONTINUE
  25    CONTINUE
  10  CONTINUE
C
      CALL JEDEMA()
      END
