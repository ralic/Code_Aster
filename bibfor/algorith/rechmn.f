      SUBROUTINE RECHMN(IZONE,NZOCO,NSYME,REAAPP,REACTU,
     &                  NOMA,NEWGEO,DEFICO,RESOCO,IESCL)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 07/10/2004   AUTEUR MABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2004  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C
      IMPLICIT     NONE
      INTEGER      IZONE
      INTEGER      NZOCO
      INTEGER      NSYME
      INTEGER      REAAPP
      INTEGER      REACTU
      CHARACTER*8  NOMA
      CHARACTER*24 DEFICO
      CHARACTER*24 RESOCO
      CHARACTER*24 NEWGEO
      INTEGER      IESCL
C
C ----------------------------------------------------------------------
C ROUTINE APPELEE PAR : RECHME
C ----------------------------------------------------------------------
C
C RECHERCHE DU NOEUD MAITRE LE PLUS PROCHE DU NOEUD ESCLAVE
C UTILISE DANS L'APPARIEMENT MAITRE/ESCLAVE
C ON NE PREND PAS COMME NOEUDS ESCLAVES CEUX STOCKES
C DANS LE TABLEAU SANSNO ET PROVENANT DES MOTS-CLES SANS_NO ET
C SANS_GROUP_NO.
C
C IN  IZONE  : NUMERO DE LA ZONE DE CONTACT
C IN  NZOCO  : NOMBRE DE ZONES DE CONTACT
C IN  NSYME  : NOMBRE DE ZONES DE CONTACT SYMETRIQUES
C IN  REAAPP : -1 SI PAS DE RECHERCHE MAIS REMPLISSAGE INITIAL DES SD
C              +1 SI ON CHERCHE LE NOEUD LE + PROCHE PAR "BRUTE FORCE"
C              +2 SI ON CHERCHE PAR VOISINAGE   (GRACE AU "PASSE")
C              +3 SI ON CHERCHE AVEC DES BOITES (SANS LE "PASSE")
C IN  REACTU : INDICATEUR DE REACTUALISATION POUR TOUTE LA ZONE
C              DES NORMALES ET DU JEU
C IN  NOMA   : NOM DU MAILLAGE
C IN  NEWGEO : GEOMETRIE ACTUALISEE EN TENANT COMPTE DU CHAMP DE
C              DEPLACEMENTS COURANT
C IN  DEFICO : SD DE DEFINITION DU CONTACT (ISSUE D'AFFE_CHAR_MECA)
C VAR RESOCO : SD DE TRAITEMENT NUMERIQUE DU CONTACT
C I/O IESCL  : NUMERO DU DERNIER NOEUD ESCLAVE CONNU (PEUT DIMINUER SI
C              LE NOEUD ESCLAVE EST EXCLU PAR LE MOT-CLEF SANS_NOEUD OU
C              SANS_GROUP_NO)
C
C -------------- DEBUT DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C ---------------- FIN DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER      ZAPPAR
      PARAMETER    (ZAPPAR=3)
      INTEGER      ZAPMEM
      PARAMETER    (ZAPMEM=4)
      CHARACTER*24 NDIMCO,PZONE,PSURNO,CONTNO
      INTEGER      JDIM,JZONE,JSUNO,JNOCO
      CHARACTER*24 PSANS,SANSNO,APMEMO,APPARI,PMANO
      INTEGER      JCOOR,JPSANS,JSANS,JAPMEM,JAPPAR,JPOMA
      CHARACTER*24 MANOCO,PNOMA,NOMACO
      INTEGER      JMANO,JPONO,JNOMA    
      REAL*8       COORE(3),COORM(3),R8GAEM,DMIN,DIST,PADIST
      INTEGER      NBNOE,NBNOM
      INTEGER      ISURFE,ISURFM,JDECE,JDECM,OLDPOS,JDECMA,JDEC
      INTEGER      NDE,POSNOE,NUMNOE,NDM,POSNOM,NUMNOM,M,KM
      INTEGER      POSMA,NBMA,NBNO
      INTEGER      POSMIN,SUPPOK
      INTEGER      IFM,NIV,NZOCP
C
C ----------------------------------------------------------------------
C
      CALL INFNIV(IFM,NIV)
      CALL JEMARQ()
C
      IF (NIV.GE.2) THEN
       WRITE (IFM,*) '<CONTACT> <> <> APPARIEMENT M/E - RECH. ND MAITRE'
      ENDIF
C
C ======================================================================
C               RECUPERATION D'ADRESSES ET DE DIMENSIONS
C ======================================================================
C
C --- LECTURE DES SD POUR LE CONTACT POTENTIEL
C
      NDIMCO = DEFICO(1:16)//'.NDIMCO'
      PZONE  = DEFICO(1:16)//'.PZONECO'
      CONTNO = DEFICO(1:16)//'.NOEUCO'
      PSURNO = DEFICO(1:16)//'.PSUNOCO'
      SANSNO = DEFICO(1:16)//'.SSNOCO'
      PSANS  = DEFICO(1:16)//'.PSSNOCO'
      MANOCO = DEFICO(1:16)//'.MANOCO'
      PMANO  = DEFICO(1:16)//'.PMANOCO'
      NOMACO = DEFICO(1:16)//'.NOMACO'
      PNOMA  = DEFICO(1:16)//'.PNOMACO'
      APPARI = RESOCO(1:14)//'.APPARI'
      APMEMO = RESOCO(1:14)//'.APMEMO'
C ======================================================================
      CALL JEVEUO(NDIMCO,'E',JDIM  )
      CALL JEVEUO(PZONE, 'L',JZONE )
      CALL JEVEUO(CONTNO,'L',JNOCO )
      CALL JEVEUO(PSURNO,'L',JSUNO )
      CALL JEVEUO(SANSNO,'L',JSANS )
      CALL JEVEUO(PSANS, 'L',JPSANS)
      CALL JEVEUO(MANOCO,'L',JMANO )
      CALL JEVEUO(PMANO, 'L',JPOMA )
      CALL JEVEUO(NOMACO,'L',JNOMA )
      CALL JEVEUO(PNOMA, 'L',JPONO )
      CALL JEVEUO(NEWGEO(1:19)//'.VALE','L',JCOOR)
      CALL JEVEUO(APPARI,'E',JAPPAR)
      CALL JEVEUO(APMEMO,'E',JAPMEM)
C
C --- ISURFE : NUMERO DE LA SURFACE ESCLAVE 
C --- ISURFM : NUMERO DE LA SURFACE MAITRE
C
      ISURFM = ZI(JZONE+IZONE-1) + 1
      ISURFE = ZI(JZONE+IZONE)
C
C --- NOMBRE DE NOEUDS DES SURFACES MAITRE ET ESCLAVE
C
      NBNOE = ZI(JSUNO+ISURFE) - ZI(JSUNO+ISURFE-1)
      NBNOM = ZI(JSUNO+ISURFM) - ZI(JSUNO+ISURFM-1)
C
C --- DECALAGE DANS CONTNO POUR TROUVER LES NOEUDS DES SURFACES
C
      JDECE = ZI(JSUNO+ISURFE-1)
      JDECM = ZI(JSUNO+ISURFM-1)
C
C --- NOMBRE DE NOEUDS ESCLAVES DE LA ZONE _A PRIORI_
C
      ZI(JDIM+8+IZONE) = NBNOE
C
C --- NOMBRE DE NOEUDS PRINCIPALES
C
      NZOCP  = NZOCO - NSYME

C ======================================================================
C --- APPARIEMENT PAR METHODE "BRUTE FORCE" 
C --- DOUBLE BOUCLE SUR LES NOEUDS
C ======================================================================

      IF (REAAPP.EQ.1) THEN

        DO 50 NDE = 1,NBNOE
C
C --- INDICE DANS CONTNO, NUMERO ABSOLU DU NOEUD DE LA SURFACE ESCLAVE
C
          POSNOE   = JDECE + NDE
          NUMNOE   = ZI(JNOCO+POSNOE-1)
C
C --- COORDONNEES ACTUELLES DU NOEUD ESCLAVE
C
          COORE(1) = ZR(JCOOR+3* (NUMNOE-1))
          COORE(2) = ZR(JCOOR+3* (NUMNOE-1)+1)
          COORE(3) = ZR(JCOOR+3* (NUMNOE-1)+2)
C
C --- ON REGARDE SI LE NOEUD EST INTERDIT COMME ESCLAVE
C
          IF (IZONE.LE.NZOCP) THEN
            CALL CFELSN(IZONE,NOMA,NUMNOE,POSNOE,
     &                JAPMEM,JNOCO,JPSANS,JSANS,
     &                SUPPOK)

            IF (SUPPOK.EQ.1) THEN
              ZI(JDIM+8+IZONE) = ZI(JDIM+8+IZONE) - 1
              GOTO 50
            ENDIF
          ENDIF
C
C --- RECHERCHE DU NOEUD MAITRE LE PLUS PROCHE
C
          DMIN     = R8GAEM()
C
          DO 20 NDM = 1,NBNOM
            POSNOM = JDECM + NDM
            NUMNOM = ZI(JNOCO+POSNOM-1)
            ZI(JAPMEM+ZAPMEM* (POSNOM-1)) = 0
            COORM(1) = ZR(JCOOR+3* (NUMNOM-1))
            COORM(2) = ZR(JCOOR+3* (NUMNOM-1)+1)
            COORM(3) = ZR(JCOOR+3* (NUMNOM-1)+2)
            DIST     = PADIST(3,COORE,COORM)
            IF (DIST.LT.DMIN) THEN
              POSMIN = POSNOM
              DMIN   = DIST
            END IF
   20     CONTINUE
C --- NOEUD MAITRE LE PLUS PROCHE:
C --   POSITION: POSMIN
C --   DISTANCE ESCLAVE/MAITRE: DMIN

C
C --- NOEUD ESCLAVE SUIVANT
C
          IESCL = IESCL + 1
C
C --- STOCKAGE DANS APPARI ET APMEMO.
C
          ZI(JAPPAR+ZAPPAR* (IESCL-1)+1)  = POSNOE
          ZI(JAPPAR+ZAPPAR* (IESCL-1)+2)  = -POSMIN
          ZI(JAPPAR+ZAPPAR* (IESCL-1)+3)  = REACTU


          ZI(JAPMEM+ZAPMEM* (POSNOE-1))   = 1
          ZI(JAPMEM+ZAPMEM* (POSNOE-1)+1) = POSMIN
          ZI(JAPMEM+ZAPMEM* (POSNOE-1)+2) = 0
          ZI(JAPMEM+ZAPMEM* (POSNOE-1)+3) = 0

          IF (POSMIN.NE.0) THEN 
            ZI(JAPMEM+ZAPMEM* (POSMIN-1)) = 0
          ENDIF
C
C --- CALL CFJEUM PAR CHMANO
C
   50   CONTINUE
C
C ======================================================================
C --- APPARIEMENT PAR VOISINAGE
C --- ON CHERCHE UN NOEUD CONNECTE A L'ANCIEN NOEUD LE PLUS PROCHE
C ---  PAR UNE MAILLE DE CONTACT
C ======================================================================
C
      ELSE IF (REAAPP.EQ.2) THEN

        DO 110 NDE = 1,NBNOE
C
C --- INDICE DANS CONTNO, NUMERO ABSOLU DU NOEUD DE LA SURFACE ESCLAVE
C
          POSNOE   = JDECE + NDE
          NUMNOE   = ZI(JNOCO+POSNOE-1)
C
C --- COORDONNEES ACTUELLES DU NOEUD ESCLAVE
C
          COORE(1) = ZR(JCOOR+3* (NUMNOE-1))
          COORE(2) = ZR(JCOOR+3* (NUMNOE-1)+1)
          COORE(3) = ZR(JCOOR+3* (NUMNOE-1)+2)
C
C --- ON REGARDE SI LE NOEUD EST INTERDIT COMME ESCLAVE
C
          IF (IZONE.LE.NZOCP) THEN
            CALL CFELSN(IZONE,NOMA,NUMNOE,POSNOE,
     &                JAPMEM,JNOCO,JPSANS,JSANS,
     &                SUPPOK)

            IF (SUPPOK.EQ.1) THEN
              ZI(JDIM+8+IZONE) = ZI(JDIM+8+IZONE) - 1
              GOTO 110
            ENDIF
          ENDIF
C
C --- ANCIEN NOEUD MAITRE LE PLUS PROCHE
C
          OLDPOS = ZI(JAPMEM+ZAPMEM* (POSNOE-1)+1)
C
C --- BOUCLE SUR LES MAILLES CONTENANT CET ANCIEN NOEUD
C
          JDECMA = ZI(JPOMA+OLDPOS-1)
          NBMA   = ZI(JPOMA+OLDPOS) - ZI(JPOMA+OLDPOS-1)
          DMIN   = R8GAEM()

          DO 80 M = 1,NBMA

            POSMA = ZI(JMANO+JDECMA+M-1)
C
C --- BOUCLE SUR LES NOEUDS DE LA MAILLE ET CALCUL DE LA DISTANCE
C
            JDEC = ZI(JPONO+POSMA-1)
            NBNO = ZI(JPONO+POSMA) - ZI(JPONO+POSMA-1)
            DO 70 KM = 1,NBNO
              POSNOM   = ZI(JNOMA+JDEC+KM-1)
              NUMNOM   = ZI(JNOCO+POSNOM-1)
              ZI(JAPMEM+ZAPMEM* (POSNOM-1)) = 0
              COORM(1) = ZR(JCOOR+3* (NUMNOM-1))
              COORM(2) = ZR(JCOOR+3* (NUMNOM-1)+1)
              COORM(3) = ZR(JCOOR+3* (NUMNOM-1)+2)
              DIST = PADIST(3,COORE,COORM)
              IF (DIST.LT.DMIN) THEN
                POSMIN = POSNOM
                DMIN   = DIST
              END IF
   70       CONTINUE
   80     CONTINUE
C
C --- NOEUD ESCLAVE SUIVANT
C
          IESCL = IESCL + 1
C
C --- STOCKAGE DANS APPARI ET APMEMO
C
          ZI(JAPPAR+ZAPPAR* (IESCL-1)+1)  = POSNOE
          ZI(JAPPAR+ZAPPAR* (IESCL-1)+2)  = 0
          ZI(JAPPAR+ZAPPAR* (IESCL-1)+3)  = REACTU

          ZI(JAPMEM+ZAPMEM* (POSNOE-1))   = 1
          ZI(JAPMEM+ZAPMEM* (POSNOE-1)+1) = POSMIN
          ZI(JAPMEM+ZAPMEM* (POSNOE-1)+2) = 0
          ZI(JAPMEM+ZAPMEM* (POSNOE-1)+3) = 0

          ZI(JAPMEM+ZAPMEM* (POSMIN-1))   = 0
C
C --- CALL CFJEUM PAR CHMANO
C
  110   CONTINUE
C
C ======================================================================
C --- APPARIEMENT PAR BOITES 
C --- ON PARCOURT L'ENSEMBLE DES NOEUDS APPARTENANT
C ---  A DES BOITES VOISINES DU NOEUD ESCLAVE CONSIDERE
C ======================================================================
C
      ELSE IF (REAAPP.EQ.3) THEN
C
          CALL UTMESS ('F','RECHMN','LA RECHERCHE NODALE PAR BOITES'
     &               //' N''EST PAS OPERATIONNEL')
C
      END IF
C ----------------------------------------------------------------------

      CALL JEDEMA()
      END
