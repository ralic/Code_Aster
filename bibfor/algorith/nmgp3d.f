       SUBROUTINE NMGP3D(NNO, NPG, POIDSG, VFF, DFDE, DFDN, DFDK, GEOMI,
     &                   TYPMOD, OPTION, IMATE, COMPOR, LGPG, CRIT,
     &                   INSTM, INSTP, TM, TP, HYDRM, HYDRP, SECHM,
     &                   SECHP, NZ,PHASM, PHASP, TREF,
     &                   DEPLM, DDEPL, SIGM, VIM, DFDIM, DFDIP,
     &                   SIGP, VIP, MATNS, VECTU, CODRET )

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 11/02/2003   AUTEUR PBADEL P.BADEL 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C RESPONSABLE PBADEL P.BADEL
C TOLE CRP_21

       IMPLICIT NONE
       INTEGER       NNO, NPG, IMATE, LGPG, NZ, CODRET
       CHARACTER*8   TYPMOD(*)
       CHARACTER*16  OPTION, COMPOR(4)
       REAL*8        POIDSG(NPG), VFF(NNO,NPG), DFDE(*), DFDN(*)
       REAL*8        DFDK(*), GEOMI(3,NNO), CRIT(3), INSTM, INSTP
       REAL*8        TM(NNO), TP(NNO), PHASM(NZ,NPG),PHASP(NZ,NPG),TREF
       REAL*8        HYDRM(NNO), HYDRP(NNO), SECHM(NNO), SECHP(NNO)
       REAL*8        DEPLM(3,NNO), DDEPL(3,NNO), SIGM(6,NPG)
       REAL*8        VIM(LGPG,NPG), DFDIM(NNO,3),DFDIP(NNO,3)
       REAL*8        SIGP(6,NPG), VIP(LGPG,NPG)
       REAL*8        MATNS(3,NNO,3,NNO), VECTU(3,NNO)
C.......................................................................
C
C     BUT:  CALCUL  DES OPTIONS RIGI_MECA_TANG, RAPH_MECA ET FULL_MECA
C           EN GRANDES DEFORMATIONS 3D
C.......................................................................
C IN  NNO     : NOMBRE DE NOEUDS DE L'ELEMENT
C IN  NPG     : NOMBRE DE POINTS DE GAUSS
C IN  POIDSG  : POIDS DES POINTS DE GAUSS
C IN  VFF     : VALEUR  DES FONCTIONS DE FORME
C IN  DFDE    : DERIVEE DES FONCTIONS DE FORME ELEMENT DE REFERENCE
C IN  DFDN    : DERIVEE DES FONCTIONS DE FORME ELEMENT DE REFERENCE
C IN  DFDK    : DERIVEE DES FONCTIONS DE FORME ELEMENT DE REFERENCE
C IN  GEOMI   : COORDONNEES DES NOEUDS (CONFIGURATION INITIALE)
C IN  TYPMOD  : TYPE DE MODELISATION
C IN  OPTION  : OPTION DE CALCUL
C IN  IMATE   : MATERIAU CODE
C IN  COMPOR  : COMPORTEMENT
C IN  LGPG    : "LONGUEUR" DES VARIABLES INTERNES POUR 1 POINT DE GAUSS
C                CETTE LONGUEUR EST UN MAJORANT DU NBRE REEL DE VAR.INT.
C IN  CRIT    : CRITERES DE CONVERGENCE LOCAUX
C IN  INSTM   : VALEUR DE L'INSTANT T-
C IN  INSTP   : VALEUR DE L'INSTANT T+
C IN  TM      : TEMPERATURE AUX NOEUDS EN T-
C IN  TP      : TEMPERATURE AUX NOEUDS EN T+
C IN  HYDRM   : HYDRATATION AUX POINTS DE GAUSS EN T-
C IN  HYDRP   : HYDRATATION AUX POINTS DE GAUSS EN T+
C IN  SECHM   : SECHAGE AUX NOEUDS EN T-
C IN  SECHP   : SECHAGE AUX NOEUDS EN T+
C IN  PHASM   : PHASES METALLURGIQUES EN T-
C IN  PHASP   : PHASES METALLURGIQUES EN T+
C IN  TREF    : TEMPERATURE DE REFERENCE
C IN  DEPLM   : DEPLACEMENT EN T-
C IN  DDEPL   : INCREMENT DE DEPLACEMENT ENTRE T- ET T+
C IN  SIGM    : CONTRAINTES DE CAUCHY EN T-
C IN  VIM     : VARIABLES INTERNES EN T-
C OUT DFDIM   : DERIVEES DES FONCTIONS DE FORMES CONF T- (DERN PT GAUSS)
C OUT DFDIP   : DERIVEES DES FONCTIONS DE FORMES CONF T+ (DERN PT GAUSS)
C OUT SIGP    : CONTRAINTES DE CAUCHY (RAPH_MECA ET FULL_MECA)
C OUT VIP     : VARIABLES INTERNES    (RAPH_MECA ET FULL_MECA)
C OUT MATUU   : MATRICE DE RIGIDITE PROFIL (RIGI_MECA_TANG ET FULL_MECA)
C OUT VECTU   : FORCES INTERIEURES (RAPH_MECA ET FULL_MECA)
C.......................................................................
      LOGICAL GRAND, AXI, RESI, RIGI
      INTEGER KPG,N,I,M,J,K,L
      REAL*8  GEOMM(3,27), GEOMP(3,27)
      REAL*8  FM(3,3), DF(3,3), FBID(3,3), R, EPS(6), POIDS
      REAL*8  DSIDEP(6,3,3)
      REAL*8  SIGDF(3,27), DSIDF(3,3,3,27)
      REAL*8  TEMPM, TEMPP, HYDRGM, HYDRGP, SECHGM, SECHGP
      REAL*8  DEFAM, DEFAP
      REAL*8  ELGEOM(10,27)
      INTEGER IND(3,3),COD(27)
      PARAMETER (GRAND = .TRUE., AXI = .FALSE.)
      DATA    IND / 1, 4, 5,
     &              4, 2, 6,
     &              5, 6, 3 /


C - INITIALISATION ET VERIFICATIONS
      IF (NNO.GT.27) CALL UTMESS('F','NMGP3D','MAUVAIS DIMENSIONNEMENT'
     &                            // 'DE GEOMM ET GEOMP')
      RESI = OPTION(1:4).EQ.'RAPH' .OR. OPTION(1:4).EQ.'FULL'
      RIGI = OPTION(1:4).EQ.'RIGI' .OR. OPTION(1:4).EQ.'FULL'
      DO 5 N = 1,NNO
        DO 6 I = 1,3
          GEOMM(I,N) = GEOMI(I,N) + DEPLM(I,N)
          GEOMP(I,N) = GEOMI(I,N) + DEPLM(I,N) + DDEPL(I,N)
 6      CONTINUE
 5    CONTINUE
      IF (RESI) CALL R8INIR(3*NNO,     0.D0, VECTU, 1)
      IF (RIGI) CALL R8INIR(9*NNO*NNO, 0.D0, MATNS, 1)

C - CALCUL DES ELEMENTS GEOMETRIQUES SPECIFIQUES LOIS DE COMPORTEMENT

      CALL LCEGEO(NNO,NPG,POIDSG,VFF,DFDE,DFDN,DFDK,GEOMI,TYPMOD,
     &            OPTION,IMATE,COMPOR,LGPG,ELGEOM)

C - INITIALISATION CODES RETOURS
      DO 1955 KPG=1,NPG
         COD(KPG)=0
1955  CONTINUE

C - CALCUL POUR CHAQUE POINT DE GAUSS
      DO 10 KPG=1,NPG

C - CALCUL DE LA TEMPERATURE AU POINT DE GAUSS
C - DE L HYDRATATION ET DU SECHAGE AU POINT DE GAUSS
        TEMPM = 0.D0
        TEMPP = 0.D0
        HYDRGM = HYDRM(KPG)
        HYDRGP = HYDRP(KPG)
        SECHGM = 0.D0
        SECHGP = 0.D0
        DO 15 N=1,NNO
          TEMPM = TEMPM + TM(N)*VFF(N,KPG)
          TEMPP = TEMPP + TP(N)*VFF(N,KPG)
          SECHGM = SECHGM + SECHM(N)*VFF(N,KPG)
          SECHGP = SECHGP + SECHP(N)*VFF(N,KPG)
 15     CONTINUE


C - CALCUL DES ELEMENTS GEOMETRIQUES
C     CALCUL DE F EN T-  =>  FM
        CALL NMGEOM(3,NNO,AXI,GRAND,GEOMI,KPG,POIDSG(KPG),
     &              VFF(1,KPG),DFDE,DFDN,DFDK,DEPLM,POIDS,DFDIM,
     &              FM,EPS,R)

C     CALCUL DE F POUR DT  =>  DF  ET DFDIM
        CALL NMGEOM(3,NNO,AXI,GRAND,GEOMM,KPG,POIDSG(KPG),
     &              VFF(1,KPG),DFDE,DFDN,DFDK,DDEPL,POIDS,DFDIM,
     &              DF,EPS,R)

C     CALCUL DE DFDIP ET POIDS EN T+
        CALL NMGEOM(3,NNO,AXI,GRAND,GEOMP,KPG,POIDSG(KPG),
     &              VFF(1,KPG),DFDE,DFDN,DFDK,DEPLM,POIDS,DFDIP,
     &              FBID,EPS,R)


C - LOI DE COMPORTEMENT
        CALL NMCOMP(3,TYPMOD,IMATE,COMPOR,CRIT,INSTM,INSTP,
     &              TEMPM,TEMPP,TREF,HYDRGM,HYDRGP,SECHGM,SECHGP,
     &              FM,DF,SIGM(1,KPG),VIM(1,KPG),
     &              OPTION,DEFAM,DEFAP,NZ,PHASM(1,KPG),PHASP(1,KPG),
     &            ELGEOM(1,KPG),SIGP(1,KPG),VIP(1,KPG),DSIDEP,COD(KPG))

       IF(COD(KPG).EQ.1) THEN
         GOTO 1956
       ENDIF

C - CALCUL DE LA MATRICE DE RIGIDITE
        IF (RESI) THEN
          DO 500 N = 1,NNO
            DO 510 I = 1,3
              SIGDF(I,N) = SIGP(IND(I,1),KPG)*DFDIP(N,1)
     &                   + SIGP(IND(I,2),KPG)*DFDIP(N,2)
     &                   + SIGP(IND(I,3),KPG)*DFDIP(N,3)
 510        CONTINUE
 500      CONTINUE
        ELSE
          DO 520 N = 1,NNO
            DO 530 I = 1,3
              SIGDF(I,N) = SIGM(IND(I,1),KPG)*DFDIP(N,1)
     &                   + SIGM(IND(I,2),KPG)*DFDIP(N,2)
     &                   + SIGM(IND(I,3),KPG)*DFDIP(N,3)
 530        CONTINUE
 520      CONTINUE
        END IF
        IF (RIGI) THEN
          DO 600 I = 1,3
            DO 610 N = 1,NNO
              DO 620 K = 1,3
                DO 630 L = 1,3
                  DSIDF(I,K,L,N) = DSIDEP(IND(I,1),K,L)*DFDIP(N,1)
     &                           + DSIDEP(IND(I,2),K,L)*DFDIP(N,2)
     &                           + DSIDEP(IND(I,3),K,L)*DFDIP(N,3)
 630            CONTINUE
 620          CONTINUE
 610        CONTINUE
 600      CONTINUE
          DO 130 N = 1,NNO
            DO 131 M = 1,NNO
              DO 132 I = 1,3
                DO 133 J = 1,3
                  MATNS(I,N,J,M) = MATNS(I,N,J,M) + POIDS * (
     &                 DFDIP(N,I) * SIGDF(J,M) - DFDIP(M,I) * SIGDF(J,N)
     &               + DFDIM(N,1) * DSIDF(J,I,1,M)
     &               + DFDIM(N,2) * DSIDF(J,I,2,M)
     &               + DFDIM(N,3) * DSIDF(J,I,3,M)    )
 133            CONTINUE
 132          CONTINUE
 131        CONTINUE
 130      CONTINUE
        ENDIF

C - CALCUL DE LA FORCE INTERIEURE
        IF(RESI) THEN
          DO 180 N=1,NNO
            DO 181 I=1,3
              VECTU(I,N) = VECTU(I,N) + POIDS * SIGDF(I,N)
 181        CONTINUE
 180      CONTINUE
        ENDIF
 10   CONTINUE
1956  CONTINUE
C - SYNTHESE DES CODES RETOURS
      CALL CODERE(COD,NPG,CODRET)
      END
