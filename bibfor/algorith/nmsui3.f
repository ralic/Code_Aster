      SUBROUTINE NMSUI3(SDIMPR,TYPCHA,NBMA  ,NBNO  ,NBPI  ,
     &                  NBSPI ,NBCMP ,EXTRCH,EXTRCP,EXTRGA,
     &                  LISTMA,CHNOEU,CHELGA,CHAMP ,ISUIV )
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 28/03/2011   AUTEUR ABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT      NONE
      INTEGER       NBCMP,NBNO,NBMA
      INTEGER       NBPI,NBSPI
      CHARACTER*4   TYPCHA
      CHARACTER*24  SDIMPR
      CHARACTER*8   EXTRCH,EXTRCP,EXTRGA
      CHARACTER*19  CHAMP,CHNOEU,CHELGA
      CHARACTER*24  LISTMA
      INTEGER       ISUIV
C
C ----------------------------------------------------------------------
C
C ROUTINE *_NON_LINE (SUIVI_DDL - UTILITAIRE)
C
C EXTRAIRE LES VALEURS - ECRITURE DANS LE TABLEAU DE CONVERGENCE
C
C ----------------------------------------------------------------------
C
C
C IN  SDIMPR : SD AFFICHAGE
C IN  TYPCHA : TYPE DU CHAMP
C IN  NBCMP  : NOMBRE DE COMPOSANTES DANS LA SD
C IN  NBNO   : NOMBRE DE NOEUDS DANS LA SD
C IN  NBMA   : NOMBRE DE MAILLES DANS LA SD
C IN  NBPI   : NOMBRE DE POINTS D'INTEGRATION 
C IN  NBSPI  : NOMBRE DE SOUS-POINTS D'INTEGRATION
C IN  LISTMA : LISTE CONTENANT LES MAILLES
C IN  EXTRGA : TYPE D'EXTRACTION SUR UNE MAILLE
C IN  EXTRCH : TYPE D'EXTRACTION SUR LE CHAMP
C IN  EXTRCP : TYPE D'EXTRACTION SUR LES COMPOSANTES
C IN  CHNOEU : VECTEUR DE TRAVAIL CHAMPS AUX NOEUDS
C IN  CHELGA : VECTEUR DE TRAVAIL CHAMPS AUX ELEMENTS
C IN  CHAMP  : CHAMP A EXTRAIRE
C I/O ISUIV  : NUMERO COURANT DU SUIVI_DDL
C
C --- DEBUT DECLARATIONS NORMALISEES JEVEUX ----------------------------
C
      INTEGER      ZI
      COMMON  / IVARJE / ZI(1)
      REAL*8       ZR
      COMMON  / RVARJE / ZR(1)
      COMPLEX*16   ZC
      COMMON  / CVARJE / ZC(1)
      LOGICAL      ZL
      COMMON  / LVARJE / ZL(1)
      CHARACTER*8  ZK8
      CHARACTER*16    ZK16
      CHARACTER*24        ZK24
      CHARACTER*32            ZK32
      CHARACTER*80                ZK80
      COMMON  / KVARJE / ZK8(1) , ZK16(1) , ZK24(1) , ZK32(1) , ZK80(1)
C
C --- FIN DECLARATIONS NORMALISEES JEVEUX ------------------------------
C
      INTEGER      INO,IMA,IPI,ISPI,NUMMAI
      INTEGER      NBNOR,NBMAR
      INTEGER      IVALCP,JCESD,JMA
      REAL*8       VALR
      INTEGER      NVALCP
      INTEGER      NPI,NSPI,NMAPT,NMASPT,NBPIR,NBSPIR
      INTEGER      JNOEU,JELGA
      CHARACTER*19 CHELES
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
C
C --- INITIALISATIONS
C
      CHELES = '&&NMSUI3.TRAV'
C
C --- PASSAGE EN CHAM_ELEM_S      
C
      IF (TYPCHA.EQ.'ELGA') THEN
        CALL SDMPIC('CHAM_ELEM',CHAMP )
        CALL CELCES(CHAMP ,'V',CHELES )
        CALL JEVEUO(CHELES(1:19)//'.CESD','L',JCESD)
      ENDIF
C
C --- NOMBRE DE NOEUDS POUR LA BOUCLE
C
      IF (TYPCHA.EQ.'NOEU') THEN
        IF (EXTRCH.EQ.'VALE') THEN
          NBNOR  = NBNO
        ELSEIF ((EXTRCH.EQ.'MIN').OR.
     &          (EXTRCH.EQ.'MAX').OR.
     &          (EXTRCH.EQ.'MOY')) THEN
          NBNOR = 1
        ELSE
          CALL ASSERT(.FALSE.)
        ENDIF
      ENDIF
C
C --- NOMBRE DE MAILLES POUR LA BOUCLE
C
      IF (TYPCHA.EQ.'ELGA') THEN
        IF (EXTRCH.EQ.'VALE') THEN
          NBMAR  = NBMA
        ELSEIF ((EXTRCH.EQ.'MIN').OR.
     &          (EXTRCH.EQ.'MAX').OR.
     &          (EXTRCH.EQ.'MOY')) THEN
          NBMAR = 1
        ELSE
          CALL ASSERT(.FALSE.)
        ENDIF
      ENDIF
C
C --- NOMBRE DE COMPOSANTES POUR LA BOUCLE          
C
      IF (EXTRCP.EQ.' ') THEN
        NVALCP    = NBCMP
      ELSE
        NVALCP    = 1
      ENDIF
C
C --- VALEUR NODALES
C
      IF (TYPCHA.EQ.'NOEU') THEN
        CALL JEVEUO(CHNOEU,'L',JNOEU)
C
        DO 20 INO = 1,NBNOR
C
C ------- ECRITURE DES VALEURS
C
          DO 21 IVALCP = 1,NVALCP
            VALR = ZR(JNOEU+IVALCP-1
     &                     +NBCMP*(INO-1))
            CALL NMSUIY(SDIMPR,VALR  ,ISUIV )
  21      CONTINUE
  20    CONTINUE
      ENDIF
C
C --- VALEURS AUX POINTS DE GAUSS
C
      IF (TYPCHA.EQ.'ELGA') THEN
        CALL JEVEUO(CHELGA,'L',JELGA)
        CALL JEVEUO(LISTMA,'L',JMA)
C           
C ----- BOUCLE SUR LES MAILLES
C
        DO 30 IMA = 1,NBMAR
C      
C ----- MAILLE COURANTE
C        
        NUMMAI  = ZI(JMA-1+IMA)
C
C ------- NOMBRE EFFECTIF DE POINTS/SOUS-POINTS SUR LA MAILLE
C
          NMAPT  = ZI(JCESD+5+4*(NUMMAI-1))
          NMASPT = ZI(JCESD+5+4*(NUMMAI-1)+1)
C
C ------- PLAFONNEMENT
C
          NPI    = NBPI
          NSPI   = NBSPI
          IF (NPI.GT.NMAPT)   NPI  = NMAPT
          IF (NSPI.GT.NMASPT) NSPI = NMASPT
C
C ------- NOMBRE DE POINTS/SOUS-POINTS POUR LA BOUCLE          
C
          IF (EXTRGA.EQ.'VALE') THEN
            NBPIR  = NPI
            NBSPIR = NSPI
          ELSE
            NBPIR  = 1
            NBSPIR = 1            
          ENDIF
C
C ------- BOUCLE SUR LES POINTS/SOUS_POINTS
C
          DO 45 IPI = 1,NBPIR
            DO 46 ISPI = 1,NBSPIR
C              
C ----------- LECTURE DES VALEURS         
C
              DO 47 IVALCP = 1,NVALCP
                VALR = ZR(JELGA+NBCMP*NBPI*NBSPI*(IMA-1)
     &                   +NBPI*NBSPI*(IVALCP-1)
     &                   +NBSPI*(IPI-1)
     &                   +(ISPI-1))
                CALL NMSUIY(SDIMPR,VALR  ,ISUIV )
  47          CONTINUE 
  46        CONTINUE
  45      CONTINUE
  30    CONTINUE
      ENDIF
C
      CALL JEDETR(CHELES)
      CALL JEDEMA()
C
      END
