      SUBROUTINE XBASCO(NDIM,NUNOA,NUNOB,NUNOM,A,B,C,S,JGRLN,JGRLT,
     &                                                      JCNSV,JCNSL)

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 29/09/2006   AUTEUR VABHHTS J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2006  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE GENIAUT S.GENIAUT

      IMPLICIT NONE

      INTEGER       NDIM,NUNOA,NUNOB,NUNOM,JGRLN,JGRLT,JCNSV,JCNSL
      REAL*8        A(NDIM),B(NDIM),C(NDIM),S

C
C   CREATION DU CHAM_NO_S DE LA BASE COVARIANTE (POUR LE MOMENT,
C     SEULEMENT POUR LES POINTS DE CONTACT SUR LES ARETES)
C                    (COMME DANS TE0510)
C
C   IN
C       NDIM   : DIMENSION DE L'ESPACE
C       NUNOA  : NUMERO DU NOEUD EXTREMITE 1 DE L'ARETE
C       NUNOB  : NUMERO DU NOEUD EXTREMITE 2 DE L'ARETE
C       NUNOC  : NUMERO DU NOEUD MILIEU DE L'ARETE
C       A      : COORDONNEES DU NOEUD EXTREMITE 1 DE L'ARETE
C       B      : COORDONNEES DU NOEUD EXTREMITE 2 DE L'ARETE
C       C      : COORDONNEES DU POINT DE CONTACT SUR L'ARETE
C       S      : POSITION RELATIVE DU POINT DE CONTACT SUR L'ARETE
C       JGRLT  : ADRESSE DU GRADIENT DE LA LEVEL SET TANGENTE
C       JGRLT  : ADRESSE DU GRADIENT DE LA LEVEL SET NORMALE
C
C   OUT
C       JCNSV  : ADRESSE VALEUR DU CHAM_NO_S DE LA BASE COVARIANTE
C       JCNSL  : ADRESSE LOGIQUE DU CHAM_NO_S DE LA BASE COVARIANTE



C -------------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ----------------
C
      CHARACTER*32       JEXNUM , JEXNOM , JEXATR
      INTEGER            ZI
      COMMON  / IVARJE / ZI(1)
      REAL*8             ZR
      COMMON  / RVARJE / ZR(1)
      COMPLEX*16         ZC
      COMMON  / CVARJE / ZC(1)
      LOGICAL            ZL
      COMMON  / LVARJE / ZL(1)
      CHARACTER*8        ZK8
      CHARACTER*16                ZK16
      CHARACTER*24                          ZK24
      CHARACTER*32                                    ZK32
      CHARACTER*80                                              ZK80
      COMMON  / KVARJE / ZK8(1) , ZK16(1) , ZK24(1) , ZK32(1) , ZK80(1)
C
C -------------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ----------------

      INTEGER    J
      REAL*8     ND(3),GRLT(NDIM),NORME,PS,TAU1(3),TAU2(3),NORM2,DDOT


      CALL JEMARQ()


C     INITIALISATIONS POUR LE CAS 2D (CAR LA ROUTINE NORMEV PREND TJS
C     3 DIMENSIONS)
      TAU1(3)=0.D0
      ND(3)=0.D0
      TAU2(1)=0.D0
      TAU2(2)=0.D0
      TAU2(3)=0.D0

C     CALCUL DE LA NORMALE ET DU GRADIENT DE LST AU POINT DE CONTACT
      DO 132 J=1,NDIM
        ND(J)  = (1-S) * ZR(JGRLN-1+NDIM*(NUNOA-1)+J)
     &         +    S  * ZR(JGRLN-1+NDIM*(NUNOB-1)+J)
        GRLT(J)= (1-S) * ZR(JGRLT-1+NDIM*(NUNOA-1)+J)
     &         +    S  * ZR(JGRLT-1+NDIM*(NUNOB-1)+J)
 132  CONTINUE

C     NORMALISATION DE LA NORMALE ND
      CALL NORMEV(ND,NORME)

C     TAU1 = PROJETE DEU GRADIENT SUR LE PLAN DE NORMALE ND
      PS=DDOT(NDIM,GRLT,1,ND,1)
      DO 133 J=1,NDIM
        TAU1(J)=GRLT(J)-PS*ND(J)
 133  CONTINUE
      CALL NORMEV(TAU1,NORME)

      IF (NORME.LT.1.D-12) THEN
        CALL U2MESS('A','ELEMENTS4_5')
        CALL UTIMPR('L',' POINT ',NDIM,C )

C       ESSAI AVEC LE PROJETE DE OX
        TAU1(1)=1.D0-ND(1)*ND(1)
        TAU1(2)=0.D0-ND(1)*ND(2)
        IF (NDIM .EQ. 3) TAU1(3)=0.D0-ND(1)*ND(3)
        CALL NORMEV(TAU1,NORM2)
        IF (NORM2.LT.1.D-12) THEN
C         ESSAI AVEC LE PROJETE DE OY
          TAU1(1)=0.D0-ND(2)*ND(1)
          TAU1(2)=1.D0-ND(2)*ND(2)
          IF (NDIM .EQ. 3) TAU1(3)=0.D0-ND(2)*ND(3)
          CALL NORMEV(TAU1,NORM2)
        ENDIF
        CALL ASSERT(NORM2.GT.1.D-12)
      ENDIF
      IF (NDIM .EQ. 3) THEN
        CALL PROVEC(ND,TAU1,TAU2)
      ENDIF

C     ARCHIVAGE DE LA BASE COVARIANTE
      IF (NDIM.EQ.2) THEN
        ZR(JCNSV-1+12*(NUNOM-1)+1) = C(1)
        ZR(JCNSV-1+12*(NUNOM-1)+2) = C(2)
        ZR(JCNSV-1+12*(NUNOM-1)+3) = 0.D0
        ZR(JCNSV-1+12*(NUNOM-1)+4) = ND(1)
        ZR(JCNSV-1+12*(NUNOM-1)+5) = ND(2)
        ZR(JCNSV-1+12*(NUNOM-1)+6) = 0.D0
        ZR(JCNSV-1+12*(NUNOM-1)+7) = TAU1(1)
        ZR(JCNSV-1+12*(NUNOM-1)+8) = TAU1(2)
        ZR(JCNSV-1+12*(NUNOM-1)+9) = C(1)
        ZR(JCNSV-1+12*(NUNOM-1)+10) = 0.D0
        ZR(JCNSV-1+12*(NUNOM-1)+11) = 0.D0
        ZR(JCNSV-1+12*(NUNOM-1)+12) = 0.D0
      ELSEIF (NDIM.EQ.3) THEN
        ZR(JCNSV-1+12*(NUNOM-1)+1) = C(1)
        ZR(JCNSV-1+12*(NUNOM-1)+2) = C(2)
        ZR(JCNSV-1+12*(NUNOM-1)+3) = C(3)
        ZR(JCNSV-1+12*(NUNOM-1)+4) = ND(1)
        ZR(JCNSV-1+12*(NUNOM-1)+5) = ND(2)
        ZR(JCNSV-1+12*(NUNOM-1)+6) = ND(3)
        ZR(JCNSV-1+12*(NUNOM-1)+7) = TAU1(1)
        ZR(JCNSV-1+12*(NUNOM-1)+8) = TAU1(2)
        ZR(JCNSV-1+12*(NUNOM-1)+9) = TAU1(3)
        ZR(JCNSV-1+12*(NUNOM-1)+10) = TAU2(1)
        ZR(JCNSV-1+12*(NUNOM-1)+11) = TAU2(2)
        ZR(JCNSV-1+12*(NUNOM-1)+12) = TAU2(3)
      ENDIF

      DO 900 J=1,12
        ZL(JCNSL-1+12*(NUNOM-1)+J)=.TRUE.
 900  CONTINUE

      CALL JEDEMA()
      END
