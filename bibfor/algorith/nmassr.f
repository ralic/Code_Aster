      SUBROUTINE NMASSR(FONACT,NUMEDD,RESOCO,VALPLU,MESSTR,
     &                  SSTRU ,VEFINT,VEDIRI,CNFINT,CNDIRI)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 19/12/2007   AUTEUR ABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2007  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT NONE
      LOGICAL       FONACT(*)
      CHARACTER*24  RESOCO,NUMEDD,VALPLU(8)
      CHARACTER*19  CNFINT,CNDIRI
      CHARACTER*8   VEFINT,VEDIRI
      CHARACTER*8   MESSTR
      CHARACTER*19  SSTRU
C 
C ----------------------------------------------------------------------
C
C ROUTINE MECA_NON_LINE (ALGORITHME)
C
C ASSEMBLAGE DES FORCES INTERIEURES ET DES REACTIONS D'APPUI
C      
C ----------------------------------------------------------------------
C
C
C IN  FONACT : FONCTIONNALITES SPECIFIQUES ACTIVEES (CF NMFONC)
C IN  NUMEDD : NUME_DDL DE LA MATRICE
C IN  RESOCO : SD DE RESOLUTION DU CONTACT
C IN  VALPLU : ETAT EN T+
C IN  VEDIRI : VECTEURS ELEMENTAIRES DES REACTIONS D'APPUI
C IN  VEFINT : VECTEURS ELEMENTAIRES DES FORCES INTERNES
C IN  MESSTR : MATRICES ELEMENTAIRES DES SOUS-ELEMENTS STATIQUES
C IN  SSTRU  : MATRICE ASSEMBLEE DES SOUS-ELEMENTS STATIQUES
C OUT CNFINT : VECTEUR ASSEMBLE DES FORCES INTERIEURES    
C OUT CNDIRI : VECTEUR ASSEMBLE DES REACTIONS D'APPUI
C
C -------------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ----------------
C
      INTEGER ZI
      COMMON /IVARJE/ ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C -------------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ----------------
C
      LOGICAL      ISFONC,LCTCD,LCTCC,LMACR
      INTEGER      NBVEC,NEQ,I,ICONTA
      INTEGER      JAFMU,JDIRI
      CHARACTER*24 DEPPLU,K24BID
      CHARACTER*8  LIVEL(2),K8BID
      CHARACTER*8  SCONEL
      REAL*8       LICOEF(2)
      CHARACTER*24 MELEME
      INTEGER      JMELEM 
      INTEGER      IFM,NIV     
C 
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
      CALL INFDBG('MECA_NON_LINE',IFM,NIV)
C
C --- AFFICHAGE
C
      IF (NIV.GE.2) THEN
        WRITE (IFM,*) '<MECANONLINE><FINT> CALCUL FORCES INTERNES' 
      ENDIF
C
C --- FONCTIONNALITES ACTIVEES
C    
      LCTCD  = ISFONC(FONACT,'CONT_DISCRET')
      LCTCC  = ISFONC(FONACT,'CONT_CONTINU')   
      LMACR  = ISFONC(FONACT,'MACR_ELEM_STAT')
C     
C --- DECOMPACTION VARIABLES CHAPEAUX
C
      CALL DESAGG(VALPLU,DEPPLU,K24BID,K24BID,K24BID,
     &            K24BID,K24BID,K24BID,K24BID)       
C
C --- FORCES DU CONTACT CONTINU
C
      IF (LCTCC) THEN
        MELEME = RESOCO(1:14)//'.MELEM'
        CALL JEVEUO(MELEME,'L',JMELEM)  
        SCONEL    = ZK8(JMELEM+2-1)       
        NBVEC     = 2
        LIVEL(2)  = SCONEL
        LICOEF(2) = 1.D0
        LICOEF(1) = 1.D0
      ELSE
        NBVEC     = 1
        LICOEF(1) = 1.D0          
      ENDIF
C
C --- ASSEMBLAGE DES FORCES INTERIEURES
C     
      IF (NIV.GE.2) THEN
        WRITE (IFM,*) '<MECANONLINE><FINT> ... FORCES INTERNES' 
      ENDIF 
      LIVEL(1)  = VEFINT          
      CALL ASSVEC('V',CNFINT,NBVEC,LIVEL,LICOEF,NUMEDD,' ','ZERO',1)
      IF (NIV.GE.2) THEN
        CALL NMDEBG('VECT',CNFINT,6)
      ENDIF             
C
C --- ASSEMBLAGE DES REACTIONS D'APPUIS
C 
      IF (NIV.GE.2) THEN
        WRITE (IFM,*) '<MECANONLINE><FINT> ... REACTIONS APPUI' 
      ENDIF     
      NBVEC = 1 
      LIVEL(1)  = VEDIRI
      CALL ASSVEC('V',CNDIRI,NBVEC,LIVEL,LICOEF,NUMEDD,' ','ZERO',1) 
      IF (NIV.GE.2) THEN
        CALL NMDEBG('VECT',CNDIRI,6)
      ENDIF       
C
C --- PRISE EN COMPTE DES FORCES DE FROTTEMENT
C
      IF (LCTCD) THEN
        CALL JEEXIN(RESOCO(1:14)//'.AFMU', ICONTA)
        IF (ICONTA.NE.0) THEN
          CALL JEVEUO(CNDIRI(1:19)//'.VALE','E',JDIRI)
          CALL JEVEUO(RESOCO(1:14)//'.AFMU','L',JAFMU)
          CALL JELIRA(CNFINT(1:19)//'.VALE','LONMAX',NEQ,K8BID)
          DO 10 I = 1, NEQ
            ZR(JDIRI+I-1) =  ZR(JDIRI+I-1) +  ZR(JAFMU+I-1)
 10       CONTINUE
        ENDIF
      ENDIF
C
C --- PRISE EN COMPTE DES MACRO-ELEMENTS STATIQUES
C
      IF (LMACR) THEN
        CALL NMMACV(DEPPLU,MESSTR,SSTRU ,CNFINT)
      ENDIF
C
      CALL JEDEMA()
      END
