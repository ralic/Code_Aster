        SUBROUTINE CALCMM(NBCOMM,CPMONO,NMAT,PGL,NFS,NSG,TOUTMS,
     &                 COMP,NVI,VIND,IROTA)
        IMPLICIT NONE
        INTEGER NMAT,NBCOMM(NMAT,3),NVI,IROTA,NFS,NSG
        REAL*8 PGL(3,3),TOUTMS(NFS,NSG,6),VIND(*)
        CHARACTER*16 COMP(*)
        CHARACTER*24 CPMONO(5*NMAT+1)

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 18/12/2012   AUTEUR SELLENET N.SELLENET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE PROIX J-M.PROIX
C       IN
C         NBCOMM :  NOMBRE DE COEF COEFTIAU PAR FAMILLE
C         CPMONO :  NOMS DES LOIS COEFTIAU PAR FAMILLE
C           NMAT :  NOMBRE MAXI DE COEF MATERIAU
C          PGL   : MATRICE DE PASSAGE GLOBAL LOCAL DU MONOCRISTAL
C         COMP   :  COMPOR - LOI ET TYPE DE DEFORMATION
C         NVI    :  NB VARIABLES INTERNES
C         VIND   :  VARIABLES INTERNES A T
C         IROTA  :  >0 POUR ROTATION DE RESEAU, 0 SINON
C     OUT:
C        TOUTMS  :  TOUS LES TENSEURS MUS=SYM(MS*NS) EN HPP, 
C                   TOUS LES VECTEURS MS ET NS EN GDEF
C
C     CETTE ROUTINE CALCULE LES TENSEURS MS POUR GAGNER DU TEMPS
C
C     ----------------------------------------------------------------
      CHARACTER*16 NOMFAM
      REAL*8 MS(6),NG(3),Q(3,3),LG(3),IDEN(3,3)
      INTEGER NBFSYS,I,IFA,NBSYS,IS,J,IR
      DATA IDEN/1.D0,0.D0,0.D0, 0.D0,1.D0,0.D0, 0.D0,0.D0,1.D0/
C     ----------------------------------------------------------------

C         CALCUL DES TENSEURS MS POUR GAGNER DU TEMPS
      NBFSYS=NBCOMM(NMAT,2)
      IF (NBFSYS.GT.5) THEN
         CALL U2MESS('F','ALGORITH_68')
      ENDIF
      IR=0
     
      IF (COMP(3)(1:5).NE.'PETIT') THEN
C  EN VUE D'OPTIMISER, STOCKER LG ET NG
         IR=0
         DO 12 IFA=1,NBFSYS
            NOMFAM=CPMONO(5*(IFA-1)+1)
            CALL LCMMSG(NOMFAM,NBSYS,0,PGL,MS,NG,LG,IR,Q)
            DO 13 IS=1,NBSYS
               CALL LCMMSG(NOMFAM,NBSYS,IS,PGL,MS,NG,LG,IR,Q)
               DO 14 I=1,3
                  TOUTMS(IFA,IS,I)=LG(I)
                  TOUTMS(IFA,IS,I+3)=NG(I)
 14             CONTINUE
 13          CONTINUE
 12       CONTINUE
 
      ELSE
      
C        ROTATION RESEAU ROTA_RESEAU_CALC - DEBUT
         IF (IROTA.EQ.2) THEN
            IR=1
            DO 29 I = 1, 3
            DO 29 J=1,3
               Q(I,J)=VIND(NVI-19+3*(I-1)+J)+IDEN(I,J)
 29         CONTINUE
         ENDIF
         NBCOMM(NMAT,1)=IROTA
C        ROTATION RESEAU FIN

         DO 2 IFA=1,NBFSYS
            NOMFAM=CPMONO(5*(IFA-1)+1)
            CALL LCMMSG(NOMFAM,NBSYS,0,PGL,MS,NG,LG,IR,Q)
            DO 3 IS=1,NBSYS
               CALL LCMMSG(NOMFAM,NBSYS,IS,PGL,MS,NG,LG,IR,Q)
               DO 4 I=1,6
                  TOUTMS(IFA,IS,I)=MS(I)
 4             CONTINUE

 3          CONTINUE
 2       CONTINUE
      ENDIF
      END
