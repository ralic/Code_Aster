      SUBROUTINE MERESO ( RESULT,MODELE, MATE, CARELE, FOMULT, LISCHA,
     &                    ITPS, PARTPS,
     &                    NUMEDD, VECASS,
     &                    ASSMAT, SOLVEU, MATASS, MAPREC,
     &                    BASE, COMPOR )
C ----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 13/06/2012   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C ----------------------------------------------------------------------
C     MECANIQUE STATIQUE - RESOLUTION
C     * *                  ****
C ----------------------------------------------------------------------
C     BUT:  FAIRE UN CALCUL DE MECANIQUE STATIQUE : K(T)*U = F(T)
C           A UN INSTANT DONNE
C ----------------------------------------------------------------------
C IN  MODELE  : NOM DU MODELE
C IN  MATE    : NOM DU MATERIAU
C IN  CARELE  : NOM D'1 CARAC_ELEM
C IN  FOMULT  : LISTE DES FONCTIONS MULTIPLICATRICES
C IN  LISCHA  : INFORMATION SUR LES CHARGEMENTS
C IN  ITPS    : NUMERO DU PAS DE TEMPS
C IN  PARTPS  : PARAMETRES TEMPORELS
C IN  NUMEDD  : PROFIL DE LA MATRICE
C IN  VECASS  : SECOND MEMBRE ASSEMBLE
C IN  ASSMAT  : BOOLEEN POUR LE CALCUL DE LA MATRICE
C IN  SOLVEU  : METHODE DE RESOLUTION 'LDLT' OU 'GCPC'
C IN/OUT  MAPREC  : MATRICE PRECONDITIONNEE
C IN  BASE    : BASE DE TRAVAIL
C IN  COMPOR : COMPOR POUR LES MULTIFIBRE (POU_D_EM)
C   -------------------------------------------------------------------
C     ASTER INFORMATIONS:
C       30/01/04 (OB): MODIF CRITER POUR SOLVEUR FETI.
C-----------------------------------------------------------------------
C
      IMPLICIT NONE

C 0.1. ==> ARGUMENTS

      INCLUDE 'jeveux.h'
      INTEGER ITPS
      LOGICAL ASSMAT
      CHARACTER*1 BASE
      CHARACTER*19 LISCHA, SOLVEU
      CHARACTER*19 VECASS
      CHARACTER*19 MATASS, MAPREC
      CHARACTER*24 MODELE, CARELE, FOMULT
      CHARACTER*24 NUMEDD, COMPOR
      CHARACTER*(*) MATE
      CHARACTER*8  RESULT

      REAL*8 PARTPS(*),RBID

C 0.2. ==> COMMUNS

      COMPLEX*16         CBID

C 0.3. ==> VARIABLES LOCALES

      CHARACTER*6 NOMPRO
      PARAMETER ( NOMPRO = 'MERESO' )
      INTEGER JCRI, JCRR, JCRK
      INTEGER JPARA, IAINST, ISLVK
      INTEGER IRET
      INTEGER IFM, NIV
      CHARACTER*8 K8BID
      CHARACTER*19 CHDEPL, CHSOL
      CHARACTER*24 CNCHCI,K24BLA
      CHARACTER*24 CRITER
      CHARACTER*24 DEPL
      CHARACTER*24 VAPRIN, REPRIN

C DEB-------------------------------------------------------------------
C====
C 1. PREALABLES
C====

C-----RECUPERATION DU NIVEAU D'IMPRESSION

      CALL INFNIV(IFM,NIV)

      K24BLA = ' '

C 1.2. ==> NOM DES STRUCTURES

      CALL JEVEUO(SOLVEU//'.SLVK','L',ISLVK)
      CHSOL  = '&&'//NOMPRO//'_SOLUTION  '
      IF (ZK24(ISLVK).EQ.'FETI') THEN
        CRITER = '&&'//NOMPRO//'_RESFET_FETI    '
      ELSE
        CRITER = '&&'//NOMPRO//'_RESGRA_GCPC    '
      ENDIF

      DEPL  = '&&MERESO.DEPL'


C====
C 3. MATRICE ET SECOND MEMBRE
C====

      CALL MEACMV ( MODELE, MATE, CARELE, FOMULT, LISCHA,
     &              ITPS, PARTPS,
     &              NUMEDD, ASSMAT, SOLVEU,
     &              VECASS, MATASS, MAPREC, CNCHCI,
     &              0, K24BLA, ' ', VAPRIN, REPRIN,
     &              BASE, COMPOR )

C====
C 4. RESOLUTION AVEC VECASS COMME SECOND MEMBRE
C====

      CALL RESOUD ( MATASS, MAPREC, VECASS, SOLVEU, CNCHCI,
     &              'V', CHSOL, CRITER,0,RBID,CBID,.TRUE.)

C 5.1. ==> NETTOYAGE DU CHAMP CINEMATIQUE CNCHCI QUI EST RECREE A
C          CHAQUE FOIS
      CALL DETRSD('CHAMP_GD',CNCHCI)

C====
C 5. SAUVEGARDE DE LA SOLUTION
C====

C 5.1. ==> SAUVEGARDE DU CHAMP SOLUTION CHSOL DANS DEPL
      CALL COPISD ( 'CHAMP_GD','V',CHSOL(1:19),DEPL(1:19) )

C 5.2. ==> DESTRUCTION DU CHAMP SOLUTION CHSOL

      CALL DETRSD ('CHAMP_GD',CHSOL)

C 5.3. ==> STOCKAGE DE LA SOLUTION, DEPL, DANS LA STRUCTURE DE RESULTAT
C          EN TANT QUE CHAMP DE DEPLACEMENT A L'INSTANT COURANT
C
      WRITE (IFM,1000) 'DEPL', PARTPS(1), ITPS

      CALL RSEXCH(RESULT,'DEPL',ITPS,CHDEPL,IRET)
      IF ( IRET .LE. 100 ) THEN
C     NETTOYAGE DES SCORIES DE SD FETI, NORMALEMENT INUTILE, MAIS ON
C     NE SAIT JAMAIS !
        CALL ASSDE2(DEPL)
        CALL COPISD('CHAMP_GD','G',DEPL(1:19),CHDEPL(1:19))
        CALL RSNOCH(RESULT,'DEPL',ITPS,' ')
      ENDIF

C*** INST

      CALL RSADPA (RESULT, 'E', 1, 'INST', ITPS,0,IAINST,K8BID)
      ZR(IAINST) = PARTPS(1)

C*** METHODE, RENUM, ...

      CALL RSADPA (RESULT, 'E', 1, 'METHODE', ITPS,0,JPARA,K8BID)
      ZK16(JPARA) = ZK24(ISLVK)(1:16)
      CALL RSADPA (RESULT, 'E', 1, 'RENUM', ITPS,0,JPARA,K8BID)
      ZK16(JPARA) = ZK24(ISLVK+3)(1:16)
      CALL RSADPA (RESULT, 'E', 1, 'STOCKAGE', ITPS,0,JPARA,K8BID)
      IF ( ZK24(ISLVK)(1:4) .EQ. 'LDLT' ) THEN
         ZK16(JPARA) = 'LIGN_CIEL'
      ELSE
         ZK16(JPARA) = 'MORSE'
      ENDIF

C*** LES CRITERES

      CALL JEEXIN ( CRITER(1:19)//'.CRTI', IRET )
      IF ( IRET .NE. 0 ) THEN
        CALL JEVEUO(CRITER(1:19)//'.CRTI','L',JCRI)
        CALL JEVEUO(CRITER(1:19)//'.CRTR','L',JCRR)
        CALL JEVEUO(CRITER(1:19)//'.CRDE','L',JCRK)
        CALL RSADPA (RESULT, 'E', 1, ZK16(JCRK), ITPS,0,JPARA,K8BID)
        ZI(JPARA) = ZI(JCRI)
      CALL RSADPA (RESULT, 'E', 1, ZK16(JCRK+1), ITPS,0,JPARA,K8BID)
        ZR(JPARA) = ZR(JCRR)
      ENDIF
      CALL UTTCPU('CPU.OP0046.3', 'FIN',' ')

 1000 FORMAT(1P,3X,'CHAMP STOCKE : ',A16,' INSTANT : ',1PE12.5,
     &         '  NUMERO D''ORDRE : ',I5)

      END
