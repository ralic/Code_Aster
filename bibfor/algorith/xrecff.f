      SUBROUTINE XRECFF(FISS,CHFOND,BASFON,LNOFF)
      IMPLICIT NONE

      INCLUDE 'jeveux.h'
      INTEGER       LNOFF
      CHARACTER*8   FISS
      CHARACTER*24  CHFOND,BASFON


C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 13/06/2012   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE GENIAUT S.GENIAUT
C
C       RECUPERATION DE LA LISTE DES POINTS DU FOND DE FISSURE
C       SUR LEQUEL ON VA EFFECTUER LE CALCUL + BASE LOCALE EN FOND
C       DE FISSURE
C
C  IN  : FISS   : SD_FISS_XFEM
C  OUT : CHFOND : FOND DE FISSURE SUR LEQUEL ON FERA LE POST TRAITEMENT
C  OUT : BASFON : BASE LOCALE RELATIVE A CHFOND
C  OUT : LNOFF  : NOMBRE DE POINTS DU FOND CHFOND
C
C
C     ------------------------------------------------------------------
C
      INTEGER  IFF,IFOMUL,NUMFON,IBID,IDEPFI,IARRFI,IFON,IBAS

      INTEGER  I,J,K,NFONU,JFONU,IBASFF,JBASU
      REAL*8   SMAX,S,S1,S2,XYZ1,XYZ2
      CHARACTER*24  FONTMP,BASTMP
      INTEGER      IARG
C ----------------------------------------------------------------------

      CALL JEMARQ()

C     LISTE DES POINTS DES FONDS DE FISSURES
      CALL JEVEUO(FISS//'.FONDFISS','L',IFF)

C     LISTE DES FONDS MULTIPLES
      CALL JEVEUO(FISS//'.FONDMULT','L',IFOMUL)

C     BASE LOCALE EN FOND DE FISSURE
      CALL JEVEUO(FISS//'.BASEFOND','L',IBASFF)

C     ------------------------------------------------------------------
C     TRAITEMENT DU MOT-CLE NUME_FOND :
C     RESTRICTION DU FOND ET DE LA BASE AU NUMERO DU FOND DEMANDE
C     ------------------------------------------------------------------

C     NUMERO DU FOND A TRAITER
      CALL GETVIS('THETA','NUME_FOND',1,IARG,1,NUMFON,IBID)

      IDEPFI=ZI(IFOMUL-1+2*(NUMFON-1)+1)
      IARRFI=ZI(IFOMUL-1+2*(NUMFON-1)+2)
      LNOFF=IARRFI-IDEPFI+1

C     CREATION D'UN FOND TEMPORAIRE RESTREINT AU NUMFON
      FONTMP = '&&XREFF.FONFIS_TEMP'
      CALL WKVECT(FONTMP,'V V R',LNOFF*4,IFON)
        DO 15 I=1,LNOFF
          DO 16 J=1,4
            ZR(IFON-1+4*(I-1)+J)=ZR(IFF-1+4*(I+(IDEPFI-1)-1)+J)
 16       CONTINUE
 15     CONTINUE

C     CREATION D'UNE BASE TEMPORAIRE RESTREINTE AU NUMFON
      BASTMP = '&&XREFF.BASFON_TEMP'
      CALL WKVECT(BASTMP,'V V R',LNOFF*6,IBAS)
        DO 17 I=1,LNOFF
          DO 18 J=1,6
            ZR(IBAS-1+6*(I-1)+J)=ZR(IBASFF-1+6*(I+(IDEPFI-1)-1)+J)
 18       CONTINUE
 17     CONTINUE

C     ------------------------------------------------------------------
C     TRAITEMENT DU MOT-CLE NB_POINT_FOND : 
C     CREATION DU NOUVEAU FOND ET DE LA NOUVELLE BASE
C     ------------------------------------------------------------------

C     DOIT-ON PRENDRE UNE REPARTITION UNIFORME ?
      CALL GETVIS('THETA','NB_POINT_FOND',1,IARG,1,NFONU,IBID)
      IF (IBID.EQ.0) NFONU = 0

      IF (NFONU.GT.0) THEN

C       SI OUI : MODIFICATION DE LA LISTE DES POINTS DU FOND
C                ET DE LA BASE

        CALL ASSERT(NFONU.GE.2)

C       CREATION DU FOND MODIFIE
        CALL WKVECT(CHFOND,'V V R',4*NFONU,JFONU)

C       CREATION DE LA BASE MODIFIEE
        CALL WKVECT(BASFON,'V V R',6*NFONU,JBASU)

C       1ER ET DERNIER POINTS
        DO 100 J=1,4
          ZR(JFONU-1+4*(    1-1)+J)=ZR(IFON-1+4*(    1-1)+J)
          ZR(JFONU-1+4*(NFONU-1)+J)=ZR(IFON-1+4*(LNOFF-1)+J)
 100    CONTINUE

        DO 101 J=1,6
          ZR(JBASU-1+6*(    1-1)+J)=ZR(IBAS-1+6*(    1-1)+J)
          ZR(JBASU-1+6*(NFONU-1)+J)=ZR(IBAS-1+6*(LNOFF-1)+J)
 101    CONTINUE

C       NOUVEAUX POINTS
        SMAX = ZR(IFON-1+4*(LNOFF-1)+4)
        DO 102 I=2,NFONU-1
          S = (I-1)*SMAX/(NFONU-1)
          DO 103 K = 1,LNOFF
            IF (ZR(IFON-1+4*(K-1)+4).GT.S) GOTO 110
 103      CONTINUE
 110      CONTINUE
C         ON INTERPOLE LES COORD ENTRE CELLES DU SEGMENT [K-1,K]
          S1 = ZR(IFON-1+4*(K-1-1)+4)
          S2 = ZR(IFON-1+4*(  K-1)+4)
          DO 111 J=1,3
            XYZ1 = ZR(IFON-1+4*(K-1-1)+J)
            XYZ2 = ZR(IFON-1+4*(  K-1)+J)
            ZR(JFONU-1+4*(I-1)+J) =  XYZ1 + (XYZ2-XYZ1)*(S-S1)/(S2-S1)
 111      CONTINUE
          DO 112 J=1,6
            XYZ1 = ZR(IBAS-1+6*(K-1-1)+J)
            XYZ2 = ZR(IBAS-1+6*(  K-1)+J)
            ZR(JBASU-1+6*(I-1)+J) =  XYZ1 + (XYZ2-XYZ1)*(S-S1)/(S2-S1)
 112      CONTINUE
          ZR(JFONU-1+4*(I-1)+4) = S
 102    CONTINUE

C       ON ECRASE LNOFF
        LNOFF = NFONU

      ELSE

C       SI NON : ON RECOPIE TELLE QUELLE LA LISTE DES POINTS DU FOND
        CALL JEDUPO(FONTMP,'V',CHFOND,.FALSE.)

C       ET ON RECOPIE TELLE QUELLE LA BASE 
        CALL JEDUPO(BASTMP,'V',BASFON,.FALSE.)

      ENDIF

C     MENAGE
      CALL JEDETR(FONTMP)
      CALL JEDETR(BASTMP)

      CALL JEDEMA()
      END
