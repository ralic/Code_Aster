      SUBROUTINE  XRIGE3(ELREFP,NDIM,COORSE,IGEOM,HE,DDLH,DDLC,NFE,
     &                   BASLOC,NNOP,NPG,TYPMOD,
     &                   LGPG,LSN,LST,SIG,IDECPG,MATUU,CODRET)

      IMPLICIT NONE
      INTEGER       NDIM,IGEOM,LGPG,CODRET,NNOP,NPG,DDLH,DDLC,NFE
      INTEGER       IDECPG
      CHARACTER*8   ELREFP,TYPMOD(*)
      CHARACTER*24  COORSE
      REAL*8        BASLOC(9*NNOP),HE
      REAL*8        LSN(NNOP),LST(NNOP),SIG(90),MATUU(*)


C            CONFIGURATION MANAGEMENT OF EDF VERSION
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 28/09/2010   AUTEUR MASSIN P.MASSIN 
C ======================================================================
C COPYRIGHT (C) 1991 - 2007  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C TOLE CRP_21
C
C.......................................................................
C
C     BUT:  CALCUL  DE L'OPTION RIGI_MECA_GE AVEC X-FEM EN 3D
C.......................................................................

C IN  ELREFP  : ÉLÉMENT DE RÉFÉRENCE PARENT
C IN  NDIM    : DIMENSION DE L'ESPACE
C IN  COORSE  : COORDONNÉES DES SOMMETS DU SOUS-ÉLÉMENT
C IN  IGEOM   : COORDONNÉES DES NOEUDS DE L'ÉLÉMENT PARENT
C IN  HE      : VALEUR DE LA FONCTION HEAVISIDE SUR LE SOUS-ÉLT
C IN  DDLH    : NOMBRE DE DDL HEAVYSIDE (PAR NOEUD)
C IN  DDLC    : NOMBRE DE DDL DE CONTACT (PAR NOEUD)
C IN  NFE     : NOMBRE DE FONCTIONS SINGULIÈRES D'ENRICHISSEMENT
C IN  BASLOC  : BASE LOCALE AU FOND DE FISSURE AUX NOEUDS
C IN  NNOP    : NOMBRE DE NOEUDS DE L'ELEMENT PARENT
C IN  NPG     : NOMBRE DE POINTS DE GAUSS DU SOUS-ÉLÉMENT
C IN  TYPMOD  : TYPE DE MODELISATION
C IN  LGPG    : "LONGUEUR" DES VARIABLES INTERNES POUR 1 POINT DE GAUSS
C               CETTE LONGUEUR EST UN MAJORANT DU NBRE REEL DE VAR. INT.
C IN  LSN     : VALEUR DE LA LEVEL SET NORMALE AUX NOEUDS PARENTS
C IN  LST     : VALEUR DE LA LEVEL SET TANGENTE AUX NOEUDS PARENTS
C IN  IDECPG  : POSITION DANS LA FAMILLE 'XFEM' DU 1ER POINT DE GAUSS
C               DU SOUS ELEMENT COURRANT (EN FAIT IDECPG+1)
C IN SIG     : CONTRAINTES DE CAUCHY
C OUT MATUU   : MATRICE DE MASSE PROFIL
C......................................................................
C
C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ---------------------
      INTEGER  ZI
      COMMON  / IVARJE / ZI(1)
      REAL*8             ZR
      COMMON  / RVARJE / ZR(1)
      COMPLEX*16         ZC
      COMMON  / CVARJE / ZC(1)
      LOGICAL            ZL
      COMMON  / LVARJE / ZL(1)
      CHARACTER*8        ZK8
      CHARACTER*16                ZK16
      CHARACTER*24                          ZK24
      CHARACTER*32                                    ZK32
      CHARACTER*80                                              ZK80
      COMMON  / KVARJE / ZK8(1) , ZK16(1) , ZK24(1) , ZK32(1) , ZK80(1)
C --------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ---------------------
C
      INTEGER  KPG,KK,N,I,M,J,J1,KKD,INO,IG,IRET,IJ,IBID
      INTEGER  NNO,NNOS,NPGBIS,DDLS,DDLD,DDLDN,CPT,CPT1,NDIMB,IPG
      INTEGER  JCOOPG,JDFD2,JGANO,JCOORS,IDFDE,IVF,IPOIDS
      REAL*8   DSIDEP(6,6),F(3,3),EPS(6)
      REAL*8   POIDS,TMP1,FE(4),BASLOG(9)
      REAL*8   XG(NDIM),XE(NDIM),FF(NNOP),JAC,LSNG,LSTG
      REAL*8   DFDX(4),DFDY(4),DFDZ(4)
      REAL*8   DFDI(NNOP,NDIM),PFF(6,NNOP,NDIM),DGDGL(4,3)
      REAL*8   ENR(NNOP,NDIM+DDLH+NDIM*NFE),GRAD(3,3)
      REAL*8   DEPL0(NDIM+DDLH+NDIM*NFE+DDLC,NNOP)
      REAL*8  RAC2,RHO
      DATA    RAC2 / 1.4142135623731D0 /
C--------------------------------------------------------------------
C
C     ATTENTION, DEPL ET VECTU SONT ICI DIMENSIONNÉS DE TELLE SORTE
C     QU'ILS NE PRENNENT PAS EN COMPTE LES DDL SUR LES NOEUDS MILIEU

C     NOMBRE DE DDL DE DEPLACEMENT À CHAQUE NOEUD SOMMET
      DDLD=NDIM+DDLH+NDIM*NFE
      DDLDN = DDLD/NDIM

C     NOMBRE DE DDL TOTAL (DEPL+CONTACT) À CHAQUE NOEUD SOMMET
      DDLS=DDLD+DDLC
C
C     ADRESSE DES COORD DU SOUS ELT EN QUESTION
      CALL JEVEUO(COORSE,'L',JCOORS)
C
C       TE4-'XINT' : SCHÉMAS À 15 POINTS
      CALL ELREF5('TE4','XINT',NDIMB,NNO,NNOS,NPGBIS,IPOIDS,JCOOPG,IVF,
     &                  IDFDE,JDFD2,JGANO)

      CALL ASSERT(NPG.EQ.NPGBIS.AND.NDIM.EQ.NDIMB)

C-----------------------------------------------------------------------
C     BOUCLE SUR LES POINTS DE GAUSS DU SOUS-TÉTRA
      DO 100 KPG=1,NPG

C       COORDONNÉES DU PT DE GAUSS DANS LE REPÈRE RÉEL : XG
        CALL VECINI(NDIM,0.D0,XG)
        DO 110 I=1,NDIM
          DO 111 N=1,NNO
            XG(I)=XG(I)+ZR(IVF-1+NNO*(KPG-1)+N)*ZR(JCOORS-1+3*(N-1)+I)
 111      CONTINUE
 110    CONTINUE

        DO 300 I=1,NNOP
          DO 301 J=1,DDLS
            DEPL0(J,I)=0.D0
 301      CONTINUE
 300    CONTINUE

C       JUSTE POUR CALCULER LES FF
        CALL REERE3(ELREFP,NNOP,IGEOM,XG,DEPL0,.FALSE.,NDIM,HE,
     &              IBID,IBID,DDLH,NFE,DDLS,FE,DGDGL,'NON',
     &              XE,FF,DFDI,F,EPS,GRAD)

        IF (NFE.GT.0) THEN
C         BASE LOCALE  ET LEVEL SETS AU POINT DE GAUSS
          CALL VECINI(9,0.D0,BASLOG)
          LSNG = 0.D0
          LSTG = 0.D0
          DO 113 INO=1,NNOP
            LSNG = LSNG + LSN(INO) * FF(INO)
            LSTG = LSTG + LST(INO) * FF(INO)
            DO 114 I=1,9
              BASLOG(I) = BASLOG(I) + BASLOC(9*(INO-1)+I) * FF(INO)
 114        CONTINUE
 113      CONTINUE
C
C         FONCTION D'ENRICHISSEMENT AU POINT DE GAUSS ET LEURS DÉRIVÉES
          CALL XCALFE(XG,HE,LSNG,LSTG,BASLOG,FE,DGDGL,IRET)
C         ON A PAS PU CALCULER LES DERIVEES DES FONCTIONS SINGULIERES
C         CAR ON SE TROUVE SUR LE FOND DE FISSURE
          CALL ASSERT(IRET.NE.0)

        ENDIF
C
C       COORDONNÉES DU POINT DE GAUSS DANS L'ÉLÉMENT DE RÉF PARENT : XE
C       ET CALCUL DE FF, DFDI, ET EPS

        CALL REERE3(ELREFP,NNOP,IGEOM,XG,DEPL0,.FALSE.,NDIM,HE,
     &              IBID,IBID,DDLH,NFE,DDLS,FE,DGDGL,'DFF',
     &              XE,FF,DFDI,F,EPS,GRAD)

C       POUR CALCULER LE JACOBIEN DE LA TRANSFO SSTET->SSTET REF
C       ET LES DERIVEES DES FONCTIONS DE FORME,
C       ON ENVOIE DFDM3D AVEC LES COORD DU SS-ELT
        CALL DFDM3D(NNO,KPG,IPOIDS,IDFDE,ZR(JCOORS),
     &                                   DFDX,DFDY,DFDZ,JAC)
        DO 127 I=1,NDIM
          DO 128 N=1,NNOP
C----------LA PARTIE CORRESPOINDANTE AU FF CLASSIQUES
            CPT= 1
            PFF(CPT,N,I) =  DFDI(N,I)
C----------LA PARTIE CORRESPONDANTE A L'ENRICHEMENT HEAVISIDE
            IF (DDLH.EQ.NDIM) THEN
              CPT = 2
              PFF(CPT,N,I) =  DFDI(N,I)*HE
            ENDIF
C----------LA PARTIE CORRESPONDANTE A L'ENRICHEMENT CRACK-TIP
            DO 126 IG = 1,NFE
              CPT =CPT+1
              PFF(CPT,N,I) = DFDI(N,I)*FE(IG)+FF(N)*DGDGL(IG,I)
 126        CONTINUE
            CALL ASSERT(CPT.EQ.DDLDN)
 128      CONTINUE
 127    CONTINUE
C
        DO 240 N=1,NNOP
          DO 241 M=1,N
            DO 242 I=1,DDLDN
              DO 243 J=1,DDLDN
                TMP1 = SIG((KPG-1)*6+1)*PFF(I,N,1)*PFF(J,M,1)
     &                + SIG((KPG-1)*6+2)*PFF(I,N,2)*PFF(J,M,2)
     &                + SIG((KPG-1)*6+3)*PFF(I,N,3)*PFF(J,M,3)
     &                + SIG((KPG-1)*6+4)*(PFF(I,N,1)*PFF(J,M,2)
     &                           +PFF(I,N,2)*PFF(J,M,1))/RAC2
     &                + SIG((KPG-1)*6+5)*(PFF(I,N,1)*PFF(J,M,3)
     &                           +PFF(I,N,3)*PFF(J,M,1))/RAC2
     &                + SIG((KPG-1)*6+6)*(PFF(I,N,3)*PFF(J,M,2)
     &                           +PFF(I,N,2)*PFF(J,M,3))/RAC2
C              STOCKAGE EN TENANT COMPTE DE LA SYMETRIE
                IF (M.EQ.N) THEN
                  J1 = I
                ELSE
                  J1 = DDLDN
                ENDIF
                IF (J.LE.J1) THEN
                  DO 244 IJ = 1,NDIM
                    KKD = (DDLS*(N-1)+(I-1)*NDIM+IJ-1)
     &                   * (DDLS*(N-1)+(I-1)*NDIM+IJ) /2
                    KK = KKD + DDLS*(M-1)+(J-1)*NDIM+IJ
                    MATUU(KK) = MATUU(KK) + TMP1*JAC
 244              CONTINUE
                ENDIF
 243          CONTINUE
 242        CONTINUE
 241      CONTINUE
 240    CONTINUE
 
 100  CONTINUE

      END
