      SUBROUTINE NMEVCC(SDDISC,NUMINS,DEFICO,RESOCO,IECHEC,
     &                  IEVDAC)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 16/04/2012   AUTEUR ABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT     NONE
      CHARACTER*24 DEFICO,RESOCO
      INTEGER      IECHEC,IEVDAC,NUMINS
      CHARACTER*19 SDDISC
C
C ----------------------------------------------------------------------
C
C ROUTINE MECA_NON_LINE (ALGORITHME - EVENEMENTS)
C
C DETECTION DE L'EVENEMENT COLLISION
C
C ----------------------------------------------------------------------
C
C
C IN  SDDISC : SD DISCRETISATION TEMPORELLE
C IN  NUMINS : NUMERO D'INSTANT
C IN  DEFICO : SD DE DEFINITION DU CONTACT
C IN  RESOCO : SD DE RESOLUTION DU CONTACT
C IN  IECHEC : OCCURRENCE DE L'ECHEC
C OUT IEVDAC : VAUT IECHEC SI EVENEMENT DECLENCHE
C                   0 SINON
C
C --- DEBUT DECLARATIONS NORMALISEES JEVEUX ----------------------------
C
      INTEGER            ZI
      COMMON  / IVARJE / ZI(1)
      REAL*8             ZR
      COMMON  / RVARJE / ZR(1)
      COMPLEX*16         ZC
      COMMON  / CVARJE / ZC(1)
      LOGICAL            ZL
      COMMON  / LVARJE / ZL(1)
      CHARACTER*8        ZK8
      CHARACTER*16                ZK16
      CHARACTER*24                          ZK24
      CHARACTER*32                                    ZK32
      CHARACTER*80                                              ZK80
      COMMON  / KVARJE / ZK8(1) , ZK16(1) , ZK24(1) , ZK32(1) , ZK80(1)
C
C --- FIN DECLARATIONS NORMALISEES JEVEUX ------------------------------
C
      INTEGER      IFM,NIV
      CHARACTER*24 CTEVCO
      INTEGER      JCTEVC
      REAL*8       ETACIN,ETACFI,ETACOL
      REAL*8       FINCOL,SUBDUR
      INTEGER      CFDISI,NTPC,IPTC
      REAL*8       DIINST,INSTAM,INSTAP
      LOGICAL      LEVENT
      INTEGER      IBID
      CHARACTER*8  K8BID
      INTEGER      CFMMVD,ZEVEN
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
      CALL INFDBG('MECA_NON_LINE',IFM,NIV)
C
C --- AFFICHAGE
C
      IF (NIV.GE.2) THEN
        WRITE (IFM,*) '<MECANONLINE> ... COLLISION'
      ENDIF
C
C --- INITIALISATIONS
C
      IEVDAC = 0
      LEVENT = .FALSE.
      CALL ASSERT(NUMINS.GT.0)
C
C --- PARAMETRES
C
      NTPC   = CFDISI(DEFICO,'NTPC')
      INSTAP = DIINST(SDDISC,NUMINS)
      INSTAM = DIINST(SDDISC,NUMINS-1)
      CALL UTDIDT('L'   ,SDDISC,'ECHE',IECHEC,'SUBD_DUREE'   ,
     &            SUBDUR,IBID  ,K8BID )
C
C --- ACCES OBJETS DU CONTACT
C
      CTEVCO = RESOCO(1:14)//'.EVENCO'
      CALL JEVEUO(CTEVCO,'E',JCTEVC)
      ZEVEN  = CFMMVD('ZEVEN')
C
C --- STATUT DE LA COLLISION
C
      DO 10 IPTC = 1,NTPC
        ETACIN = ZR(JCTEVC+ZEVEN*(IPTC-1)+1-1)
        ETACFI = ZR(JCTEVC+ZEVEN*(IPTC-1)+2-1)
        ETACOL = ZR(JCTEVC+ZEVEN*(IPTC-1)+3-1)
        FINCOL = ZR(JCTEVC+ZEVEN*(IPTC-1)+4-1)
C
C ----- COLLISION EN COURS CE N'EST PAS UN EVENEMENT
C
        IF (ETACOL.EQ.1.D0) THEN
          IF (INSTAP.GT.FINCOL) THEN
            ETACOL = 0.D0
            FINCOL = 0.D0
          ENDIF
        ELSEIF (ETACOL.EQ.0.D0) THEN
C
C ------- COLLISION QUI S'ACTIVE: C'EST UN EVENEMENT
C
          IF ((ETACIN.EQ.1.D0).OR.(ETACFI.EQ.1.D0)) THEN
            ETACOL = 1.D0
            FINCOL = INSTAM+SUBDUR
            LEVENT = .TRUE.
          ENDIF
        ENDIF
        ZR(JCTEVC+ZEVEN*(IPTC-1)+3-1) = ETACOL
        ZR(JCTEVC+ZEVEN*(IPTC-1)+4-1) = FINCOL
  10  CONTINUE
C
C --- ACTIVATION EVENEMENT
C
      IF (LEVENT) THEN
        IEVDAC = IECHEC
        CALL U2MESS('I','MECANONLINE6_41')
      ENDIF
C
      CALL JEDEMA()
      END
