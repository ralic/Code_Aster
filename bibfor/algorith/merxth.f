      SUBROUTINE MERXTH(MODELE,CHARGE,INFCHA,CARELE,MATE,INST,CHTNI,
     &                  MERIGI,COMPOR,TMPCHI,TMPCHF)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 11/09/2002   AUTEUR VABHHTS J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.

C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.

C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
      IMPLICIT NONE
      CHARACTER*24 MODELE,CHARGE,INFCHA,CARELE,INST,CHTNI,MERIGI,MATE,
     &             COMPOR,TMPCHI,TMPCHF
C ----------------------------------------------------------------------
C CALCUL DES MATRICES TANGENTES ELEMENTAIRES
C EN THERMIQUE NON LINEAIRE
C  - TERMES DE VOLUME
C  - TERMES DE SURFACE DUS AUX CONDITIONS LIMITES

C IN  MODELE  : NOM DU MODELE
C IN  CHARGE  : LISTE DES CHARGES
C IN  INFCHA  : INFORMATIONS SUR LES CHARGEMENTS
C IN  CARELE  : CHAMP DE CARA_ELEM
C IN  MATE    : MATERIAU CODE
C IN  INST    : CARTE CONTENANT LA VALEUR DE L'INSTANT
C IN  CHTNI   : IEME ITEREE DU CHAMP DE TEMPERATURE
C IN  COMPOR  : COMPORTEMENT (POUR LE SECHAGE)
C IN  TMPCHI  : CHAMP DE TEMPERAT. A T    (POUR LE CALCUL DE D-SECHAGE)
C IN  TMPCHI  : CHAMP DE TEMPERAT. A T+DT (POUR LE CALCUL DE D-SECHAGE)
C OUT MERIGI  : MATRICES ELEMENTAIRES

C --- DEBUT DECLARATIONS NORMALISEES JEVEUX ----------------------------

      CHARACTER*32 JEXNUM,JEXNOM,JEXATR
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)

C --- FIN DECLARATIONS NORMALISEES JEVEUX ------------------------------
      CHARACTER*8 NOMCHA,LPAIN(9),LPAOUT(1),K8BID
      CHARACTER*16 OPTION
      CHARACTER*24 LIGREL(2),LCHIN(9),LCHOUT(1),LIGRCH
      CHARACTER*24 CHGEOM,CHCARA(15)
      INTEGER IRET,NCHAR,ILIRES,JMER,ICHA,JCHAR,JINF
      LOGICAL EXICAR,EXIGEO
C ----------------------------------------------------------------------
      INTEGER NBCHMX
      PARAMETER (NBCHMX=4)
      INTEGER NBOPT(NBCHMX),NLIGR(NBCHMX),K
      CHARACTER*6 NOMOPR(NBCHMX),NOMOPF(NBCHMX),NOMCHP(NBCHMX)
      CHARACTER*7 NOMPAR(NBCHMX),NOMPAF(NBCHMX)
      DATA NOMCHP/'.COEFH','.FLUNL','.RAYO','.HECHP'/
      DATA NOMOPR/'COEF_R','      ','RAYO_R','PARO_R'/
      DATA NOMOPF/'COEF_F','FLUXNL','RAYO_F','PARO_F'/
      DATA NOMPAR/'PCOEFHR','       ','PRAYONR','PHECHPR'/
      DATA NOMPAF/'PCOEFHF','PFLUXNL','PRAYONF','PHECHPF'/
      DATA NBOPT/4,4,4,4/
      DATA NLIGR/1,1,1,2/
C DEB ------------------------------------------------------------------
      CALL JEMARQ()
      CALL JEEXIN(CHARGE,IRET)
      IF (IRET.NE.0) THEN
        CALL JELIRA(CHARGE,'LONMAX',NCHAR,K8BID)
        CALL JEVEUO(CHARGE,'L',JCHAR)
      ELSE
        NCHAR = 0
      END IF

      CALL MEGEOM(MODELE,'       ',EXIGEO,CHGEOM)
      CALL MECARA(CARELE,EXICAR,CHCARA)

      CALL JEEXIN(MERIGI,IRET)
      IF (IRET.EQ.0) THEN
        MERIGI = '&&METRIG.LISTE_RESU'
        CALL MEMARE('V',MERIGI(1:8),MODELE(1:8),MATE,CARELE,'MTAN_THER')
        CALL WKVECT(MERIGI,'V V K24',NCHAR*NBCHMX+1,JMER)
      ELSE
        CALL JEVEUO(MERIGI,'E',JMER)
      END IF

      LIGREL(1) = MODELE(1:8)//'.MODELE'

      ILIRES = 0

      IF (MODELE.NE.'        ') THEN
        LPAIN(1) = 'PGEOMER'
        LCHIN(1) = CHGEOM
        LPAIN(2) = 'PMATERC'
        LCHIN(2) = MATE
        LPAIN(3) = 'PTEMPSR'
        LCHIN(3) = INST
        LPAIN(4) = 'PTEMPEI'
        LCHIN(4) = CHTNI
        LPAIN(5) = 'PCOMPOR'
        LCHIN(5) = COMPOR
        LPAIN(6) = 'PTMPCHI'
        LCHIN(6) = TMPCHI
        LPAIN(7) = 'PTMPCHF'
        LCHIN(7) = TMPCHF
        LPAOUT(1) = 'PMATTTR'
        LCHOUT(1) = MERIGI(1:8)//'.ME001'
        OPTION = 'MTAN_RIGI_MASS'
        ILIRES = ILIRES + 1
        CALL CODENT(ILIRES,'D0',LCHOUT(1) (12:14))
        CALL CALCUL('S',OPTION,LIGREL(1),7,LCHIN,LPAIN,1,LCHOUT,LPAOUT,
     &              'V')
        CALL EXISD('CHAMP_GD',LCHOUT(1) (1:19),IRET)
        IF (IRET.NE.0) THEN
          ZK24(JMER-1+ILIRES) = LCHOUT(1)
          CALL JEECRA(MERIGI,'LONUTI',ILIRES,' ')
        ELSE
          ILIRES = ILIRES - 1
        END IF
      END IF

      IF (NCHAR.GT.0) THEN
        CALL JEVEUO(INFCHA,'L',JINF)
        DO 20 ICHA = 1,NCHAR
          IF (ZI(JINF+NCHAR+ICHA).GT.0) THEN
            NOMCHA = ZK24(JCHAR+ICHA-1) (1:8)
            LIGREL(2) = NOMCHA//'.CHTH.LIGRE'
            LPAIN(1) = 'PGEOMER'
            LCHIN(1) = CHGEOM
            LPAIN(3) = 'PTEMPSR'
            LCHIN(3) = INST
            LPAIN(4) = 'PTEMPEI'
            LCHIN(4) = CHTNI
            LPAOUT(1) = 'PMATTTR'
            LCHOUT(1) = MERIGI(1:8)//'.ME001'

            DO 10 K = 1,NBCHMX
              LCHIN(2) = ZK24(JCHAR+ICHA-1) (1:8)//'.CHTH'//NOMCHP(K)//
     &                   '.DESC'
              CALL JEEXIN(LCHIN(2),IRET)
              IF (IRET.GT.0) THEN
                IF (ZI(JINF+NCHAR+ICHA).EQ.1) THEN
                  OPTION = 'MTAN_THER_'//NOMOPR(K)
                  LPAIN(2) = NOMPAR(K)
                ELSE IF (ZI(JINF+NCHAR+ICHA).EQ.2 .OR.
     &                   ZI(JINF+NCHAR+ICHA).EQ.3) THEN
                  OPTION = 'MTAN_THER_'//NOMOPF(K)
                  LPAIN(2) = NOMPAF(K)
                END IF
                ILIRES = ILIRES + 1
                CALL CODENT(ILIRES,'D0',LCHOUT(1) (12:14))
                CALL CALCUL('S',OPTION,LIGREL(NLIGR(K)),NBOPT(K),LCHIN,
     &                      LPAIN,1,LCHOUT,LPAOUT,'V')
                CALL EXISD('CHAMP_GD',LCHOUT(1) (1:19),IRET)
                IF (IRET.NE.0) THEN
                  ZK24(JMER-1+ILIRES) = LCHOUT(1)
                  CALL JEECRA(MERIGI,'LONUTI',ILIRES,K8BID)
                ELSE
                  ILIRES = ILIRES - 1
                END IF
              END IF
   10       CONTINUE
          END IF

   20   CONTINUE

      END IF
   30 CONTINUE
C FIN ------------------------------------------------------------------
      CALL JEDEMA()
      END
