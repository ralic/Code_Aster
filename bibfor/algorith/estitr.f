      SUBROUTINE ESTITR(NBM,AMORI,MASGI,EPS,TTRANS,NPF,NPFMAX,TEXT,IER)
      IMPLICIT NONE
C-----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 16/05/2000   AUTEUR KXBADNG T.KESTENS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C-----------------------------------------------------------------------
C DESCRIPTION : ESTIMATION DE LA DUREE DU TRANSITOIRE
C -----------
C               APPELANT : TRANSI
C
C-------------------   DECLARATION DES VARIABLES   ---------------------
C
C ARGUMENTS
C ---------
      INTEGER    NBM
      REAL*8     AMORI(*), MASGI(*), EPS, TTRANS
      INTEGER    NPF, NPFMAX
      REAL*8     TEXT(*)
      INTEGER    IER
C
C VARIABLES LOCALES
C -----------------
      INTEGER    I
      REAL*8     TTEMP, TEMPF, KSIOMM, KSIOMC
C
C FONCTIONS INTRINSEQUES
C ----------------------
C     INTRINSIC  ABS, DBLE, LOG
C
C ROUTINES EXTERNES
C -----------------
C     EXTERNAL   UTMESS
C
C-------------------   DEBUT DU CODE EXECUTABLE    ---------------------
C
      IER = 0
C
C--1. RECHERCHE DE 2*KSI0*OMEGA0 MINIMUM POUR LES DIFFERENTS MODES
C
      KSIOMM = 1.0D+10
      DO 10 I = 1, NBM
         KSIOMC = DBLE(ABS(AMORI(I)/MASGI(I)))
         IF ( KSIOMC.LT.KSIOMM ) KSIOMM = KSIOMC
  10  CONTINUE
C
C--2. CALCUL DU TRANSITOIRE POUR UNE PRECISION EPS INDEPENDANTE DU JEU
C
      TTRANS = - LOG(EPS) / KSIOMM
C
C--3. CALCUL DU TEMPS DE TRANSITOIRE MINIMUM EN FONCTION DU PAS DE
C     DISCRETISATION DE LA FORCE EXTERIEURE
C
      TTEMP = 0.0D0
      DO 20 I = 1, 15
         TEMPF = ( (2.0D0 ** (DBLE(I))) - 1.0D0 ) * (TEXT(2) - TEXT(1))
         IF ( TTRANS.LT.TEMPF ) THEN
            GO TO 21
         ELSE
            TTEMP = TEMPF
         ENDIF
  20  CONTINUE
  21  CONTINUE
C
      NPF = I - 1
      TTRANS = TTEMP
      IF ( I.EQ.16 )
     &   CALL UTMESS('A','ESTITR','DUREE DU TRANSITOIRE LIMITEE '//
     &               'PAR LES POSSIBILITES DE LA TRANSFORMEE '//
     &               'DE FOURIER RAPIDE ')
C
      IF ( TTRANS.GT.TEXT(NPFMAX) ) THEN
         IER = 1
         CALL UTMESS('A','ESTITR','DUREE DE LA SIMULATION TEMPORELLE '//
     &               'INSUFFISANTE POUR PASSAGE DU TRANSITOIRE')
      ENDIF
C
C --- FIN DE ESTITR.
      END
