      SUBROUTINE DLARCH (RESULT,NEQ, ISTOC, IARCHI, TEXTE,
     &                    ALARM, IFM, TEMPS,
     &                    NBTYAR, TYPEAR, MASSE,
     &                    DEPL, VITE, ACCE,
     &                    FEXTE,FAMOR,FLIAI )
C ---------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 03/07/2012   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C
C  ARCHIVAGE DANS L'OBJET CHAMNO DU CHAMP DE DEPLACEMENT,DE VITESSE
C  ET/OU D'ACCELERATION ISSU D'UN CALCUL TRANSITOIRE DIRECT
C
C ---------------------------------------------------------------------
C  IN  : NEQ       : NOMBRE D'EQUATIONS
C  IN  : ISTOC     : PILOTAGE DU STOCKAGE DES RESULTATS
C  IN  : IARCHI    : PILOTAGE DE L'ARCHIVAGE DES RESULTATS
C  IN  : TEXTE     : COMMENTAIRE A IMPRIMER
C  IN  : ALARM     : EMISSION D'ALARME SI >0
C  IN  : TEMPS     : INSTANT D'ARCHIVAGE
C  IN  : NBTYAR    : TAILLE DE TYPEAR
C  IN  : TYPEAR    : TABLEAU INDIQUANT SI ON ARCHIVE LES DIFFERENTS
C                    CHAMPS (DEPL, VIT ET ACC) (NBTYAR)
C  IN  : MASSE     : NOM DE LA MATRICE DE MASSE
C  IN  : DEPL      : TABLEAU DES DEPLACEMENTS
C  IN  : VITE      : TABLEAU DES VITESSES
C  IN  : ACCE      : TABLEAU DES ACCELERATIONS
C
C     ------------------------------------------------------------------
C CORPS DU PROGRAMME
      IMPLICIT   NONE
C DECLARATION PARAMETRES D'APPELS
C
      INCLUDE 'jeveux.h'
      INTEGER NEQ, ISTOC, IARCHI, ALARM, IFM
      INTEGER NBTYAR

      REAL*8 DEPL(NEQ), VITE(NEQ), ACCE(NEQ)
      REAL*8 FEXTE(NEQ),FAMOR(NEQ),FLIAI(NEQ)
      REAL*8 TEMPS

      CHARACTER*8 MASSE
      CHARACTER*8 RESULT
      CHARACTER*16 TYPEAR(NBTYAR)
      CHARACTER*(*) TEXTE


C
      INTEGER IAUX, JAUX, ITYPE
      INTEGER LGCOMM
      INTEGER LXLGUT
      CHARACTER*8 K8B

      CHARACTER*24 CHAMNO
C
C====
C 1. PREALABLES
C====


C 1.2. ==> INSTANT

      IF ( ISTOC.EQ.0 ) THEN
        IARCHI = IARCHI + 1
        CALL RSADPA(RESULT,'E',1,'INST',IARCHI,0,IAUX,K8B)
        ZR(IAUX) = TEMPS
      ELSE
        CALL RSADPA(RESULT,'L',1,'INST',IARCHI,0,IAUX,K8B)
        TEMPS = ZR(IAUX)
      ENDIF

C 1.3. ==> COMMENTAIRE
C
      LGCOMM = LXLGUT(TEXTE)
C
C====
C 2. STOCKAGE DES CHAMPS DESIGNES
C====
C
      DO 21 , ITYPE = 1, NBTYAR
C
        IF ( TYPEAR(ITYPE).NE.'    ' ) THEN
C
          CALL RSEXCH(RESULT,TYPEAR(ITYPE),IARCHI,CHAMNO,IAUX)
          IF ( IAUX.EQ.0 ) THEN
            IF ( ALARM.GT.0 ) THEN
              CALL U2MESK('A','ALGORITH2_64',1,CHAMNO)
            ENDIF
            GO TO 21
          ELSE IF ( IAUX.EQ.100 ) THEN
            CALL VTCREM(CHAMNO,MASSE,'G','R')
          ELSE
            CALL ASSERT(.FALSE.)
          ENDIF

          CHAMNO(20:24)  = '.VALE'
          CALL JEVEUO(CHAMNO,'E',JAUX)
C
          IF ( TYPEAR(ITYPE).EQ.'DEPL' ) THEN
            DO 211 , IAUX = 1, NEQ
              ZR(JAUX+IAUX-1) = DEPL(IAUX)
  211       CONTINUE
          ELSEIF ( TYPEAR(ITYPE).EQ.'VITE' ) THEN
            DO 212 , IAUX = 1, NEQ
              ZR(JAUX+IAUX-1) = VITE(IAUX)
  212       CONTINUE
          ELSEIF ( TYPEAR(ITYPE).EQ.'ACCE' ) THEN
            DO 213 ,  IAUX = 1, NEQ
              ZR(JAUX+IAUX-1) = ACCE(IAUX)
  213       CONTINUE
          ELSEIF ( TYPEAR(ITYPE).EQ.'FORC_EXTE' ) THEN
            DO 214 ,  IAUX = 1, NEQ
              ZR(JAUX+IAUX-1) = FEXTE(IAUX)
  214       CONTINUE
          ELSEIF ( TYPEAR(ITYPE).EQ.'FORC_AMOR' ) THEN
            DO 215 ,  IAUX = 1, NEQ
              ZR(JAUX+IAUX-1) = FAMOR(IAUX)
  215       CONTINUE
          ELSEIF ( TYPEAR(ITYPE).EQ.'FORC_LIAI' ) THEN
            DO 216 ,  IAUX = 1, NEQ
              ZR(JAUX+IAUX-1) = FLIAI(IAUX)
  216       CONTINUE
          ENDIF
C
          CALL JELIBE(CHAMNO)
          CALL RSNOCH(RESULT,TYPEAR(ITYPE),IARCHI)
C
        ENDIF
C
   21 CONTINUE
C
      ISTOC = 1
C
      IF ( LGCOMM.EQ.0 ) THEN
        WRITE(IFM,2000) (TYPEAR(IAUX),IAUX=1,NBTYAR), IARCHI, TEMPS
      ELSE
        WRITE(IFM,2001) TEXTE(1:LGCOMM),
     &                  (TYPEAR(IAUX),IAUX=1,NBTYAR), IARCHI, TEMPS
      ENDIF
 2000 FORMAT(1P,3X,'CHAMP(S) STOCKE(S):',3(1X,A4),3(1X,A9),
     &             ' NUME_ORDRE:',I8,' INSTANT:',D12.5)
 2001 FORMAT(1P,3X,A,1X,'CHAMP(S) STOCKE(S):',3(1X,A4),3(1X,A9),
     &             ' NUME_ORDRE:',I8,' INSTANT:',D12.5)
C
      END
