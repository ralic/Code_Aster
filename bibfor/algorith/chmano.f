      SUBROUTINE CHMANO(NOMA  ,IZONE ,NEWGEO,DEFICO,RESOCO,
     &                  ILIDEB,NZLIAI)
C     
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 22/12/2009   AUTEUR ABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C TOLE CRP_20
C
      IMPLICIT     NONE
      INTEGER      IZONE
      INTEGER      ILIDEB,NZLIAI
      CHARACTER*8  NOMA
      CHARACTER*24 NEWGEO
      CHARACTER*24 DEFICO,RESOCO
C      
C ----------------------------------------------------------------------
C
C ROUTINE CONTACT (METHODES DISCRETES - APPARIEMENT - MAIT/ESCL)
C
C RECHERCHE DE LA MAILLE LA PLUS PROCHE CONNAISSANT LE NOEUD LE PLUS
C PROCHE
C
C ----------------------------------------------------------------------
C
C
C IN  IZONE  : NUMERO DE LA ZONE DE CONTACT ACTUELLE
C IN  ILIDEB : INDICE DE LA PREMIERE LIAISON POUR LA ZONE
C IN  NOMA   : NOM DU MAILLAGE
C IN  NEWGEO : GEOMETRIE ACTUALISEE EN TENANT COMPTE DU CHAMP DE
C              DEPLACEMENTS COURANT
C IN  DEFICO : SD DE DEFINITION DU CONTACT (ISSUE D'AFFE_CHAR_MECA)
C IN  RESOCO : SD DE TRAITEMENT NUMERIQUE DU CONTACT
C OUT NZLIAI : NOMBRE FINAL DE LIAISON SUR LA ZONE (ON A ENLEVE LES
C               LIAISONS CORRESPONDANT AUX SANS_GROUP_NO)
C
C -------------- DEBUT DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C ---------------- FIN DECLARATIONS NORMALISEES JEVEUX -----------------
C
      CHARACTER*24 APMEMO
      INTEGER      JAPMEM
      INTEGER      CFMMVD,ZAPME
      INTEGER      CFDISI,ITEMAX
      REAL*8       CFDISR,EPSMAX,TOLEOU      
      INTEGER      IFM,NIV
      INTEGER      NTNOE,NBNOM,NBDDLE,NDIM
      INTEGER      POSNOM
      INTEGER      NUMMAM,TYPAPP,POSAPP
      INTEGER      POSNSM(9)
      INTEGER      KE,ISURFE,JDECE,NBNOE,NUMNOE,CODRET
      REAL*8       JEUPM,JEU
      REAL*8       NORM(3),TAU1(3),TAU2(3)
      REAL*8       COORDE(3),COORDP(3)
      REAL*8       TAU1M(3),TAU2M(3),KSI1,KSI2
      REAL*8       COEFNO(9),NOOR,R8PREM
      CHARACTER*4  TYPNOE
      CHARACTER*8  NOMNOE
      INTEGER      POSMAM,POSNOE,ILIAI
      LOGICAL      MMINFL,DIRAPP,LDIST
      LOGICAL      CFDISL,LCTFD,LCTF3D      
      CHARACTER*24 TANGCO
      INTEGER      JTANGO     
      REAL*8       MMINFR,DIR(3)     
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()      
      CALL INFDBG('CONTACT',IFM,NIV)
C   
C --- AFFICHAGE
C
      IF (NIV.GE.2) THEN
        WRITE (IFM,*) '<CONTACT> APPARIEMENT MAIT/ESCL - MAILLE LA '//
     &                 'PLUS PROCHE' 
      ENDIF   
C
C --- LECTURE DES STRUCTURES DE DONNEES DE CONTACT
C
      APMEMO = RESOCO(1:14)//'.APMEMO'
      TANGCO = RESOCO(1:14)//'.TANGCO'       
      CALL JEVEUO(APMEMO,'L',JAPMEM)
      CALL JEVEUO(TANGCO,'E',JTANGO)   
      ZAPME  = CFMMVD('ZAPME')                    
C
C --- INFOS SUR LA CHARGE DE CONTACT
C
      LCTFD  = CFDISL(DEFICO,'FROT_DISCRET')
      LCTF3D = CFDISL(DEFICO,'FROT_3D'     ) 
C
C --- NUMEROS DE SURFACE ESCLAVE
C
      CALL CFZONE(DEFICO,IZONE ,'ESCL',ISURFE)
C
C --- NOMBRE DE NOEUDS DE LA SURFACE ESCLAVE
C
      CALL CFNBSF(DEFICO,ISURFE,'NOEU',NBNOE ,JDECE )            
C      
C --- INITIALISATION DES VARIABLES
C    
      NTNOE  = CFDISI(DEFICO,'NTNOE' )
      NDIM   = CFDISI(DEFICO,'NDIM'  )  
      DIR(1) = 0.D0
      DIR(2) = 0.D0
      DIR(3) = 0.D0            
C
C --- INFOS GENERIQUES POUR L'ALGORITHME D'APPARIEMENT
C         
      TOLEOU = MMINFR(DEFICO,'TOLE_PROJ_EXT' ,IZONE )
      EPSMAX = CFDISR(DEFICO,'PROJ_NEWT_RESI')
      ITEMAX = CFDISI(DEFICO,'PROJ_NEWT_ITER') 
      DIRAPP = MMINFL(DEFICO,'TYPE_APPA_FIXE',IZONE ) 
      IF (DIRAPP) THEN
        DIR(1) = MMINFR(DEFICO,'TYPE_APPA_DIRX',IZONE)
        DIR(2) = MMINFR(DEFICO,'TYPE_APPA_DIRY',IZONE)      
        DIR(3) = MMINFR(DEFICO,'TYPE_APPA_DIRZ',IZONE) 
      ENDIF     
C
C --- BOUCLE SUR LES NOEUDS ESCLAVES
C
      IF (NIV.GE.2) THEN
        WRITE (IFM,*) '<CONTACT> APPARIEMENT MAIT/ESCL - '//
     &                 'PROJECTION' 
      ENDIF 
C
C --- PREMIERE LIAISON DE LA ZONE
C
      ILIAI  = ILIDEB
C
C --- BOUCLE SUR LES NOEUDS ESCLAVES
C
      DO 80 KE = 1,NBNOE
C
C --- POSITION ET NUMERO DU NOEUD ESCLAVE
C
        POSNOE  = JDECE + KE
        CALL CFPOSM(NOMA  ,DEFICO,'NOEU',1     ,POSNOE,
     &              NUMNOE,CODRET)
        IF (CODRET.LT.0) THEN
          CALL ASSERT(.FALSE.) 
        ENDIF
C
C --- LE NOEUD DOIT-IL ETRE APPARIE ?
C      
        TYPAPP = ZI(JAPMEM+ZAPME*(POSNOE-1)+1-1)
C
C --- NOEUD ESCLAVE A APPARIER
C     
        IF (TYPAPP.EQ.3) THEN
C        
C --- INDICE DU NOEUD MAITRE LE PLUS PROCHE
C 
          POSNOM = ZI(JAPMEM+ZAPME*(POSNOE-1)+2-1)  
          CALL ASSERT(POSNOM.NE.0)      
C
C --- INFOS SUR NOEUD ESCLAVE
C
          CALL CFCARN(NOMA  ,DEFICO,RESOCO,NEWGEO,POSNOE,
     &                NBDDLE,COORDE,TYPNOE,NOMNOE)
          IF (TYPNOE.NE.'ESCL') THEN
            CALL ASSERT(.FALSE.)
          ENDIF
C
C --- PROJECTION SUR LA MAILLE MAITRE
C --- CALCUL DU JEU MINIMUM, DES COORDONNEES DU POINT PROJETE
C --- ET DES DEUX VECTEURS TANGENTS
C         
          CALL CFPROJ(NOMA  ,DEFICO,NEWGEO,POSNOM,ITEMAX,
     &                EPSMAX,TOLEOU,DIRAPP,DIR   ,COORDE,
     &                POSMAM,NUMMAM,JEUPM ,KSI1  ,KSI2  ,
     &                TAU1M ,TAU2M ,LDIST ) 
C
C --- LE NOEUD ESCLAVE EST EXCLU (VA AU COIN !)
C
          IF (.NOT.LDIST) THEN
            TYPAPP = -3
            GOTO 65
          ENDIF      
C
C --- CARACTERISTIQUES DE LA MAILLE MAITRE
C      
          CALL CFPOSN(NOMA  ,DEFICO,POSMAM,POSNSM,NBNOM )
C
C --- COORDONNEES PROJECTION DU NOEUD ESCLAVE SUR LA MAILLE MAITRE
C
          CALL CFCOOR(NOMA  ,DEFICO,NEWGEO,POSMAM,KSI1  ,
     &                KSI2  ,COORDP)             
C          
C --- RE-DEFINITION BASE TANGENTE SUIVANT OPTIONS    
C
          CALL CFTANR(NOMA  ,NDIM  ,DEFICO,RESOCO,IZONE ,
     &                POSNOE,'MAIL',POSMAM,NUMMAM,KSI1  ,
     &                KSI2  ,TAU1M ,TAU2M ,TAU1  ,TAU2  )    
C
C --- CALCUL DE LA NORMALE INTERIEURE
C
          CALL MMNORM(NDIM  ,TAU1  ,TAU2  ,NORM  ,NOOR)   
          IF (NOOR.LE.R8PREM()) THEN
            CALL U2MESK('F','CONTACT3_26',1,NOMNOE)
          ENDIF
C 
C --- CALCUL DU JEU SUIVANT NORMALE 
C
          CALL CFNEWJ(NDIM  ,COORDE,COORDP,NORM  ,JEU   )
C
C --- CALCUL DES COEFFICIENTS DE LA RELATION LINEAIRE SUR NOEUDS MAITRES
C    
          CALL CFRELI(NOMA  ,NUMMAM,NBNOM ,KSI1  ,KSI2  ,
     &                COEFNO)
C
C --- PARAMETRES DE L'APPARIEMENT 
C     
          POSAPP = POSMAM
          TYPAPP = 2              
C
C --- AJOUT DE LA LIAISON NODALE
C
          CALL CFADDM(RESOCO,NTNOE ,LCTFD ,LCTF3D,POSNOE,
     &                ILIAI ,NDIM  ,NBDDLE,NBNOM ,POSNSM,
     &                COEFNO,TAU1  ,TAU2  ,NORM  ,JEU   )
   65     CONTINUE
C
C --- STOCKAGE PARAMETRES DE L'APPARIEMENT 
C    
          CALL CFPARM(NOMA  ,DEFICO,RESOCO,ILIAI ,TYPAPP,
     &                POSNOE,POSNOM,POSAPP)
C
C --- LIAISON SUIVANTE
C     
          IF (TYPAPP.EQ.2) THEN
            ILIAI  = ILIAI + 1                   
            NZLIAI = NZLIAI + 1          
          ENDIF       
        ENDIF
  80  CONTINUE 
C
      CALL JEDEMA()
      END
