      SUBROUTINE PARMTR(NP4,NFOUR,NBM,TTRANS,AMOR,PULS,PULSD,S0,Z0,
     &                  OMEGAF,ZA4,ZA5)
      IMPLICIT NONE
C-----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 13/12/2006   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C-----------------------------------------------------------------------
C DESCRIPTION : CALCUL DES PARAMETRES (ALGO METHODE INTEGRALE)
C -----------   POUR PASSAGE DU TRANSITOIRE
C
C               APPELANT : CALTRA
C
C-------------------   DECLARATION DES VARIABLES   ---------------------
C
C ARGUMENTS
C ---------
      INTEGER     NP4, NFOUR, NBM
      REAL*8      TTRANS, AMOR(*), PULS(*), PULSD(*)
      COMPLEX*16  S0(*), Z0(*)
      REAL*8      OMEGAF(*)
      COMPLEX*16  ZA4(NP4,*), ZA5(NP4,*)
C
C VARIABLES LOCALES
C -----------------
      INTEGER     I, J, N2M1
      REAL*8      KSI0, ZERO
      COMPLEX*16  ZOMEGA
C
C FONCTIONS INTRINSEQUES
C ----------------------
C     INTRINSIC   DCMPLX, EXP, SQRT
C
C
C-------------------   DEBUT DU CODE EXECUTABLE    ---------------------
C
      ZERO = 0.0D0
      N2M1 = (NFOUR/2) - 1
C
      DO 10 I = 1, NBM
         KSI0 = AMOR(I)/(2.0D0*PULS(I))
         IF ( KSI0.GE.1.0D0 ) THEN
            KSI0 =  0.99D0
            CALL U2MESS('A','ALGORITH9_74')
         ELSE IF ( KSI0.LE.-1.0D0 ) THEN
            KSI0 = -0.99D0
            CALL U2MESS('A','ALGORITH9_75')
         ENDIF
         PULSD(I) = PULS(I) * SQRT( 1.0D0 - KSI0*KSI0 )
         S0(I)    = DCMPLX( -KSI0*PULS(I), PULSD(I) )
         Z0(I)    = EXP( S0(I)*TTRANS )
C
         DO 11 J = 1, N2M1
            ZOMEGA = DCMPLX( ZERO, OMEGAF(J) )
            ZA4(J,I) = (Z0(I) - EXP(ZOMEGA*TTRANS)) /  (S0(I) - ZOMEGA)
            ZA5(J,I) = (Z0(I) - EXP(-ZOMEGA*TTRANS)) /  (S0(I) + ZOMEGA)
  11     CONTINUE
C
  10  CONTINUE
C
C --- FIN DE PARMTR.
      END
