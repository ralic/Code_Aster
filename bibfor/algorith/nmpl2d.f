       SUBROUTINE  NMPL2D(NNO,NPG,IPOIDS,IVF,IDFDE,GEOM,TYPMOD,
     &                    OPTION,IMATE,COMPOR,LGPG,CRIT,
     &                    INSTAM,INSTAP,
     &                    TM,TP,TREF,
     &                    HYDRM,HYDRP,
     &                    SECHM,SECHP,SREF,
     &                    IRRAM,IRRAP,
     &                    NZ,PHASM,PHASP,
     &                    DEPLM,DEPLP,EPAM,EPAP,DEFANE,
     &                    ANGMAS,
     &                    SIGM,VIM,
     &                    MATSYM,DFDI,DEF,SIGP,VIP,MATUU,VECTU,CODRET)

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 03/11/2004   AUTEUR CIBHHPD L.SALMONA 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE PBADEL P.BADEL
C TOLE CRP_21

       IMPLICIT NONE
C
       INTEGER     NNO,NPG,IMATE,LGPG,NZ,CODRET,COD(9),IPOIDS,IVF,IDFDE
C
       CHARACTER*8   TYPMOD(*)
       CHARACTER*16  OPTION, COMPOR(4)
C
       REAL*8        INSTAM,INSTAP
       REAL*8        ANGMAS(3)
       REAL*8        GEOM(2,NNO), CRIT(3), TM(NNO),TP(NNO)
       REAL*8        HYDRM(NNO), HYDRP(NNO), SECHM(NNO), SECHP(NNO)
       REAL*8        IRRAM(NNO),IRRAP(NNO)
       REAL*8        PHASM(NZ,NPG),PHASP(NZ,NPG),TREF,SREF
       REAL*8        DEPLM(1:2,1:NNO),DEPLP(1:2,1:NNO),DFDI(NNO,2)
       REAL*8        DEF(4,NNO,2),EPAM(*),EPAP(*)
       REAL*8        SIGM(4,NPG),SIGP(4,NPG)
       REAL*8        VIM(LGPG,NPG),VIP(LGPG,NPG)
       REAL*8        MATUU(*),VECTU(2,NNO)
C
       LOGICAL       DEFANE,MATSYM
C.......................................................................
C
C     BUT:  CALCUL  DES OPTIONS RIGI_MECA_TANG, RAPH_MECA ET FULL_MECA
C           EN HYPO-ELASTICITE EN 2D
C.......................................................................
C IN  NNO     : NOMBRE DE NOEUDS DE L'ELEMENT
C IN  NPG     : NOMBRE DE POINTS DE GAUSS
C IN  POIDSG  : POIDS DES POINTS DE GAUSS
C IN  VFF     : VALEUR  DES FONCTIONS DE FORME
C IN  DFDE    : DERIVEE DES FONCTIONS DE FORME ELEMENT DE REFERENCE
C IN  DFDK    : DERIVEE DES FONCTIONS DE FORME ELEMENT DE REFERENCE
C IN  GEOM    : COORDONEES DES NOEUDS
C IN  TYPMOD  : TYPE DE MODEELISATION
C IN  OPTION  : OPTION DE CALCUL
C IN  IMATE   : MATERIAU CODE
C IN  COMPOR  : COMPORTEMENT
C IN  LGPG    : "LONGUEUR" DES VARIABLES INTERNES POUR 1 POINT DE GAUSS
C               CETTE LONGUEUR EST UN MAJORANT DU NBRE REEL DE VAR. INT.
C IN  CRIT    : CRITERES DE CONVERGENCE LOCAUX
C IN  INSTAM  : INSTANT PRECEDENT
C IN  INSTAP  : INSTANT DE CALCUL
C IN  TM      : TEMPERATURE AUX NOEUDS A L'INSTANT PRECEDENT
C IN  TP      : TEMPERATURE AUX NOEUDS A L'INSTANT DE CALCUL
C IN  TREF    : TEMPERATURE DE REFERENCE
C IN  HYDRM   : HYDRATATION AUX POINTS DE GAUSS EN T-
C IN  HYDRP   : HYDRATATION AUX POINTS DE GAUSS EN T+
C IN  SECHM   : SECHAGE AUX NOEUDS EN T-
C IN  SECHP   : SECHAGE AUX NOEUDS EN T+
C IN  SREF    : SECHAGE DE REFERENCE
C IN  IRRAM   : IRRADIATION AUX NOEUDS EN T-
C IN  IRRAP   : IRRADIATION AUX NOEUDS EN T+
C IN  PHASM   : PHASE METALLURGIQUE A L'INSTANT PRECEDENT
C IN  PHASP   : PHASE METALLURGIQUE A L'INSTANT DE CALCUL
C IN  DEPLM   : DEPLACEMENT A L'INSTANT PRECEDENT
C IN  DEPLP   : INCREMENT DE DEPLACEMENT
C IN  EPAM    : DEFORMATIONS ANELASTIQUES A L'INSTANT PRECEDENT
C IN  EPAP    : DEFORMATIONS ANELASTIQUES A L'INSTANT DU CALCUL
C IN  DEFANE  : VRAI SI LES DEFORMATIONS ANELASTIQUES SONT PRESENTES
C IN  ANGMAS  : LES TROIS ANGLES DU MOT_CLEF MASSIF (AFFE_CARA_ELEM)
C IN  SIGM    : CONTRAINTES A L'INSTANT PRECEDENT
C IN  VIM     : VARIABLES INTERNES A L'INSTANT PRECEDENT
C OUT DFDI    : DERIVEE DES FONCTIONS DE FORME  AU DERNIER PT DE GAUSS
C OUT DEF     : PRODUIT DER. FCT. FORME PAR F   AU DERNIER PT DE GAUSS
C OUT SIGP    : CONTRAINTES DE CAUCHY (RAPH_MECA ET FULL_MECA)
C OUT VIP     : VARIABLES INTERNES    (RAPH_MECA ET FULL_MECA)
C OUT MATUU   : MATRICE DE RIGIDITE PROFIL (RIGI_MECA_TANG ET FULL_MECA)
C OUT VECTU   : FORCES NODALES (RAPH_MECA ET FULL_MECA)
C.......................................................................
C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ---------------------
      INTEGER  ZI
      COMMON  / IVARJE / ZI(1)
      REAL*8             ZR
      COMMON  / RVARJE / ZR(1)
      COMPLEX*16         ZC
      COMMON  / CVARJE / ZC(1)
      LOGICAL            ZL
      COMMON  / LVARJE / ZL(1)
      CHARACTER*8        ZK8
      CHARACTER*16                ZK16
      CHARACTER*24                          ZK24
      CHARACTER*32                                    ZK32
      CHARACTER*80                                              ZK80
      COMMON  / KVARJE / ZK8(1) , ZK16(1) , ZK24(1) , ZK32(1) , ZK80(1)
C --------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ---------------------

      LOGICAL GRAND,AXI

      INTEGER KPG,KK,KKD,N,I,M,J,J1,KL

      REAL*8 DSIDEP(6,6),F(3,3),EPS(6),DEPS(6),R,SIGMA(6),SIGN(6)
      REAL*8 POIDS,TEMPM,TEMPP,TMP,EPSANP(6),EPSANM(6),SIG(6)
      REAL*8 HYDRGM,HYDRGP,SECHGM,SECHGP,IRRAGM,IRRAGP,ELGEOM(10,9)

      REAL*8  RAC2,R8VIDE

C - INITIALISATION

      RAC2   = SQRT(2.D0)
      GRAND  = .FALSE.
      AXI    = TYPMOD(1) .EQ. 'AXIS'

C - CALCUL DES ELEMENTS GEOMETRIQUES SPECIFIQUES AU COMPORTEMENT

      CALL LCEGEO(NNO,NPG,IPOIDS,IVF,IDFDE,GEOM,TYPMOD,OPTION,
     &            IMATE,COMPOR,LGPG,ELGEOM)

C - INITIALISATION CODES RETOURS
      DO 1955 KPG=1,NPG
         COD(KPG)=0
1955  CONTINUE
C
C - CALCUL POUR CHAQUE POINT DE GAUSS
C
      DO 800 KPG=1,NPG
C
C - CALCUL DE LA TEMPERATURE AU POINT DE GAUSS
C - DE L HYDRATATION ET DU SECHAGE AU POINT DE GAUSS
C - DE L IRRADIATION ET DES DEFORMATIONS ANELASTIQUES 
C   AU POINT DE GAUSS
C
        TEMPM = 0.D0
        TEMPP = 0.D0
        HYDRGM = HYDRM(KPG)
        HYDRGP = HYDRP(KPG)
        SECHGM = 0.D0
        SECHGP = 0.D0
        IRRAGM = 0.D0
        IRRAGP = 0.D0
        DO 5 J = 1,6
          EPSANM(J)=0.D0
          EPSANP(J)=0.D0
  5     CONTINUE
C
        DO 10 N=1,NNO
          IF (DEFANE) THEN
            DO 15 J = 1,4
              EPSANM(J)=EPSANM(J)+EPAM(4*(N-1)+J)
     &                            *ZR(IVF+N+(KPG-1)*NNO-1)
              EPSANP(J)=EPSANP(J)+EPAP(4*(N-1)+J)
     &                            *ZR(IVF+N+(KPG-1)*NNO-1)
 15         CONTINUE
          END IF
          TEMPM = TEMPM + TM(N)*ZR(IVF+N+(KPG-1)*NNO-1)
          TEMPP = TEMPP + TP(N)*ZR(IVF+N+(KPG-1)*NNO-1)
          SECHGM = SECHGM + SECHM(N)*ZR(IVF+N+(KPG-1)*NNO-1)
          SECHGP = SECHGP + SECHP(N)*ZR(IVF+N+(KPG-1)*NNO-1)
          IRRAGM = IRRAGM + IRRAM(N)*ZR(IVF+N+(KPG-1)*NNO-1)
          IRRAGP = IRRAGP + IRRAP(N)*ZR(IVF+N+(KPG-1)*NNO-1)
 10     CONTINUE
C
C - CALCUL DES ELEMENTS GEOMETRIQUES
C
C     CALCUL DE DFDI,F,EPS,DEPS,R(EN AXI) ET POIDS
C
        DO 20 J = 1,6
          EPS (J)=0.D0
          DEPS(J)=0.D0
20      CONTINUE
C
        CALL NMGEOM(2,NNO,AXI,GRAND,GEOM,KPG,IPOIDS,IVF,IDFDE,
     &              DEPLM,POIDS,DFDI,F,EPS,R)
C
C     CALCUL DE DEPS
C
        CALL NMGEOM(2,NNO,AXI,GRAND,GEOM,KPG,IPOIDS,IVF,IDFDE,
     &              DEPLP,POIDS,DFDI,F,DEPS,R)
C
C      CALCUL DES PRODUITS SYMETR. DE F PAR N,
        DO 40 N=1,NNO
          DO 30 I=1,2
            DEF(1,N,I) =  F(I,1)*DFDI(N,1)
            DEF(2,N,I) =  F(I,2)*DFDI(N,2)
            DEF(3,N,I) =  0.D0
            DEF(4,N,I) = (F(I,1)*DFDI(N,2) + F(I,2)*DFDI(N,1))/RAC2
 30       CONTINUE
 40     CONTINUE
C
C      TERME DE CORRECTION (3,3) AXI QUI PORTE EN FAIT SUR LE DDL 1
        IF (AXI) THEN
          DO 50 N=1,NNO
            DEF(3,N,1) = F(3,3)*ZR(IVF+N+(KPG-1)*NNO-1)/R
 50       CONTINUE
        ENDIF
C
        DO 60 I=1,3
          SIGN(I) = SIGM(I,KPG)
 60     CONTINUE
        SIGN(4) = SIGM(4,KPG)*RAC2

C
C - LOI DE COMPORTEMENT
       CALL NMCOMP(2,TYPMOD,IMATE,COMPOR,CRIT,
     &             INSTAM,INSTAP,
     &             TEMPM,TEMPP,TREF,
     &             HYDRGM,HYDRGP,
     &             SECHGM,SECHGP,SREF,
     &             IRRAGM,IRRAGP,
     &             EPS,DEPS,
     &             SIGN,VIM(1,KPG),
     &             OPTION,
     &             EPSANM,EPSANP,
     &             NZ,PHASM(1,KPG),PHASP(1,KPG),
     &             R8VIDE(),R8VIDE(),
     &             ANGMAS,
     &             ELGEOM(1,KPG),
     &             SIGMA,VIP(1,KPG),DSIDEP,COD(KPG))
C
       IF(COD(KPG).EQ.1) THEN
         GOTO 1956
       ENDIF

C
C - CALCUL DE LA MATRICE DE RIGIDITE
C
        IF ( OPTION(1:10) .EQ. 'RIGI_MECA_'
     &  .OR. OPTION(1: 9) .EQ. 'FULL_MECA'    ) THEN
C
          IF (MATSYM) THEN
            DO 160 N=1,NNO
              DO 150 I=1,2
                DO 151,KL=1,4
                  SIG(KL)=0.D0
                  SIG(KL)=SIG(KL)+DEF(1,N,I)*DSIDEP(1,KL)
                  SIG(KL)=SIG(KL)+DEF(2,N,I)*DSIDEP(2,KL)
                  SIG(KL)=SIG(KL)+DEF(3,N,I)*DSIDEP(3,KL)
                  SIG(KL)=SIG(KL)+DEF(4,N,I)*DSIDEP(4,KL)
151             CONTINUE
                DO 140 J=1,2
                  DO 130 M=1,N
                    IF (M.EQ.N) THEN
                      J1 = I
                    ELSE
                      J1 = 2
                    ENDIF
C
C                   RIGIDITE ELASTIQUE
                    TMP=0.D0
                    TMP=TMP+SIG(1)*DEF(1,M,J)
                    TMP=TMP+SIG(2)*DEF(2,M,J)
                    TMP=TMP+SIG(3)*DEF(3,M,J)
                    TMP=TMP+SIG(4)*DEF(4,M,J)
C
C                   STOCKAGE EN TENANT COMPTE DE LA SYMETRIE
                    IF (J.LE.J1) THEN
                       KKD = (2*(N-1)+I-1) * (2*(N-1)+I) /2
                       KK = KKD + 2*(M-1)+J
                       MATUU(KK) = MATUU(KK) + TMP*POIDS
                    END IF
C
 130              CONTINUE
 140            CONTINUE
 150          CONTINUE
 160        CONTINUE
          ELSE
            DO 560 N=1,NNO
              DO 550 I=1,2
                DO 551,KL=1,4
                  SIG(KL)=0.D0
                  SIG(KL)=SIG(KL)+DEF(1,N,I)*DSIDEP(1,KL)
                  SIG(KL)=SIG(KL)+DEF(2,N,I)*DSIDEP(2,KL)
                  SIG(KL)=SIG(KL)+DEF(3,N,I)*DSIDEP(3,KL)
                  SIG(KL)=SIG(KL)+DEF(4,N,I)*DSIDEP(4,KL)
551             CONTINUE
                DO 540 J=1,2
                  DO 530 M=1,NNO
C
C                   RIGIDITE ELASTIQUE
                    TMP=0.D0
                    TMP=TMP+SIG(1)*DEF(1,M,J)
                    TMP=TMP+SIG(2)*DEF(2,M,J)
                    TMP=TMP+SIG(3)*DEF(3,M,J)
                    TMP=TMP+SIG(4)*DEF(4,M,J)
C
C                   STOCKAGE SANS SYMETRIE
                    KK = 2*NNO*(2*(N-1)+I-1) + 2*(M-1)+J
                    MATUU(KK) = MATUU(KK) + TMP*POIDS
C
 530              CONTINUE
 540            CONTINUE
 550          CONTINUE
 560        CONTINUE
          ENDIF
        ENDIF
C
C
C - CALCUL DE LA FORCE INTERIEURE ET DES CONTRAINTES DE CAUCHY
C
        IF(OPTION(1:9).EQ.'FULL_MECA'.OR.
     &     OPTION(1:9).EQ.'RAPH_MECA') THEN
C
          DO 230 N=1,NNO
            DO 220 I=1,2
              DO 210 KL=1,4
                VECTU(I,N)=VECTU(I,N)+DEF(KL,N,I)*SIGMA(KL)*POIDS
 210          CONTINUE
 220        CONTINUE
 230      CONTINUE
C
          DO 310 KL=1,3
            SIGP(KL,KPG) = SIGMA(KL)
 310      CONTINUE
          SIGP(4,KPG) = SIGMA(4)/RAC2
C
        ENDIF
800   CONTINUE
1956  CONTINUE
C - SYNTHESE DES CODES RETOURS
      CALL CODERE(COD,NPG,CODRET)
      END
