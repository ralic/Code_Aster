      SUBROUTINE MPJEFT(CORRES)
C ----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 29/09/2006   AUTEUR VABHHTS J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2002  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C
C   MPJEFT : CREATION TABLE DE CORRESPONDANCE POUR PROJ_MESU_MODAL
C
C   IN/OUT : CORRES : NOM DE LA TABLE
C
C ----------------------------------------------------------------------
      IMPLICIT NONE
      CHARACTER*16 CORRES

C --- DEBUT DECLARATIONS NORMALISEES JEVEUX ----------------------------

      CHARACTER*32 JEXNUM,JEXNOM
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)

C --- FIN DECLARATIONS NORMALISEES JEVEUX ------------------------------

      CHARACTER*4 CDIM1,EXIVOL,EXIPOU,EXIRDM,EXIPLA,EXICOQ
      CHARACTER*8 K8B,NOMA1,NOMA2,MODEL1,MODEL2,LABK8
      CHARACTER*8 LISIN1,LISIN2,LISOU1,LISOU2
      CHARACTER*16 TYMOCL(2),MOTCLE(2)
      INTEGER NDIM,NCAS,N1,NBOCC,IOCC,IE,IBID,NBNO2,NUNO1,NUNO2
      INTEGER IAGNO2,IDECAL,INO,IREP
      INTEGER LLIN1,LLIN2,LLOU2,NBNCAL,NBNLIS
      INTEGER KK,NBNMES,NBNO1,IAGNO1,IACONO,IACONB,IACONU,IACOCF,I,NBOLD
      INTEGER NDECAL, IT1, IT2, IT3, IFRES, IUNIFI
      REAL*8 COEF,RBID
C----------------------------------------------------------------------
C       DESCRIPTION DE LA SD CORRESP_2_MAILLA : CORRES
C       --------------------------------------------------------
C          (CORRESPONDANCE ENTRE LES 2 MODELES MODEL1 ET MODEL2)
C  DESCRIPTION DE LA SD CORRESP_2_MAILLA :

C  CORRESP_2_MAILLA (K16)  ::= RECORD
C     '.PJEF_NO'  : S V K8 LONG=2
C     '.PJEF_NB'  : S V I  LONG=NNO2 (= NB_NO(M2))
C     '.PJEF_NU'  : S V I  LONG=LONT
C     '.PJEF_CF'  : S V R  LONG=LONT


C     '.PJEF_NO' (1) : NOM DU MAILLAGE 1 : M1
C     '.PJEF_NO' (2) : NOM DU MAILLAGE 2 : M2

C     '.PJEF_NB' (INO2) : NOMBRE DE NOEUDS DE M1 QUI DOIVENT SERVIR
C                          A L'INTERPOLATION DU NOEUD INO2 DE M2

C     '.PJEF_NU' : CONTIENT LES NUMEROS DES NOEUDS DE M1 SERVANT A
C                  L'INTERPOLATION DES NOEUDS DE M2 (MIS BOUT A BOUT)
C     '.PJEF_CF' : CONTIENT LES COEFFICIENTS POUR LES NOEUDS DE
C                  M1 SERVANT A L'INTERPOLATION DES NOEUDS DE M2
C                  (MIS BOUT A BOUT)

C    EXEMPLE D'UTILISATION :
C      ON VEUT SAVOIR COMMENT INTERPOLER INO2 A PARTIR DU MAILLAGE M1
C      SOIT NBNO1='.PJEF_NB'(INO2)
C      SOIT DECAL= SOMME POUR INO<INO2 DE '.PJEF_NB'(INO)
C      VAL2(INO2)=0
C      DO I=1,NBNO1
C        NUNO1='.PJEF_NU' (DECAL+I)
C        COEFR='.PJEF_CF' (DECAL+I)
C        VAL2(INO2)=VAL2(INO2)+COEFR*VAL1(UNO1)
C      END DO
C----------------------------------------------------------------------
C DEB ------------------------------------------------------------------
      CALL JEMARQ()

      CALL GETVID('MODELE_CALCUL','MODELE',1,1,1,MODEL1,N1)
      CALL GETVID('MODELE_MESURE','MODELE',1,1,1,MODEL2,N1)

      CALL DISMOI('F','NOM_MAILLA',MODEL1,'MODELE',IBID,NOMA1,IE)
      CALL DISMOI('F','NOM_MAILLA',MODEL2,'MODELE',IBID,NOMA2,IE)

      CALL DISMOI('F','NB_NO_MAILLA',NOMA2,'MAILLAGE',
     &                   NBNMES,K8B,IBID)

      CALL DISMOI('F','NB_NO_MAILLA',NOMA1,'MAILLAGE',
     &                   NBNCAL,K8B,IBID)

C     DETERMINATION DE LA DIMENSION DE L'ESPACE (NDIM) :
C     --------------------------------------------------------
      CALL DISMOI('F','Z_CST',NOMA1,'MAILLAGE',IBID,CDIM1,IE)

      IF (CDIM1.EQ.'OUI') THEN
        NDIM = 2
      ELSE
        NDIM = 3
      END IF

C     PAR DEFAUT ON FAIT UNE INTERPOLATION SUR TOUS LES NOEUDS
C     DU MAILLAGE 2 (MAILLAGE EXPERIMENTAL)
C     ON SURCHARGE ENSUITE LES CORRESPONDANCES MANUELLES

C     DETERMINATION DU CAS DE FIGURE : 2D, 3D OU 2.5D : NCAS
C     --------------------------------------------------------
      IF (NDIM.EQ.2) THEN
        NCAS = 2
      ELSE IF (NDIM.EQ.3) THEN
        CALL DISMOI('F','EXI_ELTVOL',MODEL1,'MODELE',IBID,EXIVOL,IE)
        IF (EXIVOL.EQ.'OUI') THEN
          NCAS = 3
        ELSE
          NCAS = 4
        END IF
      END IF
      CALL DISMOI('F','EXI_RDM',MODEL1,'MODELE',IBID,EXIRDM,IE)
      IREP = 0
      CALL DISMOI('F','EXI_PLAQUE',MODEL1,'MODELE',IBID,EXIPLA,IE)
      CALL DISMOI('F','EXI_COQ3D',MODEL1,'MODELE',IBID,EXICOQ,IE)
      IF (EXIPLA.EQ.'OUI') IREP = 1
      IF (EXICOQ.EQ.'OUI') IREP = 1
      IF ((EXIRDM.EQ.'OUI') .AND. (IREP.EQ.0)) NCAS = 0
      CALL DISMOI('F','EXI_POUX',MODEL1,'MODELE',IBID,EXIPOU,IE)
      IF (EXIPOU.EQ.'OUI') NCAS = 5


C POUR L'INSTANT ON SE LIMITE AU CAS OU IL N'Y A PAS DE MELANGE
C  ELEMENT VOLUMIQUE / ELEMENT SURFACIQUE / ELEMENT LINEIQUE

      IF (NCAS.EQ.2) THEN
        CALL PJ2DCO('TOUT',MODEL1,MODEL2,0,0,0,0,' ',' ',CORRES,
     &              .FALSE.,RBID)
      ELSE IF (NCAS.EQ.3) THEN
        CALL PJ3DCO('TOUT',MODEL1,MODEL2,0,0,0,0,' ',' ',CORRES,
     &              .FALSE.,RBID)
      ELSE IF (NCAS.EQ.4) THEN
        CALL PJ4DCO('TOUT',MODEL1,MODEL2,0,0,0,0,' ',' ',CORRES,
     &              .FALSE.,RBID)
      ELSE IF (NCAS.EQ.5) THEN
        CALL PJ5DCO(MODEL1,MODEL2,CORRES)
      ELSE

        CALL U2MESS('A','ALGORITH6_19')
C
C     CREATION DE LA SD CORRESP_2_MAILLA : CORRES (PROCHE)
C     ---------------------------------------------------
        CALL WKVECT(CORRES//'.PJEF_NO','V V K8',2,IACONO)
        CALL WKVECT(CORRES//'.PJEF_NB','V V I',NBNMES,IACONB)
        CALL WKVECT(CORRES//'.PJEF_NU','V V I',NBNMES,IACONU)
        CALL WKVECT(CORRES//'.PJEF_CF','V V R',NBNMES,IACOCF)

        ZK8(IACONO-1 +1)=NOMA1
        ZK8(IACONO-1 +2)=NOMA2

        DO 10, INO=1,NBNMES
          ZI(IACONB-1 +INO)=1
          ZR(IACOCF-1 +INO)=1.D0
 10     CONTINUE
C
C CREATION DES LISTES DE NOEUDS UTILISEES
C ***************************************
C
C       -> LISTE DES NOEUDS DE MESURE
        LISIN1 = 'LINNOMES'
C
C       -> LISTE DES NOEUDS DU MODELE NUMERIQUE
        LISIN2 = 'LINNOCAL'
C
C       -> LISTE TRIEE DES NOEUDS DE MESURE
        LISOU1 = 'LOUNOMES'
C
C       -> LISTE TRIEE DES NOEUDS NUMERIQUES EN VIS-A-VIS DE LISOU1
        LISOU2 = 'LOUNOCAL'
C
C ALLOCATION ET REMPLISSAGE DES LISTES DE NOEUDS DE MESURE
C ********************************************************
        CALL WKVECT (LISIN1, 'V V K8', NBNMES, LLIN1)
C
        DO 60 INO = 1,NBNMES
          CALL JENUNO (JEXNUM (NOMA2//'.NOMNOE',INO),
     &                 ZK8(LLIN1-1+INO))
 60     CONTINUE
C
C ALLOCATION ET REMPLISSAGE DE LA LISTE DES NOEUDS NUMERIQUES
C ***********************************************************
        CALL WKVECT (LISIN2, 'V V K8', NBNCAL, LLIN2)
C
        DO 61 INO = 1,NBNCAL
          CALL JENUNO (JEXNUM (NOMA1//'.NOMNOE',INO),
     &                 ZK8(LLIN2-1+INO))
 61     CONTINUE
C
C RECHERCHE DES NOEUDS EN VIS-A-VIS
C *********************************
        CALL PACOA2 (LISIN1, LISIN2, NBNMES, NBNCAL, NOMA2, NOMA1,
     &               LISOU1, LISOU2, NBNLIS)
C
        CALL JEVEUO (LISOU2,'L',LLOU2)
C
        IF (NBNLIS .NE. NBNMES) THEN
          CALL U2MESS('F','ALGORITH6_20')
        ENDIF

        DO 62 INO = 1,NBNMES
          CALL JENONU (JEXNOM (NOMA1//'.NOMNOE',ZK8(LLOU2-1+INO)),
     &                 ZI(IACONU-1+INO))
 62     CONTINUE
C
        CALL JEDETR(LISIN1)
        CALL JEDETR(LISIN2)
        CALL JEDETR(LISOU1)
        CALL JEDETR(LISOU2)

      END IF

C     SURCHARGE SI CORRESPONDANCE MANUELLE

      CALL GETFAC('CORR_MANU',NBOCC)
      IF (NBOCC.GT.0) THEN

C       CORRESPONDANCE MANUELLE SUR CERTAINS NOEUDS
C       ------------------------

        DO 30 IOCC = 1,NBOCC

C        -- RECUPERATION DE LA LISTE DE NOEUDS LNO1 (CALCUL):
C        ----------------------------------------------
          MOTCLE(1) = 'NOEU_CALCUL'
          TYMOCL(1) = 'NOEUD'
          CALL RELIEM(MODEL1,NOMA1,'NU_NOEUD','CORR_MANU',IOCC,1,
     &                MOTCLE,TYMOCL,'&&PJEFTE.LINONU1',NBNO1)
          IF (NBNO1 .EQ. 0) THEN
            CALL U2MESS('F','ALGORITH6_21')
          ENDIF
          CALL JEVEUO('&&PJEFTE.LINONU1','L',IAGNO1)


C        -- RECUPERATION DE LA LISTE DE NOEUDS LNO2 (MESURE):
C        ----------------------------------------------
          MOTCLE(1) = 'NOEU_MESURE'
          TYMOCL(1) = 'NOEUD'
          CALL RELIEM(MODEL2,NOMA2,'NU_NOEUD','CORR_MANU',IOCC,1,
     &                MOTCLE,TYMOCL,'&&PJEFTE.LINONU2',NBNO2)
          IF (NBNO2 .EQ. 0) THEN
            CALL U2MESS('F','ALGORITH6_22')
          ENDIF
          CALL JEVEUO('&&PJEFTE.LINONU2','L',IAGNO2)

C        -- REACTUALISATION DU CORRESP_2_MAILLA POUR IOCC
C        ----------------------------------------------
          IF (NBNO1 .NE. NBNO2) THEN
            CALL U2MESS('F','ALGORITH6_23')
          ENDIF
          IF (NBNO1 .GT. 1) THEN
            CALL U2MESS('F','ALGORITH6_24')
          ENDIF
C        -- RECUPERATION DES NUMEROS DES NOEUDS
          NUNO1 = ZI(IAGNO1)
          NUNO2 = ZI(IAGNO2)

C        -- RECUPERATION DES ADRESSES SD CORRES
C     ------------------------------------------------
          CALL JEVEUO(CORRES//'.PJEF_NB','E',IACONB)
          CALL JEVEUO(CORRES//'.PJEF_NU','E',IACONU)
          CALL JEVEUO(CORRES//'.PJEF_CF','E',IACOCF)

C RECUPERATION DE LA DIMENSION DE PJEF_NU OU PJEF_CF
          IDECAL = 0
          DO 51 I = 1,NBNMES
            IDECAL = IDECAL + ZI(IACONB-1 +I)
 51       CONTINUE
C CREATION DES VECTEURS TAMPON
          CALL WKVECT('TAMPON1','V V I',NBNMES,IT1)
          CALL WKVECT('TAMPON2','V V I',IDECAL,IT2)
          CALL WKVECT('TAMPON3','V V R',IDECAL,IT3)
C RECOPIE DE PJEF_NB PJEF_NU ET PJEF_CF
          DO 52 I = 1,NBNMES
            ZI(IT1-1 +I) =  ZI(IACONB-1 +I)
 52       CONTINUE
          DO 53 I = 1,IDECAL
            ZI(IT2-1 +I) =  ZI(IACONU-1 +I)
            ZR(IT3-1 +I) =  ZR(IACOCF-1 +I)
 53       CONTINUE
C DESTRUCTION DES ANCIENS PJEF_NB PJEF_NU ET PJEF_CF
          CALL JEDETR(CORRES//'.PJEF_NB')
          CALL JEDETR(CORRES//'.PJEF_NU')
          CALL JEDETR(CORRES//'.PJEF_CF')

C        -- DECALAGE DES DONNEES
          IDECAL = 0
          DO 31 I = 1,NUNO2 - 1
            IDECAL = IDECAL + ZI(IT1-1 +I)
 31       CONTINUE

          NBOLD = ZI(IT1-1 +NUNO2)
          ZI(IT1-1 + NUNO2)=1
          ZI(IT2-1 + IDECAL+1)= NUNO1
          ZR(IT3-1 + IDECAL+1)= 1.D0

          NDECAL = 0
          DO 32 I = NUNO2 +1, NBNMES
            NDECAL = NDECAL + ZI(IT1-1 + I)
 32       CONTINUE

          DO 33 I = 1, NDECAL
            ZI(IT2-1+IDECAL+1+I) = ZI(IT2-1+IDECAL+NBOLD+I)
            ZR(IT3-1+IDECAL+1+I) = ZR(IT3-1+IDECAL+NBOLD+I)
 33       CONTINUE

C CREATION DES NOUVEAUX OBJETS
          NBOLD = IDECAL + 1 + NDECAL
          CALL WKVECT(CORRES//'.PJEF_NB','V V I',NBNMES,IACONB)
          CALL WKVECT(CORRES//'.PJEF_NU','V V I',NBOLD,IACONU)
          CALL WKVECT(CORRES//'.PJEF_CF','V V R',NBOLD,IACOCF)
C RECOPIE DES DONNEES
          DO 54 I = 1,NBNMES
            ZI(IACONB-1 + I) =  ZI(IT1-1 +I)
 54       CONTINUE
          DO 55 I = 1,NBOLD
            ZI(IACONU-1 + I) =  ZI(IT2-1 +I)
            ZR(IACOCF-1 + I) =  ZR(IT3-1 +I)
 55       CONTINUE

C DESTRUCTION DES VECTEURS TAMPON
          CALL JEDETR('TAMPON1')
          CALL JEDETR('TAMPON2')
          CALL JEDETR('TAMPON3')


          CALL JEDETR('&&PJEFTE.LINONU1')
          CALL JEDETR('&&PJEFTE.LINONU2')

   30   CONTINUE

      END IF

C     AFFICHAGE DE LA CORRESPONDANCE DES NOEUDS
      IFRES = IUNIFI ('MESSAGE')
      WRITE(IFRES,'(A)') 'TABLE DE CORRESPONDANCE DES NOEUDS '
      WRITE(IFRES,'(A)') '----------------------------------'
      CALL JEVEUO(CORRES//'.PJEF_NB','L',IACONB)
      CALL JEVEUO(CORRES//'.PJEF_NU','L',IACONU)
      CALL JEVEUO(CORRES//'.PJEF_CF','L',IACOCF)
C
      KK = 0
      DO 40 I = 1,NBNMES
        CALL JENUNO(JEXNUM(NOMA2//'.NOMNOE',I),LABK8)
        WRITE(IFRES,1000) LABK8
        NBNO1 = ZI(IACONB-1 +I)
        DO 41 IOCC = 1,NBNO1
          KK = KK + 1
          CALL JENUNO(JEXNUM(NOMA1//'.NOMNOE',ZI(IACONU-1+KK)),LABK8)
          COEF = ZR(IACOCF-1 +KK)
          WRITE(IFRES,1001) LABK8,COEF
   41   CONTINUE
   40 CONTINUE

 1000 FORMAT (' NOEUD MESURE :  ',A8)
 1001 FORMAT ('       ',A8,'    POIDS : ',D12.5)

      CALL JEDEMA()
      END
