      SUBROUTINE VEDITH(MODELE,CHARGE,INFCHA,INST,TYPESE,NOPASE,VECELZ)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 11/09/2002   AUTEUR VABHHTS J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.

C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.

C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C ----------------------------------------------------------------------
C     CALCUL DES VECTEURS ELEMENTAIRES DES ELEMENTS DE LAGRANGE

C IN  MODELE  : NOM DU MODELE
C IN  CHARGE  : LISTE DES CHARGES
C IN  INFCHA  : INFORMATIONS SUR LES CHARGEMENTS
C IN  INST    : CARTE CONTENANT LA VALEUR DE L'INSTANT
C IN  TYPESE  : TYPE DE SENSIBILITE
C                0 : CALCUL STANDARD, NON DERIVE
C                SINON : DERIVE (VOIR NTTYSE)
C . POUR UN CALCUL DE DERIVEE, ON A LES DONNEES SUIVANTES :
C IN  NOPASE  : NOM DU PARAMETRE SENSIBLE
C OUT/JXOUT VECELZ  : VECT_ELEM
C ----------------------------------------------------------------------
      IMPLICIT   NONE

C 0.1. ==> ARGUMENTS

      INTEGER TYPESE
      CHARACTER*(*) NOPASE
      CHARACTER*24 MODELE,CHARGE,INFCHA,INST,VECELZ

C 0.2. ==> COMMUNS
C --- DEBUT DECLARATIONS NORMALISEES JEVEUX ----------------------------
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C --- FIN DECLARATIONS NORMALISEES JEVEUX ------------------------------

C 0.3. ==> VARIABLES LOCALES

      CHARACTER*6 NOMPRO
      PARAMETER (NOMPRO='VEDITH')

      CHARACTER*8 NOMCH0,NOMCHS,NOMCHA
      CHARACTER*8 K8BID,VECELE,LPAIN(3),PAOUT,NEWNOM
      CHARACTER*16 OPTION
      CHARACTER*24 LIGRCH,LIGRCS,LCHIN(3),RESUEL,CHGEOM
      INTEGER IAUX,IRET,NCHAR,ILVE,JLVE,JINF,JCHAR,ICHA,JNOLI,NBNOLI
      INTEGER NUMDI
      INTEGER EXICHA
      LOGICAL EXIGEO,BIDON

C ----------------------------------------------------------------------

      CALL JEMARQ()
      NEWNOM = '.0000000'
      BIDON = .TRUE.

      CALL JEEXIN(CHARGE,IRET)
      IF (IRET.NE.0) THEN
        CALL JELIRA(CHARGE,'LONMAX',NCHAR,K8BID)
        IF (NCHAR.NE.0) THEN
          BIDON = .FALSE.
          CALL JEVEUO(CHARGE,'L',JCHAR)
          CALL JEVEUO(INFCHA,'L',JINF)
        END IF
      END IF

      VECELE = '&&'//NOMPRO
      RESUEL = '&&'//NOMPRO//'.???????'

C     -- ALLOCATION DU VECT_ELEM :
C     -----------------------------
      CALL DETRSD('VECT_ELEM',VECELE)
      CALL MEMARE('V',VECELE,MODELE(1:8),' ',' ','CHAR_THER')
      IF (BIDON) THEN
        CALL WKVECT(VECELE//'.LISTE_RESU','V V K24',1,JLVE)
        CALL JEECRA(VECELE//'.LISTE_RESU','LONUTI',0,K8BID)
        GO TO 40
      ELSE
        CALL WKVECT(VECELE//'.LISTE_RESU','V V K24',NCHAR,JLVE)
        CALL JEECRA(VECELE//'.LISTE_RESU','LONUTI',0,K8BID)
      END IF

      CALL MEGEOM(MODELE(1:8),ZK24(JCHAR) (1:8),EXIGEO,CHGEOM)

      PAOUT = 'PVECTTR'
      LPAIN(2) = 'PGEOMER'
      LCHIN(2) = CHGEOM
      LPAIN(3) = 'PTEMPSR'

      LCHIN(3) = INST

      ILVE = 0
      DO 30 ICHA = 1,NCHAR
        NUMDI = ZI(JINF+ICHA)
        IF (NUMDI.GT.0) THEN
          NOMCH0 = ZK24(JCHAR+ICHA-1) (1:8)
          LIGRCH = NOMCH0//'.CHTH.LIGRE'
          IF (TYPESE.NE.0) THEN
            CALL PSRENC(NOMCH0,NOPASE,NOMCHS,EXICHA)

C           ATTENTION : DANS LE CAS D'UN CALCUL DE SENSIBILITE, ON
C           UTILISERA LE LIGREL DU CHARGEMENT STANDARD. POUR CELA ON
C           REMPLACE LE NOM JEVEUX STOCKE DANS LE CHARGEMENT SENSIBLE
C           PAR LE NOM STANDARD, LIGRCH. EVIDEMMENT, A LA FIN DU
C           TRAITEMENT, ON REMETTRA LE VRAI NOM, SAUVEGARDE DANS LIGRCS.

            IF (EXICHA.EQ.0) THEN
              CALL JELIRA(NOMCHS//'.CHTH.CIMPO.NOLI','LONMAX',NBNOLI,
     &                    K8BID)
              CALL JEVEUO(NOMCHS//'.CHTH.CIMPO.NOLI','E',JNOLI)
              LIGRCS = ZK24(JNOLI)
              DO 10,IAUX = 0,NBNOLI - 1
                ZK24(JNOLI+IAUX) = LIGRCH
   10         CONTINUE
              NOMCHA = NOMCHS
            END IF
          ELSE
            EXICHA = 0
            NOMCHA = NOMCH0
          END IF
          IF (EXICHA.EQ.0) THEN
            LCHIN(1) = NOMCHA//'.CHTH.CIMPO.DESC'
            IF (NUMDI.EQ.1) THEN
              OPTION = 'THER_DDLI_R'
              LPAIN(1) = 'PDDLIMR'
            ELSE IF (NUMDI.EQ.2) THEN
              OPTION = 'THER_DDLI_F'
              LPAIN(1) = 'PDDLIMF'
            ELSE IF (NUMDI.EQ.3) THEN
              OPTION = 'THER_DDLI_F'
              LPAIN(1) = 'PDDLIMF'
            END IF

            ILVE = ILVE + 1

            CALL GCNCO2(NEWNOM)
            RESUEL(10:16) = NEWNOM(2:8)
            CALL CORICH('E',RESUEL,ICHA,IAUX)

            CALL CALCUL('S',OPTION,LIGRCH,3,LCHIN,LPAIN,1,RESUEL,PAOUT,
     &                  'V')
            ZK24(JLVE-1+ILVE) = RESUEL

C           ON REMET LE VRAI NOM DU LIGREL POUR LE CHARGEMENT SENSIBLE :
            IF (TYPESE.NE.0) THEN
              DO 20,IAUX = 0,NBNOLI - 1
                ZK24(JNOLI+IAUX) = LIGRCS
   20         CONTINUE
            END IF
          END IF
        END IF
   30 CONTINUE
      CALL JEECRA(VECELE//'.LISTE_RESU','LONUTI',ILVE,' ')

   40 CONTINUE

      VECELZ = VECELE//'.LISTE_RESU'
      CALL JEDEMA()
      END
