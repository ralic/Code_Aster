      SUBROUTINE XMILAC(NDIM,IGEOM,JPTINT,JTABCO,
     &                  TABDIR,JGRLSN,P,R,JPTMIL,MILAC)
      IMPLICIT NONE

      INTEGER       NDIM,IGEOM,JPTINT,JPTMIL,JTABCO,R,P
      INTEGER       JGRLSN,TABDIR(4)
      REAL*8        MILAC(NDIM)

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 20/12/2010   AUTEUR PELLET J.PELLET 
C TOLE CRS_1404
C ======================================================================
C COPYRIGHT (C) 1991 - 2010  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C                      CALCUL ET STOCKAGE DES COORDONNEES DU MILIEU DE
C                      L'ARETE CREEE LORS DU SOUS DECOUPAGE
C
C     ENTREE
C       NDIM    : DIMENSION TOPOLOGIQUE DU MAILLAGE
C       IGEOM   : COORDONNEES DES NOEUDS DE L'ELEMENT PARENT
C       NUMAEP  : NUMERO DES NOEUDS DE L'ELEMENT ENFANT ET LEUR
C                 EQUIVALENCE DANS L'ELEMENT PARENT
C       JPTINT  : COORDONNÉES DES POINTS D'INTERSECTION
C       JTABCO  : COORDONNÉES DES NOEUDS DE L'ELEMENT ENFANT
C       JTABLS  : LSN DES NOEUDS DE L'ELEMENT ENFANT
C         P     :
C         R     :
C
C     SORTIE
C       MILAC  : COORDONNEES DU POINT MILIEU DE L'ARETE CREEE
C
C     ----------------------------------------------------------------
C
C     ----- DEBUT COMMUNS NORMALISES  JEVEUX  ------------------------
      REAL*8           ZR
      COMMON  /RVARJE/ ZR(1)
C     -----  FIN  COMMUNS NORMALISES  JEVEUX  ------------------------
      INTEGER     NBNOMX
      PARAMETER   (NBNOMX = 27)

      INTEGER      IRET,J,K
      REAL*8       VAB(3),VBA(3),VAC(3),VN101C(3),VNFI(3),VNMA(3)
      REAL*8       V1(3),V2(3),KSIP(3),VN(3),KSIF(3)
      REAL*8       PSCAL1,PSCAL2,PSCAL3,PSCAL4,PSCAL5,NORME
      REAL*8       A(3),B(3),C(3),E(3),F(3),TAB(8*NDIM)
      REAL*8       FF(9),H,R8PREM,PINT1(3),PINT2(3)
      INTEGER      IBID,JTABAR,NNOP
      CHARACTER*8  ELREFP,ELP
      CHARACTER*24 TABAR
      LOGICAL      DEJA
      PARAMETER    (ELP='QU8')
C
C --------------------------------------------------------------------

      CALL JEMARQ()
      CALL ASSERT(NDIM.EQ.2)

C --- INITIALISATION
      CALL ELREF1(ELREFP)
      CALL ELREF4(' ','RIGI',IBID,NNOP,IBID,IBID,IBID,IBID,IBID,IBID)

      CALL VECINI(3,0.D0,VAB)
      CALL VECINI(3,0.D0,VBA)
      CALL VECINI(3,0.D0,VAC)
      CALL VECINI(3,0.D0,V1)
      CALL VECINI(3,0.D0,V2)
      CALL VECINI(3,0.D0,VN101C)
      CALL VECINI(3,0.D0,VNFI)
      CALL VECINI(3,0.D0,F)
      CALL VECINI(3,0.D0,VN)
      CALL VECINI(NDIM,0.D0,MILAC)
      DEJA=.FALSE.

C --- RECUPERATION DES COORDONNES
      DO 101 K=1,NDIM
        PINT1(K)=ZR(JPTINT-1+NDIM*(R-2)+K)
        PINT2(K)=ZR(JPTINT-1+NDIM*(R-1)+K)
 101  CONTINUE

      DO 10 K=1,NDIM
        A(K)=ZR(JTABCO-1+NDIM*(TABDIR(1)-1)+K)
        B(K)=ZR(JTABCO-1+NDIM*(TABDIR(2)-1)+K)
        C(K)=ZR(JTABCO-1+NDIM*(TABDIR(3)-1)+K)
        E(K)=ZR(JTABCO-1+NDIM*(TABDIR(4)-1)+K)
 10   CONTINUE

C --- VECTEURS AB, AC, BA
      DO 102 K=1,NDIM
        VAB(K) = B(K)-A(K)
        VAC(K) = C(K)-A(K)
        VBA(K) = -VAB(K)
 102  CONTINUE

C --- NORMALE A 101C : VN101C
      DO 103 K=1,2
        IF (ABS(C(K)-PINT1(K)).LT.R8PREM()) THEN
          VN101C(1) = 2-K
          VN101C(2) = K-1
          DEJA=.TRUE.
        END IF
 103  CONTINUE

      IF (.NOT.DEJA) THEN
        H = (C(2)-PINT1(2)) / (C(1)-PINT1(1))
        V1(2) = 1/SQRT(H*H+1)
        V1(1) = -H*V1(2)
        V2(2) = -1/SQRT(H*H+1)
        V2(1) = -H*V2(2)
C       ORIENTATION DE VN101C (PASCAL AVEC BA>0)
        CALL PDSCA1 (V1,VBA,PSCAL1)
        CALL PDSCA1 (V2,VBA,PSCAL2)
        IF (PSCAL1.GT.0.D0) THEN
          VN101C(1) = V1(1)
          VN101C(2) = V1(2)
        ELSEIF (PSCAL2.GT.0.D0) THEN
          VN101C(1) = V2(1)
          VN101C(2) = V2(2)
        END IF
      END IF

C --- NORMALE A LA FISSURE : VNFI

C     CALCUL DES COORDONNEES DE REF DE 101
      CALL REEREG('S',ELREFP,NNOP,ZR(IGEOM),PINT1,2,KSIP,IRET)

C     CALCUL DES FONCTIONS DE FORME DE L'ELEMENT EN KSI
      CALL ELRFVF(ELREFP,KSIP,NBNOMX,FF,IBID)

        DO 114 J=1,NDIM
          DO 115 K=1,NNOP
            VNFI(J)  = VNFI(J) + FF(K)*ZR(JGRLSN-1+NDIM*(K-1)+J)
 115      CONTINUE
 114    CONTINUE
        CALL NORMEV(VNFI,NORME)

C     ORIENTATION DE VNFI (PASCAL AVEC BA>0)
      CALL PDSCA1 (VNFI,VBA,PSCAL3)
      IF(PSCAL3.LT.0.D0) THEN
        DO 20 K=1,NDIM
           VNFI(K) = -VNFI(K)
 20    CONTINUE
      END IF

      CALL PDSCA1 (VN101C,VNFI,PSCAL5)

C --- NORMALE AU MAILLAGE : VNMA
      CALL PROVEC(VAB,VAC,VNMA)
C --- PRODUIT VECTORIEL ENTRE NORMALEFISSURE ET NORMALEARETE : VN
      CALL PROVEC(VNFI,VN101C,VN)
C --- PRODUIT SCALAIRE VN.VNMA
      CALL PDSCA1 (VN,VNMA,PSCAL4)

C --- CALCUL COORDONNEES POINT MILIEU ARETE
      IF (PSCAL4.GT.0.D0) THEN
C     ARETE COURBE
        DO 105 K = 1,NDIM
          TAB(0*NDIM+K)= B(K)
          TAB(1*NDIM+K)= C(K)
          TAB(2*NDIM+K)= PINT2(K)
          TAB(3*NDIM+K)= PINT1(K)
          TAB(4*NDIM+K)= E(K)
          TAB(5*NDIM+K)= ZR(JPTMIL-1+2*(P-1)*NDIM+K)
          TAB(6*NDIM+K)= ZR(JPTMIL-1+(2*P)*NDIM+K)
          TAB(7*NDIM+K)= ZR(JPTMIL-1+2*(P-2)*NDIM+K)
 105    CONTINUE

C       CALCUL DES FONCTIONS DE FORME DE L'ELEMENT EN (0,0)
        KSIF(1)=0.D0
        KSIF(2)=0.D0
        CALL ELRFVF(ELP,KSIF,NBNOMX,FF,IBID)

        DO 106 J=1,NDIM
          DO 107 K=1,8
            F(J) = F(J) + FF(K)*TAB((K-1)*NDIM+J)
 107      CONTINUE
 106    CONTINUE
        TABAR='&&XMILAC.TABAR'
        CALL WKVECT(TABAR,'V V R',3*NDIM,JTABAR)
C       TABLEAU DES COORDONNEES DES NOEUDS DE L'ARETE C-101-F
        DO 108 K=1,NDIM
          ZR(JTABAR-1+K)=C(K)
          ZR(JTABAR-1+NDIM+K)=PINT1(K)
          ZR(JTABAR-1+2*NDIM+K)=F(K)
 108    CONTINUE

        CALL MILFIC(NDIM,JTABAR,MILAC)

        CALL JEDETR(TABAR)

      ELSE
C     ARETE DROITE
        DO 109 K=1,NDIM
          MILAC(K)=(PINT1(K)+C(K))/2
 109    CONTINUE

      ENDIF

      CALL JEDEMA()
      END
