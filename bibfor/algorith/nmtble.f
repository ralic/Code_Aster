      SUBROUTINE NMTBLE(NIVEAU, 
     &                  NEQ   ,MODELE,NOMA  ,DEFICO,RESOCO,
     &                  DEPMOI,DEPPLU,FONACT,INST  ,MAXREL,
     &                  SDIMPR)

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 24/09/2007   AUTEUR ABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2003  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE MABBAS M.ABBAS
C TOLE CRP_21
C
      IMPLICIT NONE

      INTEGER      NIVEAU,NEQ
      CHARACTER*8  NOMA
      CHARACTER*24 DEFICO,RESOCO,SDIMPR
      CHARACTER*24 DEPMOI,DEPPLU
      CHARACTER*24 MODELE
      REAL*8       INST(*)
      LOGICAL      MAXREL,FONACT(*)   
C
C ----------------------------------------------------------------------
C
C   STAT_NON_LINE : ROUTINE DE FIN DE BOUCLE DES ITERATIONS SE SITUANT
C                   ENTRE LES PAS DE TEMPS ET L'EQUILIBRE
C
C                   POUR LE MOMENT, SERT A LA METHODE CONTINUE DE
C                   CONTACT
C
C                   SERVIRA DANS L'AVENIR POUR LES CALCULS DE
C                   SENSIBILITE
C
C                   LES ITERATIONS ONT LIEU ENTRE CETTE ROUTINE
C                   (NMTBLE) ET SA COUSINE (NMIBLE) QUI COMMUNIQUENT
C                   POUR LE MOMENT PAR LA VARIABLE NIVEAU
C
C I/O NIVEAU : INDICATEUR D'UTILISATION DE LA BOUCLE 
C     -1     ON N'UTILISE PAS CETTE BOUCLE
C      4     BOUCLE CONTACT METHODE CONTINUE
C              INITIALISATION 
C      3     BOUCLE CONTACT METHODE CONTINUE
C              BOUCLE GEOMETRIE
C      2     BOUCLE CONTACT METHODE CONTINUE
C              BOUCLE SEUILS DE FROTTEMENT
C      1     BOUCLE CONTACT METHODE CONTINUE
C              BOUCLE CONTRAINTES ACTIVES
C
C IN  NOMA   : NOM DU MAILLAGE
C IN  DEFICO : SD DE DEFINITION DU CONTACT
C IN  DEPPLU : DEPLACEMENT APRES CONVERGENCE DE NEWTON
C IN  DEPMOI : DEPLACEMENT AVANT CONVERGENCE DE NEWTON
C IN  SDIMPR : SD AFFICHAGE
C IN  MODELE : NOM DU MODELE
C IN  INST   : INST(1) - INSTANT COURANT
C              INST(2) - INCREMENT DE TEMPS
C              INST(3) - VALEUR DU THETA (INTEGRATION PAR THETA METHODE)
C IN  MAXREL : .TRUE. SI CRITERE RESI_GLOB_RELA ET CHARGEMENT = 0,
C                     ON UTILISE RESI_GLOB_MAXI
C
C -------------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ----------------
C
      INTEGER            ZI
      COMMON  / IVARJE / ZI(1)
      REAL*8             ZR
      COMMON  / RVARJE / ZR(1)
      COMPLEX*16         ZC
      COMMON  / CVARJE / ZC(1)
      LOGICAL            ZL
      COMMON  / LVARJE / ZL(1)
      CHARACTER*8        ZK8
      CHARACTER*16                ZK16
      CHARACTER*24                          ZK24
      CHARACTER*32                                    ZK32
      CHARACTER*80                                              ZK80
      COMMON  / KVARJE / ZK8(1) , ZK16(1) , ZK24(1) , ZK32(1) , ZK80(1)
C
C -------------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ----------------
C 
      LOGICAL      MMCVCA,MMCVFR,MMCVGO
      CHARACTER*8  NOMO
      CHARACTER*24 K24BLA,K24BID
      REAL*8       R8BID
      INTEGER      IBID
      LOGICAL      LFROTT,IXFEM        
C
C ----------------------------------------------------------------------
C
      NOMO   = MODELE   
      K24BLA = ' '             
C         
      IF (NIVEAU.LT.0) THEN 
        GOTO 9999
      ENDIF
C
C --- INFOS SUR LE CONTACT
C
      CALL MMINFP(0    ,DEFICO,K24BLA,'FROTTEMENT',
     &            IBID ,R8BID ,K24BID,LFROTT)
      IXFEM  = FONACT(9)        
C      
      GO TO (101,102,103) NIVEAU
C
C --- NIVEAU: 1   BOUCLE CONTACT METHODE CONTINUE
C ---             BOUCLE CONTRAINTES ACTIVES
C
 101  CONTINUE
C
C --- CALCUL STATUTS DU CONTACT
C
      IF (IXFEM) THEN
        CALL XMMBCA(NOMA  ,NOMO  ,DEFICO,RESOCO,DEPPLU,
     &              DEPMOI,MMCVCA)      
      ELSE
        CALL MMMBCA(NOMA  ,NEQ   ,INST  ,DEFICO,RESOCO,
     &              DEPPLU,MMCVCA)      
      ENDIF
C
      CALL MMCONV(NIVEAU,DEFICO,RESOCO,SDIMPR,DEPPLU,
     &            MAXREL,IXFEM ,LFROTT,MMCVCA,MMCVFR,       
     &            MMCVGO)
     
      IF (.NOT.MMCVCA) THEN
        NIVEAU = 1
        GOTO 999      
      ENDIF      
C
C --- NIVEAU: 2   BOUCLE CONTACT METHODE CONTINUE
C ---             BOUCLE SEUILS DE FROTTEMENT
C
 102  CONTINUE

      IF (.NOT.LFROTT) THEN 
        GOTO 103
      ENDIF
      
      NIVEAU = 2
C
C --- CALCUL SEUILS DE FROTTEMENT
C
      IF (IXFEM) THEN
        CALL XREACL(NOMA  ,NOMO  ,DEPPLU,DEFICO,RESOCO)      
      ELSE
        CALL REACLM(NOMA  ,DEFICO,RESOCO,DEPPLU)      
      ENDIF
C       
      CALL MMCONV(NIVEAU,DEFICO,RESOCO,SDIMPR,DEPPLU,
     &            MAXREL,IXFEM ,LFROTT,MMCVCA,MMCVFR,       
     &            MMCVGO)
        
      IF (.NOT.MMCVFR) THEN   
        NIVEAU = 2
        GOTO 999      
      ENDIF     
C
C --- NIVEAU: 3   BOUCLE CONTACT METHODE CONTINUE
C ---             BOUCLE GEOMETRIE
C
 103  CONTINUE
C
      NIVEAU = 3
C
      CALL MMCONV(NIVEAU,DEFICO,RESOCO,SDIMPR,DEPPLU,
     &            MAXREL,IXFEM ,LFROTT,MMCVCA,MMCVFR,       
     &            MMCVGO)
     
      IF (MMCVGO) THEN 
        NIVEAU = 0
        GOTO 9999      
      ELSE 
        NIVEAU = 3     
      ENDIF       
C
 999  CONTINUE  
C
C --- AFFICHAGE TABLEAU CONVERGENCE
C
      CALL IMPSDM(SDIMPR(1:14),'ITER_NEWT',' ')
      CALL NMIMPR('IMPR','ETAT_CONV',' ',0.D0,0)    
      CALL NMIMPR('IMPR','LIGNE    ',' ',0.D0,0)     
C
 9999 CONTINUE     

      END
