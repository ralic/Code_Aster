      SUBROUTINE CFGEOM(PREMIE,LREAC,ITERAT,INST,
     &                  NOMA,DEFICO,RESOCO,
     &                  DEPPLU,DEPDEL)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 07/10/2004   AUTEUR MABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2004  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C
      IMPLICIT     NONE
      LOGICAL      PREMIE
      LOGICAL      LREAC  
      INTEGER      ITERAT
      REAL*8       INST
      CHARACTER*8  NOMA
      CHARACTER*24 DEFICO
      CHARACTER*24 RESOCO
      CHARACTER*24 DEPPLU
      CHARACTER*24 DEPDEL
C
C ----------------------------------------------------------------------
C ROUTINE APPELEE PAR : NMCOFR
C ----------------------------------------------------------------------
C
C ROUTINE D'AIGUILLAGE POUR L'ACTUALISATION GEOMETRIQUE DU CONTACT:
C  APPARIEMENT, PROJECTION, JEUX
C
C IN  PREMIE : VAUT .TRUE. SI C'EST LE PREMIER CALCUL (PAS DE PASSE)
C IN  LREAC  : VAUT .TRUE. SI REACTUALISATION GEOMETRIQUE
C IN  ITERAT : ITERATION DE NEWTON
C IN  INST   : VALEUR DE L'INSTANT DE CALCUL
C IN  NOMA   : NOM DU MAILLAGE
C IN  DEFICO : SD DE DEFINITION DU CONTACT (ISSUE D'AFFE_CHAR_MECA)
C IN  RESOCO : SD DE TRAITEMENT NUMERIQUE DU CONTACT
C IN  DEPPLU : CHAMP DE DEPLACEMENTS A L'ITERATION DE NEWTON PRECEDENTE
C IN  DEPDEL : INCREMENT DE DEPLACEMENT CUMULE
C
C -------------- DEBUT DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C ---------------- FIN DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER      ZMETH
      PARAMETER    (ZMETH=8)
      INTEGER      ZREAC
      PARAMETER    (ZREAC=4)
      CHARACTER*24 OLDGEO,NEWGEO
      CHARACTER*24 NDIMCO,APREAC,METHCO,SYMECO
      INTEGER      JDIM,JREAC,JMETH,JSYME
      INTEGER      IFM,NIV
      INTEGER      NZOCO,IZONE,NSYME
      REAL*8       ALPHA
C
C ----------------------------------------------------------------------
C
      CALL INFNIV (IFM,NIV)
      CALL JEMARQ ()
C
C --- NOMBRE DE ZONES DE CONTACT
C
      NDIMCO = DEFICO(1:16)//'.NDIMCO'
      CALL JEVEUO(NDIMCO,'L',JDIM)
      NZOCO  = ZI(JDIM+1)
C
C --- NOMBRE DE ZONES SYMETRIQUES
C
      SYMECO = DEFICO(1:16)//'.SYMECO'
      CALL JEVEUO(SYMECO,'L',JSYME)    
      NSYME  = ZI(JSYME)
C
C --- DETERMINATION DES REACTUALISATIONS A FAIRE DANS CHAQUE ZONE
C
      APREAC = RESOCO(1:14)//'.APREAC'
      METHCO = DEFICO(1:16)//'.METHCO'

      CALL JEVEUO(METHCO,'L',JMETH)
      CALL JEVEUO(APREAC,'E',JREAC)

      DO 10 IZONE = 1,NZOCO
        CALL REACCO (PREMIE,ZI(JMETH+ZMETH*(IZONE-1)+1),
     &               ZI(JREAC+ZREAC*(IZONE-1)))
 10   CONTINUE
C
C --- ACTUALISATION DE LA GEOMETRIE
C 
      OLDGEO = NOMA // '.COORDO'
      NEWGEO = '&&NMCONT.ACTUGEO'
      ALPHA  = 1.0D0

      IF (LREAC) THEN
C 
C --- REACTUALISATION
C
         IF (NIV.GE.2) THEN
            WRITE (IFM,*) '<CONTACT> REACTUALISATION GEOMETRIQUE' 
         ENDIF
C 
C --- REACTUALISATION DES DEPLACEMENTS
C 
         IF (NIV.GE.2) THEN
          WRITE (IFM,*) '<CONTACT> <> REACTUALISATION DES DEPLACEMENTS' 
         ENDIF 
         CALL VTGPLD(OLDGEO, ALPHA, DEPPLU, 'V', NEWGEO )
C 
C --- APPARIEMENT NOEUD ESCLAVE - MAILLE (OU NOEUD) MAITRE
C 
         IF (NIV.GE.2) THEN
            WRITE (IFM,*) '<CONTACT> <> APPARIEMENT DES SURFACES' 
         ENDIF
         CALL RECHCO(PREMIE,LREAC,NZOCO,NSYME,INST,
     &               NOMA,NEWGEO,DEFICO,RESOCO)
C 
C --- PROJECTION (NOEUDS MAITRES, DDLS MAITRES, COEFFICIENTS, JEU)
C 
         IF (NIV.GE.2) THEN
            WRITE (IFM,*) '<CONTACT> <> PROJECTION DES NOEUDS' 
         ENDIF
         CALL PROJCO(NOMA,NEWGEO,INST,DEFICO,RESOCO)
C
C --- SAUVEGARDE DU JEU EN DEBUT DE PAS DE TEMPS EN CAS DE REDECOUPAGE 
C
         CALL SOVJEU(RESOCO)

      ELSE
         IF (NIV.GE.2) THEN
           WRITE (IFM,*) '<CONTACT> PAS DE REACTUALISATION GEOMETRIQUE' 
         ENDIF
         IF (ITERAT.EQ.0) THEN
            CALL REAJEU(RESOCO,DEPPLU,DEPDEL)
         ENDIF
      ENDIF
C
C --- IMPRESSIONS POUR LES DEVELOPPEURS
C
      IF (NIV.GE.2) THEN
        CALL CFIMP4(DEFICO,RESOCO,NOMA,IFM)
      ENDIF
C
      CALL JEDEMA()
      END
