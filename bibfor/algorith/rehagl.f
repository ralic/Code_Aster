      SUBROUTINE REHAGL(NOMRES,RESGEN,MAILSK,PROFNO)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 03/07/2012   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C***********************************************************************
C    T. KERBER     DATE 16/05/93
C-----------------------------------------------------------------------
C  BUT:      < RESTITUTION HARMONIQUE GLOBALE >
      IMPLICIT NONE
C
C      RESTITUER EN BASE PHYSIQUE SUR UN MAILLAGE SQUELETTE
C  LES RESULTATS ISSUS DE LA SOUS-STRUCTURATION GENERALE
C  LE CONCEPT RESULTAT EST UN RESULTAT COMPOSE "DYNA_HARMO"
C
C-----------------------------------------------------------------------
C
C NOMRES   /I/: NOM K8 DU CONCEPT DYNA_HARMO RESULTAT
C RESGEN   /I/: NOM K8 DU RESULTAT GENERALISE AMONT
C MAILSK   /I/: NOM K8 DU MAILLAGE SQUELETTE SUPPORT
C PROFNO   /I/: NOM K19 DU PROF_CHNO DU SQUELETTE
C
C
C
      INCLUDE 'jeveux.h'
C
C
C
      CHARACTER*6 PGC
      CHARACTER*8 NOMRES,BASMOD
      CHARACTER*8 CHSYMB,SYMB(3)
      CHARACTER*8 MAILSK,MODGEN,RESGEN,SOUTR,KBID
      CHARACTER*19 NUMDDL
      CHARACTER*19 NUMGEN,CHAMNE,PROFNO
      CHARACTER*19 NUME(3)
      CHARACTER*24 CREFE(2),CHAMOL,CHAMBA,INDIRF
      CHARACTER*24 VALK(4)
      CHARACTER*24 CHAMNO,CHAREF
      CHARACTER*16 NOMPAR(1)
      INTEGER IAFREQ(1)
      COMPLEX*16  CBID
      CHARACTER*1 K1BID
C
C-----------------------------------------------------------------------
C-----------------------------------------------------------------------
      INTEGER I ,IAD ,IAR ,IBID ,IDEP ,IEQ ,IER 
      INTEGER IRET ,ISYMB ,J ,K ,L ,LDNEW ,LLCHAB 
      INTEGER LLCHOL ,LLIND ,LLINSK ,LLNUEQ ,LLORS ,LLPRS ,LLREF 
      INTEGER LLROT ,LREF ,LTROTX ,LTROTY ,LTROTZ ,LTVEC ,NBBAS 
      INTEGER NBCMP ,NBCOU ,NBFREQ ,NBNOT ,NBSST ,NBSYMB ,NEQ 
      INTEGER NEQS ,NUTARS 
      REAL*8 FREQ ,RBID 
C-----------------------------------------------------------------------
      DATA PGC/'REHAGL'/
      DATA SOUTR/'&SOUSSTR'/
      DATA NOMPAR/'FREQ'/
C-----------------------------------------------------------------------
C
      CALL JEMARQ()
      INDIRF = '&&'//PGC//'.INDIR.SST'
C
C-----------------ECRITURE DU TITRE-------------------------------------
C
      CALL TITRE
C
C------------------------VERIF SQUELETTE--------------------------------
C
      CALL JEEXIN(MAILSK//'.INV.SKELETON',IRET)
      IF (IRET.EQ.0) THEN
            VALK (1) = MAILSK
        CALL U2MESG('F', 'ALGORITH14_27',1,VALK,0,0,0,0.D0)
      END IF

      CALL JEVEUO(MAILSK//'.INV.SKELETON','L',LLINSK)
C
C-----DETERMINATION DES CHAMPS CALCULES LORS DES CALCULS HARMONIQUES----
C
C     LES NOMS SYMBOLIQUES DES CHAMPS CALCULES, PARMI DEPL, VITE ET ACCE
C     SONT STOCKES DANS LE VECTEUR SYMB. LEURS NUME_DDL SONT ECRITS
C     DANS LE VECTEUR NUM.
C
      ISYMB = 0
C
      CALL RSEXCH(RESGEN,'DEPL',1,CHAMNO,IRET)
C
      IF (IRET.EQ.0) THEN
        ISYMB = ISYMB + 1
        SYMB(ISYMB) = 'DEPL'
        CHAREF = CHAMNO(1:19)//'.REFE'
        CALL JEVEUO(CHAREF,'L',LREF)
        NUME(ISYMB) = ZK24(LREF+1)
        CALL JELIBE(CHAREF)
      END IF
C
C
      CALL RSEXCH(RESGEN,'VITE',1,CHAMNO,IRET)
C
      IF (IRET.EQ.0) THEN
        ISYMB = ISYMB + 1
        SYMB(ISYMB) = 'VITE'
        CHAREF = CHAMNO(1:19)//'.REFE'
        CALL JEVEUO(CHAREF,'L',LREF)
        NUME(ISYMB) = ZK24(LREF+1)
        CALL JELIBE(CHAREF)
      END IF
C
C
      CALL RSEXCH(RESGEN,'ACCE',1,CHAMNO,IRET)
C
      IF (IRET.EQ.0) THEN
        ISYMB = ISYMB + 1
        SYMB(ISYMB) = 'ACCE'
        CHAREF = CHAMNO(1:19)//'.REFE'
        CALL JEVEUO(CHAREF,'L',LREF)
        NUME(ISYMB) = ZK24(LREF+1)
        CALL JELIBE(CHAREF)
      END IF
C
C     NOMBRE DE CHAMPS SYMBOLIQUES CALCULES.
C     ON S'ASSURE QUE LEUR NOMBRE EST NON NUL.
C
      NBSYMB = ISYMB
C
      IF (NBSYMB.EQ.0) THEN
            VALK (1) = RESGEN
        CALL U2MESG('F', 'ALGORITH14_35',1,VALK,0,0,0,0.D0)
      END IF
C
C     VERIFICATION DE LA COHERENCE DES NUM_DDL
C
      NUMGEN = NUME(1)
      IF (NBSYMB.GT.1) THEN
        DO 10 I = 2,NBSYMB
          IF (NUME(I).NE.NUMGEN) THEN
            VALK (1) = SYMB(I)
            VALK (2) = NUME(I)
            VALK (3) = SYMB(1)
            VALK (4) = NUMGEN
            CALL U2MESG('F', 'ALGORITH14_36',4,VALK,0,0,0,0.D0)
          END IF

   10   CONTINUE
      END IF
C
C
C-------------------RECUPERATION DU MODELE GENERALISE-------------------
C
      NUMGEN(15:19) = '.NUME'
      CALL JEVEUO(NUMGEN//'.REFN','L',LLREF)
      MODGEN = ZK24(LLREF)
C
      CALL JELIRA(MODGEN//'      .MODG.SSNO','NOMMAX',NBSST,K1BID)
      KBID = '  '
      CALL MGUTDM(MODGEN,KBID,1,'NB_CMP_MAX',NBCMP,KBID)
C
C--------------RECUPERATION DES ROTATIONS-------------------------------
C
      CALL WKVECT('&&'//PGC//'ROTX','V V R',NBSST,LTROTX)
      CALL WKVECT('&&'//PGC//'ROTY','V V R',NBSST,LTROTY)
      CALL WKVECT('&&'//PGC//'ROTZ','V V R',NBSST,LTROTZ)
      DO 15 I = 1,NBSST
        CALL JEVEUO(JEXNUM(MODGEN//'      .MODG.SSOR',I),'L',LLROT)
        ZR(LTROTZ+I-1) = ZR(LLROT)
        ZR(LTROTY+I-1) = ZR(LLROT+1)
        ZR(LTROTX+I-1) = ZR(LLROT+2)
   15 CONTINUE
C
C-------------------CREATION DU PROF-CHAMNO-----------------------------
C
      CALL GENUGL(PROFNO,INDIRF,MODGEN,MAILSK)
      CALL JELIRA(PROFNO//'.NUEQ','LONMAX',NEQ,K1BID)
C
C----------------------RECUPERATION DU NOMBRE DE NOEUDS-----------------
C
      CALL DISMOI('F','NB_NO_MAILLA',MAILSK,'MAILLAGE',NBNOT,KBID,IRET)
C
C----------------------RECUPERATION DE LA BASE MODALE-------------------
C
      CREFE(1) = MAILSK
      CREFE(2) = PROFNO
C
C--------------RECUPERATION NOMBRE DE FREQUENCES D'EXCITATION-----------
C
      CALL RSORAC(RESGEN,'LONUTI',IBID,RBID,KBID,CBID,RBID,KBID,
     &            NBFREQ,1, IBID)
C
C
C--------------------ALLOCATION STRUCTURE DE DONNEES RESULTAT-----------
C
      CALL RSCRSD('G',NOMRES,'DYNA_HARMO',NBFREQ)
C
C----------------------RESTITUTION PROPREMENT DITE----------------------
C
      CALL JEVEUO(NUMGEN//'.NUEQ','L',LLNUEQ)
      CALL JENONU(JEXNOM(NUMGEN//'.LILI',SOUTR),IBID)
      CALL JEVEUO(JEXNUM(NUMGEN//'.ORIG',IBID),'L',LLORS)
      CALL JENONU(JEXNOM(NUMGEN//'.LILI',SOUTR),IBID)
      CALL JEVEUO(JEXNUM(NUMGEN//'.PRNO',IBID),'L',LLPRS)
C
C----------------------RESTITUTION PROPREMENT DITE----------------------
C
      CALL JEVEUO(NUMGEN//'.NUEQ','L',LLNUEQ)
C
C     BOUCLE SUR LES FREQUENCES ETUDIEES
C
      DO 20 I = 1,NBFREQ
C
C     BOUCLE SUR LES CHAMPS A RESTITUER
C
C
        DO 30 ISYMB = 1,NBSYMB
C
          CHSYMB = SYMB(ISYMB)
C
C  REQUETTE NOM ET ADRESSE CHAMNO GENERALISE
          CALL DCAPNO(RESGEN,CHSYMB,I,CHAMOL)
          CALL JEVEUO(CHAMOL,'L',LLCHOL)
C
C  REQUETTE NOM ET ADRESSE NOUVEAU CHAMNO
          CALL RSEXCH(NOMRES,CHSYMB,I,CHAMNE,IER)
          CALL VTCREA(CHAMNE,CREFE,'G','C',NEQ)
          CALL JEVEUO(CHAMNE//'.VALE','E',LDNEW)
C
C   BOUCLE SUR LES SOUS-STRUCTURES
C
          DO 40 K = 1,NBSST
            CALL JEEXIN(JEXNUM(INDIRF,K),IRET)
C
C TEST SI LA SST GENERE DES DDL GLOBAUX
C
            IF (IRET.NE.0) THEN
              KBID = '  '
              CALL MGUTDM(MODGEN,KBID,K,'NOM_BASE_MODALE',IBID,BASMOD)
              CALL DISMOI('F','NB_MODES_TOT',BASMOD,'RESULTAT',
     &                      NBBAS,KBID,IER)
              KBID = '  '
              CALL MGUTDM(MODGEN,KBID,K,'NOM_NUME_DDL',IBID,NUMDDL)
              CALL DISMOI('F','NB_EQUA',NUMDDL,'NUME_DDL',NEQS,KBID,
     +                    IRET)
              CALL WKVECT('&&'//PGC//'.TRAV','V V C',NEQS,LTVEC)
C
C RECUPERATION DU NUMERO TARDIF DE LA SST
C
              DO 50 J = 1,NBSST
                IF (ZI(LLORS+J-1).EQ.K) NUTARS = J
   50         CONTINUE
              IEQ = ZI(LLPRS+ (NUTARS-1)*2)
C
C  BOUCLE SUR LES MODES PROPRES DE LA BASE
C
              DO 60 J = 1,NBBAS
                CALL DCAPNO(BASMOD,'DEPL',J,CHAMBA)
                CALL JEVEUO(CHAMBA,'L',LLCHAB)
C     BOUCLE SUR LES EQUATIONS PHYSIQUES
                IAD = LLCHOL + ZI(LLNUEQ+IEQ+J-2) - 1
C
C  BOUCLE SUR LES DDL DE LA BASE
C
                DO 70 L = 1,NEQS
                  ZC(LTVEC+L-1) = ZC(LTVEC+L-1) + ZR(LLCHAB+L-1)*ZC(IAD)
   70           CONTINUE
                CALL JELIBE(CHAMBA)
   60         CONTINUE
              CALL JEVEUO(JEXNUM(INDIRF,K),'L',LLIND)
              CALL JELIRA(JEXNUM(INDIRF,K),'LONMAX',NBCOU,K1BID)
              NBCOU = NBCOU/2
              DO 80 L = 1,NBCOU
                IDEP = ZI(LLIND+ (L-1)*2)
                IAR = ZI(LLIND+ (L-1)*2+1)
                ZC(LDNEW+IAR-1) = ZC(LTVEC+IDEP-1)
   80         CONTINUE
              CALL JELIBE(JEXNUM(INDIRF,K))
              CALL JEDETR('&&'//PGC//'.TRAV')
            END IF

   40     CONTINUE
          CALL RSNOCH(NOMRES,CHSYMB,I)
C
          CALL JELIBE(CHAMOL)
C
C ROTATION DU CHAMPS AU NOEUDS
C
          CALL ROTCHC(PROFNO,ZC(LDNEW),ZR(LTROTZ),NBSST,ZI(LLINSK),
     +                NBNOT,NBCMP,3)
          CALL ROTCHC(PROFNO,ZC(LDNEW),ZR(LTROTY),NBSST,ZI(LLINSK),
     +                NBNOT,NBCMP,2)
          CALL ROTCHC(PROFNO,ZC(LDNEW),ZR(LTROTX),NBSST,ZI(LLINSK),
     +                NBNOT,NBCMP,1)
   30   CONTINUE
C
C     ENREGISTREMENT DES FREQUENCES DANS NOMRES
C
        CALL RSADPA(RESGEN,'L',1,NOMPAR,I,0,IAFREQ,KBID)
        FREQ = ZR(IAFREQ(1))
        CALL RSADPA(NOMRES,'E',1,NOMPAR,I,0,IAFREQ,KBID)
        ZR(IAFREQ(1)) = FREQ
C
   20 CONTINUE
C
      CALL JEDETC('V','&&'//PGC,1)
      CALL JELIBE(NUMGEN//'.NUEQ')
C
      GOTO 9999

 9999 CONTINUE
      CALL JEDEMA()
      END
