      SUBROUTINE XNORME(INDIPT,IPTBOR,VECTN,NBFACB,
     &                  NUNOA,NUNOB,NUNOC,JCOOR,COORG)
      IMPLICIT NONE

      INCLUDE 'jeveux.h'
      INTEGER       IPTBOR(2),NBFACB,JCOOR
      INTEGER       NUNOA,NUNOB,NUNOC,INDIPT
      REAL*8        VECTN(12),COORG(3)

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 13/06/2012   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C
C             CALCUL ET STOCKAGE DE LA NORMALE D'UNE FACE
C
C     ENTREE
C       INDIPT   : INDICE DU POINT DE BORD
C       VECTN    : VECTEUR CONTENANT LES VECTEURS NORMAUX DES FACES DE
C                  BORD DE LA MAILLE
C       NBFACB   : NOMBRE DE FACES DE BORD DANS LA MAILLE
C       NUNOA    : NOEUD DE LA FACE COURANTE
C       NUNOB    : NOEUD DE LA FACE COURANTE
C       NUNOC    : NOEUD DE LA FACE COURANTE
C       JCOOR    : ADRESSE DES COORDONNEES DES NOEUDS DU MAILLAGE
C       COORG    : COORDONNEES DU CENTRE DE GRAVITE DE LA MAILLE
C
C     SORTIE
C       IPTBOR   : VECTEUR CONTENANT LES INDICES DES POINTS DE BORD DE
C                  LA MAILLE
C     ------------------------------------------------------------------

      INTEGER         K
      REAL*8          AB(3),AC(3),AG(3),NORMAL(3),V(3),DDOT,PROJ
C ----------------------------------------------------------------------
      CALL JEMARQ()

C     CALCUL DE LA NORMALE
      DO 240 K=1,3
C       A,B ET C SONT DES NOEUDS DE LA FACE
        AB(K)=ZR(JCOOR-1+3*(NUNOB-1)+K)-ZR(JCOOR-1+3*(NUNOA-1)+K)
        AC(K)=ZR(JCOOR-1+3*(NUNOC-1)+K)-ZR(JCOOR-1+3*(NUNOA-1)+K)
C       G EST LE CENTRE DE GRAVITE DE LA MAILLE
        AG(K)=COORG(K)-ZR(JCOOR-1+3*(NUNOA-1)+K)
240   CONTINUE

      CALL PROVEC(AB,AC,NORMAL)

C     ORIENTATION DE LA NORMALE VERS L'EXTERIEUR
      PROJ = DDOT(3,NORMAL,1,AG,1)

      IF (PROJ.GT.0) THEN
        NORMAL(1) = -NORMAL(1)
        NORMAL(2) = -NORMAL(2)
        NORMAL(3) = -NORMAL(3)
      ENDIF
C
C     NOMBRE DE FACES DE BORD DANS LA MAILLE
      NBFACB=NBFACB+1

C     STOCKAGE DE LA NORMALE
      VECTN(1+3*(NBFACB-1)) = NORMAL(1)
      VECTN(2+3*(NBFACB-1)) = NORMAL(2)
      VECTN(3+3*(NBFACB-1)) = NORMAL(3)

C     IPTBOR(2) SERT SI LA MAILLE POSSEDE 2 POINTS
C     DE FOND QUI SONT SUR UNE FACE DE BORD
C     (CAS TRES SPECIAL OU LE FOND DE FISSURE N'EST QUE DANS UNE SEULE
C      MAILLE)
      IF (IPTBOR(1).EQ.0) THEN
        IPTBOR(1) = INDIPT
      ELSE
        IPTBOR(2) = INDIPT
      ENDIF

      CALL JEDEMA()
      END
