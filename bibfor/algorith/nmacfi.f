      SUBROUTINE NMACFI(FONACT,VEASSE,CNFFDO,CNDFDO)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 13/06/2012   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT NONE
      INCLUDE 'jeveux.h'
      CHARACTER*19 CNFFDO,CNDFDO
      CHARACTER*19 VEASSE(*)
      INTEGER      FONACT(*)
C 
C ----------------------------------------------------------------------
C
C ROUTINE MECA_NON_LINE (ALGORITHME)
C
C CALCUL DU VECTEUR DES CHARGEMENTS CONSTANTS POUR L'ACCELERATION 
C INITIALE
C      
C ----------------------------------------------------------------------
C
C
C IN  FONACT : FONCTIONNALITES ACTIVEES (VOIR NMFONC)
C IN  VEASSE : VARIABLE CHAPEAU POUR NOM DES VECT_ASSE  
C OUT CNFFDO : VECT_ASSE DE TOUTES LES FORCES FIXES DONNES
C OUT CNDFDO : VECT_ASSE DE TOUS LES DEPLACEMENTS FIXES DONNES
C
C
C
C

      INTEGER      IFM,NIV   
      INTEGER      IFDO
      INTEGER      N
      CHARACTER*19 CNFIXE(20)
      REAL*8       COFIXE(20)  
      CHARACTER*19 CNFEDO,CNDIDO
      CHARACTER*19 CNCINE,CNDIDI,CNSSTR,CNSSTF
      LOGICAL      ISFONC,LDIDI
      LOGICAL      LMACR,LSSTF
C 
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
      CALL INFDBG('MECA_NON_LINE',IFM,NIV)
C
C --- AFFICHAGE
C
      IF (NIV.GE.2) THEN
        WRITE (IFM,*) '<MECANONLINE> ...... CALCUL CHARGEMENT FIXE' 
      ENDIF
C
C --- FONCTIONNALITES ACTIVEES
C       
      LDIDI  = ISFONC(FONACT,'DIDI') 
      LMACR  = ISFONC(FONACT,'MACR_ELEM_STAT')   
      LSSTF  = ISFONC(FONACT,'SOUS_STRUC')      
C
C --- INITIALISATIONS
C
      IFDO   = 0       
      CALL VTZERO(CNFFDO)
      CALL VTZERO(CNDFDO)         
C
C --- DEPLACEMENTS DONNES (Y COMPRIS DIDI SI NECESSAIRE)
C
      CALL NMCHEX(VEASSE,'VEASSE','CNDIDO',CNDIDO)    
      IFDO         = IFDO+1 
      CNFIXE(IFDO) = CNDIDO
      COFIXE(IFDO) = 1.D0

      IF (LDIDI) THEN
        CALL NMCHEX(VEASSE,'VEASSE','CNDIDI',CNDIDI)
        IFDO         = IFDO+1 
        CNFIXE(IFDO) = CNDIDI
        COFIXE(IFDO) = 1.D0
      ENDIF   
C      
C --- CONDITIONS CINEMATIQUES IMPOSEES           
C   
      CALL NMCHEX(VEASSE,'VEASSE','CNCINE',CNCINE) 
      IFDO         = IFDO+1 
      CNFIXE(IFDO) = CNCINE
      COFIXE(IFDO) = 1.D0        
C
C --- VECTEUR RESULTANT DEPLACEMENT FIXE
C       
      DO 17 N = 1,IFDO
        CALL VTAXPY(COFIXE(N),CNFIXE(N),CNDFDO)  
        IF (NIV.GE.2) THEN
          WRITE (IFM,*) '<MECANONLINE> ......... DEPL. FIXE' 
          WRITE (IFM,*) '<MECANONLINE> .........  ',N,' - COEF: ',
     &                   COFIXE(N)
          CALL NMDEBG('VECT',CNFIXE(N),IFM)
        ENDIF         
 17   CONTINUE      
C      
      IFDO   = 0               
C
C --- FORCES DONNEES
C
      CALL NMCHEX(VEASSE,'VEASSE','CNFEDO',CNFEDO)
      IFDO         = IFDO+1 
      CNFIXE(IFDO) = CNFEDO
      COFIXE(IFDO) = 1.D0     
C
C --- FORCES ISSUES DES MACRO-ELEMENTS STATIQUES
C      
      IF (LMACR) THEN
        CALL NMCHEX(VEASSE,'VEASSE','CNSSTR',CNSSTR) 
        IFDO         = IFDO+1 
        CNFIXE(IFDO) = CNSSTR
        COFIXE(IFDO) = -1.D0      
      ENDIF  
C        
C --- FORCES ISSUES DU CALCUL PAR SOUS-STRUCTURATION 
C      
      IF (LSSTF) THEN
        CALL NMCHEX(VEASSE,'VEASSE','CNSSTF',CNSSTF) 
        IFDO         = IFDO+1 
        CNFIXE(IFDO) = CNSSTF
        COFIXE(IFDO) = 1.D0     
      ENDIF                               
C
C --- VECTEUR RESULTANT FORCE FIXE
C       
      DO 10 N = 1,IFDO
        CALL VTAXPY(COFIXE(N),CNFIXE(N),CNFFDO)  
        IF (NIV.GE.2) THEN
          WRITE (IFM,*) '<MECANONLINE> ......... FORC. FIXE' 
          WRITE (IFM,*) '<MECANONLINE> .........  ',N,' - COEF: ',
     &                 COFIXE(N)
          CALL NMDEBG('VECT',CNFIXE(N),IFM)
        ENDIF            
 10   CONTINUE          
C
      CALL JEDEMA()
      END
