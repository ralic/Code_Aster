      SUBROUTINE ASSVSU(   NNO,  NNOS, NFACE,  GEOM,  CRIT,
     &                   DEPLM, DDEPL,CONGEM,CONGEP, VINTM,
     &                   VINTP,DEFGEM,DEFGEP,  DSDE, MATUU,
     &                   VECTU,RINSTM,RINSTP,OPTION, IMATE,
     &                  MECANI,PRESS1,PRESS2, TEMPE,DIMDEF,
     &                  DIMCON,DIMUEL,NBVARI,  NDIM,COMPOR,
     &                  TYPMOD, TYPVF,   AXI,PERMAN,NVOIMA,
     &                  NSCOMA,NBVOIS,LIVOIS,NBNOVO,NBSOCO,
     &                  LISOCO                             )
      IMPLICIT NONE

      INTEGER MAXFA
      PARAMETER (MAXFA=6)

      INTEGER NNO,NNOS,NFACE,KPG,SPT
      INTEGER IMATE,DIMDEF,DIMCON,DIMUEL
      INTEGER MECANI(5),PRESS1(7),PRESS2(7),TEMPE(5)
      INTEGER NBVARI,NDIM,TYPVF
      INTEGER NVOIMA,NSCOMA,NBVOIS
      INTEGER LIVOIS(NVOIMA),NBNOVO(NVOIMA)
      INTEGER NBSOCO(NVOIMA),LISOCO(NVOIMA,NSCOMA,2)
      REAL*8 GEOM(NDIM,NNO),CRIT(*)
      REAL*8 DDEPL(DIMUEL),DEPLM(DIMUEL)
      REAL*8 CONGEM(DIMCON,MAXFA+1),CONGEP(DIMCON,MAXFA+1)
      REAL*8 VINTM(NBVARI,MAXFA+1),VINTP(NBVARI,MAXFA+1)
      REAL*8 DEFGEM(DIMDEF),DEFGEP(DIMDEF)
      REAL*8 DSDE(DIMCON,DIMDEF)
      REAL*8 MATUU((NBVOIS+1)*DIMUEL*DIMUEL)
      REAL*8 RINSTP,RINSTM,VECTU(DIMUEL)
      CHARACTER*8 TYPMOD(2),FAMI,POUM
      CHARACTER*16 OPTION,COMPOR(*)
      LOGICAL AXI,PERMAN
C ----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 16/01/2012   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C TOLE CRP_20 CRP_21
C
C      BUT :
C           CALCUL DES OPTIONS RIGI_MECA_TANG, RAPH_MECA ET FULL_MECA
C           EN MECANIQUE DES MILIEUX POREUX AVEC COUPLAGE THM
C
C
C IN AXI AXISYMETRIQUE?
C IN TYPMOD MODELISATION (D_PLAN, AXI, 3D ?)
C IN MODINT METHODE D'INTEGRATION (CLASSIQUE,LUMPEE(D),REDUITE(R) ?)
C IN NNO NB DE NOEUDS DE L'ELEMENT
C IN NNOS NB DE NOEUDS SOMMETS DE L'ELEMENT
C IN NFACE NB DE FACES AU SENS BORD DE DIMENSION DIM-1 NE SERT
C QU EN VF
C IN NNOM NB DE NOEUDS MILIEUX DE FACE OU D ARRETE NE SERT QU EN EF
C IN NDDLS NB DE DDL SUR LES SOMMETS
C IN NDDLM NB DE DDL SUR LES MILIEUX DE FACE OU D ARRETE
C NE SERT QU EN EF
C IN NDDLFA NB DE DDL SUR LES FACE DE DIMENSION DIM-1 NE SERT
C QU EN VF
C IN NDDLK NB DE DDL AU CENTRE
C IN TYPVF 1 OU 2 : SCHEMA A DEUX POINTS OU SUDM
C IN NDIM DIMENSION DE L'ESPACE
C IN DIMUEL NB DE DDL TOTAL DE L'ELEMENT
C IN DIMCON DIMENSION DES CONTRAINTES GENERALISEES ELEMENTAIRES
C IN DIMDEF DIMENSION DES DEFORMATIONS GENERALISEES ELEMENTAIRES
C IN GEOM : COORDONNEES DES NOEUDS
C IN OPTION : OPTION DE CALCUL
C IN IMATE : MATERIAU CODE
C IN COMPOR : COMPORTEMENT
C IN CRIT : CRITERES DE CONVERGENCE LOCAUX + ALPHA
C IN DDEPL : DELTA DEPL
C IN DEPLP : DEPLACEMENT A L INSTANT PLUS
C IN DEPLM : DEPLACEMENT A L INSTANT MOINS
C IN MECANI : TABLEAU CONTENANT
C YAMEC = MECA(1), YAMEC = 1 >> IL Y A UNE EQUATION MECANI
C ADDEME = MECA(2), ADRESSE DANS LES TABLEAUX DES DEFORMAT
C GENERALISEES AU POINT DE GAUSS DEFGEP ET DEFGEM DES
C DEFORMATIONS CORRESPONDANT A LA MECANIQUE
C ADCOME = MECA(3), ADRESSE DANS LES TABLEAUX DES CONTRAIN
C GENERALISEES AU POINT DE GAUSS CONGEP ET CONGEM DES
C CONTRAINTES CORRESPONDANT A LA MECANIQUE
C NDEFME = MECA(4), NOMBRE DE DEFORMATIONS MECANIQUES
C NCONME = MECA(5), NOMBRE DE CONTRAINTES MECANIQUES
C IN PRESS1 : TABLEAU CONTENANT
C YAP1 = PRESS1(1), YAP1 = 1 >> IL Y A UNE EQUATION DE PRE
C ADDEP1 = PRESS1(3), ADRESSE DANS LES TABLEAUX DES DEFORM
C GENERALISEES AU POINT DE GAUSS DEFGEP ET DEFGEM DES
C DEFORMATIONS CORRESPONDANT A LA PREMIERE PRESSION
C ADCP11=PRESS1(4), ADRESSE DANS LES TABLEAUX DES CONTRAIN
C GENERALISEES AU POINT DE GAUSS CONGEP ET CONGEM DES
C CONTRAINTES CORRESPONDANT A LA PREMIERE PHASE DU
C PREMIER CONSTITUANT
C ADCP12=PRESS1(5), ADRESSE DANS LES TABLEAUX DES CONTRAIN
C GENERALISEES AU POINT DE GAUSS CONGEP ET CONGEM DES
C CONTRAINTES CORRESPONDANT A LA DEUXIEME PHASE DU
C PREMIER CONSTITUANT
C NDEFP1 = PRESS1(6), NOMBRE DE DEFORMATIONS PRESSION 1
C NCONP1 = PRESS1(7), NOMBRE DE CONTRAINTES POUR
C CHAQUE PHASE DU CONSTITUANT 1
C IN PRESS2 : TABLEAU CONTENANT
C YAP2 = PRESS2(1), YAP2 = 1 >> IL Y A UNE EQUATION DE PRE
CC ADDEP2 = PRESS2(3), ADRESSE DANS LES TABLEAUX DES DEFORM
C GENERALISEES AU POINT DE GAUSS DEFGEP ET DEFGEM DES
C DEFORMATIONS CORRESPONDANT A LA PREMIERE PRESSION
C ADCP21=PRESS2(4), ADRESSE DANS LES TABLEAUX DES CONTRAIN
C GENERALISEES AU POINT DE GAUSS CONGEP ET CONGEM DES
C CONTRAINTES CORRESPONDANT A LA PREMIERE PHASE DU
C SECOND CONSTITUANT
C ADCP22=PRESS2(5), ADRESSE DANS LES TABLEAUX DES CONTRAIN
C GENERALISEES AU POINT DE GAUSS CONGEP ET CONGEM DES
C CONTRAINTES CORRESPONDANT A LA DEUXIEME PHASE DU
C SECOND CONSTITUANT
C NDEFP2 = PRESS2(6), NOMBRE DE DEFORMATIONS PRESSION 2
C NCONP2 = PRESS2(7), NOMBRE DE CONTRAINTES POUR
C CHAQUE PHASE DU CONSTITUANT 2
C
C IN TEMPE : TABLEAU CONTENANT
C YATE = TEMPE(1), YAMEC = 1 >> IL Y A UNE EQUATION THERMI
C ADDETE = TEMPE(2), ADRESSE DANS LES TABLEAUX DES DEFORMA
C GENERALISEES AU POINT DE GAUSS DEFGEP ET DEFGEM DES
C DEFORMATIONS CORRESPONDANT A LA THERMIQUE
C ADCOTE = TEMPE(3), ADRESSE DANS LES TABLEAUX DES CONTRAI
C GENERALISEES AU POINT DE GAUSS CONGEP ET CONGEM DES
C CONTRAINTES CORRESPONDANT A LA THERMIQUE
C NDEFTE = TEMPE(4), NOMBRE DE DEFORMATIONS THERMIQUES
C NCONTE = TEMPE(5), NOMBRE DE CONTRAINTES THERMIQUES
C OUT CODRET : CODE RETOUR LOIS DE COMPORTEMENT
C OUT CONGEP : CONTRAINTES
C OUT VINTP : VARIABLES INTERNES
C OUT MATUU : MATRICE DE RIGIDITE PROFIL(RIGI_MECA_TANG ET FULL_MECA)
C OUT VECTU : FORCES NODALES (RAPH_MECA ET FULL_MECA)
C
C ......................................................................
C
C --------- DEBUT DECLARATIONS NORMALISEES JEVEUX ----------------------
C
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8    ZK8
      CHARACTER*16          ZK16
      CHARACTER*24                  ZK24
      CHARACTER*32                          ZK32
      CHARACTER*80                                  ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C --------- FIN DECLARATIONS NORMALISEES JEVEUX ------------------------
C
      INTEGER   CON,  DCONP1,  DCONP2,  DIFFU,  DDIFP1,  DDIFP2
      PARAMETER(CON=1,DCONP1=2,DCONP2=3,DIFFU=4,DDIFP1=5,DDIFP2=6)

      INTEGER   MOB,  DMOBP1,  DMOBP2,  MASSE,   DMASP1,   DMASP2
      PARAMETER(MOB=7,DMOBP1=8,DMOBP2=9,MASSE=10,DMASP1=11,DMASP2=12)

      INTEGER   WLIQ,  WVAP,  AIRDIS,  AIRSEC,  EAU,  AIR,  DENSIT
      PARAMETER(WLIQ=1,WVAP=2,AIRDIS=3,AIRSEC=4,EAU=1,AIR=2,DENSIT=14)

      INTEGER   VKINT,   KXX,  KYY,  KZZ,  KXY,  KYZ,  KZX
      PARAMETER(VKINT=13,KXX=1,KYY=2,KZZ=3,KXY=4,KYZ=5,KZX=6)

      INTEGER   RHOGA,  RHOLQ,  RHOGA1,  RHOGA2,  RHOLQ1,  RHOLQ2
      PARAMETER(RHOGA=1,RHOLQ=2,RHOGA1=3,RHOGA2=4,RHOLQ1=5,RHOLQ2=6)

      INTEGER MAXDIM
      PARAMETER (MAXDIM=3)

      INTEGER NSCOM1
      PARAMETER (NSCOM1=4)
C
      INTEGER YAMEC,YAP1,YAP2,YATE
      INTEGER ADDEME,ADDEP1,ADDEP2,ADDETE,ADCOME,ADCP11,ADCP12
      INTEGER ADCP21,ADCP22,ADCOTE
      INTEGER NVIM,NVIT, NVIH, NVIC
      INTEGER ADVIME,ADVITH,ADVIHY,ADVICO
      INTEGER VIHRHO,VICPHI,VICPVP,VICSAT,VICPR1,VICPR2
      INTEGER IPG,NSC,ISC,ISCL(NSCOM1)
      INTEGER IGEOMV,IMATEV,IDEPMV,IDDEPV,ICOMPV,ICARCV
      INTEGER IVARMV,ICONMV,ICONPV,IVARPV
      INTEGER RETCOM,IRET,KVOIS,FA,I,J
C
      REAL*8 PESA(3)
      REAL*8 RTHMC,R8PREM
      REAL*8 VALCEN(14,6),VALCEV(14,6,MAXFA)
      REAL*8 VALFAC(MAXFA,14,6)
      REAL*8 VALFAV(MAXFA,14,6,MAXFA)
      REAL*8 KINTVF(6),NKN,NKNV
      REAL*8 P10,P10V,P20,P20V
C
      INTEGER CODMES(1)
      CHARACTER*16 THMC,LOI,MECA, THER, HYDR
      CHARACTER*24 VALK(2)
C
      LOGICAL TANGE, CONT
C
C ==============================================
C VARIABLES LOCALES POUR CALCULS VF
C ==============================================
C
C PCP PRESSION CAPILLAIRE AU CENTRE DE LA MAILLE
C PCPV PRESSION CAPILLAIRE CENTRE VOISIN
C
C PWP PRESSION EAU
C PWPV PRESSION EAU VOISIN
C DPWP1 DERIVEE PRESSION EAU PAR P1
C DPWP2 DERIVEE PRESSION EAU PAR P2
C
C PGP PRESSION DE GAZ AU CENTRE DE LA MAILLE
C PGPV PRESSION GAZ CENTRE VOISIN
C
C CVP CONCENTRATION VAPEUR DANS PHASE GAZEUSE
C DCVP1 DERIVEE CVP /P1
C DCVP2 DERIVEE CVP /P2
C CVPV CONCENTRATION VAPEUR DANS PHASE GAZEUSE VOISIN
C DCVP1V DERIVEE CVPV /P1V
C DCVP2V DERIVEE CVPV /P2V
C
C CAS CONCENTRATION AIR SEC DANS PHASE GAZEUSE
C DCAS1 DERIVEE CAS /P1
C DCAS2 DERIVEE CAS /P2
C CASV CONCENTRATION AIR SEC DANS PHASE GAZEUSE VOISIN
C DCAS1V DERIVEE CASV /P1V
C DCAS2V DERIVEE CASV /P2V
C
C CAD ENTRATION AIR DISSOUS
C DCAD1 DERIVEE CAD /P1
C DCAD2 DERIVEE CAD /P2
C CADV CONCENTRATION AIR DISSOUS VOISIN
C DCAD1V DERIVEE CADV /P1V
C DCAD2V DERIVEE CADV /P2V
C
C KINTFA PERMEABILITE INTRINSEQUE SUR UNE FACE
C NT*K*N CACULEE PAR MOYENNE HARMONIQUE
C
C MOBWFA MOBILITE EAU SUR FACE
C DWP1 DERIVEE MODIBILTE EAU / PRESSION 1
C DWP2 DERIVEE MODIBILTE EAU / PRESSION 2
C DWP1V DERIVEE MODIBILTE EAU / PRESSION 1 DU VOISIN
C DWP2V DERIVEE MODIBILTE EAU / PRESSION 2 DU VOISIN

C MOADFA MOBILITE AIR DISSOUS SUR FACE
C DADP1 DERIVEE MODIBILTE AIR DISSOUS / PRESSION 1
C DADP2 DERIVEE MODIBILTE AIR DISSOUS / PRESSION 2
C DADP1V DERIVEE MODIBILTE AIR DISSOUS / PRESSION 1 DU VOISIN
C DADP2V DERIVEE MODIBILTE AIR DISSOUS / PRESSION 2 DU VOISIN
C
C MOASFA MOBILITE AIR SEC SUR FACE
C DASP1 DERIVEE MODIBILTE AIR SEC / PRESSION 1
C DASP2 DERIVEE MODIBILTE AIR SEC / PRESSION 2
C DASP1V DERIVEE MODIBILTE AIR SEC / PRESSION 1 DU VOISIN
C DVPP1V DERIVEE MODIBILTE VAPEUR / PRESSION 1 DU VOISIN

C MOVPFA MOBILITE VAPEUR SUR FACE
C DVPP1 DERIVEE MODIBILTE VAPEUR / PRESSION 1
C DVPP2 DERIVEE MODIBILTE VAPEUR / PRESSION 2
C DASP2V DERIVEE MODIBILTE AIR SEC / PRESSION 2 DU VOISIN
C DVPP2V DERIVEE MODIBILTE VAPEUR / PRESSION 2 DU VOISIN


C FLUW FLUX MASSIQUE EAU SUR FACE POUR TPFA
C FLUVP FLUX MASSIQUE VAPEUR SUR FACE POUR TPFA
C FLUAS FLUX MASSIQUE AIR SEC SUR FACE POUR TPFA
C FLUAD FLUX MASSIQUE AIR DISSOUS SUR FACE POUR TPFA
C
C FWP1 DERIVEE FLUX EAU SUR FACE / PRESSION 1 POUR TPFA
C FWP2 DERIVEE FLUX EAU SUR FACE / PRESSION 2 POUR TPFA
C FWP1V DERIVEE FLUX EAU SUR FACE / PRESSION 1 DU VOIS POUR TPFA
C FWP2V DERIVEE FLUX EAU SUR FACE / PRESSION 2 DU VOIS POUR TPFA

C FVPP1 DERIVEE FLUX VAPEUR SUR FACE / PRESSION 1 POUR TPFA
C FVPP2 DERIVEE FLUX VAPEUR SUR FACE / PRESSION 2 POUR TPFA
C FVPP1V DERIVEE FLUX VAPEUR SUR FACE / PRESSION 1 DU VOIS POUR TPFA
C FVPP2V DERIVEE FLUX VAPEUR SUR FACE / PRESSION 2 DU VOIS POUR TPFA

C FASP1 DERIVEE FLUX AIR SEC SUR FACE / PRESSION 1 POUR TPFA
C FASP2 DERIVEE FLUX AIR SEC SUR FACE / PRESSION 2 POUR TPFA
C FASP1V DERIVEE FLUX AIR SEC SUR FACE / PRESSION 1 DU VOIS POUR TPFA
C FASPV2 DERIVEE FLUX AIR SEC SUR FACE / PRESSION 2 DU VOIS POUR TPFA
C
C FADP1 DERIVEE FLUX AIR DISSOUS SUR FACE / PRESSION 1 POUR TPFA
C FADP2 DERIVEE FLUX AIR DISSOUS SUR FACE / PRESSION 2 POUR TPFA
C FADP1V DERIVEE FLUX AIR DISSOUS SUR FACE/PRESSION1 DU VOIS POUR TPFA
C FADP2V DERIVEE FLUX AIR DISSOUS SUR FACE/PRESSION 2 DU VOIS POUR TPFA
C
C =====================================================================
C VARIABLES LOCALES POUR CALCULS VF SUSHI
C =====================================================================
C
C PCPF PRESSION CAPILLAIRE SUR LA FACE
C
C PGPF PRESSION DE GAZ SUR LA FACE
C DPGP1F DERIVEE DE PRESSION DE GAZ /P1 SUR LA FACE
C DPGP2F DERIVEE DE PRESSION DE GAZ /P2 SUR LA FACE
C
C PWPF PRESSION EAU SUR LA FACE
C DPWP1F DERIVEE DE PRESSION EAU /P1 SUR LA FACE
C DPWP2F DERIVEE DE PRESSION EAU /P2 SUR LA FACE
C
C CVPF CONCENTRATION VAPEUR DANS PHASE GAZEUSE SUR LA FACE
C DCVP1F DERIVEE CVP /P1 SUR LA FACE
C DCVP2F DERIVEE CVP /P2 SUR LA FACE
C
C CASF CONCENTRATION AIR SEC DANS PHASE GAZEUSE SUR LA FACE
C DCAS1F DERIVEE CAS /P1 SUR LA FACE
C DCAS2F DERIVEE CAS /P2 SUR LA FACE
C
C CADF CONCENTRATION AIR DISSOUS SUR LA FACE
C DCAD1F DERIVEE CAD /P1 SUR LA FACE
C DCAD2F DERIVEE CAD /P2 SUR LA FACE
C
C MOBWF(KVOIS,MAXFA) MOBILITE EAU SUR FACE
C DW1F DERIVEE MOBILITE EAU /P1 SUR K
C DW2F DERIVEE MOBILITE EAU /P2 SUR K
C DW1FV DERIVEE MOBILITE EAU /P1 SUR L
C DW2FV DERIVEE MOBILITE EAU /P2 SUR L

C MOADF(KVOIS,MAXFA) MOBILITE AIR DISSOUS SUR FACE
C DAD1F DERIVEE MOBILITE AIR DISSOUS /P1 SUR K
C DAD2F DERIVEE MOBILITE AIR DISSOUS /P2 SUR K
C DAD1FV DERIVEE MOBILITE AIR DISSOUS /P1 SUR L
C DAD2FV DERIVEE MOBILITE AIR DISSOUS /P2 SUR L
C
C MOASF(KVOIS,MAXFA) MOBILITE AIR SEC SUR FACE
C DASP1F DERIVEE MOBILITE AIR SEC /P1 SUR FACE
C DASP2F DERIVEE MOBILITE AIR SEC /P2 SUR FACE
C
C MOVPF(KVOIS,MAXFA) MOBILITE VAPEUR SUR FACE
C DVP1F DERIVEE MOBILITE VAPEUR /P1 SUR K
C DVP2F DERIVEE MOBILITE VAPEUR /P2 SUR K
C DVP1FV DERIVEE MOBILITE VAPEUR /P1 SUR L
C DVP2FV DERIVEE MOBILITE VAPEUR /P2 SUR L
C
C FLKS FLUX (VOLUMIQUE) LIQUIDE F_{K,SIGMA}(PWP)
C DFLKS1 DERIVEE DE FLKS/P1
C DFLKS2 DERIVEE DE FLKS/P2
C
C FTGKS(IFA) FLUX (VOLUMIQUE)GAZ ~F_{K,SIGMA}
C SUR FACE IFA EN NUM DE K
C FTGKS1 DERIVEE DE FTGKS/P1 :
C FTGKS1(MAXFA+1,MAXFA,)
C FTGKS1( 1,IFA ) D_FTGKS(IFA)/DP1K
C FTGKS1(JFA+1,IFA ) D_FTGKS(IFA)/DP1_FACE_JFA_DE_K
C FTGKS2 DERIVEE DE FTGKS/P2
C
C FCLKS FLUX (VOLUMIQUE)LIQUIDE ^F_{K,SIGMA}(CAD)
C DFCLKS1 DERIVEE DE FCLKS/P1
C DFCLKS2 DERIVEE DE FCLKS/P2
C
C FTGKS FLUX (VOLUMIQUE)GAZ ~F_{K,SIGMA}(CVP)
C FTGKS1 DERIVEE DE FTGKS/P1 :
C FTGKS2 DERIVEE DE FTGKS/P2
C
C C MATRICE INTERVENANT DS LE CALCUL DES FLUX FGKS,FLKS
C D MATRICE INTERVENANT DS LE CALCUL DES FLUX FTGKS,FCLKS
C YSS MATRICE INTERVENANT DS LE CALCUL DES MATRICES C ET D
C
C FLUWS FLUX MASSIQUE EAU TOTAL DANS MAILLE SUSHI
C FLUVPS FLUX MASSIQUE VAPEUR TOTAL DANS MAILLE SUSHI
C FLUASS FLUX MASSIQUE AIR SEC TOTAL DANS MAILLE SUSHI
C FLUADS FLUX MASSIQUE AIR DISSOUS TOTAL DANS MAILLE SUSHI
C
C FW1S(MAXFA+1) DERIVEE FLUW / P1_K PUIS P_1,SIGMA
C FW2S(MAXFA+1) DERIVEE FLUW / P2_K PUIS P_2,SIGMA
C FW1SV(IFA DE 1A MAXFA) D_FLUW / D_P1_L
C L : VOSIN DE K PAR IFA
C FW2SV((IFA DE 1A MAXFA) D_FLUW / D_P2_L
C L : VOSIN DE K PAR IFA

C FVP1S(MAXFA+1) DERIVEE FLUVP / P1_K PUIS P_1,SIGMA
C FVP2S(MAXFA+1) DERIVEE FLUVP / P2_K PUIS P_2,SIGMA
C FVP1SV(IFA DE 1A MAXFA) D_FLUVP / D_P1_L
C L : VOSIN DE K PAR IFA
C FVP2SV(IFA DE 1A MAXFA) D_FLUVP / D_P2_L
C L : VOSIN DE K PAR IFA
C
C FAS1S(MAXFA+1) DERIVEE FLUAS / P1_K PUIS P_1,SIGMA
C FAS2S(MAXFA+1) DERIVEE FLUAS / P2_K PUIS P_2,SIGMA
C FAS1SV(IFA DE 1A MAXFA) D_FLUW / D_P1_L
C L : VOSIN DE K PAR IFA
C FAS2SV(IFA DE 1A MAXFA) D_FLUW / D_P1_L
C L : VOSIN DE K PAR IFA
C
C FAD1S(MAXFA+1) DERIVEE FLUAD / P1_K PUIS P_1,SIGMA
C FAD2S(MAXFA+1) DERIVEE FLUAD / P2_K PUIS P_2,SIGMA
C FAD1SV(IFA DE 1A MAXFA) D_FLAD / D_P1_L
C L : VOSIN DE K PAR IFA
C FAD2SV(IFA DE 1A MAXFA) D_FLAD / D_P2_L
C L : VOSIN DE K PAR IFA
C
C DIVP1V DERIVEE DIFFUSION EN VP/PREMIERE VARIABLE EN L
C DIVP2V DERIVEE DIFFUSION EN VP/SECONDE VARIABLE EN L
C DIAS1V DERIVEE DIFFUSION EN AS/PREMIERE VARIABLE EN L
C DIAS2V DERIVEE DIFFUSION EN AS/SECONDE VARIABLE EN L
C DIAD1V DERIVEE DIFFUSION EN AD/PREMIERE VARIABLE EN L
C DIAD2V DERIVEE DIFFUSION EN AD/SECONDE VARIABLE EN L
C
C FMVPS FLUX MASSIQUE VAPEUR INTERVENANT DS EQ DE CONTINUITE
C POUR UNE ARETE EXTERNE
C
C FMWS FLUX MASSIQUE EAU INTERVENANT DS EQ
C DE CONTINUITE POUR UNE ARETE EXTERNE
C
C FMASS FLUX MASSIQUE AIR SEC INTERVENANT DS EQ
C DE CONTINUITE POUR UNE ARETE EXTERNE
C
C FMADS FLUX MASSIQUE AIR DISSOUS INTERVENANT DS EQ
C DE CONTINUITE POUR UNE ARETE EXTERNE
C
C FM1VPS(MAXFA+1,NFACE) DERIVEE DE FMVPS / P_K PUIS P_1,SIGMA
C FM2VPS(MAXFA+1,NFACE) DERIVEE DE FMVPS / P_K PUIS P_2,SIGMA
C FM1WS(MAXFA+1,NFACE) DERIVEE DE FMWS / P_K PUIS P_1,SIGMA
C FM2WS(MAXFA+1,NFACE) DERIVEE DE FMWS / P_K PUIS P_2,SIGMA
C FM1ASS(MAXFA+1,NFACE) DERIVEE DE FMASS / P_K PUIS P_1,SIGMA
C FM2ASS(MAXFA+1,NFACE) DERIVEE DE FMASS / P_K PUIS P_2,SIGMA
C FM1ADS(MAXFA+1,NFACE) DERIVEE DE FMADS / P_K PUIS P_1,SIGMA
C FM2ADS(MAXFA+1,NFACE) DERIVEE DE FMADS / P_K PUIS P_2,SIGMA
C
C
C**********************************************************************
C JE TRAITE DANS CETTE VERSION DE ASSHVF LES TERMES DE DIFFUSION EN
C RÉALISANT UNE MOYENNE ENTRE LA MAILLE COURANTE (K) ET LA MAILLE
C VOISINE L.
C
C REMARQUE :
C ON UTILISE LA NUMEROTATION LOCALE DE LA MAILLE COURANTE POUR
C LES VARIABLES INDENTES PAR K ET ON UTILISE LA NUMEROTATION
C LOCALE DE LA AMILLE L POUR LES VARIABLES INDENTES PAR L
C EXEMPLE : FCLKS => NUMEROTATION LOCALE DE K
C FCLLS => NUMEROTATION LOCALE DE L
C**********************************************************************
C
C FCLLS FLUX (VOLUMIQUE)LIQUIDE DE LA MAILLE L: ^F_{L,SIGMA}(CAD)
C DFCLKS1 DERIVEE DE FCLLS/P1
C DFCLKS2 DERIVEE DE FCLLS/P
C
C FTGLS(IFAV) FLUX (VOLUMIQUE)GAZ DE LA MAILLE L: ~F_{L,SIGMA}(CVP)
C SUR FACE IFAV EN NUM DE L =VOISIN DE K
C FTGLS1 DERIVEE DE FTGLS/P1
C FTGLS1(MAXFA+1,MAXFA,)
C FTGLS1( 1,IFAV ) D_FTGLS(IFAV)/DP1L
C FTGLS1(JFAV+1,IFAV ) D_FTGLS(IFAV)/DP1_FACE_JFAV_DE_L
C JFAV EST EN NUMEROTATION LOCALE DE L
C FTGLS2 DERIVEE DE FTGLS/P2
C
C
C FLVPSK(IFA) PRODUIT DE LA DIFFUSION DE VAPEUR D'EAU PAR FTGKS
C SUR FACE IFA EN NUM DE K
C FVP1SK DERIVEE DE FLVPSK PAR RAPPORT A PRE1 AU CENTRE ET
C AUX ARETES :
C FVP1SK(MAXFA+1,MAXFA,)
C FVP1SK( 1,IFA ) D_FLVPSK(IFA)/DP1K
C FVP1SK(JFA+1,IFA ) D_FLVPSK(IFA)/DP1_FACE_JFA_DE_K
C FVP2SK DERIVEE DE FLVPSK PAR RAPPORT A PRE2 AU CENTRE ET
C AUX ARETES
C
C FLASSK PRODUIT DE LA DIFFUSION DE L AIR SEC PAR FTGKS
C FAS1SK DERIDEE DE FLASSK PAR RAPPORT A PRE1 AU CENTRE ET
C AUX ARETES
C FAS2SK DERIDEE DE FLASSK PAR RAPPORT A PRE2 AU CENTRE ET
C AUX ARETES
C
C FLADSK PRODUIT DE LA DIFFUSION DE L AIR DISSOUS PAR FCLKS
C FAD1SK DERIDEE DE FLADSK PAR RAPPORT A PRE1 AU CENTRE ET
C AUX ARETES
C FAD2SK DERIDEE DE FLADSK PAR RAPPORT A PRE2 AU CENTRE ET
C AUX ARETES
C
C FLVPSL(IFAV) PRODUIT DE LA DIFFUSION DE VAPEUR D'EAU PAR FTGKS
C SUR FACE IFAV EN NUM DE L =VOISIN DE K
C FVP1SL DERIVEE DE FLVPSL PAR RAPPORT A PRE1 AU CENTRE ET
C AUX ARETES :
C FVP1SL(MAXFA+1,MAXFA,)
C FVP1SL( 1,IFAV ) D_FLVPSL(IFAV)/DP1L
C FVP1SL(JFAV+1,IFAV ) D_FLVPSL(IFAV)/DP1_FACE_JFAV_DE_L
C JFAV EST EN NUMEROTATION LOCALE DE L
C FVP2SL DERIVEE DE FLVPSK PAR RAPPORT A PRE2 AU CENTRE ET
C AUX ARETES
C
C FLASSL PRODUIT DE LA DIFFUSION DE L AIR SEC PAR FTGLS
C FAS1SL DERIDEE DE FLASSL PAR RAPPORT A PRE1 AU CENTRE ET
C AUX ARETES
C FAS2SL DERIDEE DE FLASSL PAR RAPPORT A PRE2 AU CENTRE ET
C AUX ARETES
C
C FLADSL PRODUIT DE LA DIFFUSION DE L AIR DISSOUS PAR FCLLS
C FAD1SL DERIDEE DE FLADSL PAR RAPPORT A PRE1 AU CENTRE ET
C AUX ARETES
C FAD2SL DERIDEE DE FLADSL PAR RAPPORT A PRE2 AU CENTRE ET
C AUX ARETES
C
C MOYVP(MAXFA) DEMI SOMME DE FLVPSK ET FLVPSL
C FA ETANT CONJUGUEES / FA DAN K = FAV DANS L
C MOYVP(FA) 0.5(FLVPSK(FA) ET FLVPSL(FAV))
C MOYVP1(MAXFA,MAXFA+1,0:1)
C DERIVEES DE MOYVP POUR LA VARIABLE P1 :
C MOYVP1(IFA,1,0 ) D_MOYVP(IFA)/DP1K
C MOYVP1(IFA,JFA+1,0 ) D_MOYVP(IFA)/DP1_FACE_JFA_DE_K
C MOYVP1(IFA,1,1 ) D_MOYVP(IFA)/DP1L
C MOYVP1(IFA,JFAV+1,1 ) D_MOYVP(IFA)/DP1_FACE_JFAV_DE_L
C
C MOYVP2(MAXFA,MAXFA+1,0:1) DERIVEES DE MOYVP POUR UNE ARETE
C COMMUNE A K ET L PAR RAPPORT A PRE2 AU CENTRE PUIS
C AU ARETE DE K PUIS PAR RAPPORT A PRE2 AU CENTRE PUIS
C AU ARETE DE L
C
C MOYAS TABLEAU STOCKANT LA DEMI SOMME DE FLASSK ET FLASSL
C MOYAS1(MAXFA,MAXFA+1,0:1) DERIVEES DE MOYAS
C MOYAS2(MAXFA,MAXFA+1,0:1) DERIVEES DE MOYAS
C
C MOYAD TABLEAU STOCKANT LA DEMI SOMME DE FLADSK ET FLADSL
C MOYAD1(MAXFA,MAXFA+1,0:1) DERIVEES DE MOYAD
C MOYAD2(MAXFA,MAXFA+1,0:1) DERIVEES DE MOYAD
C
C FAV NUMERO DE L ARETE DANS LA NUMEROTATION LOCALE DE
C LA MAILLE VOISINE L
C
C VOIFA(IFA,1) = NUMERO DE MAILLE PARTAGEANT LA FACE IFA
C VOIFA(IFA,2) = NUMERO DE LA FACE IFA
C DANS LA NUMEROTATION LOCALE DU VOISIN

C  VALFAC(I,CON,WLIQ)     CONCENTRATION DE L EAU LIQUIDE SUR ARRETE I
C  VALFAC(I,DCONP1,WLIQ)  D_CON_EAU_LIQU_I /P1
C  VALFAC(I,DCONP2,WLIQ)  D_CON_EAU_LIQU_I /P2
C  VALFAC(I,DIFFU,WLIQ)   DIFFUW SUR ARETE I
C  VALFAC(I,DDIFP1,WLIQ)  D_DIFFUW_I /P1
C  VALFAC(I,DDIFP2,WLIQ)  D_DIFFUW_I /P2
C  VALFAC(I,MOB,WLIQ)     MOBILITE DE L EAU LIQUIDE SUR ARETE I
C  VALFAC(I,DMOBP1,WLIQ)  D_MO_LIQU /P1_CENTRE
C  VALFAC(I,DMOBP2,WLIQ)  D_MO_LIQU /P2_CENTRE
C
C NB: DE MEME POUR WVAP(EAU VAPEUR),AIRDIS(AIR DISSOUS),AIRSEC(AIR SEC)
C
C VALFAV  MEME TABLEAU MAIS POUR LES VOISINS
C  VALCEN(MOB,WLIQ)       MOBILITE EAU LIQUIDE CENTRE
C  VALCEN(DMOBP1,WLIQ)    D_MO_LIQU /P1_CENTRE
C  VALCEN(DMOBP2,WLIQ)    D_MO_LIQU /P2_CENTRE
C NB: DE MEME POUR WVAP(EAU VAPEUR),AIRDIS(AIR DISSOUS),AIRSEC(AIR SEC)
C  VALCEN(CON,WVAP)       CONCENTRATION EAU VAPEUR
C  VALCEN(DCONP1,WVAP)
C  VALCEN(DCONP2,WVAP)
C  VALCEN(CON,AIRDIS)     CONCENTRATION AIR DISSOUS
C  VALCEN(DCONP1,AIRDIS)
C  VALCEN(DCONP2,AIRDIS)
C  VALCEN(DIFFU,WLIQ)     DIFFU EAU LIQUIDE
C  VALCEN(DDIFP1,WLIQ)
C  VALCEN(DDIFP2,WLIQ)
C NB: DE MEME POUR WVAP(EAU VAPEUR),AIRDIS(AIR DISSOUS),AIRSEC(AIR SEC)
C
C VARIATION MASSE TOTALE EAU/DTEMPS
C
C   VALCEN(MASSE ,EAU) MASSE EAU
C   VALCEN(DMASP1,EAU)
C   VALCEN(DMASP2,EAU)
C
C VARIATION MASSE TOTALE AIR/DTEMPS
C
C  VALCEN(MASSE ,AIR) MASSE AIR
C  VALCEN(DMASP1,AIR)
C  VALCEN(DMASP2,AIR)
C
C   VALCEV IDEM POUR VOISIN
C =====================================================================
C VARIABLES COMMUNES
C =====================================================================

      LOGICAL VF
      INTEGER NUMAV
      REAL*8  XLK,YLK,TKL
      REAL*8  MFACE(MAXFA),DFACE(MAXFA),
     &        XFACE(MAXDIM,MAXFA),
     &        NORMFA(MAXDIM,MAXFA),VOL,VOLV
      INTEGER IFA,JFA,JFAV,IDIM
      REAL*8  PCP,PCPV,DPWP1,DPWP2
      REAL*8  PGP,PGPV,DPGP1,DPGP2
      REAL*8  PWP,PWPV
      REAL*8  CVP,CVPV,DCVP1,DCVP2,DCVP1V,DCVP2V
      REAL*8  CAS,CASV,DCAS1,DCAS2,DCAS1V,DCAS2V
      REAL*8  CAD,CADV,DCAD1,DCAD2,DCAD1V,DCAD2V

      INTEGER NFACEM
      INTEGER VOIFA(MAXFA,2)
      LOGICAL FINTER(MAXFA)
C =====================================================================
C VARIABLES VF A 2 POINTS
C =====================================================================
      REAL*8 FLUW,FLUVP,FLUAS,FLUAD
      REAL*8 FWP1,FWP2,FWP1V,FWP2V
      REAL*8 FVPP1,FVPP2,FVPP1V,FVPP2V
      REAL*8 FASP1,FASP2,FASP1V,FASP2V
      REAL*8 FADP1,FADP2,FADP1V,FADP2V
C
      REAL*8 KINTFA
      REAL*8 MOBWFA,MOADFA,MOASFA,MOVPFA
      REAL*8 DWP1V,DADP1V,DWP2V,DADP2V,DWP1,DADP1,DWP2,DADP2
      REAL*8 DASP1V,DVPP1V,DASP2V,DVPP2V,DASP1,DVPP1,DASP2,DVPP2
      REAL*8 DIVP1V,DIVP2V
      REAL*8 DIAS1V,DIAS2V
      REAL*8 DIAD1V,DIAD2V

C =====================================================================
C VARIABLES VF SUSHI
C =====================================================================

      REAL*8 FLUWS,FLUVPS,FLUASS,FLUADS
C
      REAL*8 FW1S(MAXFA+1) ,FW2S(MAXFA+1)
      REAL*8 FVP1S(MAXFA+1),FVP2S(MAXFA+1)
      REAL*8 FAS1S(MAXFA+1),FAS2S(MAXFA+1)
      REAL*8 FAD1S(MAXFA+1),FAD2S(MAXFA+1)
C
      REAL*8 FW1SV(MAXFA) ,FW2SV(MAXFA)
      REAL*8 FVP1SV(MAXFA),FVP2SV(MAXFA)
      REAL*8 FAS1SV(MAXFA),FAS2SV(MAXFA)
      REAL*8 FAD1SV(MAXFA),FAD2SV(MAXFA)
C
      REAL*8 FMVPS(MAXFA),FMWS(MAXFA)
      REAL*8 FMASS(MAXFA),FMADS(MAXFA)
C
      REAL*8 FM1VPS(MAXFA+1,MAXFA),FM2VPS(MAXFA+1,MAXFA)
      REAL*8 FM1WS(MAXFA+1,MAXFA) ,FM2WS(MAXFA+1,MAXFA)
      REAL*8 FM1ASS(MAXFA+1,MAXFA),FM2ASS(MAXFA+1,MAXFA)
      REAL*8 FM1ADS(MAXFA+1,MAXFA),FM2ADS(MAXFA+1,MAXFA)
C
      REAL*8 PCPF(MAXFA),PGPF(MAXFA),DPGP1F(MAXFA),
     &       DPGP2F(MAXFA),PWPF(MAXFA),DPWP1F(MAXFA),
     &       DPWP2F(MAXFA),CVPF(MAXFA),DCVP1F(MAXFA),
     &       CADF(MAXFA),DCAD1F(MAXFA),DCAD2F(MAXFA),
     &       DCVP2F(MAXFA)
C
      REAL*8 MOBWF(0:NVOIMA,MAXFA) ,MOADF(0:NVOIMA,MAXFA),
     &       MOASF(0:NVOIMA,MAXFA) ,MOVPF(0:NVOIMA,MAXFA),
     &       DW1F(0:NVOIMA,MAXFA)  ,DW2F(0:NVOIMA,MAXFA),
     &       DW1FV(0:NVOIMA,MAXFA) ,DW2FV(0:NVOIMA,MAXFA),
     &       DVP1F(0:NVOIMA,MAXFA) ,DVP2F(0:NVOIMA,MAXFA),
     &       DVP1FV(0:NVOIMA,MAXFA),DVP2FV(0:NVOIMA,MAXFA),
     &       DAS1F(0:NVOIMA,MAXFA) ,DAS2F(0:NVOIMA,MAXFA),
     &       DAS1FV(0:NVOIMA,MAXFA),DAS2FV(0:NVOIMA,MAXFA),
     &       DAD1F(0:NVOIMA,MAXFA) ,DAD2F(0:NVOIMA,MAXFA),
     &       DAD1FV(0:NVOIMA,MAXFA),DAD2FV(0:NVOIMA,MAXFA)


      REAL*8 YSS(MAXDIM,MAXFA,MAXFA)
      REAL*8 C(MAXFA,MAXFA),D(MAXFA,MAXFA)

      REAL*8 FLKS(MAXFA),DFLKS1(MAXFA+1,MAXFA),
     &       DFLKS2(MAXFA+1,MAXFA),
     &       FGKS(MAXFA),DFGKS1(MAXFA+1,MAXFA),
     &       DFGKS2(MAXFA+1,MAXFA),
     &       FTGKS(MAXFA),FTGKS1(MAXFA+1,MAXFA),
     &       FTGKS2(MAXFA+1,MAXFA),
     &       FCLKS(MAXFA),FCLKS1(MAXFA+1,MAXFA),
     &       FCLKS2(MAXFA+1,MAXFA)

C
C SI FICKFA =TRUE LES DIFFUSIVITES DE FICK SONT CALCULEES A PARTIR
C DES INCONNUES AUX FACES SINON ELLES SONT CALCULEES AU CENTRE

      LOGICAL FICKFA
C SI DECEN=.FALSE. ALORS ON NE FAIT AUCUN DECENTRAGE
      LOGICAL DECEN
C=====================================================================
C NOUVELLE VARIABLES POUR SUSHI POUR TRAITER LA DIFFUSION PAR MOYENNE
      REAL*8 FCLLS(MAXFA),FCLLS1(MAXFA+1,MAXFA),
     &       FCLLS2(MAXFA+1,MAXFA),FTGLS(MAXFA),
     &       FTGLS2(MAXFA+1,MAXFA),
     &       FTGLS1(MAXFA+1,MAXFA)

      REAL*8 FLVPSK(MAXFA),FVP1SK(MAXFA+1,MAXFA),
     &       FVP2SK(MAXFA+1,MAXFA),
     &       FLASSK(MAXFA),FAS1SK(MAXFA+1,MAXFA),
     &       FAS2SK(MAXFA+1,MAXFA),
     &       FLADSK(MAXFA),FAD1SK(MAXFA+1,MAXFA),
     &       FAD2SK(MAXFA+1,MAXFA)

      REAL*8 FLVPSL(MAXFA),FVP1SL(MAXFA+1,MAXFA),
     &       FVP2SL(MAXFA+1,MAXFA),
     &       FLASSL(MAXFA),FAS1SL(MAXFA+1,MAXFA),
     &       FAS2SL(MAXFA+1,MAXFA),
     &       FLADSL(MAXFA),FAD1SL(MAXFA+1,MAXFA),
     &       FAD2SL(MAXFA+1,MAXFA)

      REAL*8 MOYVP(MAXFA),MOYVP1(MAXFA,MAXFA+1,0:1),
     &       MOYVP2(MAXFA,MAXFA+1,0:1),
     &       MOYAS(MAXFA),MOYAS1(MAXFA,MAXFA+1,0:1),
     &       MOYAS2(MAXFA,MAXFA+1,0:1),
     &       MOYAD(MAXFA),MOYAD1(MAXFA,MAXFA+1,0:1),
     &       MOYAD2(MAXFA,MAXFA+1,0:1)

      REAL*8 DL (MAXFA,MAXFA)
      REAL*8 YSSL (MAXDIM,MAXFA,MAXFA)
      REAL*8 CL (MAXFA,MAXFA)

      INTEGER FAV,IFAV
      REAL*8  MFACEL(MAXFA),DFACEL(MAXFA),
     &        XFACEL(MAXDIM,MAXFA),
     &        NORFAL(MAXDIM,MAXFA)

      REAL*8   CVPFV(MAXFA),CVPFV1(MAXFA),CVPFV2(MAXFA)
      REAL*8   CADFV(MAXFA),CADFV1(MAXFA),CADFV2(MAXFA)
      INTEGER  NFACEV
      INTEGER  NNOV,NNOSV
      REAL*8   ALPHA
      INTEGER  NUFLOC
      LOGICAL  UTICER
      REAL*8   XG(MAXDIM),XL(MAXDIM)
      REAL*8   RHOL,RHOG,DRHOL1,DRHOL2,DRHOG1,DRHOG2
      REAL*8   ZERO
      INTEGER  ZZADMA,IVOIS,LIG,COL
      INTEGER  IADP1K,IADP2K,IADP1,IADP2
      INTEGER  ADCM1,ADCM2,ADCF1,ADCF2
C---------------------------------------
C FONCTIONS FORMULES D ADRESSAGE DES DDL
C---------------------------------------
      ZZADMA(IVOIS,LIG,COL)=IVOIS*DIMUEL*DIMUEL+(LIG-1)*DIMUEL+COL
      IADP1(FA)=2*(FA-1)+1
      IADP2(FA)=2*(FA-1)+2
      ADCF1(FA)=2*(FA-1)+1
      ADCF2(FA)=2*(FA-1)+2
      IF ( TYPVF.EQ.1) THEN
       IADP1K= 1
       IADP2K= 2
       ADCM1 = 1
       ADCM2 = 2
      ELSE
       IADP1K=2*NFACE+1
       IADP2K=2*NFACE+2
       ADCM1 = 2*NFACE+1
       ADCM2 = 2*NFACE+2
      ENDIF
      CALL INICSU(VALCEN,VALCEV, VALFAC, VALFAV,MAXFA)
C
C
      ALPHA = CRIT(13)
C============================
C ACTUELLEMENT ON OBLIGE A PRENDRE LE CENTRE
C AU CENTRE DE GRAVITE
C SI ON VEUT TESTER LE CENTRE DU CERCLE CIRCONSCRIT
C ON PRENDRA UTICER = TRUE
C===============================
       UTICER = .FALSE.
C
      CALL ASSERT(
     & (OPTION(1:9).EQ.'RIGI_MECA' ) .OR.
     & (OPTION(1:9).EQ.'RAPH_MECA' ) .OR.
     & (OPTION(1:9).EQ.'FULL_MECA' ))
C
      FICKFA = .FALSE.
      VF = .TRUE.
      PERMAN = .FALSE.
C
      DECEN = .TRUE.
C
      DO 1 IFA = 1 , NFACE
         FINTER(IFA)=.FALSE.
         VOIFA(IFA,1)=0
         VOIFA(IFA,2)=0
    1 CONTINUE

      DO 100 IDIM = 1 , NDIM
         XG(IDIM)=GEOM(IDIM,NNO)
  100 CONTINUE
C
      ZERO=0.D0
C
C ====================================================================
C --- DETERMINATION DES VARIABLES CARACTERISANT LE MILIEU ------------
C ====================================================================
      YAMEC = MECANI(1)
      ADDEME = MECANI(2)
      ADCOME = MECANI(3)
      YAP1 = PRESS1(1)
      ADDEP1 = PRESS1(3)
      ADCP11 = PRESS1(4)
      ADCP12 = PRESS1(5)
      YAP2 = PRESS2(1)
      ADDEP2 = PRESS2(3)
      ADCP21 = PRESS2(4)
      ADCP22 = PRESS2(5)
      YATE = TEMPE(1)
      ADDETE = TEMPE(2)
      ADCOTE = TEMPE(3)
C ====================================================================
C --- CALCUL DE CONSTANTES TEMPORELLES -------------------------------
C ====================================================================
C
      LOI = ' '
      FAMI='FPG1'
      KPG=1
      SPT=1
      POUM='+'
      CALL RCVALB(FAMI,KPG,SPT,POUM,IMATE,' ','THM_INIT', 0, ' ', 0.D0,
     &  1, 'COMP_THM',RTHMC, CODMES, 1)
      THMC = COMPOR(8)
      IF ( (RTHMC-1.0D0).LT.R8PREM() ) THEN
         LOI = 'LIQU_SATU'
      ELSE IF ( (RTHMC-2.0D0).LT.R8PREM() ) THEN
        LOI = 'GAZ'
      ELSE IF ( (RTHMC-3.0D0).LT.R8PREM() ) THEN
         LOI = 'LIQU_VAPE'
      ELSE IF ( (RTHMC-4.0D0).LT.R8PREM() ) THEN
         LOI = 'LIQU_VAPE_GAZ'
      ELSE IF ( (RTHMC-5.0D0).LT.R8PREM() ) THEN
         LOI = 'LIQU_GAZ'
      ELSE IF ( (RTHMC-6.0D0).LT.R8PREM() ) THEN
         LOI = 'LIQU_GAZ_ATM'
      ELSE IF ( (RTHMC-9.0D0).LT.R8PREM() ) THEN
         LOI = 'LIQU_AD_GAZ_VAPE'
      ELSE IF ( (RTHMC-10.0D0).LT.R8PREM() ) THEN
         LOI = 'LIQU_AD_GAZ'
      ENDIF
      IF (THMC.NE.LOI) THEN
          VALK(1) = LOI
          VALK(2) = THMC
          CALL U2MESK('F','ALGORITH_34', 2 ,VALK)
      ENDIF
C ====================================================================
C DECLARATION DE DEUX LOGIQUES POUR SAVOIR CE QUE L ON DOIT CALCULER
C TANGE => CALCUL OPERATEUR TANGENT => MATUU
C CONT => CALCUL RESIDU => VECTU
C ====================================================================
      CONT = .FALSE.
      TANGE = .FALSE.
C
      IF (OPTION(1:9).EQ.'RIGI_MECA') THEN
         TANGE = .TRUE.
      ELSE IF (OPTION(1:9).EQ.'RAPH_MECA') THEN
         CONT = .TRUE.
      ELSE IF (OPTION(1:9).EQ.'FULL_MECA') THEN
         TANGE = .TRUE.
         CONT = .TRUE.
      ELSE
          VALK(1) = OPTION
          CALL U2MESK('F','VOLUFINI_8', 1 ,VALK)
      ENDIF
C ====================================================================
C --- INITIALISATION A ZERO MATUU ET VECTU
C ====================================================================
      IF (TANGE) THEN
         DO 2, I = 1 , (NBVOIS+1)*DIMUEL*DIMUEL
            MATUU(I)=0.D0
    2 CONTINUE
      ENDIF
      IF(CONT) THEN
         DO 3 I = 1 , DIMUEL
            VECTU(I)=0.D0
    3 CONTINUE
      ENDIF
C ================================================================
C --- INITIALISATION
C ================================================================
      PCPV=0.D0
      PGPV=0.D0
      PWPV=0.D0
      DO 31 I=1,MAXFA
            PCPF(I)  =0.D0
            PGPF(I)  =0.D0
            DPGP1F(I)=0.D0
            DPGP2F(I)=0.D0
            PWPF(I)  =0.D0
            DPWP1F(I)=0.D0
            DPWP2F(I)=0.D0
            CVPF(I)  =0.D0
            DCVP1F(I)=0.D0
            DCVP2F(I)=0.D0
            CADF(I)  =0.D0
            DCAD1F(I)=0.D0
            DCAD2F(I)=0.D0
   31 CONTINUE
C ================================================================
C --- CALCUL DES QUANTITES GEOMETRIQUES
C ================================================================
      CALL CABHVF(MAXFA,MAXDIM,
     &            NDIM,NNO,NNOS,NFACE,AXI,GEOM,
     &            VOL,MFACE,DFACE,XFACE,NORMFA,UTICER)
C ================================================================
C --- CALCUL DES DEFORMATIONS GENERALISEES ----------------------
C ON MET DANS LE TABLEAU DES DEF GENERALISES LES PRESSIONS
C LES GRADIENTS SONT MIS A ZERO CAR ON NE SAIT PAS LES CALCULER
C A CE NIVEAU EN VF4
C ================================================================
      IF((TYPVF.EQ.1).OR.(TYPVF.EQ.2)) THEN
         IF (YAP1.EQ.1) THEN
            DEFGEM(ADDEP1)= DEPLM(IADP1K)
            DEFGEP(ADDEP1)= DDEPL(IADP1K)+DEPLM(IADP1K)
            DO 4 I=1,NDIM
               DEFGEM(ADDEP1+I)=0.D0
               DEFGEP(ADDEP1+I)=0.D0
    4       CONTINUE
            IF (YAP2.EQ.1) THEN
               DEFGEM(ADDEP2)= DEPLM(IADP2K)
               DEFGEP(ADDEP2)= DDEPL(IADP2K)+DEPLM(IADP2K)
               DO 5 I=1,NDIM
                  DEFGEM(ADDEP2+I)=0.D0
                  DEFGEP(ADDEP2+I)=0.D0
    5          CONTINUE
            ENDIF
         ENDIF
      ELSE
         CALL U2MESG('F','VOLUFINI_9',0,' ',1,TYPVF,0,0.D0)
      ENDIF
C ===============================================
C ==== INITIALISATION DE DSDE ================
C ===============================================
      DO 6 I=1, DIMCON
         DO 6 J = 1 , DIMDEF
            DSDE(I,J)=0.D0
    6 CONTINUE
C
      CALL COMTHM(OPTION,PERMAN,VF,0,VALFAC,VALCEN,
     &            IMATE,TYPMOD,COMPOR,
     &            CRIT,RINSTM,RINSTP,
     &            NDIM,DIMDEF,DIMCON,NBVARI,YAMEC,YAP1,
     &            YAP2,YATE,ADDEME,ADCOME,ADDEP1,ADCP11,
     &            ADCP12,ADDEP2,ADCP21,ADCP22,ADDETE,ADCOTE,
     &            DEFGEM,DEFGEP,
     &            CONGEM,CONGEP,
     &            VINTM(1,1),VINTP(1,1),
     &            DSDE,PESA,RETCOM,1,1,
     &               P10,P20)
      IF ( RETCOM.NE.0) THEN
         CALL U2MESK('F','COMPOR1_9', 0 ,' ')
      ENDIF
      IF((TYPVF.EQ.2) ) THEN
         DO 7 FA= 1 , NFACE
            IF (YAP1.EQ.1) THEN
               DEFGEM(ADDEP1)= DEPLM(IADP1(FA))
               DEFGEP(ADDEP1)= DDEPL(IADP1(FA))+DEPLM(IADP1(FA))
               DO 701 I=1,NDIM
                  DEFGEM(ADDEP1+I)=0.D0
                  DEFGEP(ADDEP1+I)=0.D0
  701          CONTINUE
               IF (YAP2.EQ.1) THEN
                  DEFGEM(ADDEP2)= DEPLM(IADP2(FA))
                  DEFGEP(ADDEP2)= DDEPL(IADP2(FA))+DEPLM(IADP2(FA))
                  DO 702 I=1,NDIM
                     DEFGEM(ADDEP2+I)=0.D0
                     DEFGEP(ADDEP2+I)=0.D0
  702             CONTINUE
               ENDIF
            ELSE
               CALL U2MESG('F','VOLUFINI_9',0,' ',1,TYPVF,0,0.D0)
            ENDIF
C ===============================================
C ==== INITIALISATION DE DSDE ================
C ===============================================
            DO 703 I=1, DIMCON
               DO 61 J = 1 , DIMDEF
                  DSDE(I,J)=0.D0
   61          CONTINUE
  703       CONTINUE
            CALL COMTHM(OPTION,PERMAN,VF,FA,VALFAC,VALCEN,
     &                  IMATE,TYPMOD,COMPOR,
     &                  CRIT,RINSTM,RINSTP,
     &                  NDIM,DIMDEF,DIMCON,NBVARI,YAMEC,YAP1,
     &                  YAP2,YATE,ADDEME,ADCOME,ADDEP1,ADCP11,
     &                  ADCP12,ADDEP2,ADCP21,ADCP22,ADDETE,ADCOTE,
     &                  DEFGEM,DEFGEP,
     &                  CONGEM,CONGEP,VINTM(1,FA+1),VINTP(1,FA+1),
     &                  DSDE,PESA,RETCOM,1,1,
     &                  P10,P20)
            IF ( RETCOM.NE.0) THEN
               CALL U2MESK('F','COMPOR1_9', 0 ,' ')
            ENDIF
    7    CONTINUE
      ENDIF
      IF( CONT ) THEN
         VECTU(ADCM1)=VALCEN(MASSE ,EAU)*VOL
         VECTU(ADCM2)=VALCEN(MASSE ,AIR)*VOL
      ENDIF
      IF ( TANGE ) THEN
         MATUU(ZZADMA(0,ADCM1,IADP1K))=VALCEN(DMASP1,EAU)*VOL
         MATUU(ZZADMA(0,ADCM2,IADP1K))=VALCEN(DMASP1,AIR)*VOL
         MATUU(ZZADMA(0,ADCM1,IADP2K))=VALCEN(DMASP2,EAU)*VOL
         MATUU(ZZADMA(0,ADCM2,IADP2K))=VALCEN(DMASP2,AIR)*VOL
      ENDIF
      RHOL=VALCEN(DENSIT ,RHOLQ)
      DRHOL1=VALCEN(DENSIT ,RHOLQ1)
      DRHOL2=VALCEN(DENSIT ,RHOLQ2)
C
      RHOG=VALCEN(DENSIT ,RHOGA)
      DRHOG1=VALCEN(DENSIT ,RHOGA1)
      DRHOG2=VALCEN(DENSIT ,RHOGA2)
C
C CALCUL DES QUANTITES RELATIVES AUX FACES POUR LE VF SUSHI
C
      IF((TYPVF.EQ.2) ) THEN
C
C CALCUL DES MATRICES YSS C ET D
C
         IF ( NDIM.EQ.2) THEN
            KINTVF(1) = VALCEN(VKINT ,KXX)
            KINTVF(2) = VALCEN(VKINT ,KYY)
            KINTVF(3) = VALCEN(VKINT ,KXY)
            KINTVF(4) = 0.D0
            KINTVF(5) = 0.D0
            KINTVF(6) = 0.D0
         ELSE
            KINTVF(1) = VALCEN(VKINT ,KXX)
            KINTVF(2) = VALCEN(VKINT ,KYY)
            KINTVF(3) = VALCEN(VKINT ,KZZ)
            KINTVF(4) = VALCEN(VKINT ,KXY)
            KINTVF(5) = VALCEN(VKINT ,KYZ)
            KINTVF(6) = VALCEN(VKINT ,KZX)
         ENDIF
         CALL CACDSU(MAXFA,MAXDIM,ALPHA,
     &               NDIM,NNO,NFACE,GEOM,
     &               VOL,MFACE,DFACE,XFACE,NORMFA,KINTVF,YSS,C,D)
         PCP = DDEPL(IADP1K)+DEPLM(IADP1K)+P10
         PGP = DDEPL(IADP2K)+DEPLM(IADP2K)+P20
         IF (LOI .EQ. 'LIQU_AD_GAZ' ) THEN
C
C ON STOCK PC ET PG DU CENTRE SUR TOUS LES POINTS DE GAUSS
C EN VUE DE POST TRAITEMENT
C CECI EST PROVISOIRE
C
            CALL NVITHM(COMPOR, MECA, THMC, THER, HYDR, NVIM, NVIT,
     &                  NVIH, NVIC, ADVIME, ADVITH, ADVIHY, ADVICO,
     &                  VIHRHO, VICPHI, VICPVP, VICSAT,VICPR1,VICPR2)
            DO 71 IPG=1,NFACE+1
               VINTP(ADVICO+VICPR1,IPG) = PCP
               VINTP(ADVICO+VICPR2,IPG) = PGP
   71       CONTINUE
         ENDIF
         DPGP1 = 0.D0
         DPGP2 = 1.D0
         DO 8 IFA =1,NFACE
            PCPF(IFA) = DDEPL(IADP1(IFA))+DEPLM(IADP1(IFA))+P10
            PGPF(IFA) = DDEPL(IADP2(IFA))+DEPLM(IADP2(IFA))+P20
            DPGP1F(IFA) = 0.D0
            DPGP2F(IFA) = 1.D0
    8    CONTINUE
         PWP = PGP-PCP
         DPWP1 = -1.D0
         DPWP2 = +1.D0
         DO 9 IFA=1,NFACE
            PWPF(IFA) = PGPF(IFA)-PCPF(IFA)
            DPWP1F(IFA) = -1.D0
            DPWP2F(IFA) = 1.D0
    9    CONTINUE
         CVP = VALCEN(CON,WVAP)
         DCVP1 = VALCEN(DCONP1,WVAP)
         DCVP2 = VALCEN(DCONP2,WVAP)
         DO 10 IFA=1,NFACE
            CVPF (IFA) = VALFAC(IFA,CON,WVAP)
            DCVP1F(IFA) = VALFAC(IFA,DCONP1,WVAP)
            DCVP2F(IFA) = VALFAC(IFA,DCONP2,WVAP)
   10    CONTINUE
         CAS = 1-CVP
         DCAS1 = -DCVP1
         DCAS2 = -DCVP2
         CAD = VALCEN(CON,AIRDIS)
         DCAD1 = VALCEN(DCONP1,AIRDIS)
         DCAD2 = VALCEN(DCONP2,AIRDIS)
         DO 11 IFA=1,NFACE
            CADF(IFA) = VALFAC(IFA,CON,AIRDIS)
            DCAD1F(IFA) = VALFAC(IFA,DCONP1,AIRDIS)
            DCAD2F(IFA) = VALFAC(IFA,DCONP2,AIRDIS)
   11    CONTINUE
C ===========================================================
C INITIALISATION
C ===========================================================
         DO 12 IFA =1,MAXFA+1
            FW1S(IFA)=0.D0
            FW2S(IFA)=0.D0
            FVP1S(IFA)=0.D0
            FVP2S(IFA)=0.D0
            FAS1S(IFA)=0.D0
            FAS2S(IFA)=0.D0
            FAD1S(IFA)=0.D0
            FAD2S(IFA)=0.D0
   12    CONTINUE
         DO 121 IFA =1,MAXFA
            FW1SV(IFA)=0.D0
            FW2SV(IFA)=0.D0
            FVP1SV(IFA)=0.D0
            FVP2SV(IFA)=0.D0
            FAS1SV(IFA)=0.D0
            FAS2SV(IFA)=0.D0
            FAD1SV(IFA)=0.D0
            FAD2SV(IFA)=0.D0
  121    CONTINUE
         FLUWS=0.D0
         FLUVPS=0.D0
         FLUASS=0.D0
         FLUADS=0.D0
         DO 13 IFA=1,MAXFA
            FMVPS(IFA)=0.D0
            FMWS(IFA)=0.D0
            FMASS(IFA)=0.D0
            FMADS(IFA)=0.D0
C
            FLKS(IFA) =0.D0
            FGKS(IFA) =0.D0
            FCLKS(IFA) =0.D0
            FTGKS(IFA) =0.D0
C
            FLVPSK(IFA)=0.D0
            FLASSK(IFA)=0.D0
            FLADSK(IFA)=0.D0
C
            MOYVP(IFA)=0.D0
            MOYAS(IFA)=0.D0
            MOYAD(IFA)=0.D0
   13    CONTINUE
         DO 14 JFA=1,MAXFA
            DO 14 IFA=1,MAXFA+1
               DFLKS1(IFA,JFA)=0.D0
               DFLKS2(IFA,JFA)=0.D0
               DFGKS1(IFA,JFA)=0.D0
               DFGKS2(IFA,JFA)=0.D0
               FTGKS1(IFA,JFA)=0.D0
               FTGKS2(IFA,JFA)=0.D0
               FCLKS1(IFA,JFA)=0.D0
               FCLKS2(IFA,JFA)=0.D0
C
               FM1WS(IFA,JFA)=0.D0
               FM2WS(IFA,JFA)=0.D0
               FM1VPS(IFA,JFA)=0.D0
               FM2VPS(IFA,JFA)=0.D0
               FM1ASS(IFA,JFA)=0.D0
               FM2ASS(IFA,JFA)=0.D0
               FM1ADS(IFA,JFA)=0.D0
               FM2ADS(IFA,JFA)=0.D0
C
               FVP1SK(IFA,JFA)=0.D0
               FVP2SK(IFA,JFA)=0.D0
               FAS1SK(IFA,JFA)=0.D0
               FAS2SK(IFA,JFA)=0.D0
               FAD1SK(IFA,JFA)=0.D0
               FAD2SK(IFA,JFA)=0.D0
   14    CONTINUE
         DO 15 IVOIS = 0, NVOIMA
            DO 16 JFA =1, MAXFA
               MOBWF(IVOIS,JFA)=0.D0
               MOASF(IVOIS,JFA)=0.D0
               MOADF(IVOIS,JFA)=0.D0
               MOVPF(IVOIS,JFA)=0.D0
C
               DW1F(IVOIS,JFA)=0.D0
               DW2F(IVOIS,JFA)=0.D0
               DW1FV(IVOIS,JFA)=0.D0
               DW2FV(IVOIS,JFA)=0.D0
C
               DAD1F(IVOIS,JFA)=0.D0
               DAD2F(IVOIS,JFA)=0.D0
               DAD1FV(IVOIS,JFA)=0.D0
               DAD2FV(IVOIS,JFA)=0.D0
C
               DAS1F(IVOIS,JFA)=0.D0
               DAS2F(IVOIS,JFA)=0.D0
               DAS1FV(IVOIS,JFA)=0.D0
               DAS2FV(IVOIS,JFA)=0.D0
C
               DVP1F(IVOIS,JFA)=0.D0
               DVP2F(IVOIS,JFA)=0.D0
               DVP1FV(IVOIS,JFA)=0.D0
               DVP2FV(IVOIS,JFA)=0.D0
   16       CONTINUE
   15    CONTINUE
         DO 17 IFA = 1,MAXFA
            DO 18 JFA =1, MAXFA+1
               DO 19 IVOIS =0, 1
                  MOYVP1(IFA,JFA,IVOIS)=0.D0
                  MOYVP2(IFA,JFA,IVOIS)=0.D0
                  MOYAS1(IFA,JFA,IVOIS)=0.D0
                  MOYAS2(IFA,JFA,IVOIS)=0.D0
                  MOYAD1(IFA,JFA,IVOIS)=0.D0
                  MOYAD2(IFA,JFA,IVOIS)=0.D0
   19          CONTINUE
   18       CONTINUE
   17    CONTINUE
C ========================================
C FLUX VOLUMIQUES
C ========================================
         CALL VFCFKS(.TRUE.,TANGE,MAXFA,NFACE,
     &              CVP,DCVP1,DCVP2,CVPF,
     &              DCVP1F,DCVP2F,D,
     &              PESA,ZERO,ZERO,ZERO,XG,XFACE,MAXDIM,NDIM,
     &              FTGKS,FTGKS1,FTGKS2)
C
         CALL VFCFKS(.TRUE.,TANGE,MAXFA,NFACE,
     &               CAD,DCAD1,DCAD2,CADF,
     &               DCAD1F,DCAD2F,D,
     &               PESA,ZERO,ZERO,ZERO,XG,XFACE,MAXDIM,NDIM,
     &               FCLKS,FCLKS1,FCLKS2)
         IF(.NOT.FICKFA) THEN
            DO 20 IFA=1,NFACE
               VALFAC(IFA,DIFFU,WVAP)= VALCEN(DIFFU,WVAP)
               VALFAC(IFA,DIFFU,AIRSEC)= VALCEN(DIFFU,AIRSEC)
C
               VALFAC(IFA,DIFFU,AIRDIS)= VALCEN(DIFFU,AIRDIS)
               VALFAC(IFA,DIFFU,WLIQ) = VALCEN(DIFFU,WLIQ)
C
               VALFAC(IFA,DDIFP1,WVAP) = VALCEN(DDIFP1,WVAP)
               VALFAC(IFA,DDIFP2,WVAP) = VALCEN(DDIFP2,WVAP)
C
               VALFAC(IFA,DDIFP1,AIRSEC) = VALCEN(DDIFP1,AIRSEC)
               VALFAC(IFA,DDIFP2,AIRSEC) = VALCEN(DDIFP2,AIRSEC)
C
               VALFAC(IFA,DDIFP1,AIRDIS) = VALCEN(DDIFP1,AIRDIS)
               VALFAC(IFA,DDIFP2,AIRDIS) = VALCEN(DDIFP2,AIRDIS)
C
               VALFAC(IFA,DDIFP1,WLIQ) = VALCEN(DDIFP1,WLIQ)
               VALFAC(IFA,DDIFP2,WLIQ) = VALCEN(DDIFP2,WLIQ)
   20       CONTINUE
         ENDIF
C ========================================
C FLUX MASSIQUES
C ========================================
         DO 21 IFA=1,NFACE
            CALL CAFMSU(IFA,CONT,TANGE,MAXFA,NFACE,FTGKS(IFA),
     &                  FTGKS1,FTGKS2,
     &                  VALFAC(IFA,DIFFU,WVAP),
     &                  VALFAC(IFA,DDIFP1,WVAP),
     &                  VALFAC(IFA,DDIFP2,WVAP),
     &                  FLVPSK,FVP1SK,FVP2SK)
            CALL CAFMSU(IFA,CONT,TANGE,MAXFA,NFACE,FTGKS(IFA),
     &                  FTGKS1,FTGKS2,
     &                  -VALFAC(IFA,DIFFU,AIRSEC),
     &                  -VALFAC(IFA,DDIFP1,AIRSEC),
     &                  -VALFAC(IFA,DDIFP2,AIRSEC),
     &                  FLASSK,FAS1SK,FAS2SK)
            CALL CAFMSU(IFA,CONT,TANGE,MAXFA,NFACE,FCLKS(IFA),
     &                  FCLKS1,FCLKS2,
     &                  VALFAC(IFA,DIFFU,AIRDIS),
     &                  VALFAC(IFA,DDIFP1,AIRDIS),
     &                  VALFAC(IFA,DDIFP2,AIRDIS),
     &                  FLADSK,FAD1SK,FAD2SK)
   21    CONTINUE
      ELSE
            CALL ASSERT (.FALSE.)
      ENDIF
C ENDIF DE LA LIGNE      1098
C=====================================================================
C BOUCLE SUR LES VOISINS
C=====================================================================
      DO 22 KVOIS = 1 , NBVOIS
C
C NUMAV EST LE NUMERO DE MAILLE VOISINE CETTE VARIABLE SERT
C DANS TOUTES LES FONCTIONS D ACCES AUX TERMES ELEMENTAIRES
C DES VOISIN
C
         NUMAV=LIVOIS(KVOIS)
C
C FA= NUMERO DE FACE DANS MAILLE MAITRE COMMUNE
C PAR CONVENTION UNE FACE IFA A POUR PREMIER SOMMET
C LE SOMMET DE NUMEROTATION LOCALE IFA
C FAV= NUMERO DE FACE DANS LA NUMEROTATION LOCALE DE LA MAILLE
C VOISINE
C
         NSC=NBSOCO(KVOIS)
         DO 220 ISC = 1 , NSC
            ISCL(ISC)=LISOCO(KVOIS,ISC,1)
  220    CONTINUE
         FA = NUFLOC(NDIM,NSC,ISCL)
         DO 228 ISC = 1 , NSC
            ISCL(ISC)=LISOCO(KVOIS,ISC,2)
  228    CONTINUE
         FAV = NUFLOC(NDIM,NSC,ISCL)
         NNOV=NBNOVO(KVOIS)
         IF (NNOV .EQ. 7) THEN
            NNOSV=3
         ELSE IF (NNOV .EQ. 9)THEN
            NNOSV=4
         ELSE IF (NNOV .EQ. 10)THEN
            NNOSV=4
         ELSE IF (NNOV .EQ. 27)THEN
            NNOSV=4
         ELSE IF (NNOV .EQ. 27)THEN
            CALL U2MESK('F','VOLUFINI_15', 0 ,' ')
         ENDIF
         NFACEV=NNOSV
         IF(NFACE .GT. NFACEV) THEN
            NFACEM=NFACE
         ELSEIF (NFACE .LT. NFACEV) THEN
            NFACEM=NFACEV
         ELSEIF (NFACE .EQ. NFACEV) THEN
            NFACEM=NFACE
         ENDIF
         DO 229 IDIM = 1 , NDIM
            XL(IDIM)=GEOM(IDIM,NNOV)
  229    CONTINUE
         FINTER(FA)=.TRUE.
         VOIFA(FA,1)=KVOIS
         VOIFA(FA,2)=FAV
C
C ======INITIALISATION=================
         DO 133 JFA=1,MAXFA
            FLVPSL(JFA)=0.D0
            FLASSL(JFA)=0.D0
            FLADSL(JFA)=0.D0
            FCLLS(JFA) =0.D0
            FTGLS(JFA) =0.D0
            DO 144 IFA=1,MAXFA+1
               FVP1SL(IFA,JFA)=0.D0
               FVP2SL(IFA,JFA)=0.D0
               FAS1SL(IFA,JFA)=0.D0
               FAS2SL(IFA,JFA)=0.D0
               FAD1SL(IFA,JFA)=0.D0
               FAD2SL(IFA,JFA)=0.D0
               FTGLS1(IFA,JFA)=0.D0
               FTGLS2(IFA,JFA)=0.D0
               FCLLS1(IFA,JFA)=0.D0
               FCLLS2(IFA,JFA)=0.D0
  144       CONTINUE
  133    CONTINUE
C ================================================================
C --- PARAMETRES EN ENTREE POUR LE VOISIN CONSIDERE
C ================================================================
         CALL TECAC2('OOO',NUMAV,'PGEOMER',1,IGEOMV,IRET)
         CALL ASSERT(IRET.EQ.0)
         CALL TECAC2('OOO',NUMAV,'PMATERC',1,IMATEV,IRET)
         CALL ASSERT(IRET.EQ.0)
         CALL TECAC2('OOO',NUMAV,'PDEPLMR',1,IDEPMV,IRET)
         CALL ASSERT(IRET.EQ.0)
         CALL TECAC2('OOO',NUMAV,'PCOMPOR',1,ICOMPV,IRET)
         CALL ASSERT(IRET.EQ.0)
         CALL TECAC2('OOO',NUMAV,'PCARCRI',1,ICARCV,IRET)
         CALL ASSERT(IRET.EQ.0)
         CALL TECAC2('OOO',NUMAV,'PVARIMR',1,IVARMV,IRET)
         CALL ASSERT(IRET.EQ.0)
         CALL TECAC2('OOO',NUMAV,'PCONTMR',1,ICONMV,IRET)
         CALL ASSERT(IRET.EQ.0)
         IF ( TYPVF.EQ.1) THEN
C
C TRANSMITIVITES EN VF DEUX POINTS
C
            XLK = ZR(IGEOMV+NDIM*(NNO-1))-GEOM(1,NNO)
            YLK = ZR(IGEOMV+NDIM*(NNO-1)+1)-GEOM(2,NNO)
            TKL = MFACE(FA)/(XLK*NORMFA(1,FA)+YLK*NORMFA(2,FA))
         ENDIF
         IF (CONT) THEN
            CALL TECAC2('OOO',NUMAV,'PDEPLPR',1,IDDEPV,IRET)
            CALL ASSERT(IRET.EQ.0)
            CALL TECAC2('OOO',NUMAV,'PCONTPR',1,ICONPV,IRET)
            CALL ASSERT(IRET.EQ.0)
            CALL TECAC2('OOO',NUMAV,'PVARIPR',1,IVARPV,IRET)
            CALL ASSERT(IRET.EQ.0)
         ELSE
            IVARPV = IVARMV
            ICONPV = ICONMV
            IDDEPV = IDEPMV
         ENDIF
         IF ( (TYPVF.EQ.2)) THEN
C ================================================================
C --- CALCUL DES QUANTITES GEOMETRIQUES DE LA MAILLE VOISINE
C ================================================================
            CALL CABHVF(MAXFA,MAXDIM,
     &                  NDIM,NNOV,NNOSV,NFACE,AXI,ZR(IGEOMV),
     &                  VOLV,MFACEL,DFACEL,XFACEL,NORFAL,UTICER )
            CALL CACDSU(MAXFA,MAXDIM,ALPHA,
     &                  NDIM,NNOV,NFACEV,ZR(IGEOMV),
     &                  VOLV,MFACEL,DFACEL,XFACEL,NORFAL,
     &                  KINTVF,YSSL,CL,DL)
         ELSE
            CALL ASSERT (.FALSE.)
         ENDIF
C ******************************************************
C CALCUL POUR LE CENTRE DE LA MAILLE VOISINE
C ******************************************************
         IF (YAP1.EQ.1) THEN
            DEFGEM(ADDEP1)= ZR(IDEPMV+IADP1K-1)
            DEFGEP(ADDEP1)= ZR(IDDEPV+IADP1K-1)+ZR(IDEPMV+IADP1K-1)
            IF (YAP2.EQ.1) THEN
               DEFGEM(ADDEP2)= ZR(IDEPMV+IADP2K-1)
               DEFGEP(ADDEP2)= ZR(IDDEPV+IADP2K-1)+ZR(IDEPMV+IADP2K-1)
            ENDIF
         END IF
C ===============================================
C ==== INITIALISATION DE DSDE ================
C ===============================================
         DO 221 I=1, DIMCON
            DO 221 J = 1 , DIMDEF
               DSDE(I,J)=0.D0
  221    CONTINUE
         CALL COMTHM(OPTION,PERMAN,VF,0,
     &               VALFAV(1,1,1,FA),VALCEV(1,1,FA),
     &               ZI(IMATEV),TYPMOD,ZK16(ICOMPV),
     &               ZR(ICARCV),RINSTM,RINSTP,
     &               NDIM,DIMDEF,DIMCON,NBVARI,YAMEC,YAP1,
     &               YAP2,YATE,ADDEME,ADCOME,ADDEP1,ADCP11,
     &               ADCP12,ADDEP2,ADCP21,ADCP22,ADDETE,ADCOTE,
     &               DEFGEM,DEFGEP,
     &               ZR(ICONMV),ZR(ICONPV),ZR(IVARMV),ZR(IVARPV),
     &               DSDE,PESA,RETCOM,1,1,
     &               P10V,P20V)

         IF ( RETCOM.NE.0) THEN
            CALL U2MESK('F','COMPOR1_9', 0 ,' ')
         ENDIF
C ******************************************************
C CALCUL POUR LES ARETES DE LA MAILLE VOISINE
C ******************************************************
         DO 227 IFAV=1,NFACEV
            IF (YAP1.EQ.1) THEN
               DEFGEM(ADDEP1)= ZR(IDEPMV+IADP1(IFAV)-1)
               DEFGEP(ADDEP1)= ZR(IDDEPV+IADP1(IFAV)-1)+
     &                         ZR(IDEPMV+IADP1(IFAV)-1)
               IF (YAP2.EQ.1) THEN
                  DEFGEM(ADDEP2)= ZR(IDEPMV+IADP2(IFAV)-1)
                  DEFGEP(ADDEP2)= ZR(IDDEPV+IADP2(IFAV)-1)+
     &                            ZR(IDEPMV+IADP2(IFAV)-1)
               ENDIF
            END IF
C ===============================================
C ==== INITIALISATION DE DSDE ================
C ===============================================
            DO 222 I=1, DIMCON
               DO 222 J = 1 , DIMDEF
                  DSDE(I,J)=0.D0
  222       CONTINUE

            CALL COMTHM(OPTION,PERMAN,VF,IFAV,
     &                  VALFAV(1,1,1,FA),VALCEV(1,1,FA),
     &                  ZI(IMATEV),TYPMOD,ZK16(ICOMPV),
     &                  ZR(ICARCV),RINSTM,RINSTP,
     &                  NDIM,DIMDEF,DIMCON,NBVARI,YAMEC,YAP1,
     &                  YAP2,YATE,ADDEME,ADCOME,ADDEP1,ADCP11,
     &                  ADCP12,ADDEP2,ADCP21,ADCP22,ADDETE,ADCOTE,
     &                  DEFGEM,DEFGEP,
     &                  ZR(ICONMV),ZR(ICONPV),ZR(IVARMV+NBVARI*IFAV),
     &                  ZR(IVARPV+NBVARI*IFAV),DSDE,PESA,RETCOM,1,1,
     &                  P10V,P20V)

            IF ( RETCOM.NE.0) THEN
               CALL U2MESK('F','COMPOR1_9', 0 ,' ')
            ENDIF
  227    CONTINUE
C CALCUL DES VARIABLES POUR LA MAILLE VOISINE
C COMMUNES A VF 2PNTS ET SUSHI
         CVPV = VALCEV(CON,WVAP,FA)
         DCVP1V = VALCEV(DCONP1,WVAP,FA)
         DCVP2V = VALCEV(DCONP2,WVAP,FA)
C
         CASV = 1.D0-CVPV
         DCAS1V = -DCVP1V
         DCAS2V = -DCVP2V
C
         CADV = VALCEV(CON,AIRDIS,FA)
         DCAD1V = VALCEV(DCONP1,AIRDIS,FA)
         DCAD2V = VALCEV(DCONP2,AIRDIS,FA)
         IF ( (TYPVF.EQ.2)) THEN
            DO 223 IFAV=1,NFACEV
               CVPFV(IFAV)=VALFAV(IFAV,CON,WVAP,FA)
               CVPFV1(IFAV)=VALFAV(IFAV,DCONP1,WVAP,FA)
               CVPFV2(IFAV)=VALFAV(IFAV,DCONP2,WVAP,FA)
               CADFV(IFAV)=VALFAV(IFAV,CON,AIRDIS,FA)
               CADFV1(IFAV)=VALFAV(IFAV,DCONP1,AIRDIS,FA)
               CADFV2(IFAV)=VALFAV(IFAV,DCONP2,AIRDIS,FA)
  223       CONTINUE
            CALL VFCFKS(.TRUE.,TANGE,MAXFA,NFACEV,CVPV,DCVP1V,DCVP2V,
     &                  CVPFV,CVPFV1,CVPFV2,DL,PESA,ZERO,ZERO,ZERO,
     &                  XL,XFACEL,MAXDIM,NDIM,FTGLS,FTGLS1,FTGLS2)

            CALL VFCFKS(.TRUE.,TANGE,MAXFA,NFACEV,CADV,DCAD1V,DCAD2V,
     &                 CADFV,CADFV1,CADFV2,DL,PESA,ZERO,ZERO,ZERO,
     &                 XL,XFACEL,MAXDIM,NDIM,FCLLS,FCLLS1,FCLLS2)
            IF(.NOT.FICKFA) THEN
               DO 224 IFAV=1,NFACEV
                VALFAV(IFAV,DIFFU,WVAP,FA)= VALCEV(DIFFU,WVAP,FA)
                VALFAV(IFAV,DIFFU,AIRSEC,FA)= VALCEV(DIFFU,AIRSEC,FA)
C
                VALFAV(IFAV,DIFFU,AIRDIS,FA)= VALCEV(DIFFU,AIRDIS,FA)
                VALFAV(IFAV,DIFFU,WLIQ,FA) = VALCEV(DIFFU,WLIQ,FA)
C
                VALFAV(IFAV,DDIFP1,WVAP,FA) = VALCEV(DDIFP1,WVAP,FA)
                VALFAV(IFAV,DDIFP2,WVAP,FA) = VALCEV(DDIFP2,WVAP,FA)
C
                VALFAV(IFAV,DDIFP1,AIRSEC,FA) = VALCEV(DDIFP1,AIRSEC,FA)
                VALFAV(IFAV,DDIFP2,AIRSEC,FA) = VALCEV(DDIFP2,AIRSEC,FA)
C
                VALFAV(IFAV,DDIFP1,AIRDIS,FA) = VALCEV(DDIFP1,AIRDIS,FA)
                VALFAV(IFAV,DDIFP2,AIRDIS,FA) = VALCEV(DDIFP2,AIRDIS,FA)
C
                VALFAV(IFAV,DDIFP1,WLIQ,FA) = VALCEV(DDIFP1,WLIQ,FA)
                VALFAV(IFAV,DDIFP2,WLIQ,FA) = VALCEV(DDIFP2,WLIQ,FA)
  224          CONTINUE
            ENDIF
C ========================================
C FLUX MASSIQUES
C ========================================
            CALL CAFMSU(FAV,CONT,TANGE,MAXFA,NFACEV,FTGLS(FAV),
     &                  FTGLS1,FTGLS2,
     &                  VALFAV(FAV,DIFFU,WVAP,FA),
     &                  VALFAV(FAV,DDIFP1,WVAP,FA),
     &                  VALFAV(FAV,DDIFP2,WVAP,FA),
     &                  FLVPSL,FVP1SL,FVP2SL)
            CALL CAFMSU(FAV,CONT,TANGE,MAXFA,NFACEV,FTGLS(FAV),
     &                  FTGLS1,FTGLS2,
     &                  -VALFAV(FAV,DIFFU,AIRSEC,FA),
     &                  -VALFAV(FAV,DDIFP1,AIRSEC,FA),
     &                  -VALFAV(FAV,DDIFP2,AIRSEC,FA),
     &                  FLASSL,FAS1SL,FAS2SL)
            CALL CAFMSU(FAV,CONT,TANGE,MAXFA,NFACEV,FCLLS(FAV),
     &                  FCLLS1,FCLLS2,
     &                  VALFAV(FAV,DIFFU,AIRDIS,FA),
     &                  VALFAV(FAV,DDIFP1,AIRDIS,FA),
     &                  VALFAV(FAV,DDIFP2,AIRDIS,FA),
     &                  FLADSL,FAD1SL,FAD2SL)
            CALL MOFICK(FA,FAV,CONT,TANGE,MAXFA,NFACE,NFACEV,NFACEM,
     &                  FLVPSK,FVP1SK,FVP2SK,FLVPSL,FVP1SL,FVP2SL,
     &                  MOYVP,MOYVP1,MOYVP2)
            CALL MOFICK(FA,FAV,CONT,TANGE,MAXFA,NFACE,NFACEV,NFACEM,
     &                  FLASSK,FAS1SK,FAS2SK,FLASSL,FAS1SL,FAS2SL,
     &                  MOYAS,MOYAS1,MOYAS2)
            CALL MOFICK(FA,FAV,CONT,TANGE,MAXFA,NFACE,NFACEV,NFACEM,
     &                  FLADSK,FAD1SK,FAD2SK,FLADSL,FAD1SL,FAD2SL,
     &                  MOYAD,MOYAD1,MOYAD2)
         ENDIF
C====================================================================
C====================================================================
C VF À 2 POINTS
C====================================================================
C===================================================================
         IF(TYPVF.EQ.1) THEN
C
C TERMES DE FLUX
C
            PCP = DDEPL(1)+DEPLM(1)+P10
            PGP = DDEPL(2)+DEPLM(2)+P20
            DPGP1 = 0.D0
            DPGP2 = 1.D0
C
            PCPV = ZR(IDDEPV)+ZR(IDEPMV)+P10V
            PGPV = ZR(IDDEPV+1)+ZR(IDEPMV+1)+P20V
C
            PWP = PGP-PCP
            DPWP1 = -1.D0
            DPWP2 = +1.D0
            PWPV = PGPV-PCPV
C
            CVP = VALCEN(CON,WVAP)
            DCVP1 = VALCEN(DCONP1,WVAP)
            DCVP2 = VALCEN(DCONP2,WVAP)
C
            CAS = 1.D0-CVP
            DCAS1 = -DCVP1
            DCAS2 = -DCVP2
C
            CAD = VALCEN(CON,AIRDIS)
            DCAD1 = VALCEN(DCONP1,AIRDIS)
            DCAD2 = VALCEN(DCONP2,AIRDIS)
C
C NT.K.N POUR ELEMENT
C
            IF ( NDIM.EQ.2) THEN
               KINTVF(1) = VALCEN(VKINT ,KXX)
               KINTVF(2) = VALCEN(VKINT ,KYY)
               KINTVF(3) = VALCEN(VKINT ,KXY)
               KINTVF(4) = 0.D0
               KINTVF(5) = 0.D0
               KINTVF(6) = 0.D0
            ELSE
               KINTVF(1) = VALCEN(VKINT ,KXX)
               KINTVF(2) = VALCEN(VKINT ,KYY)
               KINTVF(3) = VALCEN(VKINT ,KZZ)
               KINTVF(4) = VALCEN(VKINT ,KXY)
               KINTVF(5) = VALCEN(VKINT ,KYZ)
               KINTVF(6) = VALCEN(VKINT ,KZX)
            ENDIF
            NKN = 0.D0
            CALL ASSERT((VALCEN(VKINT ,KXY).EQ.0.D0).AND.
     &                  (VALCEN(VKINT ,KYZ).EQ.0.D0).AND.
     &                  (VALCEN(VKINT ,KZX).EQ.0.D0))
            DO 225 I = 1 , NDIM
               NKN = NORMFA(I,FA)*NORMFA(I,FA)*KINTVF(I)
  225       CONTINUE
C
C NT.K.N POUR VOISIN
C
            IF ( NDIM.EQ.2) THEN
               KINTVF(1) = VALCEV(VKINT ,KXX,FA)
               KINTVF(2) = VALCEV(VKINT ,KYY,FA)
               KINTVF(3) = VALCEV(VKINT ,KXY,FA)
               KINTVF(4) = 0.D0
               KINTVF(5) = 0.D0
               KINTVF(6) = 0.D0
            ELSE
               KINTVF(1) = VALCEV(VKINT ,KXX,FA)
               KINTVF(2) = VALCEV(VKINT ,KYY,FA)
               KINTVF(3) = VALCEV(VKINT ,KZZ,FA)
               KINTVF(4) = VALCEV(VKINT ,KXY,FA)
               KINTVF(5) = VALCEV(VKINT ,KYZ,FA)
               KINTVF(6) = VALCEV(VKINT ,KZX,FA)
            ENDIF
            CALL ASSERT((VALCEV(VKINT ,KXY,FA).EQ.0.D0).AND.
     &                  (VALCEV(VKINT ,KYZ,FA).EQ.0.D0).AND.
     &                  (VALCEV(VKINT ,KZX,FA).EQ.0.D0))
            NKNV = 0.D0
            DO 226 I = 1 , NDIM
               NKNV = NORMFA(I,FA)*NORMFA(I,FA)*KINTVF(I)
  226       CONTINUE
            KINTFA=NKN*NKNV/(NKN+NKNV)
C
C DARCY EAU ET AIR DISSOUS DECENTRES SELON PHASE LIQUIDE
C
            IF(PWPV.GT.PWP) THEN
               MOBWFA = VALCEV(MOB,WLIQ,FA)
               MOADFA = VALCEV(MOB,AIRDIS,FA)
               DWP1V = VALCEV(DMOBP1,WLIQ,FA)
               DADP1V = VALCEV(DMOBP1,AIRDIS,FA)
               DWP2V = VALCEV(DMOBP2,WLIQ,FA)
               DADP2V = VALCEV(DMOBP2,AIRDIS,FA)
               DWP1 = 0.D0
               DADP1 = 0.D0
               DWP2 = 0.D0
               DADP2 = 0.D0
            ELSE
               MOBWFA = VALCEN(MOB,WLIQ)
               MOADFA = VALCEN(MOB,AIRDIS)
               DWP1 = VALCEN(DMOBP1,WLIQ)
               DADP1 = VALCEN(DMOBP1,AIRDIS)
               DWP2 = VALCEN(DMOBP2,WLIQ)
               DADP2 = VALCEN(DMOBP2,AIRDIS)
               DWP1V = 0.D0
               DADP1V = 0.D0
               DWP2V = 0.D0
               DADP2V = 0.D0
            ENDIF
C
C DARCY AIR SEC ET VAPEUR DECENTRES SELON PHASE GAZ
C
            IF(PGPV.GT.PGP) THEN
               MOASFA = VALCEV(MOB,AIRSEC,FA)
               MOVPFA = VALCEV(MOB,WVAP,FA)
               DASP1V = VALCEV(DMOBP1,AIRSEC,FA)
               DVPP1V = VALCEV(DMOBP1,WVAP,FA)
               DASP2V = VALCEV(DMOBP2,AIRSEC,FA)
               DVPP2V = VALCEV(DMOBP2,WVAP,FA)
               DASP1 = 0.D0
               DVPP1 = 0.D0
               DASP2 = 0.D0
               DVPP2 = 0.D0
            ELSE
               MOASFA = VALCEN(MOB,AIRSEC)
               MOVPFA = VALCEN(MOB,WVAP)
               DASP1 = VALCEN(DMOBP1,AIRSEC)
               DVPP1 = VALCEN(DMOBP1,WVAP)
               DASP2 = VALCEN(DMOBP2,AIRSEC)
               DVPP2 = VALCEN(DMOBP2,WVAP)
               DASP1V = 0.D0
               DVPP1V = 0.D0
               DASP2V = 0.D0
               DVPP2V = 0.D0
            ENDIF
C
C CONTRIBUTION DARCY AUX FLUX
C
C ========== CALCUL DE FLUW,FWP1,FWP2,FWP1V ET FWP2V ================
C
            CALL CAFHVF(KINTFA,TKL,MOBWFA,DWP1,DWP2,DWP1V,DWP2V,PWPV,
     &                  PWP,DPWP1,DPWP2,FLUW,FWP1,FWP2,FWP1V,FWP2V)
C
C ======== CALCUL DE FLUVP, FVPP1, FVPP2, FVPP1V ET FVPP2V ==========
C
            CALL CAFHVF(KINTFA,TKL,MOVPFA,DVPP1,DVPP2,DVPP1V,DVPP2V,
     &                  PGPV,PGP,DPGP1,DPGP2,FLUVP,FVPP1,FVPP2,FVPP1V,
     &                  FVPP2V)
C
C ======== CALCUL DE FLUAS, FASP1, FASP2, FASP1V ET FASP2V ==========
C
            CALL CAFHVF(KINTFA,TKL,MOASFA,DASP1,DASP2,DASP1V,DASP2V,
     &                  PGPV,PGP,DPGP1,DPGP2,FLUAS,FASP1,FASP2,FASP1V,
     &                  FASP2V)
C
C ======== CALCUL DE FLUAD, FADP1, FADP2, FADP1V ET FADP2V ==========
C
            CALL CAFHVF(KINTFA,TKL,MOADFA,DADP1,DADP2,DADP1V,DADP2V,
     &                  PWPV,PWP,DPWP1,DPWP2,FLUAD,FADP1,FADP2,FADP1V,
     &                  FADP2V)
C
C CONTRIBUTION FICK AUX FLUX
            DIVP1V=0.D0
            DIVP2V=0.D0
            DIAS1V=0.D0
            DIAS2V=0.D0
            DIAD1V=0.D0
            DIAD2V=0.D0
C*****************************************************************
C A VOIR SI BESOIN DE CETTE FONCTION OU SI ON PEUT METTRE CAFHVF
C*****************************************************************
C ======== CALCUL DE FLUVP, FVPP1, FVPP2, FVPP1V ET FVPP2V ==========
            CALL CAFFVF(TKL,VALCEN(DIFFU,WVAP),
     &                  VALCEN(DDIFP1,WVAP),
     &                  VALCEN(DDIFP2,WVAP),
     &                  DIVP1V,DIVP2V,CVPV,CVP,
     &                  DCVP1V,DCVP2V,DCVP1,DCVP2,FLUVP,FVPP1,
     &                  FVPP2,FVPP1V,FVPP2V)
C
C ======== CALCUL DE FLUAS, FASP1, FASP2, FASP1V ET FASP2V ==========
            CALL CAFFVF(TKL,VALCEN(DIFFU,AIRSEC),
     &                 VALCEN(DDIFP1,AIRSEC),
     &                 VALCEN(DDIFP2,AIRSEC),
     &                 DIAS1V,DIAS2V,CASV,CAS,
     &                 DCAS1V,DCAS2V,DCAS1,DCAS2,FLUAS,FASP1,
     &                 FASP2,FASP1V,FASP2V)
C ======== CALCUL DE FLUAD, FADP1, FADP2, FADP1V ET FADP2V ==========
            CALL CAFFVF(TKL,VALCEN(DIFFU,AIRDIS),
     &                 VALCEN(DDIFP1,AIRDIS),
     &                 VALCEN(DDIFP2,AIRDIS),
     &                 DIAD1V,DIAD2V,CADV,CAD,
     &                 DCAD1V,DCAD2V,DCAD1,DCAD2,FLUAD,FADP1,
     &                 FADP2,FADP1V,FADP2V )
            IF(CONT) THEN
C
C STOCKAGE DES FLUX DANS TABLEAU DES CONTRAINTES
C
               CONGEP(ADCP11+1,1)= CONGEP(ADCP11+1,1)+FLUW
               CONGEP(ADCP12+1,1)= CONGEP(ADCP12+1,1)+FLUVP
               CONGEP(ADCP21+1,1)= CONGEP(ADCP21+1,1)+FLUAS
               CONGEP(ADCP22+1,1)= CONGEP(ADCP22+1,1)+FLUAD
C
C LES FLUX PAR FACE NE SONT STOCKES QUE POUR POST TRAITEMENT
C
               CONGEP(ADCP11+1,FA+1)=FLUW
               CONGEP(ADCP12+1,FA+1)=FLUVP
               CONGEP(ADCP21+1,FA+1)=FLUAS
               CONGEP(ADCP22+1,FA+1)=FLUAD
C
C VECTEUR RESIDU
C
               VECTU(ADCM1)= VECTU(ADCM1)+FLUW+FLUVP
               VECTU(ADCM2)= VECTU(ADCM2)+FLUAS+FLUAD
            ENDIF
            IF (TANGE) THEN
C
C DERIVEES CONSERVATION EAU / P1
C
               MATUU(ZZADMA(0,ADCM1,IADP1K))=
     &               MATUU(ZZADMA(0,ADCM1,IADP1K))+ FWP1 + FVPP1
C
C DERIVEES CONSERVATION EAU / P2
C
               MATUU(ZZADMA(0,ADCM1,IADP2K))=
     &               MATUU(ZZADMA(0,ADCM1,IADP2K))+ FWP2 + FVPP2

C
C DERIVEES CONSERVATION AIR / P1
C
               MATUU(ZZADMA(0,ADCM2,IADP1K))=
     &               MATUU(ZZADMA(0,ADCM2,IADP1K))+ FASP1 + FADP1
C
C DERIVEES CONSERVATION AIR / P2
C
               MATUU(ZZADMA(0,ADCM2,IADP2K))=
     &               MATUU(ZZADMA(0,ADCM2,IADP2K))+ FASP2 + FADP2

C
C DERIVEES CONSERVATION EAU / P1 DU VOISIN
C
               MATUU(ZZADMA(KVOIS,ADCM1,IADP1K))=
     &               MATUU(ZZADMA(KVOIS,ADCM1,IADP1K))+ FWP1V + FVPP1V
C
C DERIVEES CONSERVATION EAU / P2 DU VOISIN
C
               MATUU(ZZADMA(KVOIS,ADCM1,IADP2K))=
     &               MATUU(ZZADMA(KVOIS,ADCM1,IADP2K))+ FWP2V + FVPP2V
C
C DERIVEES CONSERVATION AIR / P1 DU VOISIN
C
               MATUU(ZZADMA(KVOIS,ADCM2,IADP1K))=
     &               MATUU(ZZADMA(KVOIS,ADCM2,IADP1K))+ FASP1V + FADP1V
C
C DERIVEES CONSERVATION AIR / P2 DU VOISIN
C
               MATUU(ZZADMA(KVOIS,ADCM2,IADP2K))=
     &               MATUU(ZZADMA(KVOIS,ADCM2,IADP2K))+ FASP2V + FADP2V
            END IF
         ENDIF
C FIN DE LA BOUCLE SUR LES VOISINS
   22 CONTINUE
C====================================================================
C====================================================================
C VF SUSHI
C====================================================================
C====================================================================
      IF((TYPVF.EQ.2)) THEN
C ========================================
C FLUX VOLUMIQUES
C ========================================
         CALL VFCFKS(.TRUE.,TANGE,MAXFA,NFACE,
     &              PWP,DPWP1,DPWP2,PWPF,
     &              DPWP1F,DPWP2F,C,
     &              PESA,RHOL,DRHOL1,DRHOL2,XG,XFACE,MAXDIM,NDIM,
     &              FLKS,DFLKS1,DFLKS2)
         CALL VFCFKS(.TRUE.,TANGE,MAXFA,NFACE,
     &              PGP,DPGP1,DPGP2,PGPF,
     &              DPGP1F,DPGP2F,C,
     &              PESA,RHOG,DRHOG1,DRHOG2,XG,XFACE,MAXDIM,NDIM,
     &              FGKS,DFGKS1,DFGKS2)
C -------------------------------------------------------------------
C DECEN
C -------------------------------------------------------------------
         DO 23 IFA=1,NFACE
            KVOIS=VOIFA(IFA,1)
            IF(FINTER(IFA) .AND. DECEN)THEN
               NUMAV=LIVOIS(KVOIS)
C
C SI FLKS>=0 ON SE PLACE DS LA MAILLE COURANTE
C SINON DS LA MAILLE VOISINE (INVERSE DE VF HDI)
C
               IF(FLKS(IFA).GE.0.D0)THEN
                  MOBWF(0,IFA) = VALCEN(MOB,WLIQ)
                  DW1F(0,IFA) = VALCEN(DMOBP1,WLIQ)
                  DW2F(0,IFA) = VALCEN(DMOBP2,WLIQ)
                  DW1FV(0,IFA) = 0.D0
                  DW2FV(0,IFA) = 0.D0
C
                  MOADF(0,IFA) = VALCEN(MOB,AIRDIS)
                  DAD1F(0,IFA) = VALCEN(DMOBP1,AIRDIS)
                  DAD2F(0,IFA) = VALCEN(DMOBP2,AIRDIS)
                  DAD1FV(0,IFA) =0.D0
                  DAD2FV(0,IFA) =0.D0
               ELSE
                  MOBWF(KVOIS,IFA) = VALCEV(MOB,WLIQ,IFA)
                  DW1F(KVOIS,IFA) = 0.D0
                  DW2F(KVOIS,IFA) = 0.D0
                  DW1FV(KVOIS,IFA) = VALCEV(DMOBP1,WLIQ,IFA)
                  DW2FV(KVOIS,IFA) = VALCEV(DMOBP2,WLIQ,IFA)
C
                  MOADF(KVOIS,IFA) = VALCEV(MOB,AIRDIS,IFA)
                  DAD1F(KVOIS,IFA) = 0.D0
                  DAD2F(KVOIS,IFA) = 0.D0
                  DAD1FV(KVOIS,IFA) = VALCEV(DMOBP1,AIRDIS,IFA)
                  DAD2FV(KVOIS,IFA) = VALCEV(DMOBP2,AIRDIS,IFA)
               ENDIF
C SI FGKS>=0 ON SE PLACE DS LA MAILLE COURANTE
C SINON DS LA MAILLE VOISINE(INVERSE DE VF HDI)
C
               IF(FGKS(IFA).GE.0.D0)THEN
                  MOASF(0,IFA) = VALCEN(MOB,AIRSEC)
                  DAS1F(0,IFA) = VALCEN(DMOBP1,AIRSEC)
                  DAS2F(0,IFA) = VALCEN(DMOBP2,AIRSEC)
                  DAS1FV(0,IFA) = 0.D0
                  DAS2FV(0,IFA) = 0.D0
C
                  MOVPF(0,IFA) = VALCEN(MOB,WVAP)
                  DVP1F(0,IFA) = VALCEN(DMOBP1,WVAP)
                  DVP2F(0,IFA) = VALCEN(DMOBP2,WVAP)
                  DVP1FV(0,IFA) = 0.D0
                  DVP2FV(0,IFA) = 0.D0
               ELSE
                  MOASF(KVOIS,IFA) = VALCEV(MOB,AIRSEC,IFA)
                  DAS1F(KVOIS,IFA) = 0.D0
                  DAS2F(KVOIS,IFA) = 0.D0
                  DAS1FV(KVOIS,IFA) = VALCEV(DMOBP1,AIRSEC,IFA)
                  DAS2FV(KVOIS,IFA) = VALCEV(DMOBP2,AIRSEC,IFA)
C
                  MOVPF(KVOIS,IFA) = VALCEV(MOB,WVAP,IFA)
                  DVP1F(KVOIS,IFA) = 0.D0
                  DVP2F(KVOIS,IFA) = 0.D0
                  DVP1FV(KVOIS,IFA) = VALCEV(DMOBP1,WVAP,IFA)
                  DVP2FV(KVOIS,IFA) = VALCEV(DMOBP2,WVAP,IFA)
               ENDIF
            ELSE
               MOBWF(0,IFA) = VALCEN(MOB,WLIQ)
               DW1F(0,IFA) = VALCEN(DMOBP1,WLIQ)
               DW2F(0,IFA) = VALCEN(DMOBP2,WLIQ)
               DW1FV(0,IFA) = 0.D0
               DW2FV(0,IFA) = 0.D0
C
               MOADF(0,IFA) = VALCEN(MOB,AIRDIS)
               DAD1F(0,IFA) = VALCEN(DMOBP1,AIRDIS)
               DAD2F(0,IFA) = VALCEN(DMOBP2,AIRDIS)
               DAD1FV(0,IFA) =0.D0
               DAD2FV(0,IFA) =0.D0
C
               MOASF(0,IFA) = VALCEN(MOB,AIRSEC)
               DAS1F(0,IFA) = VALCEN(DMOBP1,AIRSEC)
               DAS2F(0,IFA) = VALCEN(DMOBP2,AIRSEC)
               DAS1FV(0,IFA) = 0.D0
               DAS2FV(0,IFA) = 0.D0
C
               MOVPF(0,IFA) = VALCEN(MOB,WVAP)
               DVP1F(0,IFA) = VALCEN(DMOBP1,WVAP)
               DVP2F(0,IFA) = VALCEN(DMOBP2,WVAP)
               DVP1FV(0,IFA) = 0.D0
               DVP2FV(0,IFA) = 0.D0
            END IF
   23    CONTINUE
C=======================================================
C CALCUL DE :FLUW,FLUVP,FLUAS,FLUAD
C=======================================================
         CALL CAFVSU(.TRUE.,TANGE,MAXFA,NFACE,FLKS,DFLKS1,DFLKS2,MOBWF,
     &               DW1F,DW2F,DW1FV,DW2FV,FLUWS,FW1S,FW2S,FW1SV,
     &               FW2SV,NBVOIS,NVOIMA)
         CALL CAFVSU(.TRUE.,TANGE,MAXFA,NFACE,FGKS,DFGKS1,DFGKS2,MOVPF,
     &               DVP1F,DVP2F,DVP1FV,DVP2FV,FLUVPS,FVP1S,FVP2S,
     &               FVP1SV,FVP2SV,NBVOIS,NVOIMA)
         CALL CAFVSU(.TRUE.,TANGE,MAXFA,NFACE,FGKS,DFGKS1,DFGKS2,MOASF,
     &               DAS1F,DAS2F,DAS1FV,DAS2FV,FLUASS,FAS1S,FAS2S,
     &               FAS1SV,FAS2SV,NBVOIS,NVOIMA)
         CALL CAFVSU(.TRUE.,TANGE,MAXFA,NFACE,FLKS,DFLKS1,DFLKS2,MOADF,
     &               DAD1F,DAD2F,DAD1FV,DAD2FV,FLUADS,FAD1S,FAD2S,
     &               FAD1SV,FAD2SV,NBVOIS,NVOIMA)
         DO 24 IFA=1,NFACE
            IF(.NOT.FINTER(IFA))THEN
               CALL CAFMSU(IFA,.TRUE.,TANGE,MAXFA,NFACE,
     &                    FGKS(IFA),DFGKS1,DFGKS2,MOVPF(0,IFA),
     &                    DVP1F(0,IFA),DVP2F(0,IFA),FMVPS,FM1VPS,
     &                    FM2VPS)
               CALL CAFMSU(IFA,.TRUE.,TANGE,MAXFA,NFACE,
     &                     FLKS(IFA),DFLKS1,DFLKS2,MOBWF(0,IFA),
     &                     DW1F(0,IFA),DW2F(0,IFA),FMWS,FM1WS,FM2WS)
               CALL CAFMSU(IFA,.TRUE.,TANGE,MAXFA,NFACE,
     &                     FGKS(IFA),DFGKS1,DFGKS2,MOASF(0,IFA),
     &                     DAS1F(0,IFA),DAS2F(0,IFA),FMASS,FM1ASS,
     &                     FM2ASS)
               CALL CAFMSU(IFA,.TRUE.,TANGE,MAXFA,NFACE,
     &                     FLKS(IFA),DFLKS1,DFLKS2,MOADF(0,IFA),
     &                     DAD1F(0,IFA),DAD2F(0,IFA),FMADS,FM1ADS,
     &                     FM2ADS)
            ENDIF
   24    CONTINUE
         IF (CONT) THEN
C ********************************************************************
C EQUATION DE LA CONTINUITE DES FLUX
C                 SI ARETE DE BORD :
C                                 | FLKS |
C                                 | FGKS |
C                 SINON :
C                                 | FMWS + FMVPS + FLVPSK |
C                                 | FMASS + FMADS + FLASSK + FLADSK|
C ********************************************************************
         DO 25 IFA=1,NFACE
            IF(FINTER(IFA))THEN
               CONGEP(ADCP11+1,IFA+1)= FLKS(IFA)
               CONGEP(ADCP12+1,IFA+1)= FGKS(IFA)
            ELSE
               CONGEP(ADCP11+1,IFA+1)=FMWS(IFA)+FMVPS(IFA)+FLVPSK(IFA)
               CONGEP(ADCP12+1,IFA+1)=FMASS(IFA)+FMADS(IFA)+FLASSK(IFA)
     &                                +FLADSK(IFA)
            END IF
            VECTU(ADCF1(IFA))=CONGEP(ADCP11+1,IFA+1)
            VECTU(ADCF2(IFA))=CONGEP(ADCP12+1,IFA+1)
   25    CONTINUE
C ********************************************************************
C EQUATION DE LA CONSERVATION DE LA MASSE
C                 SI ARETE DE BORD :
C                                 | FLUWS + FLUVPS + MOYVP |
C                                 | FLUASS + FLUADS + MOYAS + MOYAD |
C                 SINON :
C                 | FLUWS + FLUVPS + FLVPSK |
C                                 | FLUASS + FLUADS + FLASSK + FLADSK |
C ********************************************************************
         CONGEP(ADCP11+1,1)= FLUWS
         CONGEP(ADCP12+1,1)= FLUVPS
         CONGEP(ADCP21+1,1)= FLUASS
         CONGEP(ADCP22+1,1)= FLUADS
         DO 26 IFA=1,NFACE
            IF(FINTER(IFA))THEN
               CONGEP(ADCP12+1,1)=CONGEP(ADCP12+1,1)+ MOYVP(IFA)
               CONGEP(ADCP21+1,1)=CONGEP(ADCP21+1,1)+ MOYAS(IFA)
               CONGEP(ADCP22+1,1)=CONGEP(ADCP22+1,1)+ MOYAD(IFA)
            ELSE
               CONGEP(ADCP12+1,1)=CONGEP(ADCP12+1,1)+ FLVPSK(IFA)
               CONGEP(ADCP21+1,1)=CONGEP(ADCP21+1,1)+ FLASSK(IFA)
               CONGEP(ADCP22+1,1)=CONGEP(ADCP22+1,1)+ FLADSK(IFA)
            ENDIF
   26    CONTINUE
         VECTU(ADCM1)= VECTU(ADCM1)+CONGEP(ADCP11+1,1)
     &                +CONGEP(ADCP12+1,1)
         VECTU(ADCM2)= VECTU(ADCM2)+CONGEP(ADCP21+1,1)
     &                +CONGEP(ADCP22+1,1)
         END IF
         IF ( TANGE) THEN
C *******************************************************************
C EQUATION DE LA CONSERVATION DE LA MASSE POUR K
C                 (DERIVEES % VARIABLES DU CENTRE)
C                 SANS LA PARTIE FICKIENNE POUR INTERNE ET EXTERNE
C                 | FW1S + FVP1S |
C                 | FW2S + FVP2S |
C                 | FAS1S + FAD1S |
C                 | FAS2S + FAD2S |
C *******************************************************************
            MATUU(ZZADMA(0,ADCM1,IADP1K))=
     &            MATUU(ZZADMA(0,ADCM1,IADP1K))+FW1S(1)+FVP1S(1)
C
            MATUU(ZZADMA(0,ADCM1,IADP2K))=
     &            MATUU(ZZADMA(0,ADCM1,IADP2K))+FW2S(1)+FVP2S(1)
C
            MATUU(ZZADMA(0,ADCM2,IADP1K))=
     &            MATUU(ZZADMA(0,ADCM2,IADP1K))+FAS1S(1)+FAD1S(1)
C
            MATUU(ZZADMA(0,ADCM2,IADP2K))=
     &            MATUU(ZZADMA(0,ADCM2,IADP2K))+FAS2S(1)+FAD2S(1)
            DO 27 IFA=1,NFACE
C ATTENTION QD LES MAILLES SERONT DIFFERENTES NFACEV PEUT ETRE DIFFERENT
C SELON LES MAILLES
               KVOIS=VOIFA(IFA,1)
               FAV =VOIFA(IFA,2)
               NUMAV=LIVOIS(KVOIS)
C
C *******************************************************************
C EQUATION DE LA CONSERVATION DE LA MASSE POUR K
C                 (DERIVEES % VARIABLES DE L ARETE)
C                 POUR INTERNE ET EXTERNE
C                 | FW1S + FVP1S |
C                 | FW2S + FVP2S |
C                 | FAS1S + FAD1S |
C                 | FAS2S + FAD2S |
C *******************************************************************
               MATUU(ZZADMA(0,ADCM1,IADP1(IFA)))=
     &               MATUU(ZZADMA(0,ADCM1,IADP1(IFA)))
     &               +FW1S(IFA+1)+FVP1S(IFA+1)
C
               MATUU(ZZADMA(0,ADCM1,IADP2(IFA)))=
     &               MATUU(ZZADMA(0,ADCM1,IADP2(IFA)))
     &               +FW2S(IFA+1)+FVP2S(IFA+1)
C
               MATUU(ZZADMA(0,ADCM2,IADP1(IFA)))=
     &               MATUU(ZZADMA(0,ADCM2,IADP1(IFA)))
     &               +FAS1S(IFA+1)+FAD1S(IFA+1)
C
               MATUU(ZZADMA(0,ADCM2,IADP2(IFA)))=
     &               MATUU(ZZADMA(0,ADCM2,IADP2(IFA)))
     &               +FAS2S(IFA+1)+FAD2S(IFA+1)
C *******************************************************************
C EQUATION DE LA CONSERVATION DE LA MASSE POUR L
C                 (DERIVEES % VARIABLES DU CENTRE)
C                 SANS LA PARTIE FICKIENNE PR INTERNE ET EXTERNE
C                 | FW1SV + FVP1SV |
C                 | FW2SV + FVP2SV |
C                 | FAS1SV + FAD1SV |
C                 | FAS2SV + FAD2SV |
C *******************************************************************

               MATUU(ZZADMA(KVOIS,ADCM1,IADP1K))=
     &               MATUU(ZZADMA(KVOIS,ADCM1,IADP1K))+
     &               FW1SV(IFA)+FVP1SV(IFA)
C
               MATUU(ZZADMA(KVOIS,ADCM1,IADP2K))=
     &               MATUU(ZZADMA(KVOIS,ADCM1,IADP2K))+
     &               FW2SV(IFA)+FVP2SV(IFA)
C
               MATUU(ZZADMA(KVOIS,ADCM2,IADP1K))=
     &               MATUU(ZZADMA(KVOIS,ADCM2,IADP1K))+
     &               FAS1SV(IFA)+FAD1SV(IFA)
C
               MATUU(ZZADMA(KVOIS,ADCM2,IADP2K))=
     &               MATUU(ZZADMA(KVOIS,ADCM2,IADP2K))+
     &               FAS2SV(IFA)+FAD2SV(IFA)
C
               IF(FINTER(IFA))THEN
                  NNOV=NBNOVO(KVOIS)
                  CALL ASSERT(NNOV.EQ.NNO)
C *******************************************************************
C EQUATION DE LA CONSERVATION DE LA MASSE POUR K
C                 (DERIVEES % VARIABLES DU CENTRE)
C                 RAJOUT DE LA PARTIE FICKIENNE
C                 | MOYVP1 |
C                 | MOYVP2 |
C                 | MOYAS1+ MOYAD1 |
C                 | MOYAS2+ MOYAD2 |
C *******************************************************************
                  MATUU(ZZADMA(0,ADCM1,IADP1K))=
     &                  MATUU(ZZADMA(0,ADCM1,IADP1K))+ MOYVP1(IFA,1,0)
C
                  MATUU(ZZADMA(0,ADCM1,IADP2K))=
     &                  MATUU(ZZADMA(0,ADCM1,IADP2K))+ MOYVP2(IFA,1,0)
C
                  MATUU(ZZADMA(0,ADCM2,IADP1K))=
     &                  MATUU(ZZADMA(0,ADCM2,IADP1K))+ MOYAS1(IFA,1,0)
     &                                               + MOYAD1(IFA,1,0)
C
                  MATUU(ZZADMA(0,ADCM2,IADP2K))=
     &                  MATUU(ZZADMA(0,ADCM2,IADP2K))+ MOYAS2(IFA,1,0)
     &                                               + MOYAD2(IFA,1,0)
C *******************************************************************
C EQUATION DE LA CONSERVATION DE LA MASSE POUR K
C                 (DERIVEES % VARIABLES DE L ARETE)
C                 RAJOUT DE LA PARTIE FICKIENNE
C                 | MOYVP1 |
C                 | MOYVP2 |
C                 | MOYAS1+ MOYAD1 |
C                 | MOYAS2+ MOYAD2 |
C ******************************************************************
                  DO 996 JFA=1,NFACE
                     MATUU(ZZADMA(0,ADCM1,IADP1(JFA)))=
     &                     MATUU(ZZADMA(0,ADCM1,IADP1(JFA)))
     &                     +MOYVP1(IFA,JFA+1,0)
C
                     MATUU(ZZADMA(0,ADCM1,IADP2(JFA)))=
     &                     MATUU(ZZADMA(0,ADCM1,IADP2(JFA)))
     &                     +MOYVP2(IFA,JFA+1,0)
C
                     MATUU(ZZADMA(0,ADCM2,IADP1(JFA)))=
     &                     MATUU(ZZADMA(0,ADCM2,IADP1(JFA)))
     &                     +MOYAS1(IFA,JFA+1,0)+MOYAD1(IFA,JFA+1,0)
C
                     MATUU(ZZADMA(0,ADCM2,IADP2(JFA)))=
     &                     MATUU(ZZADMA(0,ADCM2,IADP2(JFA)))
     &                     +MOYAS2(IFA,JFA+1,0)+MOYAD2(IFA,JFA+1,0)
  996             CONTINUE
C *******************************************************************
C EQUATION DE LA CONTINUITE DES FLUX POUR K
C                 (DERIVEES % VARIABLES DU CENTRE)
C                 | DFLKS1 |
C                 | DFLKS2 |
C                 | DFGKS1 |
C                 | DFGKS2 |
C *****************************************************************
                  MATUU(ZZADMA(0,ADCF1(IFA),IADP1K))=
     &                  MATUU(ZZADMA(0,ADCF1(IFA),IADP1K))
     &                  +DFLKS1(1,IFA)
C
                  MATUU(ZZADMA(0,ADCF1(IFA),IADP2K))=
     &                  MATUU(ZZADMA(0,ADCF1(IFA),IADP2K))
     &                  +DFLKS2(1,IFA)
C
                  MATUU(ZZADMA(0,ADCF2(IFA),IADP1K))=
     &                  MATUU(ZZADMA(0,ADCF2(IFA),IADP1K))
     &                  +DFGKS1(1,IFA)
C
                  MATUU(ZZADMA(0,ADCF2(IFA),IADP2K))=
     &                  MATUU(ZZADMA(0,ADCF2(IFA),IADP2K))
     &                  +DFGKS2(1,IFA)
C *******************************************************************
C EQUATION DE LA CONTINUITE DES FLUX POUR K
C                 (DERIVEES % VARIABLES DE L ARETE)
C                 | DFLKS1 |
C                 | DFLKS2 |
C                 | DFGKS1 |
C                 | DFGKS2 |
C *******************************************************************
                  DO 271 JFA=1,NFACE
                     MATUU(ZZADMA(0,ADCF1(IFA),IADP1(JFA)))=
     &                     MATUU(ZZADMA(0,ADCF1(IFA),IADP1(JFA)))
     &                     +DFLKS1(1+JFA,IFA)
C
                     MATUU(ZZADMA(0,ADCF1(IFA),IADP2(JFA)))=
     &                     MATUU(ZZADMA(0,ADCF1(IFA),IADP2(JFA)))
     &                     +DFLKS2(1+JFA,IFA)
C
                     MATUU(ZZADMA(0,ADCF2(IFA),IADP1(JFA)))=
     &                     MATUU(ZZADMA(0,ADCF2(IFA),IADP1(JFA)))
     &                     +DFGKS1(1+JFA,IFA)
C
                     MATUU(ZZADMA(0,ADCF2(IFA),IADP2(JFA)))=
     &                     MATUU(ZZADMA(0,ADCF2(IFA),IADP2(JFA)))
     &                     +DFGKS2(1+JFA,IFA)
  271             CONTINUE
C *******************************************************************
C EQUATION DE LA CONSERVATION DE LA MASSE POUR L
C                 (DERIVEES % VARIABLES DU CENTRE)
C                 RAJOUT DE LA PARTIE FICKIENNE
C                 | MOYVP1 |
C                 | MOYVP2 |
C                 | MOYAS1+ MOYAD1 |
C                 | MOYAS2+ MOYAD2 |
C *******************************************************************
                  MATUU(ZZADMA(KVOIS,ADCM1,IADP1K))=
     &                  MATUU(ZZADMA(KVOIS,ADCM1,IADP1K))
     &                  + MOYVP1(IFA,1,1)
C
                  MATUU(ZZADMA(KVOIS,ADCM1,IADP2K))=
     &                  MATUU(ZZADMA(KVOIS,ADCM1,IADP2K))
     &                  + MOYVP2(IFA,1,1)
C
                  MATUU(ZZADMA(KVOIS,ADCM2,IADP1K))=
     &                  MATUU(ZZADMA(KVOIS,ADCM2,IADP1K))
     &                  + MOYAS1(IFA,1,1)+ MOYAD1(IFA,1,1)
C
                  MATUU(ZZADMA(KVOIS,ADCM2,IADP2K))=
     &                  MATUU(ZZADMA(KVOIS,ADCM2,IADP2K))
     &                  + MOYAS2(IFA,1,1)+ MOYAD2(IFA,1,1)
C *******************************************************************
C EQUATION DE LA CONSERVATION DE LA MASSE POUR L
C                 (DERIVEES % VARIABLES DE L ARETE)
C                 | MOYVP1 |
C                 | MOYVP2 |
C                 | MOYAS1+ MOYAD1 |
C                 | MOYAS2+ MOYAD2 |
C *******************************************************************
                  DO 997 JFAV=1,NFACEV
                     MATUU(ZZADMA(KVOIS,ADCM1,IADP1(JFAV)))=
     &                     MATUU(ZZADMA(KVOIS,ADCM1,IADP1(JFAV)))+
     &                     MOYVP1(IFA,JFAV+1,1)
C
                     MATUU(ZZADMA(KVOIS,ADCM1,IADP2(JFAV)))=
     &                     MATUU(ZZADMA(KVOIS,ADCM1,IADP2(JFAV)))+
     &                     MOYVP2(IFA,JFAV+1,1)
C
                     MATUU(ZZADMA(KVOIS,ADCM2,IADP1(JFAV)))=
     &                     MATUU(ZZADMA(KVOIS,ADCM2,IADP1(JFAV)))+
     &                     MOYAS1(IFA,JFAV+1,1)+MOYAD1(IFA,JFAV+1,1)
C
                     MATUU(ZZADMA(KVOIS,ADCM2,IADP2(JFAV)))=
     &                     MATUU(ZZADMA(KVOIS,ADCM2,IADP2(JFAV)))
     &                     +MOYAS2(IFA,JFAV+1,1)+MOYAD2(IFA,JFAV+1,1)
  997             CONTINUE
C -------------------------
C -------------------------
               ELSE
C -------------------------
C -------------------------
C *******************************************************************
C EQUATION DE LA CONSERVATION DE LA MASSE POUR K
C                 (DERIVEES % VARIABLES DU CENTRE)
C                 RAJOUT DE LA PARTIE FICKIENNE
C                 | FVP1SK |
C                 | FVP2SK |
C                 | FAS1SK+ FAD1SK |
C                 | FAS2SK+ FAD2SK |
C *******************************************************************
                  MATUU(ZZADMA(0,ADCM1,IADP1K))=
     &                  MATUU(ZZADMA(0,ADCM1,IADP1K))+ FVP1SK(1,IFA)
C
                  MATUU(ZZADMA(0,ADCM1,IADP2K))=
     &                  MATUU(ZZADMA(0,ADCM1,IADP2K))+ FVP2SK(1,IFA)
C
                  MATUU(ZZADMA(0,ADCM2,IADP1K))=
     &                  MATUU(ZZADMA(0,ADCM2,IADP1K))+FAS1SK(1,IFA)
     &                  +FAD1SK(1,IFA)
C
                  MATUU(ZZADMA(0,ADCM2,IADP2K))=
     &                  MATUU(ZZADMA(0,ADCM2,IADP2K))+ FAS2SK(1,IFA)
     &                  + FAD2SK(1,IFA)
C *******************************************************************
C EQUATION DE LA CONSERVATION DE LA MASSE POUR K
C                 (DERIVEES % VARIABLES DE L ARETE)
C                 | FVP1SK |
C                 | FVP2SK |
C                 | FAS1SK+ FAD1SK |
C                 | FAS2SK+ FAD2SK |
C ******************************************************************
                  DO 998 JFA=1,NFACE
                     MATUU(ZZADMA(0,ADCM1,IADP1(JFA)))=
     &                     MATUU(ZZADMA(0,ADCM1,IADP1(JFA)))
     &                     +FVP1SK(JFA+1,IFA)
C
                     MATUU(ZZADMA(0,ADCM1,IADP2(JFA)))=
     &                     MATUU(ZZADMA(0,ADCM1,IADP2(JFA)))
     &                     +FVP2SK(JFA+1,IFA)
C
                     MATUU(ZZADMA(0,ADCM2,IADP1(JFA)))=
     &                     MATUU(ZZADMA(0,ADCM2,IADP1(JFA)))
     &                     +FAS1SK(JFA+1,IFA)+FAD1SK(JFA+1,IFA)
C
                     MATUU(ZZADMA(0,ADCM2,IADP2(JFA)))=
     &                     MATUU(ZZADMA(0,ADCM2,IADP2(JFA)))
     &                     +FAS2SK(JFA+1,IFA)+FAD2SK(JFA+1,IFA)
  998             CONTINUE
C *******************************************************************
C EQUATION DE LA CONTINUITE DES FLUX POUR K
C                 (DERIVEES % VARIABLES DU CENTRE)
C                 | FM1WS + FM1VPS + FVP1SK |
C                 | FM2WS + FM2VPS + FVP2SK |
C                 | FM1ASS + FM1ADS + FAS1SK + FAD1SK |
C                 | FM2ASS + FM2ADS + FAS2SK + FAD2SK |
C *****************************************************************
                  MATUU(ZZADMA(0,ADCF1(IFA),IADP1K))=
     &                  MATUU(ZZADMA(0,ADCF1(IFA),IADP1K))+
     &                  FM1WS(1,IFA)+FM1VPS(1,IFA)+FVP1SK(1,IFA)
C
                  MATUU(ZZADMA(0,ADCF1(IFA),IADP2K))=
     &                  MATUU(ZZADMA(0,ADCF1(IFA),IADP2K))+
     &                  FM2WS(1,IFA)+FM2VPS(1,IFA)+FVP2SK(1,IFA)
C
                  MATUU(ZZADMA(0,ADCF2(IFA),IADP1K))=
     &                  MATUU(ZZADMA(0,ADCF2(IFA),IADP1K))+
     &                  FM1ASS(1,IFA)+FM1ADS(1,IFA)+FAS1SK(1,IFA)
     &                  +FAD1SK(1,IFA)
C
                  MATUU(ZZADMA(0,ADCF2(IFA),IADP2K))=
     &                  MATUU(ZZADMA(0,ADCF2(IFA),IADP2K))+
     &                  FM2ASS(1,IFA)+FM2ADS(1,IFA)+FAS2SK(1,IFA)
     &                  +FAD2SK(1,IFA)
C *******************************************************************
C EQUATION DE LA CONTINUITE DES FLUX POUR K
C                 (DERIVEES % VARIABLES DE L ARETE)
C                 | FM1WS + FM1VPS + FVP1SK |
C                 | FM2WS + FM2VPS + FVP2SK |
C                 | FM1ASS + FM1ADS + FAS1SK + FAD1SK |
C                 | FM2ASS + FM2ADS + FAS2SK + FAD2SK |
C *******************************************************************
                  DO 272 JFA=1,NFACE
                     MATUU(ZZADMA(0,ADCF1(IFA),IADP1(JFA)))=
     &                     MATUU(ZZADMA(0,ADCF1(IFA),IADP1(JFA)))
     &                     + FM1WS(JFA+1,IFA)+
     &                     FM1VPS(JFA+1,IFA)+FVP1SK(JFA+1,IFA)
C
                     MATUU(ZZADMA(0,ADCF1(IFA),IADP2(JFA)))=
     &                     MATUU(ZZADMA(0,ADCF1(IFA),IADP2(JFA)))
     &                     +FM2WS(JFA+1,IFA)+
     &                     FM2VPS(JFA+1,IFA)+FVP2SK(JFA+1,IFA)
C
                     MATUU(ZZADMA(0,ADCF2(IFA),IADP1(JFA)))=
     &                     MATUU(ZZADMA(0,ADCF2(IFA),IADP1(JFA)))
     &                     +FM1ASS(JFA+1,IFA)+FM1ADS(JFA+1,IFA)
     &                     +FAS1SK(JFA+1,IFA)+FAD1SK(JFA+1,IFA)
C
                     MATUU(ZZADMA(0,ADCF2(IFA),IADP2(JFA)))=
     &                     MATUU(ZZADMA(0,ADCF2(IFA),IADP2(JFA)))
     &                     +FM2ASS(JFA+1,IFA)+FM2ADS(JFA+1,IFA)
     &                     +FAS2SK(JFA+1,IFA)+FAD2SK(JFA+1,IFA)
  272             CONTINUE
               ENDIF
   27       CONTINUE
         END IF
      END IF
C ======================================================================
C ======================================================================
      END
