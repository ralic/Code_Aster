      SUBROUTINE EIFORE(NDIM,AXI,NNO1,NNO2,NPG,WREF,VFF1,VFF2,DFFR2,
     &  GEOM,ANG,IU,IM,SIGREF,DEPREF,VECT)

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 07/07/2008   AUTEUR LAVERNE J.LAVERNE 
C ======================================================================
C COPYRIGHT (C) 1991 - 2008  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================

       IMPLICIT NONE

       LOGICAL AXI
       INTEGER NDIM,NNO1,NNO2,NPG,IU(3,18),IM(3,9)
       REAL*8  VFF1(NNO1,NPG),VFF2(NNO2,NPG),GEOM(NDIM,NNO2),WREF(NPG)
       REAL*8  VECT(2*NNO1*NDIM+NNO2*NDIM)
       REAL*8  DFFR2(NDIM-1,NNO2,NPG),ANG(*),SIGREF,DEPREF
C ----------------------------------------------------------------------
C
C     REFE_FORC_NODA POUR LES ELEMENTS D'INTERFACE
C
C ----------------------------------------------------------------------
C IN  NDIM    : DIMENSION DES ELEMENTS 
C IN  AXI     : .TRUE. SI AXISYMETRIE
C IN  NNO1    : NOMBRE DE NOEUDS (FAMILLE U)
C IN  VFF1    : VALEUR DES FONCTIONS DE FORME (FAMILLE U)
C IN  NNO2    : NOMBRE DE NOEUDS (FAMILLE E)
C IN  VFF2    : VALEUR DES FONCTIONS DE FORME (FAMILLE E)
C IN  DFFR2   : DERIVEES DES FONCTIONS DE FORME DE REFERENCE (FAMILLE L)
C IN  NPG     : NOMBRE DE POINTS DE GAUSS
C IN  WREF    : POIDS DES POINTS DE GAUSS DE REFERENCE 
C IN  GEOM    : COORDONNEES DES NOEUDS
C IN  IU      : DECALAGE D'INDICE POUR ACCEDER AUX DDL DE DEPLACEMENT
C IN  IM      : DECALAGE D'INDICE POUR ACCEDER AUX DDL DE LAGRANGE
C IN  SIGREF  : VALEUR DE REFERENCE POUR LES CONTRAINTES
C IN  DEPREF  : VALEUR DE REFERENCE POUR LES SAUTS DE DEPLACEMENT
C OUT VECT    : FORCES INTERIEURES DE REFERENCE
C ----------------------------------------------------------------------
      INTEGER G,N,I,K,KK
      REAL*8  WG,B(3,3,18),T1
C ----------------------------------------------------------------------

      CALL R8INIR(NNO1*2*NDIM+NNO2*NDIM,0.D0,VECT,1)

      DO 1000 G=1,NPG

        CALL EICINE(NDIM,AXI,NNO1,NNO2,VFF1(1,G),VFF2(1,G),WREF(G),
     &              DFFR2(1,1,G),GEOM,ANG,WG,B)

C      VECTEUR FINT:U
        DO 300 N=1,2*NNO1
        DO 301 I=1,NDIM
          KK = IU(I,N)
          T1 = 0
          DO 320 K = 1,NDIM
            T1 = T1 + ABS(B(K,I,N))*SIGREF/NDIM
 320      CONTINUE
          VECT(KK) = VECT(KK) + WG*T1
 301    CONTINUE
 300    CONTINUE

C      VECTEUR FINT:M
        DO 350 N=1,NNO2
        DO 351 I = 1,NDIM
          KK = IM(I,N)
          T1 = ABS(VFF2(N,G))*DEPREF
          VECT(KK) = VECT(KK) + WG*T1
 351    CONTINUE
 350    CONTINUE

 1000 CONTINUE
      END
