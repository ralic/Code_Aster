      SUBROUTINE CFGLI3(NOMA  ,DEFICO,RESOCO,NEQ   ,NESMAX,
     &                  NBLIAI,NBLIAC,LLF   ,LLF1  ,LLF2  ,
     &                  AJLIAI,SPLIAI,INDIC ,XMUL  ,LIASUP)
C 
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 12/09/2011   AUTEUR ABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT     NONE
      CHARACTER*24 RESOCO,DEFICO
      CHARACTER*8  NOMA
      INTEGER      INDIC
      INTEGER      AJLIAI,SPLIAI,NBLIAI,NEQ,NESMAX
      INTEGER      NBLIAC,LLF,LLF1,LLF2
      REAL*8       XMUL
      LOGICAL      LIASUP    
C      
C ----------------------------------------------------------------------
C
C ROUTINE CONTACT (METHODES DISCRETES - RESOLUTION)
C
C VERIFICATION QUE LES LIAISONS SONT BIEN GLISSANTES - VERSION 3D
C
C ----------------------------------------------------------------------
C
C
C IN  DEFICO : SD DE DEFINITION DU CONTACT (ISSUE D'AFFE_CHAR_MECA)
C IN  RESOCO : SD DE TRAITEMENT NUMERIQUE DU CONTACT
C IN  NOMA   : NOM DU MAILLAGE
C IN  NEQ    : NOMBRE D'EQUATIONS
C IN  NBLIAI : NOMBRE DE LIAISONS DE CONTACT
C I/O NBLIAC : NOMBRE DE LIAISONS ACTIVES
C I/O AJLIAI : INDICE DANS LA LISTE DES LIAISONS ACTIVES DE LA DERNIERE
C              LIAISON CORRECTE DU CALCUL
C              DE LA MATRICE DE CONTACT ACM1AT
C I/O SPLIAI : INDICE DANS LA LISTE DES LIAISONS ACTIVES DE LA DERNIERE
C              LIAISON AYANT ETE CALCULEE POUR LE VECTEUR CM1A
C I/O LLF    : NOMBRE DE LIAISONS DE FROTTEMENT
C OUT INDIC  : +1 ON A RAJOUTE UNE LIAISON
C              -1 ON A ENLEVE UNE LIAISON
C OUT LIASUP : .TRUE. SI AU MOINS UNE LIAISON SUPPRIMEE
C
C -------------- DEBUT DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER            ZI
      COMMON  / IVARJE / ZI(1)
      REAL*8             ZR
      COMMON  / RVARJE / ZR(1)
      COMPLEX*16         ZC
      COMMON  / CVARJE / ZC(1)
      LOGICAL            ZL
      COMMON  / LVARJE / ZL(1)
      CHARACTER*8        ZK8
      CHARACTER*16                ZK16
      CHARACTER*24                          ZK24
      CHARACTER*32                                    ZK32
      CHARACTER*80                                              ZK80
      COMMON  / KVARJE / ZK8(1) , ZK16(1) , ZK24(1) , ZK32(1) , ZK80(1)
C
C ---------------- FIN DECLARATIONS NORMALISEES JEVEUX -----------------
C
      CHARACTER*24 NMGLI1,NMGLI2,NMADHR
      INTEGER      JGLI1,JGLI2,JADHR
      REAL*8       LAMBDC,LAMBDF,XVAL
      CHARACTER*1  TYPEAJ
      CHARACTER*2  TYPLI1
      CHARACTER*2  TYPEC0,TYPEF0,TYPEF1,TYPEF2
      INTEGER      ILIAI1,ILIAI2,ILIAC1,ILIAC2
      INTEGER      ILIAI,ILIAC
      CHARACTER*19 LIAC,TYPL,MU,COPO
      INTEGER      JLIAC,JTYPL,JMU,JCOPO
      CHARACTER*24 TACFIN,APPOIN,APCOFR,APDDL
      INTEGER      JTACF,JAPPTR,JAPCOF,JAPDDL
      INTEGER      CFMMVD,ZTACF
      INTEGER      LFMIN,LFMIN1,LFMIN2   
      LOGICAL      LELPIV,LELPI1,LELPI2,TROUAC
      INTEGER      NBDDL,JDECAL,BTOTAL,POSIT
      INTEGER      COMPT0
      REAL*8       AJEUFX,AJEUFY,GLIS,COEFFF
      REAL*8       R8PREM
      CHARACTER*19 DEPLC
      INTEGER      JDEPC
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
C
C --- INITIALISATIONS
C
      LFMIN  = 0
      LFMIN1 = 0
      LFMIN2 = 0
      COMPT0 = 0
      TYPEAJ = 'A'
      BTOTAL = NBLIAC + LLF + LLF1 + LLF2
      TYPEC0 = 'C0'
      TYPEF0 = 'F0'
      TYPEF1 = 'F1'
      TYPEF2 = 'F2'
      LIASUP = .FALSE.
C
C --- PAS DE LIAISONS GLISSANTES -> ON SORT
C
      IF ((LLF+LLF1+LLF2).GE.NBLIAC) THEN
        GOTO 999
      ENDIF
C 
C --- LECTURE DES STRUCTURES DE DONNEES DE CONTACT
C
      APPOIN = RESOCO(1:14)//'.APPOIN'
      APCOFR = RESOCO(1:14)//'.APCOFR'
      APDDL  = RESOCO(1:14)//'.APDDL'
      LIAC   = RESOCO(1:14)//'.LIAC'
      TYPL   = RESOCO(1:14)//'.TYPL'
      TACFIN = RESOCO(1:14)//'.TACFIN' 
      MU     = RESOCO(1:14)//'.MU'
      COPO   = RESOCO(1:14)//'.COPO'
      CALL JEVEUO(APPOIN,'L',JAPPTR)
      CALL JEVEUO(APCOFR,'L',JAPCOF)
      CALL JEVEUO(APDDL, 'L',JAPDDL)
      CALL JEVEUO(LIAC,  'L',JLIAC )
      CALL JEVEUO(TYPL  ,'L',JTYPL )
      CALL JEVEUO(TACFIN,'L',JTACF )
      CALL JEVEUO(MU,    'E',JMU   )
      CALL JEVEUO(COPO  ,'E',JCOPO )
      ZTACF  = CFMMVD('ZTACF')
C
C --- ACCES AUX CHAMPS DE TRAVAIL
C --- DEPLC : INCREMENT DE DEPLACEMENT CUMULE DEPUIS DEBUT 
C ---         DU PAS DE TEMPS AVEC CORRECTION DU CONTACT
C
      DEPLC  = RESOCO(1:14)//'.DEPC'
      CALL JEVEUO(DEPLC (1:19)//'.VALE','L',JDEPC)
C
C --- CREATION DES OBJETS DE TRAVAIL
C
      NMGLI1 = '&&CFGLI3.GLI1'
      NMGLI2 = '&&CFGLI3.GLI2'
      NMADHR = '&&CFGLI3.ADHR'
      CALL WKVECT(NMGLI1,'V V I',NBLIAC,JGLI1  )
      CALL WKVECT(NMGLI2,'V V I',NBLIAC,JGLI2  )
      CALL WKVECT(NMADHR,'V V I',NBLIAC,JADHR  )
C
C --- LIAISON GLISSANTE ?
C
      DO 100 ILIAC1 = 1, BTOTAL
        ILIAI1 = ZI(JLIAC+ILIAC1-1)
        TYPLI1 = ZK8(JTYPL+ILIAC1-1)(1:2)
        IF (TYPLI1.EQ.TYPEC0) THEN
C
          COMPT0 = COMPT0 + 1
C
C ------- INFORMATIONS SUR LA LIAISON
C
          COEFFF = ZR(JTACF+ZTACF*(ILIAI1-1)+0)
          JDECAL = ZI(JAPPTR+ILIAI1-1)
          NBDDL  = ZI(JAPPTR+ILIAI1) - ZI(JAPPTR+ILIAI1-1)
          LAMBDC = ZR(JMU+COMPT0-1)
C
C ------- CALCUL DES JEUX
C
          AJEUFX = 0.D0
          AJEUFY = 0.D0
          CALL CFELPV(ILIAI1,TYPEF1,RESOCO,NBLIAI,LELPI1)
          IF (.NOT.LELPI1) THEN
            CALL CALADU(NEQ   ,NBDDL,ZR(JAPCOF+JDECAL),
     &                  ZI(JAPDDL+JDECAL),ZR(JDEPC),AJEUFX)
          ENDIF
C
          CALL CFELPV(ILIAI1,TYPEF2,RESOCO,NBLIAI,LELPI2)
          IF (.NOT.LELPI2) THEN
            CALL CALADU(NEQ   ,NBDDL,ZR(JAPCOF+JDECAL+30*NESMAX),
     &                  ZI(JAPDDL+JDECAL),ZR(JDEPC),AJEUFY)
          ENDIF
          GLIS   = SQRT( AJEUFX**2 + AJEUFY**2 )
C
C ------- LAGRANGE DE FROTTEMENT
C
          IF (LAMBDC.GT.0.D0 ) THEN
            LAMBDF = COEFFF*LAMBDC
          ELSE
            LAMBDF = 0.D0
          ENDIF
C
C ------- EST-ELLE ADHERENTE ?
C
          IF ((GLIS.GT.R8PREM()).AND.(GLIS .LT. (LAMBDF/XMUL**2))) THEN
            TROUAC = .FALSE.
            DO 157 ILIAC2 = ILIAC1 + 1, BTOTAL
              ILIAI2 = ZI(JLIAC+ILIAC2-1)
              IF (ILIAI1.EQ.ILIAI2) TROUAC = .TRUE.
 157        CONTINUE
            IF (.NOT.TROUAC) THEN
              CALL CFELPV(ILIAI1,TYPEF0,RESOCO,NBLIAI,LELPIV)
              IF (.NOT.LELPIV) THEN
                CALL CFELPV(ILIAI1,TYPEF1,RESOCO,NBLIAI,LELPI1)
                IF (LELPI1) THEN
                  LFMIN2 = LFMIN2 + 1
                  ZI(JGLI2+LFMIN2-1) = ILIAI1
                  GOTO 100
                ENDIF
                CALL CFELPV(ILIAI1,TYPEF2,RESOCO,NBLIAI,LELPI1)
                IF (LELPI2) THEN
                  LFMIN1 = LFMIN1 + 1
                  ZI(JGLI1+LFMIN1-1) = ILIAI1
                  GOTO 100
                ENDIF
                LFMIN = LFMIN + 1
                ZI(JADHR+LFMIN-1) = ILIAI1
              ENDIF
            ENDIF
          ENDIF  
        ENDIF
  100 CONTINUE    
C
C --- AJOUTER LIAISON ADHERENTE
C
      DO 197 ILIAC=1,LFMIN
        ILIAI  = ZI(JADHR+ILIAC-1)
        POSIT  = NBLIAC + LLF + LLF1 + LLF2 + 1
        CALL CFTABL(INDIC ,NBLIAC,AJLIAI,SPLIAI,LLF   ,
     &              LLF1  ,LLF2  ,RESOCO,TYPEAJ,POSIT ,
     &              ILIAI ,TYPEF0)
        CALL CFIMP2(DEFICO,RESOCO,NOMA  ,ILIAI ,TYPEF0,
     &              'ADH')
 197  CONTINUE
      DO 198 ILIAC=1,LFMIN1
        ILIAI  = ZI(JGLI1+ILIAC-1)
        POSIT  = NBLIAC + LLF + LLF1 + LLF2 + 1
        CALL CFTABL(INDIC ,NBLIAC,AJLIAI,SPLIAI,LLF  ,
     &              LLF1  ,LLF2  ,RESOCO,TYPEAJ,POSIT,
     &              ILIAI ,TYPEF1)
        CALL CFIMP2(DEFICO,RESOCO,NOMA  ,ILIAI ,TYPEF1,
     &              'ADH')
 198  CONTINUE
      DO 199 ILIAC=1,LFMIN2
        ILIAI  = ZI(JGLI2+ILIAC-1)
        POSIT  = NBLIAC + LLF + LLF1 + LLF2 + 1
        CALL CFTABL(INDIC ,NBLIAC,AJLIAI,SPLIAI,LLF  ,
     &              LLF1  ,LLF2  ,RESOCO,TYPEAJ,POSIT,
     &              ILIAI ,TYPEF2)
        CALL CFIMP2(DEFICO,RESOCO,NOMA  ,ILIAI ,TYPEF2,
     &              'ADH')
 199  CONTINUE
C
C --- A-TON MODIFIE DES LIAISONS ?
C
      LIASUP = (LFMIN+LFMIN1+LFMIN2).NE.0
C
C --- MISE A JOUR DU PARAMETRE
C
      IF (LIASUP) THEN
        XVAL = XMUL**2
        IF (XVAL.LT.(1.D0/R8PREM())) THEN
          XMUL = XMUL*SQRT(10.D0)
        ENDIF
        ZR(JCOPO) = XMUL
      ENDIF
C
      CALL JEDETR(NMGLI1)
      CALL JEDETR(NMGLI2)
      CALL JEDETR(NMADHR)
C
 999  CONTINUE
      CALL JEDEMA()
C
      END
