      SUBROUTINE LCROTG (INDICE,DP,E,DTAUDF)

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 20/04/2011   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
      IMPLICIT NONE
      INTEGER  INDICE
      REAL*8   DP,E(6),DTAUDF(6,3,3)

C ***************************************************************
C *       INTEGRATION DE LA LOI DE ROUSSELIER LOCAL             *
C * CALCUL DE LA DERIVEE DE TAU PAR RAPPORT A DF = DTAUDF       *
C * TAU = TAU(TAU - DF)                                         *
C * TAU = TAU(E)                                                *
C * E = E(ETR)                                                  *
C * ETR =  ETR(DF)                                              *
C * DTAUDF = DTAU/DE * DE/DETR * DETR/DF + DTAU/DDF *
C ***************************************************************

C IN  INDICE : REGIME DE LA SOLUTION (ELASTIQUE, PLASTIQUE, SING)
C IN  DP     : INCREMENT DE DEFORMATION PLASTIQUE
C IN  E      : DEFORMATION ELASTIQUE
C OUT DTAUDF : DERIVEE DE TAU PAR RAPPORT A DF
C ----------------------------------------------------------------------
C  COMMON LOI DE COMPORTEMENT ROUSSELIER

      INTEGER ITEMAX, JPROLP, JVALEP, NBVALP
      REAL*8  PREC,YOUNG,NU,SIGY,SIG1,ROUSD,F0,FCR,ACCE
      REAL*8  PM,RPM,FONC,FCD,DFCDDJ,DPMAXI
      COMMON /LCROU/ PREC,YOUNG,NU,SIGY,SIG1,ROUSD,F0,FCR,ACCE,
     &               PM,RPM,FONC,FCD,DFCDDJ,DPMAXI,
     &               ITEMAX, JPROLP, JVALEP, NBVALP
C ----------------------------------------------------------------------
C  COMMON GRANDES DEFORMATIONS CANO-LORENTZ

      INTEGER IND1(6),IND2(6)
      REAL*8  KR(6),RAC2,RC(6)
      REAL*8  LAMBDA,MU,DEUXMU,UNK,TROISK,COTHER
      REAL*8  JM,DJ,JP,DJDF(3,3)
      REAL*8  ETR(6),DVETR(6),EQETR,TRETR,DETRDF(6,3,3)
      REAL*8  DTAUDE(6,6)

      COMMON /GDCLC/
     &          IND1,IND2,KR,RAC2,RC,
     &          LAMBDA,MU,DEUXMU,UNK,TROISK,COTHER,
     &          JM,DJ,JP,DJDF,
     &          ETR,DVETR,EQETR,TRETR,DETRDF,
     &          DTAUDE
C ----------------------------------------------------------------------
C ----------------------------------------------------------------------
      INTEGER IJ,KL,K,L,PQ,RS
      REAL*8  TRE,RP,DRDP,AIRE
      REAL*8  AL,AL0,AL1,AL2,AL3,AL4,BE1,BE2,A1(6,6),A2(6),A3(6),SUM
      REAL*8  DDVETR(6,6),DTRETR(6)
      REAL*8  DEDETR(6,6),DEDFCD(6)
      REAL*8  R8BID
C ----------------------------------------------------------------------


C 1 - CALCUL DES DERIVEES SPECIFIQUES A LA LOI DE ROUSSELIER
C         DE / DETR = DEDETR
C         DE / DJ   = DEDJ

      CALL R8INIR(36,0.D0,DEDETR,1)
      CALL R8INIR(6,0.D0,DEDFCD,1)
      CALL RCFONC('V',1,JPROLP,JVALEP,NBVALP,R8BID,
     &            R8BID,R8BID,PM+DP,RP,DRDP,AIRE,R8BID,R8BID)
      TRE = E(1)+E(2)+E(3)
      GOTO (100,200,600) (INDICE+1)


C 4.1 - CAS ELASTIQUE

 100  CONTINUE
      DO 110 IJ = 1,6
        DEDETR(IJ,IJ) = 1.D0
 110  CONTINUE
      GOTO 1000



C 4.2 CAS PLASTIQUE

 200  CONTINUE

C 4.2.1 - DERIVEE DE DVE PAR RAPPORT A DVETR ET DP
C DVE = DVE(DVETR - DP)
C DDVE/DDVETR = A1 ET DDVE/DDP = A2

      AL = 1.D0-3.D0/2.D0*DP/EQETR
      DO 205 IJ = 1,6
        A2(IJ) = -1.5D0*DVETR(IJ)/EQETR
 205  CONTINUE
      DO 210 IJ = 1,6
        DO 220 KL = 1,6
          A1(IJ,KL) = 2.D0/3.D0*(1-AL)*A2(IJ)*A2(KL)
 220    CONTINUE
        A1(IJ,IJ) = A1(IJ,IJ) + AL
 210  CONTINUE


C 4.2.2 - DERIVEE DE TRE PAR RAPPORT A TRETR, DP ET FCD
C TRE = TRE(TRETR - DP)
C DTRE/DTRETR = AL1, DTRE/DP = AL2, DTRE/DFCF = BE1

      AL0 = EXP(-UNK*TRE/SIG1)*EXP(-COTHER/SIG1)
      AL1 = 1.D0/(1.D0+FCD*UNK*DP*AL0/SIG1)
      AL2 = FCD*AL0*AL1
      BE1 = DP*AL0*AL1


C 4.2.3 - DERIVEE DE DP PAR RAPPORT A DVETR ET TRETR
C DP = DP(DVETR - TRETR)
C DDP/DDVETR = A3, DDP/DTRETR = AL4, DDP/DFCD = BE2

      AL3 = 1.D0/(3.D0*MU+DRDP+FCD*UNK*AL2*AL0)
      AL4 = -UNK*AL2*AL3
      DO 300 IJ = 1,6
        A3(IJ) = -DEUXMU*AL3*A2(IJ)
 300  CONTINUE
      BE2 = AL3*(SIG1*AL0-UNK*FCD*AL0*BE1)


C 4.2.4 - DERIVEE DE E PAR RAPPORT A DVETR, TRETR ET FCD
C ASSEMBLAGE DE 4.1.1 ET 4.1.2 ET 4.1.3
C E = E(DVETR - TRETR)
C DE/DDVETR = DDVETR, DE/DTRETR = DTRETR, DE/DFCD = DEDFCD

      DO 400 IJ=1,6
        DO 410 KL=1,6
          DDVETR(IJ,KL) = A1(IJ,KL) + (A2(IJ)+AL2/3.D0*KR(IJ))*A3(KL)
 410    CONTINUE
        DTRETR(IJ) = AL1*KR(IJ)/3.D0 + AL4*(A2(IJ)+AL2/3.D0*KR(IJ))
        DEDFCD(IJ) = BE1/3.D0*KR(IJ) + BE2*(A2(IJ)+AL2/3.D0*KR(IJ))
 400  CONTINUE


C 4.2.5 - DERIVEE DE E PAR RAPPORT A ETR = DEDETR

      DO 500 IJ=1,6
        DO 510 KL=1,6
          DEDETR(IJ,KL)=DDVETR(IJ,KL)+(DTRETR(IJ)-AL/3.D0*KR(IJ))*KR(KL)
 510    CONTINUE
 500  CONTINUE
      GOTO 1000



C 4.3 CAS SINGULIER

 600  CONTINUE

C 4.3.1 - DERIVEE DE DVE PAR RAPPORT A DVETR, DP ET DFCD
C DVE = DVE(DVETR - DP)=0.D0


C 4.3.2 - DERIVEE DE TRE PAR RAPPORT A TRETR, DP ET DFCD
C TRE = TRE(TRETR - DP)
C DTRE/DTRETR = AL1, DTRE/DP = AL2 ET DTRE/DFCD = BE1

      AL0 = EXP(-UNK*TRE/SIG1)*EXP(-COTHER/SIG1)
      AL1 = 1.D0/(1.D0+FCD*UNK*DP*AL0/SIG1)
      AL2 = FCD*AL0*AL1
      BE1 = DP*AL0*AL1


C 4.3.3 - DERIVEE DE DP PAR RAPPORT A DVETR, TRETR ET DFCD
C DP = DP(DVETR - TRETR)
C DDP/DDVETR = 0.D0, DDP/DTRETR = AL4 ET DDP/DFCD = BE2

      AL3 = 1.D0/(DRDP+AL0*AL2*FCD*UNK)
      AL4 = -UNK*AL2*AL3
      BE2 = AL3*(SIG1*AL0-UNK*FCD*AL0*BE1)


C 4.3.4 - DERIVEE DE E PAR RAPPORT A ETR ET FCD
C  DE/DETR = DEDETR, DE/DFCD = DEDFCD

      DO 605 IJ=1,6
        DO 610 KL=1,6
          DEDETR(IJ,KL)= (AL1+AL2*AL4)/3.D0*KR(IJ)*KR(KL)
 610    CONTINUE
        DEDFCD(IJ) = (BE1+AL2*BE2)/3.D0*KR(IJ)
 605  CONTINUE



C 2 - COMPOSITION DES DERIVATIONS :
C     DTAUDF = DTAUDE * (DEDETR * DETRDF + DEDJ * DJDF)

 1000 CONTINUE
      DO 1100 IJ = 1,6
        DO 1110 K = 1,3
          DO 1120 L = 1,3
            SUM = 0
            DO 1130 PQ = 1,6
              DO 1140 RS = 1,6
                SUM = SUM + DTAUDE(IJ,PQ)*DEDETR(PQ,RS)*DETRDF(RS,K,L)
 1140         CONTINUE
              SUM = SUM + DTAUDE(IJ,PQ)*DEDFCD(PQ)*DFCDDJ*DJDF(K,L)
 1130       CONTINUE
            DTAUDF(IJ,K,L) = SUM
 1120     CONTINUE
 1110   CONTINUE
 1100 CONTINUE

      END
