      SUBROUTINE CLETDR (DF,EM,OPTION,ETR,DETRDF)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 11/02/2003   AUTEUR PBADEL P.BADEL 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C RESPONSABLE PBADEL P.BADEL
      IMPLICIT NONE
      
      CHARACTER*16   OPTION
      REAL*8          DF(3,3),EM(6)
      REAL*8          ETR(6),DETRDF(6,3,3)

C **********************************************************
C *       INTEGRATION DE LA LOI DE ROUSSELIER LOCAL        *
C * CALCUL DE ETR ET DE LA DERIVEE DE ETR PAR RAPPORT A DF *
C **********************************************************

C IN  OPTION  : OPTION DE CALCUL
C IN  DF      : INCREMENT DU GRADIENT DE LA TRANSFORMATION
C IN  EM      : DEFORMATION ELASTIQUE EULERIENNE A L INSTANT MOINS
C                 EM = (MID-BM)/2.D0
C OUT ETR     : ETR(IJ)=(ID(IJ)-DF(IK)*DF(JK))/2+DF(IK)*EM(KP)*DF(JP)
C OUT DETRDF  : DERIVEE DE ETR PAR RAPPORT A DF

C 1 - DECLARATION INTERNE

      INTEGER         I,J1,K1,L,IND(3,3)
      REAL*8          V(3,3),BM(6),MAT(3,3),KR(6)
      DATA            KR/1.D0,1.D0,1.D0,0.D0,0.D0,0.D0/
      DATA            IND/1,4,5,
     &                   4,2,6,
     &                   5,6,3/

C 2 - CALCUL DE ETR
      
      DO 10 I=1,3
       DO 15 J1=1,3
        V(I,J1)=0.D0
        DO 20 K1=1,3        
         V(I,J1)=V(I,J1)+DF(I,K1)*EM(IND(K1,J1))         
   20   CONTINUE
   15  CONTINUE
   10 CONTINUE
      
      DO 25 I =1,3
       DO 30 J1=1,3
        ETR(IND(I,J1))=KR(IND(I,J1))/2.D0
        DO 35 K1=1,3
         ETR(IND(I,J1))=ETR(IND(I,J1))+V(I,K1)*DF(J1,K1)
     &                -(DF(I,K1)*DF(J1,K1))/2.D0
  35    CONTINUE
  30   CONTINUE
  25  CONTINUE
  
C 3 - CALCUL DE LA DERIVEE DE ETR PAR RAPPORT A DF
  
      IF(OPTION(1:14).EQ.'RIGI_MECA_TANG'.OR.
     &  OPTION(1:9) .EQ.'FULL_MECA') THEN
       DO 40 I=1,6
        BM(I)=KR(I)-2.D0*EM(I)
 40    CONTINUE

       DO 45 I=1,3
        DO 50 J1=1,3
         MAT(I,J1)=0.D0
         DO 55 K1=1,3
          MAT(I,J1)=MAT(I,J1)+DF(I,K1)*BM(IND(K1,J1))
 55      CONTINUE
 50     CONTINUE
 45    CONTINUE
 
       DO 60 I=1,3
        DO 65 J1=1,3
         DO 70 K1=1,3
          DO 75 L=1,3
           DETRDF(IND(I,J1),K1,L)=
     &    -(KR(IND(I,K1))*MAT(J1,L)+KR(IND(J1,K1))*MAT(I,L))/2.D0
 75       CONTINUE
 70      CONTINUE
 65     CONTINUE
 60    CONTINUE
 
      ENDIF       
      END
