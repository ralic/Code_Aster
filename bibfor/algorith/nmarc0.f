      SUBROUTINE NMARC0(RESULT,MODELE,MATE  ,CARELE,FONACT,
     &                  SDCRIT,SDDYNA,SDPOST,CARCRI,SDERRO,
     &                  SDPILO,LISCH2,NUMARC,INSTAN)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 20/12/2011   AUTEUR BEAURAIN J.BEAURAIN 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT NONE
      CHARACTER*8   RESULT
      INTEGER       NUMARC
      INTEGER       FONACT(*)
      REAL*8        INSTAN
      CHARACTER*19  SDDYNA,SDPOST,SDPILO
      CHARACTER*19  LISCH2,SDCRIT
      CHARACTER*24  MODELE,MATE,CARELE,SDERRO,CARCRI
C
C ----------------------------------------------------------------------
C
C ROUTINE MECA_NON_LINE (ALGORITHME - ARCHIVAGE)
C
C ARCHIVAGE DES PARAMETRES
C
C ----------------------------------------------------------------------
C
C
C IN  RESULT : NOM UTILISATEUR DU CONCEPT RESULTAT
C IN  MODELE : NOM DU MODELE
C IN  MATE   : CHAMP DE MATERIAU
C IN  SDCRIT : VALEUR DES CRITERES DE CONVERGENCE
C IN  SDPILO : SD PILOTAGE
C IN  CARCRI : CARTE DES CRITERES DE CONVERGENCE LOCAUX
C IN  SDERRO : SD ERREUR
C IN  COMPOR : CARTE DECRIVANT LE TYPE DE COMPORTEMENT
C IN  CARELE : CARACTERISTIQUES DES ELEMENTS DE STRUCTURE
C IN  SDPOST : SD POUR POST-TRAITEMENTS (CRIT_FLAMB ET MODE_VIBR)
C IN  FONACT : FONCTIONNALITES ACTIVEES (VOIR NMFONC)
C IN  SDDYNA : SD DEDIEE A LA DYNAMIQUE
C IN  LISCH2 : NOM DE LA SD INFO CHARGE POUR STOCKAGE DANS LA SD
C IN  NUMARC : NUMERO D'ARCHIVAGE
C IN  INSTAN : VALEUR DE L'INSTANT
C
C -------------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ----------------
C
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C -------------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ----------------
C
      CHARACTER*16 VALK,K16BID
      CHARACTER*19 K19BLA
      CHARACTER*8  K8BID
      LOGICAL      ISFONC,LERRT,LTHM,LFLAM,LPILO
      LOGICAL      NDYNLO,LDYNA,LVIBR,LEXGE
      CHARACTER*24 ERRTPS,TYPSEL,TYPPIL
      REAL*8       TABERR(2),THETA,VALR,CHCRIT,FREQR,COEF,CHSTAB
      INTEGER      IRET
      INTEGER      JINST,JERRT,JPARA,JCRR,JCRK,JACCES,JPLTK,JPLIR 
      INTEGER      JACCE2 
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
C
C --- INITIALISATIONS
C
      K19BLA = ' '
C
C --- FONCTIONNALITES ACTIVEES
C
      LDYNA  = NDYNLO(SDDYNA,'DYNAMIQUE' )
      LEXGE  = NDYNLO(SDDYNA,'EXPL_GENE' )
      LFLAM  = ISFONC(FONACT,'CRIT_FLAMB')
      LVIBR  = ISFONC(FONACT,'MODE_VIBR' )
      LERRT  = ISFONC(FONACT,'ERRE_TEMPS')
      LTHM   = ISFONC(FONACT,'THM'       ) 
      LPILO  = ISFONC(FONACT,'PILOTAGE'  )
C
C --- ARCHIVAGE DE THETA EN THM
C
      IF (LTHM) THEN
        CALL JEVEUO(CARCRI(1:19)//'.VALV','L',JPARA)
        THETA  = ZR(JPARA+3)
        CALL RSADPA(RESULT,'E',1,'PARM_THETA',
     &              NUMARC,0,JINST,K8BID)
        ZR(JINST) = THETA
      ENDIF
C
C --- ARCHIVAGE DE LA CHARGE CRITIQUE DU MODE DE FLAMBEMENT
C
      IF (LFLAM) THEN
        CALL NMARCP('FLAM',SDPOST,K19BLA,CHCRIT,IRET)
        IF (IRET.NE.0) THEN
          CALL RSADPA(RESULT,'E',1,'CHAR_CRIT',NUMARC,0,JACCES,K16BID)
          ZR(JACCES) = CHCRIT
        ENDIF
        CALL NMARCP('STAB',SDPOST,K19BLA,CHSTAB,IRET)
        IF (IRET.NE.0) THEN
          CALL RSADPA(RESULT,'E',1,'CRIT_STAB',1,0,JACCE2,K16BID)
          ZR(JACCE2) = CHSTAB
        ENDIF                
      ENDIF
C
C --- ARCHIVAGE DE LA FREQUENCE DU MODE VIBRATOIRE
C
      IF (LVIBR) THEN
        CALL ASSERT(LDYNA)
        CALL NMARCP('VIBR',SDPOST,K19BLA,FREQR ,IRET)
        IF (IRET.NE.0) THEN
          CALL RSADPA(RESULT,'E',1,'FREQ',NUMARC,0,JACCES,K16BID)
          ZR(JACCES) = FREQR
        ENDIF  
      ENDIF      
C
C --- ARCHIVAGE DE L'INSTANT
C
      CALL RSADPA(RESULT,'E',1,'INST',NUMARC,0,JINST,K8BID)
      ZR(JINST) = INSTAN
C
C --- ARCHIVAGE DU MODELE, MATERIAU, CARA_ELEM ET DE LA SD CHARGE
C
      CALL RSSEPA(RESULT,NUMARC,MODELE(1:8),MATE(1:8),CARELE(1:8),
     &            LISCH2)
C
C --- ARCHIVAGE DES CRITERES DE CONVERGENCE
C           
      CALL JEVEUO(SDCRIT//'.CRTR','L',JCRR)
      CALL JEVEUO(SDCRIT//'.CRDE','L',JCRK)
      VALR   = ZR(JCRR+1-1)
      VALK   = ZK16(JCRK+1-1)
      CALL RSADPA(RESULT,'E',1,VALK,NUMARC,0,JPARA,K8BID)
      ZI(JPARA) = NINT(VALR)
      VALR   = ZR(JCRR+5-1)
      VALK   = ZK16(JCRK+5-1)                  
      CALL RSADPA(RESULT,'E',1,VALK,NUMARC,0,JPARA,K8BID)
      ZR(JPARA) = VALR
      VALR   = ZR(JCRR+6-1)
      VALK   = ZK16(JCRK+6-1)
      CALL RSADPA(RESULT,'E',1,VALK,NUMARC,0,JPARA,K8BID)
      ZR(JPARA) = VALR   
C
C --- ARCHIVAGE DES INDICATEURS D'ERREUR EN TEMPS EN THM UNIQUEMENT
C
      IF (LERRT) THEN
        ERRTPS = SDERRO(1:19)//'.ERRT'      
        CALL JEVEUO(ERRTPS,'L',JERRT )
        TABERR(1) = ZR(JERRT-1+1)
        TABERR(2) = ZR(JERRT-1+2)
        CALL RSADPA(RESULT,'E',1,'ERRE_TPS_LOC' ,
     &              NUMARC,0,JINST,K8BID)
        ZR(JINST) = TABERR(1)
        CALL RSADPA(RESULT,'E',1,'ERRE_TPS_GLOB',
     &              NUMARC,0,JINST,K8BID)
        ZR(JINST) = TABERR(2)
      ENDIF
C
C --- ARCHIVAGE DE COEF_MULT SI PILOTAGE
C 
      IF (LPILO) THEN
        CALL JEVEUO(SDPILO(1:19)//'.PLTK','L',JPLTK)
        TYPPIL = ZK24(JPLTK)
        TYPSEL = ZK24(JPLTK+5)
        IF((TYPPIL.EQ.'LONG_ARC'.OR.TYPPIL.EQ.'SAUT_LONG_ARC')
     &     .AND.TYPSEL.EQ.'ANGL_INCR_DEPL') THEN
          CALL JEVEUO(SDPILO(1:19)//'.PLIR','L',JPLIR)
          COEF = ZR(JPLIR)
          CALL RSADPA(RESULT,'E',1,'COEF_MULT',NUMARC,0,JINST,K8BID)
          ZR(JINST) = COEF
        ENDIF
      ENDIF 
C
C --- ARCHIVAGE DEPL/VITE/ACCE GENERALISES
C
      IF (LEXGE) THEN
        CALL NDARAM(RESULT,SDDYNA,NUMARC)
      ENDIF               
C
      CALL JEDEMA()
      END
