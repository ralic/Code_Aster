      SUBROUTINE LCRONL (FAMI,KPG,KSP,NDIM,MATE,OPTION,FM,DF,VIM,
     &                   VIP,SIGP,DSIGDF)


C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 28/03/2007   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2003  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE ADBHHVV V.CANO

      IMPLICIT NONE
      INTEGER       NDIM, MATE, KPG, KSP
      CHARACTER*16  OPTION
      CHARACTER*(*)   FAMI
      REAL*8        DF(3,3),FM(3,3)
      REAL*8        VIM(12),VIP(12),SIGP(6),DSIGDF(6,3,3)

C.......................................................................
C       INTEGRATION DE LA LOI DE ROUSSELIER EN NON LOCAL
C   EN GRANDES DEFORMATIONS DE TYPE NOUVELLE FORMULATION DE SIMO-MIEHE
C.......................................................................
C IN  NDIM    : DIMENSION DE L'ESPACE
C IN  MATE    : MATERIAU CODE
C IN  OPTION  : OPTION DE CALCUL
C IN  FM      : GRADIENT DE LA TRANSFORMATION A L INSTANT PRECEDENT
C IN  DF      : INCREMENT DU GRADIENT DE LA TRANSFORMATION
C IN  VIM     : VARIABLES INTERNES A L INSTANT DU CALCUL PRECEDENT
C                 VIM(1)   = P (DEFORMATION PLASTIQUE CUMULEE)
C                 VIM(2:4) =  GRADIENT DE P
C                 VIM(5)   = POROSITE
C                 VIM(6:11)= DEFORMATION ELASTIQUE EULERIENNE
C                  EE = (ID-BE)/2
C IN VIP     : VARIABLES INTERNES A L'INSTANT ACTUEL
C
C OUT SIGP    : CONTRAINTE A L INSTANT ACTUELLE
C OUT DSIGDF  : DERIVEE DE SIGMA PAR RAPPORT A F
C
C          ATTENTION LES TENSEURS ET MATRICES SONT RANGES DANS
C          L'ORDRE :  XX,YY,ZZ,XY,XZ,YZ
C.......................................................................

      CHARACTER*2 K2
      CHARACTER*1 POUM

      LOGICAL RESI,RIGI
      INTEGER I1,I2,I3,IJ,K,L,PQ,NDIMSI
      REAL*8  EM(6),EP(6),YOUNG,NU,ALPHA
      REAL*8  DDOT
C ----------------------------------------------------------------------
C  COMMON SIMO - MIEHE

      INTEGER IND1(6),IND2(6),IRET
      REAL*8  KR(6),RAC2,RC(6)
      REAL*8  LAMBDA,MU,DEUXMU,UNK,TROISK,COTHER
      REAL*8  JM,DJ,JP,DJDF(3,3),TEMP
      REAL*8  BEM(6),ETR(6),DVETR(6),EQETR,TRETR,DETRDF(6,3,3)
      REAL*8  SIGMA(6),DSIGDE(6,6),DSIGDJ(6)
      COMMON /LCSMC/
     &          IND1,IND2,KR,RAC2,RC,
     &          LAMBDA,MU,DEUXMU,UNK,TROISK,COTHER,
     &          JM,DJ,JP,DJDF,
     &          BEM,ETR,DVETR,EQETR,TRETR,DETRDF,
     &          SIGMA,DSIGDE,DSIGDJ
C ----------------------------------------------------------------------


C 1 - INITIALISATION

C    DONNEES DE CONTROLE DE L'ALGORITHME
      RESI   = OPTION(1:4).EQ.'RAPH' .OR. OPTION(1:4).EQ.'FULL'
      RIGI   = OPTION(1:4).EQ.'RIGI' .OR. OPTION(1:4).EQ.'FULL'
      NDIMSI = 2*NDIM

C    TEMPERATURE
      IF (RESI) THEN
        POUM = '+'
      ELSE
        POUM = '-'
      END IF

C    PARAMETRES MATERIAU
      CALL RCVALB(FAMI,KPG,KSP,POUM,MATE,' ','ELAS',0,' ',0.D0,
     &            1,'NU',NU,K2,'F ')

      CALL RCVARC('F','TEMP',POUM,FAMI,KPG,KSP,TEMP,IRET)

      CALL RCTRAC(MATE,'TRACTION','SIGM',TEMP,
     &            I1,I2,I3,YOUNG)
      CALL RCVALB(FAMI,KPG,KSP,POUM,MATE,' ','ELAS',0,' ',0.D0,
     &            1,'ALPHA',ALPHA,K2,'  ')
      IF(K2.NE.'OK') ALPHA = 0

C    INITIALISATION DE L'OBJET COMMUN A SIMO-MIEHE
      CALL LCSMIN(FAMI,KPG,KSP,POUM,YOUNG,NU,ALPHA)

C    DEFORMATION ELASTIQUE
      CALL R8INIR(6,0.D0,EM,1)
      CALL R8INIR(6,0.D0,EP,1)
      CALL DCOPY(NDIMSI,VIM(6),1,EM,1)
      IF (RESI) THEN
        CALL DCOPY(NDIMSI,VIP(6),1,EP,1)
      ELSE
        CALL DCOPY(NDIMSI,VIM(6),1,EP,1)
      END IF
      CALL DSCAL(3,RAC2,EM(4),1)
      CALL DSCAL(3,RAC2,EP(4),1)



C 2 - CALCUL DES ELEMENTS CINEMATIQUES

      CALL LCSMCI(FM,DF,EM)



C 3 - CALCUL DES CONTRAINTES

      IF (RESI) THEN
        CALL LCSMCO(EP)
        CALL DCOPY(NDIMSI,SIGMA,1,SIGP,1)
        CALL DSCAL(NDIMSI-3,1.D0/RAC2,SIGP(4),1)
      END IF



C 4 - CALCUL DE LA MATRICE ELASTIQUE (DEDETR = ID)
C     DSIGDF = DSIGDJ*DJDF + DSIGDE * DETRDF

      IF(RIGI) THEN
        CALL LCSMTG(DF,EP)
        DO 1100 IJ = 1,6
          DO 1110 K = 1,3
            DO 1120 L = 1,3
              DSIGDF(IJ,K,L) = DSIGDJ(IJ)*DJDF(K,L)
     &                       + DDOT(6,DSIGDE(IJ,1),6,DETRDF(1,K,L),1)
 1120       CONTINUE
 1110     CONTINUE
 1100   CONTINUE

C      STOCKAGE SANS LE RACINE DE 2
        DO 200 IJ = 4,6
          CALL DSCAL(9,1/RAC2,DSIGDF(IJ,1,1),6)
 200    CONTINUE
      END IF

      END
