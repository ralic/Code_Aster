      SUBROUTINE DEFINT(MAILLA,NOMRES)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 05/07/2005   AUTEUR CIBHHPD L.SALMONA 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C***********************************************************************
C    P. RICHARD     DATE 20/02/90
C-----------------------------------------------------------------------
C  BUT:  RECUPERATION DE LA DEFINITION UTLISATEUR DES INTERFACES
      IMPLICIT REAL*8 (A-H,O-Z)
C       CREATION DE LA STRUCTURE DE DONNEES CORRESPONDANTE
C       CREATION DES LISTES GLOBALE DES NOEUDS D'INTERFACE LIBRES ET
C        BLOQUEES
C-----------------------------------------------------------------------
C
C MAILLA   /I/:NOM UT DU MAILLAGE EN AMONT
C NOMRES   /I/: NOM UT DU RESULTAT
C
C-------- DEBUT COMMUNS NORMALISES  JEVEUX  ----------------------------
C
      INTEGER          ZI
      COMMON  /IVARJE/ ZI(1)
      REAL*8           ZR
      COMMON  /RVARJE/ ZR(1)
      COMPLEX*16       ZC
      COMMON  /CVARJE/ ZC(1)
      LOGICAL          ZL
      COMMON  /LVARJE/ ZL(1)
      CHARACTER*8      ZK8
      CHARACTER*16              ZK16
      CHARACTER*24                        ZK24
      CHARACTER*32                                  ZK32
      CHARACTER*80                                            ZK80
      COMMON  /KVARJE/ ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
      CHARACTER*32 JEXNOM,JEXNUM
C
C----------  FIN  COMMUNS NORMALISES  JEVEUX  --------------------------
C
      PARAMETER   (NBECMX = 10)
      CHARACTER*8  NOMRES,MAILLA
      CHARACTER*8  NOMCOU,TYPE
      CHARACTER*8  K8BID
      CHARACTER*9  NOM,NO,GRNO
      CHARACTER*14 INT
      CHARACTER*24 NOMINT,NOTINT,TYPINT,NOEINT,DDLACT
      CHARACTER*24 TEMGUI,TEMLNO,TEMLGR,TEMMAS
      CHARACTER*80 KAR80
      INTEGER      ICODMA(NBECMX), ICODAC(NBECMX)
C
C-----------------------------------------------------------------------
C-----------------------------------------------------------------------
C
C----------------RECUPERATION DES DONNEES GRANDEUR----------------------
C
      CALL JEMARQ()
      CALL DISMOI('F','NB_CMP_MAX',NOMRES,'INTERF_DYNA',NBCMP,
     &            K8BID,IER)
      CALL DISMOI('F','NB_EC',NOMRES,'INTERF_DYNA',NBEC,
     &            K8BID,IER)
      CALL DISMOI('F','NUM_GD',NOMRES,'INTERF_DYNA',NUMGD,
     &             K8BID,IER)
C
C-------------INITIALISATION DES NOMS TRES UTILISES---------------------
C
      INT='INTERFACE'
      NOM='NOM'
      NO='NOEUD'
      GRNO='GROUP_NO'
C
C-------------- DETERMINATION DU NOMBRE D'INTERFACES -------------------
C
      CALL GETFAC(INT,IOC)
C
      IF(IOC.EQ.0) THEN
        CALL UTDEBM('A','DEFINT',
     &'PAS D'' INTERFACE DEFINIE --> UN GAGE!')
        CALL UTFINM
      ENDIF
C
      NBINT=0
C
      DO 10 I=1,IOC
        CALL GETVTX(INT,NOM,I,1,1,KAR80,NBVAL)
        IF (NBVAL.GT.0) NBINT=NBINT+1
 10   CONTINUE
C
C------------ALLOCATION REPERTOIRES DES NOMS D'INTERFACE ---------------
C
      NOMINT=NOMRES//'.IDC_NOMS'
      CALL JECREO(NOMINT,'G N K8')
      CALL JEECRA(NOMINT,'NOMMAX',NBINT,' ')
C
      NOTINT='&&DEFINT.NOM.INTF'
      CALL JECREO(NOTINT,'V N K8')
      CALL JEECRA(NOTINT,'NOMMAX',NBINT,' ')
C
C-------ALLOCATION VECTEURS DE TRAVAIL----------------------------------
C
C  NUMERO MOTCLE FACTEUR DEBUT ET FIN DESCRIPTION  INTERFACE
C
      TEMGUI='&&DEFINT.GUIDE.INTF'
      NBALL=2*NBINT
      CALL WKVECT(TEMGUI,'V V I',NBALL,LTGUI)
C
C------DETERMINATION DES OCCURENCES DEBUT ET FIN INTERFACE--------------
C          STOCKAGE NOMBRE DE GROUPES PAR INTERFACE
C   VERIFICATION EXCLUSIVITE DE NOEUD ET GROUPNO PAR OCCURENCE MOTFAC
C             ET STOCKAGE DES NOMS DES INTERFACES
C
      CALL GETVID(INT,NO,1,1,0,K8BID,NBVAN)
      CALL GETVID(INT,GRNO,1,1,0,K8BID,NBVAG)
C
      CALL GETVTX(INT,NOM,1,1,1,KAR80,NBVAL)
      NOMCOU=KAR80
      CALL LXCAPS(NOMCOU)
      CALL JECROC(JEXNOM(NOMINT,NOMCOU))
      CALL JECROC(JEXNOM(NOTINT,NOMCOU))
C
      ZI(LTGUI)=1
      NBGR=-NBVAG
      NBNO=-NBVAN
      MAXGR=0
      MAXNO=0
      NBTEMP=1
C
      IF(IOC.GT.1) THEN
C
        DO 20 I=2,IOC
          CALL GETVTX(INT,NOM,I,1,1,KAR80,NBVAL)
          NOMCOU=KAR80
          CALL LXCAPS(NOMCOU)
          CALL JEEXIN(JEXNOM(NOTINT,NOMCOU),IRET)
          IF(IRET.NE.0) THEN
            CALL UTDEBM('F','DEFINT',
     &'ARRET SUR INTERFACE DEJA DEFINIE')
            CALL UTIMPI('L',' MOT-CLE INTERFACE NUMERO  --> ',1,I)
            CALL UTIMPK('L',' INTERFACE  --> ',1,NOMCOU)
            CALL UTFINM
          ENDIF
          CALL GETVID(INT,NO,I,1,0,K8BID,NBVAN)
          CALL GETVID(INT,GRNO,I,1,0,K8BID,NBVAG)
C
          IF(NBVAL.GT.0) THEN
            MAXGR=MAX(MAXGR,NBGR)
            MAXNO=MAX(MAXNO,NBNO)
            NBGR=0
            NBNO=0
            ZI(LTGUI+NBINT+NBTEMP-1)=I-1
            CALL JECROC(JEXNOM(NOMINT,NOMCOU))
            CALL JECROC(JEXNOM(NOTINT,NOMCOU))
            NBTEMP=NBTEMP+1
            ZI(LTGUI+NBTEMP-1)=I
          ENDIF
C
C
C
          NBGR=NBGR-NBVAG
          NBNO=NBNO-NBVAN
 20     CONTINUE
C
      ENDIF
C
C TRAITEMENT DERNIERE INTERFACE
C
        MAXGR=MAX(MAXGR,NBGR)
        MAXNO=MAX(MAXNO,NBNO)
        ZI(LTGUI+NBINT+NBTEMP-1)=IOC
C
C--------ALLOCATION DE LA LISTE DES GROUPES DE L'INTERFACE COURANTE-----
C
      TEMLGR='&&DEFINT.LIST.GRP'
      IF(MAXGR.GT.0) THEN
        CALL WKVECT(TEMLGR,'V V K8',MAXGR,LTLGR)
      ENDIF
C
C--------ALLOCATION DE LA LISTE DES NOEUDS DE L'INTERFACE COURANTE------
C
      TEMLNO='&&DEFINT.LIST.NO'
      IF(MAXNO.GT.0) THEN
        CALL WKVECT(TEMLNO,'V V K8',MAXNO,LTLNO)
      ENDIF
C
C---------ALLOCATION DU  VECTEUR DES TYPES D'INTERFACES-----------------
C
      TYPINT=NOMRES//'.IDC_TYPE'
      CALL WKVECT(TYPINT,'G V K8',NBINT,LLTYP)
C
C---------ALLOCATION DU  VECTEUR DES DDL INTERFACE ACTIFS---------------
C
      DDLACT=NOMRES//'.IDC_DDAC'
      CALL JECREC(DDLACT,'G V I','NU','DISPERSE','VARIABLE',
     &             NBINT)
C
C---------ALLOCATION DU  VECTEUR DES MASQUES DDL INTERFACE--------------
C
      TEMMAS='&&DEFINT.MASQUE'
      CALL JECREC(TEMMAS,'V V I','NU','DISPERSE','VARIABLE',
     &             NBINT)
C
C---------ALLOCATION DES VECTEURS LISTE DES NOEUDS INTERFACE------------
C
      NOEINT=NOMRES//'.IDC_LINO'
      CALL JECREC(NOEINT,'G V I','NU','DISPERSE','VARIABLE',
     &             NBINT)
C
      DO 30 I=1,NBINT
        IDEB=ZI(LTGUI+I-1)
        IFIN=ZI(LTGUI+NBINT+I-1)
C
C-----STOCKAGE DU TYPE DE L'INTERFACE COURANTE--------------------------
C
        CALL GETVTX(INT,NOM,IDEB,1,1,KAR80,NBBID)
        NOMCOU=KAR80
        CALL LXCAPS(NOMCOU)
        CALL GETVTX(INT,'TYPE',IDEB,1,1,KAR80,NBVAL)
        TYPE=KAR80
        CALL LXCAPS(TYPE)
        ZK8(LLTYP+I-1)=TYPE
C
C---------SOUS-BOUCLE COMPTAGE NOMBRE DE NOEUDS DE CHAQUE INTERFACE-----
C
        NBNO=0
        DO 40 J=IDEB,IFIN
          CALL GETVID(INT,NO,J,1,0,K8BID,NBVAN)
          CALL GETVID(INT,GRNO,J,1,MAXGR,ZK8(LTLGR),NBVAG)
          CALL COMPNO(MAILLA,NBVAG,ZK8(LTLGR),NBUF)
          NBNO=NBNO-NBVAN+NBUF
 40     CONTINUE
C
C-------ALLOCATION DES VECTEURS NUMERO NOEUDS INTERFACES----------------
C                     MASQUE,DDL ACTIFS
C
        CALL JENONU(JEXNOM(NOMINT,NOMCOU),IBID)
        CALL JEECRA(JEXNUM(NOEINT,IBID),'LONMAX',NBNO,' ')
        CALL JEVEUO(JEXNUM(NOEINT,IBID),'E',LLNIN)
C
        CALL JENONU(JEXNOM(NOMINT,NOMCOU),IBID)
        CALL JEECRA(JEXNUM(DDLACT,IBID),'LONMAX',NBNO*NBEC,' ')
        CALL JEVEUO(JEXNUM(DDLACT,IBID),'E',LDACT)
C
        CALL JENONU(JEXNOM(NOMINT,NOMCOU),IBID)
        CALL JEECRA(JEXNUM(TEMMAS,IBID),'LONMAX',NBNO*NBEC,' ')
        CALL JEVEUO(JEXNUM(TEMMAS,IBID),'E',LTMAS)
C
C------SOUS-BOUCLE DETERMINATION NUMERO NOEUDS INTERFACE COURANTE-------
C        ET STOCKAGES DES MASQUES ET DDL ACTIFS POUR INTERFACES
C
        NBPRE=0
        DO 50 J=IDEB,IFIN
          CALL GETVID(INT,NO,J,1,MAXNO,ZK8(LTLNO),NBVAN)
          CALL GETVID(INT,GRNO,J,1,MAXGR,ZK8(LTLGR),NBVAG)
          CALL RECUNO(MAILLA,NBVAN,NBVAG,ZK8(LTLNO),ZK8(LTLGR),NBCOU,
     &    ZI(LLNIN+NBPRE))
C
C--------RECUPERATION DES MASQUES ET DDL ACTIFS INTERFACE---------------
C
          CALL DEFDDA(NBEC,NBCMP,NUMGD,J,'MASQUE',0,ICODMA)
          CALL DEFDDA(NBEC,NBCMP,NUMGD,IDEB,'DDL_ACTIF',1,ICODAC)
          DO 55 K=1,NBCOU
            DO 56 IEC = 1, NBEC
              ZI(LDACT+NBPRE+(K-1)*NBEC+IEC-1) = ICODAC(IEC)
              ZI(LTMAS+NBPRE+(K-1)*NBEC+IEC-1) = ICODMA(IEC)
 56         CONTINUE
 55       CONTINUE
          NBPRE=NBPRE+NBCOU*NBEC
 50     CONTINUE
 30   CONTINUE
C
C------SAUVEGARDE DES OBJECT EN GLOBALE SAUF CEUX A MODIFIER------------
C
C
C----------------RECUPERATION FREQUENCE POUR CB_HARMO-------------------
C
      CALL GETVR8('   ','FREQ',1,1,1,FREQ,IOC)
      CALL WKVECT(NOMRES//'.IDC_DY_FREQ','G V R',1,LDFREQ)
      ZR(LDFREQ)=FREQ
C
C-----------------DESTRUCTION DES OBJETS EN VOLATILE--------------------
C
      CALL JEDETR(TEMGUI)
      IF(MAXNO.GT.0) CALL JEDETR(TEMLNO)
      IF(MAXGR.GT.0) CALL JEDETR(TEMLGR)
C
 9999 CONTINUE
      CALL JEDEMA()
      END
