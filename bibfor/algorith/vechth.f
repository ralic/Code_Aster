      SUBROUTINE VECHTH(TYPCAL,MODELZ,CHARGZ,INFOCZ,CARELZ,MATEZ,INSTZ,
     &                  CHTNZ,VECELZ)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 16/01/2012   AUTEUR PELLET J.PELLET 
C TOLE CRP_20
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.

C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.

C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C TOLE CRP_20
C ----------------------------------------------------------------------
C CALCUL DES VECTEURS ELEMENTAIRES DES CHARGES THERMIQUES

C IN  TYPCAL  : TYPE DU CALCUL :
C               'THER', POUR LA RESOLUTION DE LA THERMIQUE,
C               'DLAG', POUR LE CALCUL DE LA DERIVEE LAGRANGIENNE DE T
C IN  MODELE : NOM DU MODELE
C IN  CHARGE : LISTE DES CHARGES
C IN  INFOCH : INFORMATIONS SUR LES CHARGEMENTS
C IN  CARELE : CHAMP DE CARA_ELEM
C IN  MATE   : CHAMP MATERIAU
C IN  INST   : CARTE CONTENANT LA VALEUR DE L'INSTANT
C . POUR LE CALCUL DE LA TEMPERATURE :
C IN  CHTN   : CHAMP DE TEMPERATURE A L'INSTANT PRECEDENT
C OUT VECELZ : VECT_ELEM
C   -------------------------------------------------------------------
C     SUBROUTINES APPELLEES:
C       MESSAGE:INFNIV.
C       JEVEUX:JEMARQ,JEDEMA,JEEXIN,JELIRA,JEVEUO,WKVECT,JEECRA,JEDETR.
C       MANIP SD: RSEXCH,DETRSD,MEGEOM,MECARA,EXISD,MECACT.
C       CALCUL: CALCUL.
C       FICH COMM: GETRES.
C       DIVERS: GCNCO2,CORICH.

C     FONCTIONS INTRINSEQUES:
C       AUCUNE.
C----------------------------------------------------------------------
C CORPS DU PROGRAMME
      IMPLICIT NONE

C 0.1. ==> ARGUMENTS

      CHARACTER*4 TYPCAL
      CHARACTER*(*) MODELZ,CHARGZ,INFOCZ,CARELZ,INSTZ,CHTNZ,VECELZ,
     &              MATEZ

C 0.2. ==> COMMUNS
C --- DEBUT DECLARATIONS NORMALISEES JEVEUX ----------------------------

      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C --- FIN DECLARATIONS NORMALISEES JEVEUX ------------------------------

C 0.3. ==> VARIABLES LOCALES

      CHARACTER*6 NOMPRO
      PARAMETER (NOMPRO='VECHTH')
      INTEGER NCHINX
      PARAMETER (NCHINX=9)
      INTEGER IRET,NCHAR,ICHA,ILVE,JCHAR,IBID,IFM,NIV,JINF,
     &        JLVE,K,LONLIS,NUMCHM,NCHIN,IRETH,I
      CHARACTER*8 NOMCHA,LPAIN(NCHINX),PAOUT,K8BID,NEWNOM
      CHARACTER*16 OPTION,NOMCMD
      CHARACTER*24 LIGREL(3),LCHIN(NCHINX),RESUEL,CHGEOM,CHCARA(18),
     &             MODELE,CHARGE,INFOCH,CARELE,INST,CHTN,
     &             MATE,VECELE
      LOGICAL BIDON,EXICAR,EXIGEO
C ----------------------------------------------------------------------
      INTEGER NBCHMX
      PARAMETER (NBCHMX=6)
      INTEGER NBOPT(NBCHMX),NLIGR(NBCHMX)
      CHARACTER*6 NOMPAR(NBCHMX),NOMCHP(NBCHMX),NOMOPT(NBCHMX)
      DATA NOMCHP/'.T_EXT','.FLURE','.FLUR2','.SOURE','.HECHP','.GRAIN'/
      DATA NOMOPT/'TEXT_','FLUN_','FLUX_','SOUR_','PARO_','GRAI_'/
      DATA NOMPAR/'PT_EXT','PFLUXN','PFLUXV','PSOURC','PHECHP','PGRAIN'/
      DATA NBOPT/5,3,3,3,5,6/
      DATA NLIGR/1,1,1,1,2,1/

C DEB ------------------------------------------------------------------
C====
C 1.1 PREALABLES LIES AUX OPTIONS
C====
      CALL JEMARQ()
      CALL ASSERT (TYPCAL.EQ.'THER')
      CALL INFNIV(IFM,NIV)
      NEWNOM = '.0000000'
      MODELE = MODELZ
      CHARGE = CHARGZ
      INFOCH = INFOCZ
      CARELE = CARELZ
      MATE = MATEZ
      INST = INSTZ
      CHTN = CHTNZ
      DO 10 I = 1,NCHINX
        LPAIN(I) = '        '
        LCHIN(I) = '                        '
   10 CONTINUE

C====
C 1.2 PREALABLES LIES AUX CHARGES
C====
      LIGREL(1) = MODELE(1:8)//'.MODELE'
C FLAG POUR SIGNIFIER LA PRESENCE DE CHARGES (NCHAR NON NUL)
      BIDON = .TRUE.
C TEST D'EXISTENCE DE L'OBJET JEVEUX CHARGE
      CALL JEEXIN(CHARGE,IRET)
      IF (IRET.NE.0) THEN
C LECTURE DU NBRE DE CHARGE NCHAR DANS L'OBJET JEVEUX CHARGE
        CALL JELIRA(CHARGE,'LONMAX',NCHAR,K8BID)
        IF (NCHAR.NE.0) THEN
          BIDON = .FALSE.
C LECTURE DES ADRESSES JEVEUX DES CHARGES ET DES INFOS AFFERENTES
          CALL JEVEUO(CHARGE,'L',JCHAR)
          CALL JEVEUO(INFOCH,'L',JINF)
        END IF
      END IF

C====
C 2. INIT. DES VECTEURS SECONDS MEMBRE ELEMENTAIRES
C====

      VECELE = '&&VETCHA'
      RESUEL = '&&'//NOMPRO//'.???????'

C DESTRUCTION DE LA SD VECELE DE TYPE 'VECT_ELEM'
      CALL DETRSD('VECT_ELEM',VECELE)
      VECELZ = VECELE(1:19)//'.RELR'
C CREATION ET INITIALISATION DU VECT_ELEM ASSOCIE AU MODELE MODELE(1:8),
C AU MATERIAU MATE, AU CARA_ELEM CARELE ET A LA SUR_OPTION 'CHAR_THER'
      CALL MEMARE('V',VECELE(1:19),MODELE(1:8),MATE,CARELE,'CHAR_THER')
C CREATION DU VECTEUR VECELZ DE TYPE K24, DE LONGUEUR EFFECTIVE LONLIS
C ET DE LONGUEUR UTILE NULLE
      IF (BIDON) THEN
        CALL WKVECT(VECELZ,'V V K24',1,JLVE)
        CALL JEECRA(VECELZ,'LONUTI',0,K8BID)
C PAS DE CHARGE, ON S'EN VA
        GO TO 80
      ELSE
C NCHAR CHARGES, ON PREPARE LES SDS DE STOCKAGE IDOINES
        LONLIS = NBCHMX*NCHAR
        CALL WKVECT(VECELZ,'V V K24',LONLIS,JLVE)
        CALL JEECRA(VECELZ,'LONUTI',0,K8BID)
      END IF

C RECHERCHE DU CHAMP DE GEOMETRIE CHGEOM ASSOCIE AU MODELE(1:8) ET A LA
C LISTE DE CHARGES ZK24(JCHAR)
      CALL MEGEOM(MODELE(1:8),ZK24(JCHAR) (1:8),EXIGEO,CHGEOM)
C RECHERCHE DES NOMS DES CARAELEM CHCARA DANS LA CARTE CARELE(1:8)
      CALL MECARA(CARELE(1:8),EXICAR,CHCARA)

C====
C 3. PREPARATION DES CALCULS ELEMENTAIRES
C====

C CHAMP LOCAL CONTENANT LA CARTE DES NOEUDS (X Y Z)
      LPAIN(2) = 'PGEOMER'
      LCHIN(2) = CHGEOM
C ... LA CARTE DES INSTANTS (INST DELTAT THETA KHI  R RHO)
      LPAIN(3) = 'PTEMPSR'
      LCHIN(3) = INST
C ... LE CHAM_NO DE TEMPERATURE A L'INSTANT PRECEDENT (TEMP)
      LPAIN(4) = 'PTEMPER'
      LCHIN(4) = CHTN
C ... LE COEFFICIENT D'ECHANGE
      LPAIN(5) = 'PCOEFH'
C ... LA CARTE MATERIAU (I1)
      LPAIN(6) = 'PMATERC'
      LCHIN(6) = MATE
C ... LE CHAMP DE DEPLACEMENT
      LPAIN(7) = 'PDEPLAR'
      LCHIN(7) = '&&DEPPLU'

C ... LE CHAMP RESULTAT
      PAOUT = 'PVECTTR'

C====
C 3.1 IMPRESSIONS NIVEAU 2 POUR DIAGNOSTICS...
C====

      IF (NIV.EQ.2) THEN
        WRITE (IFM,*) '*******************************************'
        WRITE (IFM,*) ' CALCUL DE SECOND MEMBRE THERMIQUE: ',NOMPRO
        WRITE (IFM,*)
        WRITE (IFM,*) ' TYPE DE CALCUL   :',TYPCAL
        WRITE (IFM,*) ' LIGREL/MODELE    :',LIGREL(1)
        WRITE (IFM,*) ' OBJ JEVEUX CHARGE:',CHARGE
        WRITE (IFM,*) '            INFOCH:',INFOCH
        WRITE (IFM,*) ' NBRE DE CHARGES  :',NCHAR
        WRITE (IFM,*) ' BOUCLE SUR LES CHARGES DE TYPE NEUMANN LIN'
      END IF

C====
C 4. BOUCLE SUR LES AFFE_CHAR_THER ==================================
C====
      ILVE = 0
      DO 70 ICHA = 1,NCHAR

C====
C 4.1 PREALABLES LIES AUX TYPES DE CL DE CET AFFE_CHAR_THER
C====
        NUMCHM = ZI(JINF+NCHAR+ICHA)

        IF (NUMCHM.GT.0) THEN

C NOM DE LA CHARGE
          NOMCHA = ZK24(JCHAR+ICHA-1) (1:8)
          LIGREL(2) = NOMCHA//'.CHTH.LIGRE'
          IF (NIV.EQ.2) THEN
            WRITE (IFM,*) ' '
            WRITE (IFM,*) '   CHARGE         :',NOMCHA
          END IF

C==== ASSEMBLAGE DU SECOND MEMBRE                                ====
C==== ELEMENTAIRE LIE AUX CHARGEMENTS                            ====

C====
C 4.2 SI ON DOIT ASSEMBLER, CONSTRUCTION AU CAS PAR CAS DE L'OPTION
C     DE CALCUL. BOUCLE SUR LES NBCHMX TYPES DE CL POSSIBLES =======
C====

          IF (NIV.EQ.2) WRITE (IFM,*)
     &        '     BOUCLE SUR LES TYPES DE CHARGES'
          DO 50 K = 1,NBCHMX

C ACCES A LA DESCRIPTION '.DESC' DES CHTH.NOMCHP(K) ET CHTH.COEFH DE LA
C CHARGE NOMCHA POUR DEFINIR LES CHAMPS LOCAUX DE LA CARTE DE CHARGEMENT
            LCHIN(1) = NOMCHA(1:8)//'.CHTH'//NOMCHP(K)
            LCHIN(5) = NOMCHA(1:8)//'.CHTH'//'.COEFH  '

C TEST EXISTENCE DE L'OBJET JEVEUX POUR SAVOIR SI LE CALCUL ELEMENTAIRE
C DE TYPE TYPCAL ASSOCIE A CETTE CL NOMCHP(K) POUR CE ICHA IEME AFFE_
C CHAR_THER EST NECESSAIRE
            IRET = 0
            CALL EXISD('CHAMP_GD',LCHIN(1),IRET)

C ASSEMBLAGE CAR LA CHARGE ICHA CORRESPOND A NOMCHP(K)
            IF (IRET.NE.0) THEN

C====
C 4.3 TEST ET TRAITEMENT SUPPLEMENTAIRE POUR LA CONDITION ECHANGE
C====
              IF (K.EQ.1) THEN
                IRETH = 0
                CALL EXISD('CHAMP_GD',LCHIN(5),IRETH)
                IF (((IRETH.EQ.0).AND. (IRET.NE.0)) .OR.
     &              ((IRET.EQ.0).AND. (IRETH.NE.0)))
     &          CALL ASSERT(.FALSE.)
              END IF

C====
C 4.4 PREPARATION FINALE DES OPTIONS DE CALCUL
C====
              IF (NUMCHM.EQ.1) THEN
C CHAMP LOCAL CONSTANT ASSOCIE A LA CARTE NOMPAR(K)//'R'

                LPAIN(1) = NOMPAR(K)//'R'
                LPAIN(5) = LPAIN(5) (1:6)//'R'
                OPTION = 'CHAR_'//TYPCAL//'_'//NOMOPT(K) (1:5)//'R'
              ELSE
C CHAMP LOCAL VARIABLE (EN ESPACE 2) OU (EN TEMPS ET/OU EN ESPACE 3)

                LPAIN(1) = NOMPAR(K)//'F'
                LPAIN(5) = LPAIN(5) (1:6)//'F'
                OPTION = 'CHAR_'//TYPCAL//'_'//NOMOPT(K) (1:5)//'F'
              END IF

C ON FIXE LE NBRE DE PARAMETRES DE L'OPTION EN PRENANT LE MAX DE
C L'OPTION REELLE ET CONSTANTE
                NCHIN = NBOPT(K)

C====
C 4.5 CAS PARTICULIERS THER_NON_LINE_MO
C====
C   DANS LE CAS TRES PARTICULIER DE LA COMMANDE THER_NON_LINE_MO
C   AVEC LA CHARGE : ECHANGE_PAROI, ON ENVOIE UNE CARTE DE TEMPS
C   OU THETA A ETE REMPLACE PAR 1-THETA
              CALL GETRES(K8BID,K8BID,NOMCMD)
              IF ((NOMCMD.EQ.'THER_NON_LINE_MO') .AND.
     &            (OPTION(11:14).EQ.'PARO')) THEN
                LPAIN(3) = 'PTEMPSR'
                LCHIN(3) = '&&OP0171.TIMEMO'
              END IF

C====
C 4.6 PRETRAITEMENTS POUR TENIR COMPTE DE FONC_MULT
C====
              CALL GCNCO2(NEWNOM)
              RESUEL(10:16) = NEWNOM(2:8)
              CALL CORICH('E',RESUEL,ICHA,IBID)

C====
C 4.7 LANCEMENT DES CALCULS ELEMENTAIRES OPTION
C====
              IF (NIV.EQ.2) THEN
                WRITE (IFM,*) '     K              :',K
                WRITE (IFM,*) '     OPTION         :',OPTION
                DO 30 I = 1,NCHIN
                  WRITE (IFM,*) '     LPAIN/LCHIN    :',LPAIN(I),' ',
     &              LCHIN(I)
   30           CONTINUE
              END IF
              CALL CALCUL('S',OPTION,LIGREL(NLIGR(K)),NCHIN,LCHIN,
     &                    LPAIN,1,RESUEL,PAOUT,'V','OUI')

C INCREMENTATION DE LONUTI ET STOCKAGE DU RESULTAT
                ILVE = ILVE + 1
                ZK24(JLVE-1+ILVE) = RESUEL

            END IF
C FIN IF IRET
   50     CONTINUE

C====
C 4.9 FIN BOUCLE SUR LES NBCHMX CL NEUMANN POSSIBLES =======
C====

C FIN TEST SUR NUMCHM
        END IF

   70 CONTINUE

C====
C 6. FIN BOUCLE SUR LES CHARGES ========================================
C====
C MODIFICATION DU NBRE DE TERMES UTILES DU VECTEUR ELEM RESULTAT
      CALL JEECRA(VECELZ,'LONUTI',ILVE,K8BID)

C SORTIE DE SECOURS EN CAS D'ABSENCE DE CHARGE
   80 CONTINUE

      CALL JEDEMA()
      END
