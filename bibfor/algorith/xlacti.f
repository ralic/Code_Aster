      SUBROUTINE XLACTI(NDIM,NFACE,NNOP,NNOM,CFACE,JAINT,AR,LACT)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 16/11/2009   AUTEUR DURAND C.DURAND 
C ======================================================================
C COPYRIGHT (C) 1991 - 2009  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C
      IMPLICIT NONE
      INTEGER      LACMAX
      PARAMETER    (LACMAX=27)
      INTEGER      NDIM,NFACE,NNOP,NNOM,CFACE(5,3)
      INTEGER      LACT(LACMAX),AR(12,2),JAINT
C      
C ----------------------------------------------------------------------
C
C ROUTINE XFEM
C
C EQUIVALENT DE XOULA DANS LE CAS DES LAGRANGES AUX NOEUDS
C POUR CHAQUE NOEUD SOMMET, 
C        LACT(INOP)=0:     LES DDL DE CONTACT DE CE SOMMET 
C                          NE SONT PAS ACTIFS
C        LACT(INOP)=NLI>0: LE POINT D'INTERSECTION POUR CE SOMMET
C                          (POUR ACCES BASE COVARIANTE)
C
C POUR OPTIMISER, ON PEUT SORTIR XLACTI DES TE'S, IL FAUT ALORS 
C PASSER UN CHAMP SUPPLÉMENTAIRE AUX TE'S
C
C ----------------------------------------------------------------------
C
C IN  NDIM   : DIMENSION DE L'ESPACE
C IN  NFACE  : NOMBRE DE FACETTES DE CONTACT
C IN  NNOP   : NOMBRE DE SOMMET DE L'ELEMENT PARENT
C IN  NNOM   : NOMBRE DE NOEUDS MILIEU DE L'ELEMENT PARENT
C IN  CFACE  : CONNECTIVITÉ DES NOEUDS DES FACETTES
C IN  JAINT  : ADRESSE DES INFORMATIONS CONCERNANT LES ARETES COUPÉES
C IN  AR     : MATRICE DE CONNECTIVITÉ DES ARETES
C OUT LACT   : LISTE PAR FACETTE DES LAMBDAS CONCERNÉS
C
C -------------- DEBUT DECLARATIONS NORMALISEES JEVEUX -----------------
C
      CHARACTER*32 JEXNUM,JEXATR
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C ---------------- FIN DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER     I,IFA,NLI,INO,INO1,INO2,IAR,VIT,ZXAIN,XXMMVD
      LOGICAL     ARCOUP
C.......................................................................
      CALL JEMARQ()
C
C --- INITIALISATIONS
C 
      ARCOUP=.FALSE.
      DO 10 I=1,LACMAX
        LACT(I)=0
 10   CONTINUE
      ZXAIN=XXMMVD('ZXAIN')
C
C --- SOMMETS DES ARETES VITALES
      ARCOUP=.FALSE.
      DO 20 IFA=1,NFACE
        DO 30 I=1,NDIM
          NLI=CFACE(IFA,I)
          IAR=ZR(JAINT-1+ZXAIN*(NLI-1)+1)
          INO=ZR(JAINT-1+ZXAIN*(NLI-1)+2)
          VIT=ZR(JAINT-1+ZXAIN*(NLI-1)+5)
          IF (INO.NE.0) THEN
            LACT(INO)=NLI
          ELSE IF (IAR.NE.0) THEN
            IF (NNOM.EQ.0) THEN
              ARCOUP=.TRUE.
            ENDIF
            IF (VIT.EQ.1) THEN
              INO1=AR(IAR,1)
              INO2=AR(IAR,2)
              LACT(INO1)=NLI
              LACT(INO2)=NLI
            ENDIF
          ENDIF
 30     CONTINUE
 20   CONTINUE
C
C --- SI UNE ARETE AU MOINS EST COUPEE, LES DDL CONTACT DE TOUS LES
C     SOMMETS SONT ACTIFS.
      IF (ARCOUP) THEN
C
C --- SOMMETS DES ARETES NON VITALES
        DO 40 IFA=1,NFACE
          DO 50 I=1,NDIM
            NLI=CFACE(IFA,I)
            IAR=ZR(JAINT-1+ZXAIN*(NLI-1)+1)
            INO=ZR(JAINT-1+ZXAIN*(NLI-1)+2)
            VIT=ZR(JAINT-1+ZXAIN*(NLI-1)+5)
            IF (IAR.NE.0) THEN
              INO1=AR(IAR,1)
              INO2=AR(IAR,2)
              IF (LACT(INO1).EQ.0) LACT(INO1)=NLI
              IF (LACT(INO2).EQ.0) LACT(INO2)=NLI
            ENDIF
 50       CONTINUE
 40     CONTINUE
C
C --- LES NOEUDS NON ENCORE MARQUES DOIVENT L'ETRE. POUR L'INSTANT,
C     ON NE SAIT PAS QUELLE BASE COVARIANTE PRENDRE, ON PREND LA
C     PREMIERE...
        DO 60 I=1,NNOP
          IF (LACT(I).EQ.0) LACT(I)=1
 60     CONTINUE
      ENDIF

      CALL JEDEMA()
      END
