      SUBROUTINE MCOPCO(NOMA,DEFICO,NEWGEO,
     &                  TYPMAI,POSMA,XI,YI,
     &                  GEOM)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 18/09/2006   AUTEUR MABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
      IMPLICIT NONE
      CHARACTER*8  NOMA
      CHARACTER*24 DEFICO
      CHARACTER*24 NEWGEO  
      CHARACTER*4  TYPMAI 
      INTEGER      POSMA        
      REAL*8       XI
      REAL*8       YI
      REAL*8       GEOM(3) 
C
C ----------------------------------------------------------------------
C ROUTINE APPELEE PAR : MMMBCA
C ----------------------------------------------------------------------
C
C CALCUL DES COORDONNEES D'UN NOEUD SUR UNE MAILLE A PARTIR 
C DES COORDONNEES PARAMETRIQUES POUR LE CONTACT METHODE CONTINUE
C
C IN  NOMA   : NOM DU MAILLAGE
C IN  NEWGEO : NOUVELLE GEOMETRIE (AVEC DEPLACEMENT GEOMETRIQUE)
C IN  DEFICO : SD POUR LA DEFINITION DE CONTACT
C IN  TYPMAI : TYPE DE MAILLE
C               'MAIT': MAILLE MAITRE
C               'ESCL': MAILLE ESCLAVE
C IN  POSMA  : INDICE DE LA MAILLE DANS CONTAMA
C IN  XI     : COORDONNEE PARAMETRIQUE KSI DU PROJETE
C IN  YI     : COORDONNEE PARAMETRIQUE ETA DU PROJETE
C OUT GEOM   : COORDONNEES DU PROJETE (EN 2D Z=0)
C
C -------------- DEBUT DECLARATIONS NORMALISEES JEVEUX -----------------
C           
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
      CHARACTER*32 JEXNUM
C
C ---------------- FIN DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER      JCOOR,JDES,JDEC
      INTEGER      NUMA,NNO,INO,NO(9),I,J,IBID,POSNNO(9)
      REAL*8       FF(9),COOR(27)
      CHARACTER*8  ALIAS
      CHARACTER*24 CONTMA,PNOMA,CONTNO,NOMACO
      INTEGER      JMACO,JPONO,JNOCO,JNOMA   
C            
C-----------------------------------------------------------------------
C
      CALL JEMARQ()
C 
      CONTMA = DEFICO(1:16)//'.MAILCO'
      PNOMA  = DEFICO(1:16)//'.PNOMACO'
      CONTNO = DEFICO(1:16)//'.NOEUCO'
      NOMACO = DEFICO(1:16)//'.NOMACO'      
      CALL JEVEUO(CONTMA,'L',JMACO)
      CALL JEVEUO(PNOMA ,'L',JPONO)
      CALL JEVEUO(CONTNO,'L',JNOCO)
      CALL JEVEUO(NOMACO,'L',JNOMA)      
      CALL JEVEUO(NEWGEO(1:19)//'.VALE','L',JCOOR)
      CALL JEVEUO(JEXNUM(NOMA//'.CONNEX',POSMA),'L',JDES)      
C
C --- INITIALISATIONS
C
      DO 10 I=1,3
        GEOM(I) = 0.D0
   10 CONTINUE 
C
C --- INFOS SUR LA MAILLE 
C 
      IF (TYPMAI.EQ.'MAIT') THEN     
        CALL MMELTY(NOMA,POSMA,ALIAS,NNO,IBID) 
      ELSEIF (TYPMAI.EQ.'ESCL') THEN
        NUMA = ZI(JMACO+POSMA-1)      
        CALL MMELTY(NOMA,NUMA,ALIAS,NNO,IBID)    
      ELSE
        CALL UTMESS('F','MCOPCO','TYPE DE MAILLE INCONNU (DVLP)') 
      ENDIF          
C
C --- POSITION DES NOEUDS DE LA MAILLE 
C      
      IF (TYPMAI.EQ.'MAIT') THEN
        DO 23 INO = 1,NNO
          NO(INO) = ZI(JDES+INO-1)
   23   CONTINUE
      ELSEIF (TYPMAI.EQ.'ESCL') THEN
        JDEC = ZI(JPONO+POSMA-1)
        DO 24 INO = 1,NNO
          POSNNO(INO+1) = ZI(JNOMA+JDEC+INO-1)
          NO(INO)       = ZI(JNOCO+POSNNO(INO+1)-1)
   24   CONTINUE      
      ENDIF           
C
C --- COORDONNEES DES NOEUDS DE LA MAILLE 
C  
      DO 20 INO = 1,NNO
        COOR(3*(INO-1)+1) = ZR(JCOOR+3* (NO(INO)-1))
        COOR(3*(INO-1)+2) = ZR(JCOOR+3* (NO(INO)-1)+1)
        COOR(3*(INO-1)+3) = ZR(JCOOR+3* (NO(INO)-1)+2)
   20 CONTINUE
C
C --- COORDONNEES DU PROJETE
C
      CALL CALFFX(ALIAS,XI,YI,FF)
      DO 40 I = 1,3
        DO 30 J = 1,NNO
          GEOM(I) = FF(J)*COOR((J-1)*3+I) + GEOM(I)
   30   CONTINUE
   40 CONTINUE
C   
      CALL JEDEMA()
      END
