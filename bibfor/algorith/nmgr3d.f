       SUBROUTINE  NMGR3D(NNO,NPG,IPOIDS,IVF,IDFDE,GEOMI,
     &                    TYPMOD,OPTION,IMATE,COMPOR,LGPG,CRIT,
     &                    INSTAM,INSTAP,
     &                    DEPLM,DEPLP,
     &                    ANGMAS,
     &                    SIGM,VIM,MATSYM,
     &                    DFDI,PFF,DEF,SIGP,VIP,MATUU,VECTU,CODRET)

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 04/09/2012   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE PELLET J.PELLET
C TOLE CRP_21

       IMPLICIT NONE
C
       INTEGER       NNO, NPG, IMATE, LGPG, CODRET,IPOIDS,IVF,IDFDE
C
       CHARACTER*8   TYPMOD(*)
       CHARACTER*16  OPTION, COMPOR(4)
C
       REAL*8        INSTAM,INSTAP
       REAL*8        GEOMI(3,NNO), CRIT(3), DETFM
       REAL*8        DEPLM(1:3,1:NNO),DEPLP(1:3,1:NNO),DFDI(NNO,3)
       REAL*8        SIGM(6,NPG),SIGP(6,NPG)
       REAL*8        VIM(LGPG,NPG),VIP(LGPG,NPG)
       REAL*8        MATUU(*),VECTU(3,NNO),MAXEPS
       REAL*8        PFF(6,NNO,NNO),DEF(6,NNO,3)
       LOGICAL       MATSYM

C
C.......................................................................
C
C     BUT:  CALCUL  DES OPTIONS RIGI_MECA_TANG, RAPH_MECA ET FULL_MECA
C           EN GRANDES ROTATIONS 3D
C.......................................................................
C IN  NNO     : NOMBRE DE NOEUDS DE L'ELEMENT
C IN  NPG     : NOMBRE DE POINTS DE GAUSS
C IN  POIDSG  : POIDS DES POINTS DE GAUSS
C IN  VFF     : VALEUR  DES FONCTIONS DE FORME
C IN  DFDE    : DERIVEE DES FONCTIONS DE FORME ELEMENT DE REFERENCE
C IN  DFDK    : DERIVEE DES FONCTIONS DE FORME ELEMENT DE REFERENCE
C IN  GEOMI   : COORDONEES DES NOEUDS SUR CONFIG INITIALE
C IN  TYPMOD  : TYPE DE MODEELISATION
C IN  OPTION  : OPTION DE CALCUL
C IN  IMATE   : MATERIAU CODE
C IN  COMPOR  : COMPORTEMENT
C IN  LGPG    : "LONGUEUR" DES VARIABLES INTERNES POUR 1 POINT DE GAUSS
C               CETTE LONGUEUR EST UN MAJORANT DU NBRE REEL DE VAR. INT.
C IN  CRIT    : CRITERES DE CONVERGENCE LOCAUX
C IN  INSTAM  : INSTANT PRECEDENT
C IN  INSTAP  : INSTANT DE CALCUL
C IN  DEPLM   : DEPLACEMENT A L'INSTANT PRECEDENT
C IN  DEPLP   : DEPLACEMENT A L'INSTANT COURANT
C IN  ANGMAS  : LES TROIS ANGLES DU MOT_CLEF MASSIF (AFFE_CARA_ELEM)
C IN  SIGM    : CONTRAINTES A L'INSTANT PRECEDENT
C IN  VIM     : VARIABLES INTERNES A L'INSTANT PRECEDENT
C IN  MATSYM  : VRAI SI LA MATRICE DE RIGIDITE EST SYMETRIQUE
C OUT DFDI    : DERIVEE DES FONCTIONS DE FORME  AU DERNIER PT DE GAUSS
C OUT DEF     : PRODUIT DER. FCT. FORME PAR F   AU DERNIER PT DE GAUSS
C OUT SIGP    : CONTRAINTES DE CAUCHY (RAPH_MECA ET FULL_MECA)
C OUT VIP     : VARIABLES INTERNES    (RAPH_MECA ET FULL_MECA)
C OUT MATUU   : MATRICE DE RIGIDITE PROFIL (RIGI_MECA_TANG ET FULL_MECA)
C OUT VECTU   : FORCES NODALES (RAPH_MECA ET FULL_MECA)
C.......................................................................
C
      LOGICAL GRAND,AXI

      INTEGER KPG,J,COD(27)

      REAL*8 DSIDEP(6,6),F(3,3),FM(3,3),EPSM(6),EPSP(6),DEPS(6)
      REAL*8 R,SIGMA(6),SIGN(6),DETF,POIDS,VFF(1)
      REAL*8 ELGEOM(10,27),ANGMAS(3),R8NNEM,RAC2

C     INITIALISATION

      RAC2   = SQRT(2.D0)
      GRAND  = .TRUE.
      AXI=.FALSE.

C     CALCUL DES ELEMENTS GEOMETRIQUES SPECIFIQUES AU COMPORTEMENT

      CALL LCEGEO(NNO   ,NPG   ,IPOIDS,IVF   ,IDFDE ,
     &            GEOMI ,TYPMOD,COMPOR,3     ,DFDI  ,
     &            DEPLM ,DEPLP ,ELGEOM)

C     INITIALISATION CODES RETOURS

      DO 1955 KPG=1,NPG
        COD(KPG)=0
1955  CONTINUE

C     ANGLE DU MOT_CLEF MASSIF (AFFE_CARA_ELEM)
C     INITIALISE A R8NNEM (ON NE S'EN SERT PAS)
      CALL R8INIR(3,  R8NNEM(), ANGMAS ,1)


C     CALCUL POUR CHAQUE POINT DE GAUSS

      DO 800 KPG=1,NPG

C        CALCUL DES ELEMENTS GEOMETRIQUES

C        CALCUL DE EPSM EN T- POUR LDC

         CALL R8INIR(6, 0.D0, EPSM ,1)
         CALL R8INIR(6, 0.D0, EPSP ,1)
         CALL NMGEOM(3,NNO,.FALSE.,GRAND,GEOMI,KPG,IPOIDS,
     &            IVF,IDFDE,DEPLM,.TRUE.,POIDS,DFDI,
     &                   FM,EPSM,R)

C        CALCUL DE F, EPSP, DFDI, R ET POIDS EN T+

         CALL NMGEOM(3,NNO,.FALSE.,GRAND,GEOMI,KPG,IPOIDS,
     &            IVF,IDFDE,DEPLP,.TRUE.,POIDS,DFDI,
     &                   F,EPSP,R)


C        CALCUL DE DEPS POUR LDC

         MAXEPS=0.D0
         DO 25 J = 1,6
            DEPS (J)=EPSP(J)-EPSM(J)
            MAXEPS=MAX(MAXEPS,ABS(EPSP(J)))
25       CONTINUE

C        VERIFICATION QUE EPS RESTE PETIT

         IF (MAXEPS.GT.0.05D0) THEN
            IF( COMPOR(1)(1:4).NE.'ELAS') THEN
               CALL U2MESR('A','COMPOR2_9',1,MAXEPS)
            ENDIF
         ENDIF

C        CONTRAINTE CAUCHY -> CONTRAINTE LAGRANGE POUR LDC EN T-

         CALL NMDETF(3,FM,DETFM)
         CALL PK2SIG(3,FM,DETFM,SIGN,SIGM(1,KPG),-1)
         SIGN(4) = SIGN(4)*RAC2
         SIGN(5) = SIGN(5)*RAC2
         SIGN(6) = SIGN(6)*RAC2

C        INTEGRATION : APPEL A LA LOI DE COMPORTEMENT

         CALL NMCOMP('RIGI',KPG,1,3,TYPMOD,IMATE,COMPOR,CRIT,
     &            INSTAM,INSTAP,
     &            6,EPSM,DEPS,
     &            6,SIGN,VIM(1,KPG),
     &            OPTION,
     &            ANGMAS,
     &            10,ELGEOM(1,KPG),
     &            SIGMA,VIP(1,KPG),36,DSIDEP,1,VFF,COD(KPG))

         IF(COD(KPG).EQ.1) THEN
            GOTO 1956
         ENDIF

C        CALCUL DE LA MATRICE DE RIGIDITE ET DE LA FORCE INTERIEURE

         CALL NMGRTG(3,NNO,POIDS,KPG,VFF,DFDI,DEF,PFF,OPTION,AXI,R,
     &                FM,F,DSIDEP,SIGN,SIGMA,MATSYM,MATUU,VECTU)

C        CALCUL DES CONTRAINTES DE CAUCHY, CONVERSION LAGRANGE -> CAUCHY

         IF (OPTION(1:4).EQ.'RAPH' .OR. OPTION(1:4).EQ.'FULL') THEN
            CALL NMDETF(3,F,DETF)
            CALL PK2SIG(3,F,DETF,SIGMA,SIGP(1,KPG),1)
         ENDIF

800   CONTINUE

1956  CONTINUE

C - SYNTHESE DES CODES RETOURS

      CALL CODERE(COD,NPG,CODRET)

      END
