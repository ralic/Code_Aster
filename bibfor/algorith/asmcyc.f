      SUBROUTINE ASMCYC(CMASS,NDIM,SOUMAT,BETA,NI,NJ,NA,
     &AXOK,LIAX,NBLIAX,LIBID)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 26/04/2011   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
      IMPLICIT REAL*8 (A-H,O-Z)
C
C***********************************************************************
C    P. RICHARD     DATE 14/03/91
C-----------------------------------------------------------------------
C  BUT:  ASSEMBLER LES MATRICES MASSE COMPLEXES RELATIVES
C   A UN PROBLEME CYCLIQUE SOUS MATRICES ET DEPHASAGE BETA
C
C
C METHODE:
C         TOUTES LES SOUS-MATRICES POSSIBLES SONT PASSEE  EN REVUE
C         PAR APPEL A DES ROUTINES D'ASSEMBLAGE SPECIALISEES
C         QUI TESTENT L'EXISTENCE DE LA SOUS-MATRICE ET NE FONT
C         RIEN SI ELLE N'EXISTE PAS
C         CHAQUE METHODE: CRAIG-BAMPTON (CB), MAC-NEAL (MN), ET
C                         CRAIG-BAMPTON HARMONIQUE (CBH) Y
C                         TROUVE SON COMPTE
C
C-----------------------------------------------------------------------
C
C CMASS    /O/: MATRICE DE MASSE COMPLEXE
C NDIM     /O/: DIMENSION DES MATRICES COMPLEXES
C SOUMAT   /I/: NOM K24 DE LA FAMILLE DES SOUS-MATRICES
C BETA     /I/: ANGLE DE PEPHASAGE EN RADIANS
C NI       /I/: NOMBRE DE MODE PROPRES DE LA BASE
C NJ       /I/: NOMBRE DE DDL GENERALISES DE DROITE
C NA       /I/: NOMBRE DE DDL GENERALISES AXE
C AXOK     /I/: FLAG ASSEMBLAGE TERMES RELATIFS DDL AXE
C LIAX     /I/: LISTE DES NUMERO DDL AXE A ASSEMBLER
C NBLIAX   /I/: NOMBRE DES DDL AXE A ASSEMBLER (<=NA)
C LIBID    /I/: LISTE BIDON LIBID(I)=I DE DIM >=MAX(NI,NJ)
C
C-------- DEBUT COMMUNS NORMALISES  JEVEUX  ----------------------------
C
      INTEGER          ZI
      COMMON  /IVARJE/ ZI(1)
      REAL*8           ZR
      COMMON  /RVARJE/ ZR(1)
      COMPLEX*16       ZC
      COMMON  /CVARJE/ ZC(1)
      LOGICAL          ZL
      COMMON  /LVARJE/ ZL(1)
      CHARACTER*8      ZK8
      CHARACTER*16              ZK16
      CHARACTER*24                        ZK24
      CHARACTER*32                                  ZK32
      CHARACTER*80                                            ZK80
      COMMON  /KVARJE/ ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C----------  FIN  COMMUNS NORMALISES  JEVEUX  --------------------------
C
      CHARACTER*24 SOUMAT
      COMPLEX*16 CMASS(*)
      INTEGER LIBID(*),LIAX(NBLIAX)
      LOGICAL AXOK,VRAI,FAUX
C
C-----------------------------------------------------------------------
      DATA VRAI,FAUX /.TRUE.,.FALSE./
C-----------------------------------------------------------------------
C
C
C-----------------MISE A ZERO DES MATRICES COMPLEXES--------------------
C
      DO 10 I=1,NDIM*(NDIM+1)/2
          CMASS(I)=DCMPLX(0.D0,0.D0)
 10   CONTINUE
C
C
C--------------DETERMINATION DES DIMENSIONS DES SOUS-MATRICES-----------
C
C
C DEBUT DES DDL GENERALISES DE DROITE
C
      ID=NI+1
C
C DEBUT DES DDL GENERALISES DE AXE
C
      IA=NI+NJ+1
C
C--------------ASSEMBLAGE DES TERMES STANDARDS--------------------------
C
C  POUR TOUTES LES METHODES
C
      CALL ACYELT(SOUMAT,'M0II',NI,CMASS,NDIM,1,1,1.D0)
C
C  POUR CRAIG-BAMPTON ET CRAIG-BAMPTON HARMONIQUE
C
      CALL ACYELT(SOUMAT,'M0JJ',NJ,CMASS,NDIM,ID,ID,1.D0)
C
      CALL ACYEL2(SOUMAT,'M0IJ',NI,NJ,FAUX,LIBID,NI,LIBID,NJ,
     &CMASS,NDIM,1,ID,1.D0)
C
      CALL ACYEL4(SOUMAT,'MPLUSJJ',NJ,NJ,FAUX,LIBID,NJ,LIBID,NJ,
     &CMASS,NDIM,ID,ID,BETA)
C
      CALL ACYEL4(SOUMAT,'MPLUSIJ',NI,NJ,FAUX,LIBID,NI,LIBID,NJ,
     &CMASS,NDIM,1,ID,BETA)
C
C  POUR MAC-NEAL
C    RIEN
C
C
C-------------------------CAS DE PRESENCE DE DDL AXE--------------------
C
      IF(AXOK) THEN
C
C
C  POUR TOUTES LES METHODES
C       RIEN
C
C
C  POUR CRAIG-BAMPTON ET CRAIG-BAMPTON HARMONIQUE
C
        CALL ACYEL2(SOUMAT,'M0IA',NI,NA,VRAI,LIBID,NI,LIAX,NBLIAX,
     &CMASS,NDIM,1,IA,1.D0)
        CALL ACYEL2(SOUMAT,'M0AJ',NA,NJ,VRAI,LIAX,NBLIAX,LIBID,NJ,
     &CMASS,NDIM,IA,ID,1.D0)
        CALL ACYEL1(SOUMAT,'M0AA',NA,NA,VRAI,LIAX,NBLIAX,LIAX,NBLIAX,
     &CMASS,NDIM,IA,IA,1.D0)
        CALL ACYEL4(SOUMAT,'MPLUSIA',NI,NA,VRAI,LIBID,NI,LIAX,NBLIAX,
     &CMASS,NDIM,1,IA,BETA)
        CALL ACYEL4(SOUMAT,'MPLUSAJ',NA,NJ,VRAI,LIAX,NBLIAX,LIBID,NJ,
     &CMASS,NDIM,IA,ID,BETA)
        CALL ACYEL4(SOUMAT,'MPLUSJA',NJ,NA,VRAI,LIBID,NJ,LIAX,NBLIAX,
     &CMASS,NDIM,ID,IA,BETA)
        CALL ACYEL4(SOUMAT,'MPLUSAA',NA,NA,VRAI,LIAX,NBLIAX,LIAX,NBLIAX,
     &CMASS,NDIM,IA,IA,BETA)
C
C  POUR MAC-NEAL
C
C    RIEN
C
      ENDIF
C
C
      END
