      SUBROUTINE NMCOMP (FAMI,KPG,KSP,NDIM,TYPMOD,IMATE,COMPOR,CRIT,
     &                   INSTAM,INSTAP,
     &                   EPSM,DEPS,
     &                   SIGM,VIM,
     &                   OPTION,ANGMAS,TAMPON,
     &                   SIGP,VIP,DSIDEP,CODRET)
C ======================================================================
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C ======================================================================
C MODIF ALGORITH  DATE 23/03/2010   AUTEUR PROIX J-M.PROIX 
C ======================================================================
C COPYRIGHT (C) 1991 - 2005  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE JMBHH01 J.M.PROIX
C TOLE CRP_20
C TOLE CRP_21
      IMPLICIT NONE
      INTEGER            KPG,KSP,NDIM,IMATE,CODRET,ICP,NUMLC
      CHARACTER*8        TYPMOD(*)
      CHARACTER*(*)      FAMI
      CHARACTER*16       COMPOR(*), OPTION,PHENOM
      REAL*8             CRIT(*), INSTAM, INSTAP
      REAL*8             EPSM(*), DEPS(*), DSIDEP(*)
      REAL*8             SIGM(*), VIM(*), SIGP(*), VIP(*)
      REAL*8             TAMPON(*)
      REAL*8             ANGMAS(*)
C ----------------------------------------------------------------------
C     INTEGRATION DES LOIS DE COMPORTEMENT NON LINEAIRE POUR LES
C     ELEMENTS ISOPARAMETRIQUES EN PETITES DEFORMATIONS
C
C IN  KPG,KSP  : NUMERO DU (SOUS)POINT DE GAUSS
C     NDIM    : DIMENSION DE L'ESPACE
C               3 : 3D , 2 : D_PLAN ,AXIS OU  C_PLAN
C     TYPMOD  : TYPE DE MODELISATION
C     IMATE   : ADRESSE DU MATERIAU CODE
C     COMPOR  : COMPORTEMENT :  (1) = TYPE DE RELATION COMPORTEMENT
C                               (2) = NB VARIABLES INTERNES / PG
C                               (3) = HYPOTHESE SUR LES DEFORMATIONS
C     CRIT    : CRITERES DE CONVERGENCE LOCAUX
C                               (1) = NB ITERATIONS MAXI A CONVERGENCE
C                                     (ITER_INTE_MAXI == ITECREL)
C                               (2) = TYPE DE JACOBIEN A T+DT
C                                     (TYPE_MATR_COMP == MACOMP)
C                                     0 = EN VITESSE     >SYMETRIQUE
C                                     1 = EN INCREMENTAL >NON-SYMETRIQUE
C                               (3) = VALEUR TOLERANCE DE CONVERGENCE
C                                     (RESI_INTE_RELA == RESCREL)
C                               (5) = NOMBRE D'INCREMENTS POUR LE
C                                     REDECOUPAGE LOCAL DU PAS DE TEMPS
C                                     (ITER_INTE_PAS  == ITEDEC)
C                                     -1,0,1 = PAS DE REDECOUPAGE
C                                     N = NOMBRE DE PALIERS
C                               (6) = TYPE D INTEGRATION LOCAL POUR
C                                     LA LOI DE COMPORTEMENT
C                                     (RESO_INTE == INTLOC)
C                                     0 = IMPLICITE
C                                     1 = RUNGE_KUTTA
C     INSTAM  : INSTANT DU CALCUL PRECEDENT
C     INSTAP  : INSTANT DU CALCUL
C     EPSM    : DEFORMATIONS A L'INSTANT DU CALCUL PRECEDENT
C     DEPS    : INCREMENT DE DEFORMATION TOTALE :
C                DEPS(T) = DEPS(MECANIQUE(T)) + DEPS(DILATATION(T))
C     SIGM    : CONTRAINTES A L'INSTANT DU CALCUL PRECEDENT
C     VIM     : VARIABLES INTERNES A L'INSTANT DU CALCUL PRECEDENT
C     OPTION  : OPTION DEMANDEE : RIGI_MECA_TANG , FULL_MECA , RAPH_MECA
C     ANGMAS  : LES TROIS ANGLES DU MOT_CLEF MASSIF (AFFE_CARA_ELEM),
C               + UN REEL QUI VAUT 0 SI NAUTIQUIES OU 2 SI EULER
C               + LES 3 ANGLES D'EULER
C VAR VIP     : VARIABLES INTERNES
C                IN  : ESTIMATION (ITERATION PRECEDENTE OU LAG. AUGM.)
C                OUT : EN T+
C OUT SIGP    : CONTRAINTES A L'INSTANT ACTUEL
C     DSIDEP  : MATRICE CARREE
C
C PRECISIONS :
C  1/ SI DEFORMATION = PETIT OU PETIT_REAC
C
C      EPSM(6), DEPS(6)  SONT LES DEFORMATIONS
C
C      LES TENSEURS ET MATRICES SONT RANGES DANS L'ORDRE :
C         XX YY ZZ SQRT(2)*XY SQRT(2)*XZ SQRT(2)*YZ
C
C  2/ SI DEFORMATION = SIMO_MIEHE
C
C  INPUT
C   VIM        VARIABLES INTERNES EN T-
C   VIP        VARIABLES INTERNES EN T+ DE L'ITERATION PRECEDENTE
C   EPSM(3,3)    GRADIENT DE LA TRANSFORMATION EN T-
C   DEPS(3,3)    GRADIENT DE LA TRANSFORMATION DE T- A T+
C
C OUTPUT SI RESI (RAPH_MECA, FULL_MECA_*)
C   VIP      VARIABLES INTERNES EN T+
C   SIGP(6)  CONTRAINTE DE KIRCHHOFF EN T+ RANGES DANS L'ORDRE
C         XX YY ZZ SQRT(2)*XY SQRT(2)*XZ SQRT(2)*YZ
C
C OUTPUT SI RIGI (RIGI_MECA_*, FULL_MECA_*)
C   DSIDEP(6,3,3) MATRICE TANGENTE D(TAU)/D(FD) * (FD)T
C                 (AVEC LES RACINES DE 2)
C ----------------------------------------------------------------------
C
C    POUR LES UTILITAIRES DE CALCUL TENSORIEL
      INTEGER NDT,NDI
      COMMON /TDIM/ NDT,NDI

      CHARACTER*16 OPTIO2,COMCOD,TEXTE(2)
      LOGICAL CP,CONVCP
      INTEGER CPL,NVV,IRETT,NCPMAX
      REAL*8  R8BID,R8VIDE

      CODRET = 0
      R8BID=R8VIDE()
      
C     CONTRAINTES PLANES
      CALL NMCPL1(COMPOR,TYPMOD,OPTION,VIP,DEPS,OPTIO2,CPL,NVV)
      CP=(CPL.NE.0)

C     DIMENSIONNEMENT POUR LE CALCUL TENSORIEL
      NDT = 2*NDIM
      NDI = NDIM

      IF (CP) THEN
        CONVCP = .FALSE.
        NCPMAX = NINT(CRIT(9))
      ELSE
        CONVCP = .TRUE.
        NCPMAX = 1
      ENDIF

C     RECUP NUMLC
      READ (COMPOR(6),'(I16)') NUMLC
      
C     BOUCLE POUR ETABLIR LES CONTRAINTES PLANES
      DO 1000, ICP = 1,NCPMAX
         
         IF ( COMPOR(1).EQ.'KIT_DDI') THEN
C        POUR EVITER LA RECURSIVITE      
            CALL NMCOUP (FAMI,KPG,KSP,NDIM,TYPMOD,IMATE,COMPOR,CP,
     &              CRIT,INSTAM,INSTAP,EPSM,DEPS,SIGM,VIM,OPTION,
     &              TAMPON,NUMLC,SIGP,VIP,DSIDEP,CODRET)
         ELSE
            CALL REDECE ( FAMI,KPG,KSP,NDIM,TYPMOD,IMATE,COMPOR,CRIT,
     &                 INSTAM,INSTAP,CP,NUMLC,R8BID,R8BID,R8BID,
     &                 EPSM, DEPS,SIGM, VIM,OPTION,TAMPON,ANGMAS,
     &                 SIGP, VIP, DSIDEP,CODRET)
         ENDIF

C       VERIFIER LA CONVERGENCE DES CONTRAINTES PLANES ET 
C       SORTIR DE LA BOUCLE SI NECESSAIRE
        IF(CP) CALL NMCPL3(COMPOR,OPTION,CRIT,DEPS,DSIDEP,NDIM,
     &     SIGP,VIP,CPL,ICP,CONVCP)
     
        IF(CONVCP) GOTO 1001
        
 1000 CONTINUE
 1001 CONTINUE

C     CONTRAINTES PLANES METHODE DE BORST
      IF (CP) THEN
C        CORRECTION JMP : CODRET PEUT AUSSI ETRE EGAL A 2.      
C         IF (CODRET.NE.1) THEN
         IF (CODRET.EQ.0) THEN
            CALL NMCPL2(COMPOR,TYPMOD,OPTION,OPTIO2,CPL,NVV,CRIT,
     &                    DEPS,DSIDEP,NDIM,SIGP,VIP,CODRET)
         ELSE
            OPTION=OPTIO2
         ENDIF
      ENDIF

      END
