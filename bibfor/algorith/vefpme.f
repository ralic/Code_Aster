      SUBROUTINE VEFPME(MODELE,CARELE,MATE,CHARGZ,INFCHZ,PARTPS,TEMPLU,
     &                  LVECHZ,LIGREZ)
      IMPLICIT REAL*8 (A-H,O-Z)
      CHARACTER*(*) MODELE,CARELE,MATE,CHARGZ,INFCHZ
      CHARACTER*(*) TEMPLU,LVECHZ,LIGREZ
      CHARACTER*19 LVECHP
      REAL*8 PARTPS(*)
C ----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 23/10/2008   AUTEUR TORKHANI M.TORKHANI 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.

C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.

C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================

C     CALCUL DES VECTEURS ELEMENTAIRES DES CHARGEMENTS MECANIQUES
C     DE NEUMANN NON SUIVEURS ET PILOTABLES.

C IN  MODELE  : NOM DU MODELE
C IN  CARELE  : CARACTERISTIQUES DES POUTRES ET COQUES
C IN  MATE    : MATERIAU
C IN  CHARGZ  : LISTE DES CHARGES
C IN  INFCHZ  : INFORMATIONS SUR LES CHARGEMENTS
C IN  PARTPS  : TABLEAU DONNANT T+, DELTAT ET THETA (POUR LE THM)
C IN  TEMPLU  : CHAMP DE TEMPERATURE A L'INSTANT T+
C IN  LIGREZ  : (SOUS-)LIGREL DE MODELE POUR CALCUL REDUIT
C                  SI ' ', ON PREND LE LIGREL DU MODELE
C VAR LVECHZ  : VECT_ELEM

C --- DEBUT DECLARATIONS NORMALISEES JEVEUX ----------------------------

      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)

C --- FIN DECLARATIONS NORMALISEES JEVEUX ------------------------------

      PARAMETER (NBCHMX=13)
      INTEGER JCHAR,JINF,JTYP
      INTEGER IBID,IRET,NCHAR,K,ICHA,NUMCHM
      CHARACTER*5 SUFFIX
      CHARACTER*6 NOMLIG(NBCHMX),NOMPAF(NBCHMX),NOMPAR(NBCHMX)
      CHARACTER*6 NOMOPF(NBCHMX),NOMOPR(NBCHMX)
      CHARACTER*7 NOMCMP(3)
      CHARACTER*8 NOMCHA,LPAIN(13),LPAOUT(1),K8BID,NEWNOM
      CHARACTER*16 OPTION
      CHARACTER*24 CHGEOM,CHCARA(18),CHTIME,LIGREL
      CHARACTER*24 LIGRMO,LIGRCH,LCHIN(13),LCHOUT(1)
      CHARACTER*24 CHARGE,INFCHA
      LOGICAL EXIGEO,EXICAR,BIDON
      COMPLEX*16 CBID

      DATA NOMLIG/'.FORNO','.F3D3D','.F2D3D','.F1D3D','.F2D2D','.F1D2D',
     &   '.F1D1D','.PRESS','.FCO3D','.FCO2D','.FLUX','.PESAN','.VEASS'/
      DATA NOMOPF/'FORC_F','FF3D3D','FF2D3D','FF1D3D','FF2D2D','FF1D2D',
     &   'FF1D1D','PRES_F','FFCO3D','FFCO2D','FLUX_F','PESA_R','     '/
      DATA NOMPAF/'FORNOF','FF3D3D','FF2D3D','FF1D3D','FF2D2D','FF1D2D',
     &   'FF1D1D','PRESSF','FFCO3D','FFCO2D','FLUXF','PESANR','      '/
      DATA NOMOPR/'FORC_R','FR3D3D','FR2D3D','FR1D3D','FR2D2D','FR1D2D',
     &   'FR1D1D','PRES_R','FRCO3D','FRCO2D','FLUX_R','PESA_R','     '/
      DATA NOMPAR/'FORNOR','FR3D3D','FR2D3D','FR1D3D','FR2D2D','FR1D2D',
     &    'FR1D1D','PRESSR','FRCO3D','FRCO2D','FLUXR','PESANR','     '/


      CALL JEMARQ()
      NEWNOM = '.0000000'
      CHARGE = CHARGZ
      INFCHA = INFCHZ
      LVECHP = LVECHZ
      LIGRMO = LIGREZ
      IF (LIGRMO.EQ.' ') LIGRMO = MODELE(1:8)//'.MODELE'
      IF (LVECHP.EQ.' ') LVECHP = '&&VEMFPI           '

      BIDON = .TRUE.
      CALL JEEXIN(CHARGE,IRET)
      IF (IRET.NE.0) THEN
        CALL JELIRA(CHARGE,'LONMAX',NCHAR,K8BID)
        IF (NCHAR.NE.0) THEN
          BIDON = .FALSE.
          CALL JEVEUO(CHARGE,'L',JCHAR)
          CALL JEVEUO(INFCHA,'L',JINF)
        END IF
      END IF

      CALL DETRSD('VECT_ELEM',LVECHP)
      CALL MEMARE('V',LVECHP,MODELE,MATE,CARELE,'CHAR_MECA')
      CALL REAJRE(LVECHP,' ','V')
      IF(BIDON) GO TO 30

      CALL MEGEOM(MODELE,ZK24(JCHAR) (1:8),EXIGEO,CHGEOM)
      CALL MECARA(CARELE,EXICAR,CHCARA)

      CHTIME = '&&VEFPME.CH_INST_R'
      NOMCMP(1) = 'INST   '
      NOMCMP(2) = 'DELTAT '
      NOMCMP(3) = 'THETA  '
      CALL MECACT('V',CHTIME,'LIGREL',LIGRMO,'INST_R  ',3,NOMCMP,IBID,
     &            PARTPS,CBID,K8BID)
      LPAIN(2) = 'PGEOMER'
      LCHIN(2) = CHGEOM
      LPAIN(3) = 'PTEMPSR'
      LCHIN(3) = CHTIME
      LPAIN(4) = 'PMATERC'
      LCHIN(4) = MATE
      LPAIN(5) = 'PCACOQU'
      LCHIN(5) = CHCARA(7)
      LPAIN(6) = 'PCAGNPO'
      LCHIN(6) = CHCARA(6)
      LPAIN(7) = 'PCADISM'
      LCHIN(7) = CHCARA(3)
      LPAIN(8) = 'PCAORIE'
      LCHIN(8) = CHCARA(1)
      LPAIN(9) = 'PCACABL'
      LCHIN(9) = CHCARA(10)
      LPAIN(10) = 'PCAARPO'
      LCHIN(10) = CHCARA(9)
      LPAIN(11) = 'PCAGNBA'
      LCHIN(11) = CHCARA(11)
      LCHIN(12) = TEMPLU
      LPAIN(12) = 'PTEMPER'
      LPAIN(13) = 'PCAMASS'
      LCHIN(13) = CHCARA(12)
      LPAOUT(1) = 'PVECTUR'
      
      DO 20 ICHA = 1,NCHAR
        NOMCHA = ZK24(JCHAR+ICHA-1) (1:8)
        LIGRCH = NOMCHA//'.CHME.LIGRE'
        NUMCHM = ZI(JINF+NCHAR+ICHA)
        IF (NUMCHM.EQ.5) THEN
          DO 10 K = 1,NBCHMX
            IF (K.EQ.1) THEN
              LIGREL = LIGRCH
            ELSE
              LIGREL = LIGRMO
            END IF
            IF (NOMLIG(K).EQ.'.VEASS') THEN
              SUFFIX = '     '
            ELSE
              SUFFIX = '.DESC'
            END IF
            LCHIN(1) = LIGRCH(1:13)//NOMLIG(K)//SUFFIX
            CALL JEEXIN(LCHIN(1),IRET)
            IF (IRET.NE.0) THEN
              CALL JEVEUO(NOMCHA//'.TYPE','L',JTYP)
              IF (ZK8(JTYP).EQ.'MECA_RE ') THEN
                OPTION = 'CHAR_MECA_'//NOMOPR(K)
                LPAIN(1) = 'P'//NOMPAR(K)
              ELSE IF (ZK8(JTYP).EQ.'MECA_FO ') THEN
                OPTION = 'CHAR_MECA_'//NOMOPF(K)
                LPAIN(1) = 'P'//NOMPAF(K)
              END IF
              LCHOUT(1) = '&&VEFPME.???????'
              CALL GCNCO2(NEWNOM)
              LCHOUT(1) (10:16) = NEWNOM(2:8)
              CALL CORICH('E',LCHOUT(1),ICHA,IBID)
              IF (NOMLIG(K).EQ.'.VEASS') THEN
                CALL JEVEUO(LCHIN(1),'L',JLCHIN)
                CALL COPISD('CHAMP_GD','V',ZK8(JLCHIN),LCHOUT(1))
              ELSE
                CALL CALCUL('S',OPTION,LIGREL,13,LCHIN,LPAIN,1,LCHOUT,
     &                    LPAOUT,'V')
              ENDIF
              CALL REAJRE(LVECHP,LCHOUT(1),'V')
            END IF
   10     CONTINUE
        END IF

   20 CONTINUE

   30 CONTINUE

      LVECHZ = LVECHP//'.RELR'
      CALL JEDEMA()
      END
