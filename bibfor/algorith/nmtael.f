      SUBROUTINE NMTAEL(FAMI,KPG,KSP,IMATE,NDIMSI,MATM,MAT,SIGM,EPSM,
     &                  DEPS,EPM,SIGDV,SIGP)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 16/10/2007   AUTEUR SALMONA L.SALMONA 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================

      IMPLICIT NONE

      INTEGER KPG,KSP,NDIMSI,IMATE
      CHARACTER*(*)  FAMI
      REAL*8  MATM(3),MAT(3)
      REAL*8  SIGM(NDIMSI),EPSM(NDIMSI),DEPS(NDIMSI)
      REAL*8  SIGDV(NDIMSI),EPM(NDIMSI),SIGP(NDIMSI)

C ----------------------------------------------------------------------
C TAHERI :  CALCUL DES CONTRAINTES ELASTIQUES ET DEFORMATIONS PLASTIQUES
C ----------------------------------------------------------------------
C IN  NDIMSI DIMENSION DES TENSEURS
C IN  MATM   CARACTERISTIQUES ELASTIQUES EN T-
C IN  MAT    CARACTERISTIQUES ELASTIQUES EN T+
C IN  SIGM   CONTRAINTES EN T-
C IN  DEPS   INCREMENT DE DEFORMATION
C OUT EPM    DEFORMATIONS PLASTIQUES EN T-
C OUT SIGDV  DEVIATEUR DES CONTRAINTES ELASTIQUES
C OUT SIGP   TENSEUR DES CONTRAINTES ELASTIQUES
C ----------------------------------------------------------------------

      INTEGER K, IRET,IRET1
      REAL*8  TROIKM,DEUMUM
      REAL*8  TROISK,DEUXMU
      REAL*8  EPMMO
      REAL*8  DEPSTH,DEPSME(6),DEPSMO,DEPSDV(6)
      REAL*8  SIGMOM,SIGDVM(6),SIGMO
      REAL*8  KRON(6)
      DATA    KRON /1.D0,1.D0,1.D0,0.D0,0.D0,0.D0/

      CALL VERIFT(FAMI,KPG,KSP,'T',IMATE,'ELAS',1,DEPSTH,IRET1)

      TROIKM = MATM(1)
      DEUMUM = MATM(2)
      TROISK = MAT(1)
      DEUXMU = MAT(2)

C    CALCUL DES DEFORMATIONS PLASTIQUES AU TEMPS -
      DO 5 K = 1,NDIMSI
        EPM(K) = EPSM(K) - SIGM(K)/DEUMUM
 5    CONTINUE
      EPMMO = (EPM(1)+EPM(2)+EPM(3)) / 3.D0
      DO 6 K = 1,3
        EPM(K) = EPM(K) - EPMMO
 6    CONTINUE

C    PARTS HYDROSTATIQUES ET DEVIATORIQUES DE L'INCR. DEFO. MECANIQUE
      DO 10 K=1,NDIMSI
        DEPSME(K) = DEPS(K) - DEPSTH*KRON(K)
 10   CONTINUE
      DEPSMO = (DEPSME(1)+DEPSME(2)+DEPSME(3))/3.D0
      DO 20 K=1,NDIMSI
        DEPSDV(K)   = DEPSME(K) - DEPSMO * KRON(K)
 20   CONTINUE


C    PART HYDROSTATIQUE DES CONTRAINTES
      SIGMOM = (SIGM(1)+SIGM(2)+SIGM(3))/3.D0
      SIGMO  = TROISK/TROIKM*SIGMOM + TROISK*DEPSMO


C    PART DEVIATORIQUE DES CONTRAINTES ELASTIQUES
      DO 30 K=1,NDIMSI
        SIGDVM(K) = SIGM(K) - SIGMOM*KRON(K)
        SIGDV(K)  = DEUXMU/DEUMUM*SIGDVM(K) + DEUXMU*DEPSDV(K)
 30   CONTINUE


C    CONTRAINTES ELASTIQUES
      DO 40 K = 1,NDIMSI
        SIGP(K)   = SIGMO*KRON(K) + SIGDV(K)
 40   CONTINUE

      END
