      SUBROUTINE DLDIF0 ( NRORES, NBPASE, INPSCO,
     &                    NEQ, ISTOC, IARCHI, IFM,
     &                    LAMORT,
     &                    IMAT, MASSE,
     &                    DEP0, VIT0, ACC0,
     &                    DEPL1, VITE1, ACCE1, VITE2,
     &                    NCHAR, NVECA, LIAD, LIFO, MODELE,
     &                    MATE, CARELE, CHARGE, INFOCH, FOMULT, NUMEDD,
     &                    DT, TEMPS,
     &                    TABWK0, TABWK1,
     &                    ARCHIV, NBTYAR, TYPEAR )
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 26/04/2011   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C TOLE CRP_21
C     ------------------------------------------------------------------
C     CALCUL MECANIQUE TRANSITOIRE PAR INTEGRATION DIRECTE
C     AVEC  METHODE EXPLICITE :  DIFFERENCES CENTREES
C     ------------------------------------------------------------------
C  IN  : NRORES    : NUMERO DE LA RESOLUTION
C                  0 : CALCUL STANDARD
C                 >0 : CALCUL DE LA DERIVEE NUMERO NRORES
C  IN  : NBPASE    : NOMBRE DE PARAMETRES SENSIBLES
C  IN  : INPSCO    : STRUCTURE CONTENANT LA LISTE DES NOMS
C  IN  : NEQ       : NOMBRE D'EQUATIONS
C  IN  : ISTOC     : PILOTAGE DU STOCKAGE DES RESULTATS
C  IN  : IARCHI    : PILOTAGE DE L'ARCHIVAGE DES RESULTATS
C  IN  : LAMORT    : LOGIQUE INDIQUANT SI IL Y A AMORTISSEMENT
C  IN  : IMAT      : TABLEAU D'ADRESSES POUR LES MATRICES
C  IN  : MASSE     : MATRICE DE MASSE
C  IN  : NCHAR     : NOMBRE D'OCCURENCES DU MOT CLE CHARGE
C  IN  : NVECA     : NOMBRE D'OCCURENCES DU MOT CLE VECT_ASSE
C  IN  : LIAD      : LISTE DES ADRESSES DES VECTEURS CHARGEMENT (NVECT)
C  IN  : LIFO      : LISTE DES NOMS DES FONCTIONS EVOLUTION (NVECT)
C  IN  : MODELE    : NOM DU MODELE
C  IN  : MATE      : NOM DU CHAMP DE MATERIAU
C  IN  : CARELE    : CARACTERISTIQUES DES POUTRES ET COQUES
C  IN  : CHARGE    : LISTE DES CHARGES
C  IN  : INFOCH    : INFO SUR LES CHARGES
C  IN  : FOMULT    : LISTE DES FONC_MULT ASSOCIES A DES CHARGES
C  IN  : NUMEDD    : NUME_DDL DE LA MATR_ASSE RIGID
C  VAR : DEP0      : TABLEAU DES DEPLACEMENTS A L'INSTANT N
C  VAR : VIT0      : TABLEAU DES VITESSES A L'INSTANT N
C  VAR : ACC0      : TABLEAU DES ACCELERATIONS A L'INSTANT N
C  IN  : TEMPS     : INSTANT COURANT
C
C CORPS DU PROGRAMME
      IMPLICIT NONE
C DECLARATION PARAMETRES D'APPELS
C
      INTEGER NRORES, NBPASE
      INTEGER NEQ, ISTOC, IARCHI, IFM
      INTEGER ARCHIV, NBTYAR
      INTEGER IMAT(3)
      INTEGER NCHAR, NVECA, LIAD(*)
C
      REAL*8 DEP0(*), VIT0(*), ACC0(*)
      REAL*8 DEPL1(NEQ), VITE1(NEQ), ACCE1(NEQ)
      REAL*8 VITE2(NEQ)
      REAL*8 TABWK0(NEQ), TABWK1(NEQ)
      REAL*8 DT, TEMPS
C
      CHARACTER*8  MASSE
      CHARACTER*13 INPSCO
      CHARACTER*16 TYPEAR(NBTYAR)
      CHARACTER*24 MODELE, MATE, CARELE, CHARGE, INFOCH, FOMULT, NUMEDD
      CHARACTER*24 LIFO(*)

      LOGICAL LAMORT


C    ----- DEBUT COMMUNS NORMALISES  JEVEUX  --------------------------
      INTEGER           ZI
      COMMON / IVARJE / ZI(1)
      REAL*8            ZR
      COMMON / RVARJE / ZR(1)
      COMPLEX*16        ZC
      COMMON / CVARJE / ZC(1)
      LOGICAL           ZL
      COMMON / LVARJE / ZL(1)
      CHARACTER*8       ZK8
      CHARACTER*16              ZK16
      CHARACTER*24                       ZK24
      CHARACTER*32                                ZK32
      CHARACTER*80                                         ZK80
      COMMON / KVARJE / ZK8(1), ZK16(1), ZK24(1), ZK32(1), ZK80(1)
C     ----- FIN COMMUNS NORMALISES  JEVEUX  ---------------------------
C
C DECLARATION VARIABLES LOCALES
C
      INTEGER IFORC1
      INTEGER IAUX, JAUX
C
      REAL*8 R8BID
C
      CHARACTER*8 RESULT
      CHARACTER*19 FORCE1
C
C====
C 1. PREALABLES
C====
C 1.1. ==> NOM DES STRUCTURES ASSOCIEES AUX DERIVATIONS
C                3. LE NOM DU RESULTAT
C               10. VARIABLE COMPLEMENTAIRE NUMERO 2 INSTANT N

      IAUX = NRORES

      JAUX = 3
      CALL PSNSLE ( INPSCO, IAUX, JAUX, RESULT )
CCC      PRINT *,'NRORES = ',NRORES,' ==> RESULT = ', RESULT
C
      JAUX = 10
      CALL PSNSLE ( INPSCO, IAUX, JAUX, FORCE1 )
CCC      PRINT *,'NRORES = ',NRORES,' ==> FORCE1 = ', FORCE1
C====
C 2. CALCUL
C====
C
C 2.1. ==> CALCUL DES DEPLACEMENTS ET VITESSES
C
      DO 21 , IAUX = 1 , NEQ
        VITE1(IAUX) = VIT0(IAUX) + DT*ACC0(IAUX)
        DEPL1(IAUX) = DEP0(IAUX) + DT*VITE1(IAUX)
   21 CONTINUE

C====
C 3. CALCUL DU SECOND MEMBRE F*
C====

      CALL JEVEUO(FORCE1(1:19)//'.VALE','E',IFORC1)

      CALL DLFEXT ( NVECA, NCHAR, TEMPS, NEQ,
     &              LIAD, LIFO, CHARGE, INFOCH, FOMULT,
     &              MODELE, MATE, CARELE, NUMEDD,
     &              NBPASE, NRORES, INPSCO, ZR(IFORC1) )

      CALL DLFDYN ( IMAT(1),IMAT(3), LAMORT, NEQ,
     &              DEPL1, VITE1, ZR(IFORC1), TABWK0)
C
C====
C 4.  RESOLUTION DU PROBLEME M . A = F ET DE LA VITESSE STOCKEE
C           --- RESOLUTION AVEC FORCE1 COMME SECOND MEMBRE ---
C====
C
      R8BID = DT/2.D0
C
      DO 40 , IAUX = 1 , NEQ
C
        ACCE1(IAUX) = TABWK1(IAUX)*ZR(IFORC1+IAUX-1)
C
C        --- VITESSE AUX INSTANTS 'TEMPS + DT' ---
        VITE2(IAUX) = VITE1(IAUX) + R8BID*ACCE1(IAUX)
C
   40 CONTINUE

C====
C 5. TRANSFERT DES NOUVELLES VALEURS DANS LES ANCIENNES
C====
C
      CALL DCOPY ( NEQ ,DEPL1, 1, DEP0, 1)
      CALL DCOPY ( NEQ ,VITE1, 1, VIT0, 1)
      CALL DCOPY ( NEQ ,ACCE1, 1, ACC0, 1)
C

C====
C 6. DANS LE CAS DE CALCUL AVEC SENSIBILITE ET POUR LE CALCUL
C    NOMINAL, SAUVEGARDE DU CHAMP SOLUTION
C====
C
      IF ( NRORES.EQ.0 .AND. NBPASE.GT.0 ) THEN
C
        IAUX = 6
        CALL SENS01 ( INPSCO,
     &                NEQ, MASSE,
     &                IAUX, DEPL1, ACCE1 )
C
      ENDIF

C====
C 7. ARCHIVAGE EVENTUEL DANS L'OBJET SOLUTION
C====
C
      IF ( ARCHIV.EQ.1 ) THEN
C
        ISTOC = 0
        JAUX = 1
C
        CALL DLARCH ( NRORES, INPSCO,
     &                NEQ, ISTOC, IARCHI, ' ',
     &                JAUX, IFM, TEMPS,
     &                NBTYAR, TYPEAR, MASSE,
     &                DEPL1, VITE2, ACCE1 )

      ENDIF
C
      END
