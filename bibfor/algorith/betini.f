        SUBROUTINE BETINI ( MATERF, NMAT, SIG, SIGEQ,  SIGH,
     &                      COEFA, COEFB, COEFAR, COEFBR, CONECO,
     &                      CONETR )
        IMPLICIT NONE
C       ================================================================
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 08/03/2004   AUTEUR REZETTE C.REZETTE 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C       ----------------------------------------------------------------
C       BETON_DOUBLE_DP: CONVEXE ELASTO PLASTIQUE POUR (MATER,SIG,P1,P2)
C                   AVEC UN SEUIL EN COMPRESSION ET UN SEUIL EN TRACTION
C       CALCUL DES TERMES CONSTANTS DU SYSTEME LINEAIRE A RESOUDRE
C       ET DES TERMES CORRESPONDANT AU COMPLEMENTAIRES DES CRITERES
C       MODIFIES
C       IN  MATERF :  COEFFICIENTS MATERIAU A T+DT
C           NMAT   :  DIMENSION MATER
C           SIG    :  CONTRAINTE A T+DT (PREDICTION ELASTIQUE)
C       OUT SIGEQ  :  CONTRAINTE EQUIVALENTE
C           SIGH   :  CONTRAINTE HYDROSTATIQUE
C           COEFA  :  TERMES CONSTANTS DU SYSTEME LINEAIRE A RESOUDRE
C           COEFB  :  TERMES MULTIPLICATEURS DES INCREMENTS DE
C                     MULTIPLICATEURS PLASTIQUES DU SYSTEME LINEAIRE
C           COEFAR :  TERMES CONSTANTS DU SYSTEME LINEAIRE
C                     CORRESPONDANT AU COMPLEMENTAIRES DES CRITERES
C           COEFBR :  TERMES MULTIPLICATEURS DES INCREMENTS DE
C                     MULTIPLICATEURS PLASTIQUES DU SYSTEME LINEAIRE
C                     CORRESPONDANT AU COMPLEMENTAIRES DES CRITERES
C           CONECO :  LIMITE SUPERIEURE DE FT POUR LA PROJECTION AU
C                     SOMMET DU CONE DE COMPRESSION
C           CONETR :  LIMITE SUPERIEURE DE FT POUR LA PROJECTION AU
C                     SOMMET DU CONE DE TRACTION
C       ----------------------------------------------------------------
        INTEGER         NMAT
        REAL*8          MATERF(NMAT,2)
        REAL*8          COEFA(2,2), COEFB(2),COEFAR(2,2), COEFBR(2)
        REAL*8          MUN, UN, D23, D13, RAC2, DEUX, TROIS, ZERO
        PARAMETER       ( MUN  = -1.D0  )
        PARAMETER       ( ZERO =  0.D0   )
        PARAMETER       ( UN   =  1.D0   )
        PARAMETER       ( DEUX =  2.D0   )
        PARAMETER       ( TROIS = 3.D0   )
        PARAMETER       ( D23  =  .66666666666666D0 )
        PARAMETER       ( D13  =  .33333333333333D0 )
        REAL*8          DEV(6), SIG(6)
        REAL*8          SIGEQ , SIGH, P, CONETR, CONECO
        REAL*8          K, LAMBDA, MU, E, NU, BETA
        REAL*8          A, B, C, D
C       ----------------------------------------------------------------
        INTEGER         NDT , NDI
        COMMON /TDIM/   NDT , NDI
C       ----------------------------------------------------------------
C
C --- INITIALISATION
C
      RAC2   = SQRT (DEUX)
      E      = MATERF(1,1)
      NU     = MATERF(2,1)
      BETA   = MATERF(3,2)
C
      A = RAC2 * (BETA - UN) / (DEUX * BETA - UN)
      B = RAC2 / TROIS * BETA / (DEUX * BETA - UN)
      C = RAC2
      D = DEUX * RAC2 / TROIS
C
C --- CONTRAINTE EQUIVALENTE
C
      CALL LCDEVI ( SIG , DEV )
      CALL LCPRSC ( DEV , DEV , P)
      SIGEQ = SQRT (1.5D0 * P)
C
C --- CONTRAINTE HYDROSTATIQUE
C
      CALL LCHYDR ( SIG , SIGH )
C
C --- COEFFICIENTS DE LAME
C
      LAMBDA = (NU * E)/((UN + NU)*(UN - DEUX * NU))
      MU =         E   /(DEUX*(UN + NU))
C
C --- MODULE DE COMPRESSION HYDROSTATIQUE
C
      K = LAMBDA + D23 * MU
C
C --- COEFFICIENTS CONSTANTS ET COEFFICIENTS MULTIPLICATEUR DES
C     MULTIPLICATEURS PLASTIQUES DANS LE SYSTEME NON LINEAIRE
C
      COEFA(1,1) = ZERO
      COEFA(1,2) = ZERO
      COEFA(2,1) = ZERO
      COEFA(2,2) = ZERO
      COEFAR(1,1) = ZERO
      COEFAR(1,2) = ZERO
      COEFAR(2,1) = ZERO
      COEFAR(2,2) = ZERO
      COEFB(1)   = ZERO
      COEFB(2)   = ZERO
      COEFBR(1)  = ZERO
      COEFBR(2)  = ZERO
C
C --- CRITERE EN COMPRESSION ACTIF
C
      COEFA(1,1)  = MUN * (D23 * MU + K * A * A) / (B * B)
      COEFA(1,2)  = MUN * (D23 * MU + K * A * C) / (B * D)
C
      COEFB(1)  = (D13 * RAC2 * SIGEQ + A * SIGH) / B
C
      COEFAR(1,1)  = MUN * K * A * A / (B * B)
      COEFAR(1,2)  = MUN * K * A * C / (B * D)
C
      COEFBR(1)  = A * SIGH / B
C
C --- CRITERE EN TRACTION ACTIF
C
      COEFA(2,1)  = MUN * (D23 * MU + K * A * C) / (B * D)
      COEFA(2,2)  = MUN * (D23 * MU + K * C * C) / (D * D)
C
      COEFB(2) = (D13 * RAC2 * SIGEQ + C * SIGH) / D
C
      COEFAR(2,1)  = MUN * K * A * C / (B * D)
      COEFAR(2,2)  = MUN * K * C * C / (D * D)
C
      COEFBR(2) = C * SIGH / D
C
C --- LIMITE SUPERIEURE DE FT POUR LA PROJECTION AU SOMMET DU CONE
C --- DE TRACTION ET DU CONE DE COMPRESSION
C
      CONECO = SIGH * A / B - SIGEQ * A * A * K /(RAC2 * MU * B)
      CONETR = SIGH * C / D - SIGEQ * C * C * K /(RAC2 * MU * D)
C
      END
