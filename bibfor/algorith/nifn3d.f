       SUBROUTINE NIFN3D(NNO1  , NNO2  , NPG   , POIDSG, VFF1  , VFF2  ,
     &                   DFDE1 ,DFDN1, DFDK1 ,  DFDI, GEOM  ,  SIG   ,
     &                   DEPLM,DDEPL,GONFLM,DGONFL,
     &                   FINTU , FINTA )
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 30/01/2002   AUTEUR VABHHTS J.TESELET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2002  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
       IMPLICIT NONE
       INTEGER       NNO1,NNO2, NPG
       REAL*8        DFDE1(*),DFDN1(*),DFDK1(*)
       REAL*8        POIDSG(NPG), VFF1(NNO1,NPG),VFF2(NNO2,NPG)
       REAL*8        GEOM(3,NNO1)
       REAL*8        SIG(7,NPG)
       REAL*8        FINTU(3,20), FINTA(2,8)
       REAL*8        DFDI(NNO1,3)
       REAL*8        DEPLM(3,NNO1),DDEPL(3,NNO1)
       REAL*8        GONFLM(2,NNO2),DGONFL(2,NNO2)

C......................................................................
C
C     BUT:  CALCUL  DE L'OPTION FORC_NODA EN QUASI INCOMPRESSIBLE
C......................................................................
C IN  NNO1    : NOMBRE DE NOEUDS DE L'ELEMENT LIES AUX DEPLACEMENTS
C IN  NNO2    : NOMBRE DE NOEUDS DE L'ELEMENT LIES A LA PRESSION
C IN  NPG     : NOMBRE DE POINTS DE GAUSS
C IN  POIDSG  : POIDS DES POINTS DE GAUSS
C IN  VFF1    : VALEUR  DES FONCTIONS DE FORME LIES AUX DEPLACEMENTS
C IN  VFF2    : VALEUR  DES FONCTIONS DE FORME LIES A LA PRESSION
C IN  DFDE1   : DERIVEE DES FONCTIONS DE FORME ELEMENT DE REFERENCE
C IN  DFDN1   : DERIVEE DES FONCTIONS DE FORME ELEMENT DE REFERENCE
C IN  DFDK1   : DERIVEE DES FONCTIONS DE FORME ELEMENT DE REFERENCE
C IN  GEOM    : COORDONEES DES NOEUDS
C IN  SIG     : CONTRAINTES A L'INSTANT PRECEDENT
C OUT DFDI    : DERIVEE DES FONCTIONS DE FORME  AU DERNIER PT DE GAUSS
C OUT FINTU   : FORCES INTERIEURES ASSOCIEES AU DEPLACEMENT
C OUT FINTA   : FORCES INTERIEURES ASSOCIEES A LA PRESSION ET GONFLEMENT
C......................................................................

      LOGICAL      GRAND, AXI
      INTEGER      KPG,N,I
      REAL*8       DEF(6,20,3)
      REAL*8       TMP, RAC2, RBID
      REAL*8       F(3,3),POIDS,EPSM(6),DEPS(6),SIGMA(6)
      REAL*8       GM,DG,DIVUM,DDIVU
      REAL*8       R8DOT
C --------------------------------------------------------------------


C - PRE REQUIS
      IF (NNO1 .GT. 20) CALL UTMESS('F','NIFN3D','DVP : TROP DE NOEUDS')

C - INITIALISATION
      CALL R8INIR(60, 0.D0, FINTU,1)
      CALL R8INIR(16, 0.D0, FINTA,1)
      RAC2  = SQRT(2.D0)
      GRAND = .FALSE.
      AXI   = .FALSE.

C - CALCUL POUR CHAQUE POINT DE GAUSS

      DO 800 KPG = 1,NPG
C - CALCUL DU GONFLEMENT
        GM = 0.D0
        DG = 0.D0
        DO 1 N = 1, NNO2
          GM = GM + VFF2(N,KPG)*GONFLM(2,N)
          DG = DG + VFF2(N,KPG)*DGONFL(2,N)
 1      CONTINUE

C      CALCUL DES ELEMENTS GEOMETRIQUES
        CALL R8INIR(6, 0.D0, EPSM,1)
        CALL R8INIR(6, 0.D0, DEPS,1)
        CALL NMGEOM(3,NNO1,AXI,GRAND,GEOM,KPG,POIDSG(KPG),
     &              VFF1(1,KPG),DFDE1,DFDN1,DFDK1,DEPLM,POIDS,DFDI,
     &              F,EPSM,RBID)
        CALL NMGEOM(3,NNO1,AXI,GRAND,GEOM,KPG,POIDSG(KPG),
     &              VFF1(1,KPG),DFDE1,DFDN1,DFDK1,DDEPL,POIDS,DFDI,
     &              F,DEPS,RBID)
        DIVUM = EPSM(1) + EPSM(2) + EPSM(3)
        DDIVU = DEPS(1) + DEPS(2) + DEPS(3)

C      CALCUL DE LA MATRICE B

        DO 35 N=1,NNO1
          DO 30 I=1,3
            DEF(1,N,I) =  F(I,1)*DFDI(N,1)
            DEF(2,N,I) =  F(I,2)*DFDI(N,2)
            DEF(3,N,I) =  F(I,3)*DFDI(N,3)
            DEF(4,N,I) = (F(I,1)*DFDI(N,2) + F(I,2)*DFDI(N,1))/RAC2
            DEF(5,N,I) = (F(I,1)*DFDI(N,3) + F(I,3)*DFDI(N,1))/RAC2
            DEF(6,N,I) = (F(I,2)*DFDI(N,3) + F(I,3)*DFDI(N,2))/RAC2
 30       CONTINUE
 35     CONTINUE

C      CALCUL DES CONTRAINTES MECANIQUES A L'EQUILIBRE

        DO 40 I=1,3
          SIGMA(I) = SIG(I,KPG)
 40     CONTINUE
        DO 65 I=4,6
          SIGMA(I) = SIG(I,KPG)*RAC2
 65     CONTINUE

C        CALCUL DE FINT_U
        DO 3 N=1,NNO1
          DO 2 I=1,3
            TMP = R8DOT(6, SIGMA,1, DEF(1,N,I),1)
            FINTU(I,N) = FINTU(I,N) + TMP*POIDS
 2        CONTINUE
 3      CONTINUE

C        CALCUL DE FINT_P ET FINT_G

        DO  4 N = 1, NNO2
          TMP = (DIVUM + DDIVU - GM - DG)*VFF2(N, KPG)
          FINTA(1,N) = FINTA(1,N) + TMP*POIDS

          TMP = SIG(7,KPG)*VFF2(N,KPG)
          FINTA(2,N) = FINTA(2,N) + TMP*POIDS
 4      CONTINUE

 800  CONTINUE
      END
