      SUBROUTINE XAPPAR(LOPTIN,NOMA,MODELE,DEFICO,RESOCO)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 30/10/2012   AUTEUR ABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C
      IMPLICIT NONE
      INCLUDE 'jeveux.h'
      LOGICAL      LOPTIN
      CHARACTER*8  NOMA,MODELE
      CHARACTER*24 DEFICO,RESOCO
C
C ----------------------------------------------------------------------
C
C ROUTINE XFEM (CONTACT - GRANDS GLISSEMENTS)
C
C REALISE L'APPARIEMENT ENTRE SURFACE ESCLAVE ET SURFACE MAITRE POUR
C LE CONTACT METHODE CONTINUE.
C
C TRAVAIL EFFECTUE EN COLLABORATION AVEC I.F.P.
C
C ----------------------------------------------------------------------
C
C
C METHODE : POUR CHAQUE POINT DE CONTACT (SUR UNE MAILLE ESCLAVE ET
C AVEC UN SCHEMA D'INTEGRATION DONNE), ON RECHERCHE LE NOEUD MAITRE LE
C PLUS PROCHE ET ON PROJETTE SUR LES MAILLES QUI L'ENTOURE
C
C STOCKAGE DES POINTS  DE CONTACT DES SURFACES  ESCLAVES ET APPARIEMENT
C
C IN  NOMA   : NOM DU MAILLAGE
C IN  MODELE : NOM DU MODELE
C IN  LOPTIN : VAUT .TRUE. SI ACTIVATION DES OPTIONS *_INIT
C IN  DEFICO : SD POUR LA DEFINITION DE CONTACT
C IN  RESOCO : SD POUR LA RESOLUTION DE CONTACT
C      
C ----------------------------------------------------------------------
C
      INTEGER      CFMMVD,ZMESX,ZTABF,XXMMVD,ZXAIN
      INTEGER      IFM,NIV
      INTEGER      I,IPC,TYCO
      INTEGER      NDIM,NTMAE,NTPC,NBPC
      INTEGER      CFDISI,MMINFI
      INTEGER      IFACE,IMAE,IZONE,IFAMIN
      INTEGER      JCESD(10),JCESV(10),JCESL(10),IAD
      INTEGER      MMAIT,AMAIT,NMAIT,GROUP,STATUE,STAMIN
      INTEGER      NUMMAE,NUMMIN
      INTEGER      NPTE,NFACE,NVIT,NARET
      REAL*8       GEOM(3),KSIPC1,KSIPC2,WPC
      REAL*8       T1MIN(3),T2MIN(3),XIMIN,YIMIN
      REAL*8       JEUMIN,COOR(3),NORM(3),NOOR
      REAL*8       RRE,RRM
      CHARACTER*8  ALIAS
      CHARACTER*19 CHS(7)
      CHARACTER*24 XFIMAI,CNCTE
      CHARACTER*24 TABFIN,MAESCX
      INTEGER      JTABF,JMAESX,NINTER
      LOGICAL      MMINFL,PROJIN,LCINIT,LGLISS
      INTEGER      JXC,JFIMAI,IFISS,IFISM,IPC2,NUMPI
C      
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
      CALL INFDBG('XFEM',IFM,NIV)
C
C --- AFFICHAGE
C
      IF (NIV.GE.2) THEN
        WRITE (IFM,*) '<XFEM> ... APPARIEMENT'
      ENDIF
C
C --- RECUPERATION DE QUELQUES DONNEES
C
      TABFIN = RESOCO(1:14)//'.TABFIN'
      MAESCX = DEFICO(1:16)//'.MAESCX' 
      XFIMAI  = DEFICO(1:16)//'.XFIMAI'
      CALL JEVEUO(TABFIN,'E',JTABF)
      CALL JEVEUO(MAESCX,'L',JMAESX)    
      CALL JEVEUO(XFIMAI,'L',JFIMAI)    
C      
      ZTABF  = CFMMVD('ZTABF')  
      ZMESX  = CFMMVD('ZMESX') 
      ZXAIN  = XXMMVD('ZXAIN')
C
C --- INITIALISATIONS
C
      CHS(1) = '&&XAPPAR.CHSLO'
      CHS(2) = '&&XAPPAR.CHSAI'
      CHS(3) = '&&XAPPAR.CHSPI'
      CHS(4) = '&&XAPPAR.CHSCF'
      CHS(5) = '&&XAPPAR.CHSGE'
      CHS(6) = '&&XAPPAR.CHSGM'
      CHS(7) = '&&XAPPAR.CHSLT'
      NTPC   = 0
      DO 9 I=1,3
        GEOM(I)  = 0.D0
        T1MIN(I) = 0.D0
        T2MIN(I) = 0.D0
  9   CONTINUE
      NDIM   = CFDISI(DEFICO,'NDIM' )
      NTMAE  = CFDISI(DEFICO,'NTMAE')
C
C --- ON RECUPERE RESPECTIVEMENT :
C --- LES CHAMPS DES GEOMETRIE ESCLAVE ET MAITRE
C --- LES INFOS SUR LE NOMBRES DE PT D'INTER ET LE NBRE DE FACETTE
C --- LES INFOS SUR LES ARETES COUPEES
C --- LES NUMERO LOCAUX DES NOEUDS DES FACETTES DE CONTACT
C --- LE CHAMP NOEUD DE LA LST
C
      CALL CELCES(MODELE//'.TOPOFAC.LO','V',CHS(1))
      CALL CELCES(MODELE//'.TOPOFAC.AI','V',CHS(2))
      CALL CELCES(MODELE//'.TOPOFAC.PI','V',CHS(3))
      CALL CELCES(MODELE//'.TOPOFAC.CF','V',CHS(4))
      CALL CELCES(MODELE//'.TOPOFAC.GE','V',CHS(5))
      CALL CELCES(MODELE//'.TOPOFAC.GM','V',CHS(6))
      CALL CELCES(MODELE//'.LTNO','V',CHS(7))
C
      DO 10 I=1,7
        CALL JEVEUO(CHS(I)//'.CESD','L',JCESD(I))
        CALL JEVEUO(CHS(I)//'.CESV','L',JCESV(I))
        CALL JEVEUO(CHS(I)//'.CESL','L',JCESL(I))
  10  CONTINUE
C
C --- BOUCLE SUR LES MAILLES ESCLAVES
C
      DO 100 IMAE=1,NTMAE
C
C --- ZONE DE CONTACT ET FISSURE LOCALE ASSOCIÉE
C
        IZONE = ZI(JMAESX+ZMESX*(IMAE-1)+2-1)
        IFISS = ZI(JMAESX+ZMESX*(IMAE-1)+5-1)
C
C --- OPTIONS SUR LA ZONE DE CONTACT
C
        TYCO   =  MMINFI(DEFICO,'INTEGRATION'   ,IZONE )
        LGLISS =  MMINFL(DEFICO,'GLISSIERE_ZONE',IZONE )
        LCINIT = (MMINFI(DEFICO,'CONTACT_INIT'  ,IZONE ).EQ.1)
        CNCTE  =  ZK8(JFIMAI-1+IZONE)//'.CNCTE'
C
C --- INFOS SUR LA MAILLE ESCLAVE COURANTE
C
        NUMMAE = ZI(JMAESX+ZMESX*(IMAE-1)+1-1)
        NBPC   = ZI(JMAESX+ZMESX*(IMAE-1)+3-1)
        STATUE = ZI(JMAESX+ZMESX*(IMAE-1)+4-1)
C
        CALL JEVEUO(MODELE//'.XFEM_CONT'  ,'L',JXC)
        IF(NDIM.EQ.2) THEN
          IF(ZI(JXC).LE.2) ALIAS='SE2'
          IF(ZI(JXC).EQ.3) ALIAS='SE3'
        ELSEIF(NDIM.EQ.3) THEN
           ALIAS='TR3'
        ENDIF
C
C --- ON RECUPERE LE NOMBRE DE POINTS D'INTERSECTION
C --- DE LA MAILLE ESCLAVE
C
        CALL CESEXI('C',JCESD(1),JCESL(1),NUMMAE,1,IFISS,1,IAD)
        CALL ASSERT(IAD.GT.0)
        NINTER   = ZI(JCESV(1)-1+IAD)
C
C --- ON RECUPERE LE NOMBRE DE POINTS PAR FACETTE
C --- DE LA MAILLE ESCLAVE
        CALL CESEXI('C',JCESD(1),JCESL(1),NUMMAE,1,IFISS,3,IAD)
        CALL ASSERT(IAD.GT.0)
        NPTE   = ZI(JCESV(1)-1+IAD)
C
C --- ON RECUPERE LE NOMBRE DE FACETTES DE CONTACT DE LA MAILLE ESCLAVE
C
        CALL CESEXI('C',JCESD(1),JCESL(1),NUMMAE,1,IFISS,2,IAD)
        CALL ASSERT(IAD.GT.0)
        NFACE  = MAX(1,ZI(JCESV(1)-1+IAD))

        IF (NBPC.EQ.0) GO TO 100
C
C --- BOUCLE SUR LES FACETTES DE CONTACT
C
        DO 105 IFACE  = 1,NFACE
C
C --- APPARIEMENT - BOUCLE SUR LES POINTS DE CONTACT
C
          DO 110 IPC = 1,NBPC
C
C --- COORDONNEES DANS ELEMENT DE REFERENCE ET POIDS DU POINT DE CONTACT
C
C --- FAUX POINT D'INTEGRATION (POUR L'ÉLIMINATION DES DDL EN TROP)
            IF (STATUE.LT.0) THEN
              DO 120 IPC2 = 1,NPTE
                CALL CESEXI('S',JCESD(4),JCESL(4),NUMMAE,1,IFISS,IPC2,
     &                      IAD)
                NUMPI = ZI(JCESV(4)-1+IAD)
                CALL CESEXI('S',JCESD(2),JCESL(2),NUMMAE,1,IFISS,
     &                  ZXAIN*(NUMPI-1)+1,IAD)
                IF (ZR(JCESV(2)-1+IAD).NE.0) GOTO 130
                CALL CESEXI('S',JCESD(2),JCESL(2),NUMMAE,1,IFISS,
     &                  ZXAIN*(NUMPI-1)+2,IAD)
                IF (ZR(JCESV(2)-1+IAD).NE.0) GOTO 130
 120          CONTINUE
              CALL ASSERT(.FALSE.)
 130          CONTINUE
              CALL MMGAUS(ALIAS ,TYCO  ,IPC2 ,KSIPC1,KSIPC2,
     &                    WPC   )
            ELSE
              CALL MMGAUS(ALIAS ,TYCO  ,IPC  ,KSIPC1,KSIPC2,
     &                    WPC   )
            ENDIF
C
C --- CALCUL DES COORDONNEES REELLES DU POINT DE CONTACT
C
            CALL XCOPCO(JCESD,JCESV,JCESL,IFISS, ALIAS ,NDIM  ,NUMMAE,
     &                  IFACE ,KSIPC1,KSIPC2,NPTE,GEOM  )
C
C --- RECHERCHE DU PT D'INTERSECTION SUR LE COTE MAÎTRE LE PLUS PROCHE
C --- DU POINT DE CONTACT
C
            IF (STATUE.GT.0.AND.STATUE.NE.2) THEN
              CALL XMREPT(JCESD,JCESV,JCESL, IZONE, NDIM  ,DEFICO,
     &                  GEOM  ,STATUE,MMAIT ,AMAIT ,NMAIT )
            ENDIF
C
C --- PROJECTION DU PT DE CONTACT SUR LA FACETTE DE CONTACT
C --- LA PLUS PROCHE
C
            CALL XMREMA(JCESD,JCESV,JCESL, NOMA  ,NDIM  ,IFISS  ,
     &                  DEFICO,IZONE ,ALIAS ,MMAIT ,AMAIT ,
     &                  NMAIT ,STATUE,GEOM  ,NUMMIN,NUMMAE,
     &                  IFAMIN,IFACE ,JEUMIN,T1MIN ,T2MIN ,
     &                  XIMIN ,YIMIN ,PROJIN,STAMIN,IFISM)
C
C --- NUMEROS DES MAILLE ESCLAVE ET MAITRE
C
            ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+1)  = NUMMAE
            ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+2)  = NUMMIN
C
C --- COORDONNEES GEOMETRIQUES DU POINT DE CONTACT
C
            ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+3)  = KSIPC1
            ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+12) = KSIPC2
C
C --- VECTEURS TANGENTS DANS LE POINT DE CONTACT
C
C --- EN 3D, ON ORIENTE LES TANGENTES
C
            IF (NDIM.EQ.3) THEN
C --- ON CALCULE LA NORMALE
              CALL PROVEC(T1MIN ,T2MIN ,NORM  )
              CALL NORMEV(NORM  ,NOOR  )
C --- ON PROJETE LA DIRECTION X SUR SUR LE PLAN TANGENT,
C --- POUR OBTENIR LA PREMIERE DIRECTION TANGENTE
              IF (ABS(NORM(1)).NE.1) THEN
                T1MIN(1) = 1-NORM(1)**2
                T1MIN(2) = -NORM(1)*NORM(2)
                T1MIN(3) = -NORM(1)*NORM(3)
              ELSE
                T1MIN(1) = -NORM(2)*NORM(1)
                T1MIN(2) = 1-NORM(2)**2
                T1MIN(3) = -NORM(2)*NORM(3)
              ENDIF
C --- DEUXIÈME DIRECTION TANGENTE
              CALL PROVEC(NORM  ,T1MIN ,T2MIN )
C
C --- NORMALISATION DES VECTEURS TANGENTS
C
              CALL NORMEV(T1MIN  ,NOOR  )
              CALL NORMEV(T2MIN  ,NOOR  )
C
            ENDIF
            ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+6)  = T1MIN(1)
            ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+7)  = T1MIN(2)
            ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+8)  = T1MIN(3)
            ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+9)  = T2MIN(1)
            ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+10) = T2MIN(2)
            ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+11) = T2MIN(3)
C
C --- NUMERO DE LA ZONE DE FISSURE
C
            ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+15) = IZONE
C
C --- POIDS DU POINT DU CONTACT
C
            ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+16) = WPC
C
C --- NOMBRE DE POINTS D'INTERSECTIONS ESCLAVE
C
            ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+24) = NPTE
            ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+14) = NINTER
C
C --- NUMERO DE LA FACETTE ESCLAVE
C
            ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+25) = IFACE
C
C --- NOMBRE DE FACETTES ESCLAVES
C
            ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+26) = NFACE
C
C --- COORDONNEES DANS L'ELEMENT PARENT MAITRE
C
            CALL XMCOOR(JCESD,JCESV,JCESL,IFISM,NDIM ,NPTE ,
     &                  NUMMIN,IFAMIN,XIMIN ,YIMIN ,COOR  )
            ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+20) = COOR(1)
            ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+21) = COOR(2)
            ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+23) = COOR(3)
C
C --- STATUT DE LA MAILLE MAITRE
C
            ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+32) = STAMIN
C
C --- CALCUL DE SQRT LST DU PT DE CONTACT MAITRE
C
            CALL XMRLST(JCESD,JCESV,JCESL, NOMA ,NUMMIN,COOR  ,RRM  )
            ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+31) = RRM
C
C --- NUMÉRO LOCALE DE FISSURE MAITRE
C
            ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+34) = IFISM
C
            IF (LOPTIN) THEN
C
C --- NUMÉRO LOCALE DE FISSURE ESCLAVE
C
              ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+33) = IFISS
C
C --- COORDONNEES DANS L'ELEMENT PARENT ESCLAVE
C
              CALL XMCOOR(JCESD,JCESV,JCESL,IFISS,NDIM,NPTE  ,
     &                    NUMMAE,IFACE ,KSIPC1,KSIPC2,COOR  )
              ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+17) = COOR(1)
              ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+18) = COOR(2)
              ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+19) = COOR(3)
C
C --- CALCUL DE SQRT LST DU PT DE CONTACT ESCLAVE
C
              CALL XMRLST(JCESD,JCESV,JCESL,NOMA  ,NUMMAE,COOR  ,RRE  )
              ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+30) = RRE
C
C --- POINT D'INTEGRATION VITAL OU PAS
C --- NUMERO DE GROUPE ET D'ARETE (SI LE PT EST SUR UNE ARETE CONNECTÉE)
C
              
              IF (MOD(TYCO,10) .EQ . 2) THEN
C---- CAS DES SCHEMAS DE GAUSS OU LE PT N'EST PAS SUR UNE ARETE
                NVIT  = 1
                GROUP = 0
                NARET = 0
              ELSE
                CALL XPIVIT(JCESD,JCESV,JCESL,IFISS,CNCTE ,NDIM ,NUMMAE,
     &                    IFACE ,KSIPC1,KSIPC2,NVIT  ,GROUP  ,
     &                    NARET )
                IF (STATUE.LT.0) NVIT=0
              ENDIF
C
              ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+27) = NVIT
              ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+4)  = GROUP
              ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+5)  = NARET
C
C --- CONTACT_INIT (13) ET MEMOIRE DE CONTACT (28) LA MEMO DE CONTACT
C --- EST INITIALISEE AVEC CONTACT_INI ET SERT POUR LE CONTACT GLISSIERE
C
              IF (LCINIT) THEN
                ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+13) = 1.D0
                ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+28) = 1.D0
              ELSE
                ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+13) = 0.D0
                ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+28) = 0.D0
              ENDIF
C
C --- ON RENSEIGNE LE CONTACT GLISSIERE SI DECLARE
C
              IF (LGLISS) THEN
                ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+29) = 1.D0
              ELSE
                ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+29) = 0.D0
              ENDIF
            ENDIF
C
C --- NOEUDS EXCLUS PAR PROJECTION HORS ZONE
C
            IF (.NOT. PROJIN) THEN
              ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+22) = 1.D0
            ELSE
              ZR(JTABF+ZTABF*NTPC+ZTABF*(IPC-1)+22) = 0.D0
            ENDIF
 110      CONTINUE
        NTPC = NTPC + NBPC
 105    CONTINUE
 100  CONTINUE
      ZR(JTABF-1+1) = NTPC
      CALL ASSERT(NTPC.EQ.CFDISI(DEFICO,'NTPC'))
C
C --- MENAGE
C
      DO 200 I=1,7
        CALL DETRSD('CHAM_ELEM_S',CHS(I))
 200  CONTINUE
C
      CALL JEDEMA()
      END
