      SUBROUTINE  XSIDE3(ELREFP,NDIM,COORSE,ELRESE,IGEOM,HE,NFH,DDLC,
     &                   DDLM,NFE,BASLOC,NNOP,NPG,IMATE,COMPOR,DEPL,LSN,
     &                   LST,NFISS,FISNO,SIG)

      IMPLICIT NONE
      INTEGER       NDIM,IGEOM,IMATE,NNOP,NPG,NFH,DDLC,DDLM,NFE
      INTEGER       NFISS,FISNO(NNOP,NFISS)
      CHARACTER*8   ELREFP,ELRESE
      CHARACTER*16  COMPOR(4)
      CHARACTER*24  COORSE
      REAL*8        BASLOC(9*NNOP),HE(NFISS)
      REAL*8        DEPL(NDIM*(1+NFH+NFE)+DDLC,NNOP)
      REAL*8        DEPL2(NDIM*(1+NFH+NFE)+DDLC,NNOP)
      REAL*8        LSN(NNOP),LST(NNOP)
      REAL*8        SIG(6,NPG)

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 20/09/2011   AUTEUR MICOL A.MICOL 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE GENIAUT S.GENIAUT
C TOLE CRP_21 CRS_1404
C.......................................................................
C
C     BUT:  CALCUL DES OPTIONS SIEF_ELGA AVEC X-FEM EN 3D
C.......................................................................

C IN  ELREFP  : ÉLÉMENT DE RÉFÉRENCE PARENT
C IN  NDIM    : DIMENSION DE L'ESPACE
C IN  COORSE  : COORDONNÉES DES SOMMETS DU SOUS-ÉLÉMENT
C IN  IGEOM   : COORDONNÉES DES NOEUDS DE L'ÉLÉMENT PARENT
C IN  HE      : VALEUR DE LA FONCTION HEAVISIDE SUR LE SOUS-ÉLT
C IN  NFH     : NOMBRE DE FONCTIONS HEAVYSIDE
C IN  DDLC    : NOMBRE DE DDL DE CONTACT (PAR NOEUD)
C IN  DDLM    : NOMBRE DE DDL PAR NOEUD MILIEU
C IN  NFE     : NOMBRE DE FONCTIONS SINGULIÈRES D'ENRICHISSEMENT
C IN  BASLOC  : BASE LOCALE AU FOND DE FISSURE AUX NOEUDS
C IN  NNOP    : NOMBRE DE NOEUDS DE L'ELEMENT PARENT
C IN  NPG     : NOMBRE DE POINTS DE GAUSS DU SOUS-ÉLÉMENT
C IN  IMATE   : MATERIAU CODE
C IN  COMPOR  : COMPORTEMENT
C IN  DEPL    : DEPLACEMENT A PARTIR DE LA CONF DE REF
C IN  LSN     : VALEUR DE LA LEVEL SET NORMALE AUX NOEUDS PARENTS
C IN  LST     : VALEUR DE LA LEVEL SET TANGENTE AUX NOEUDS PARENTS
C IN  NFISS   : NOMBRE DE FISSURES "VUES" PAR L'ÉLÉMENT
C IN  JFISNO  : POINTEUR DE CONNECTIVITÉ FISSURE/HEAVISIDE

C OUT SIG     : CONTRAINTES (SIEF_ELGA)
C......................................................................
C
C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ---------------------
      INTEGER  ZI
      COMMON  / IVARJE / ZI(1)
      REAL*8             ZR
      COMMON  / RVARJE / ZR(1)
      COMPLEX*16         ZC
      COMMON  / CVARJE / ZC(1)
      LOGICAL            ZL
      COMMON  / LVARJE / ZL(1)
      CHARACTER*8        ZK8
      CHARACTER*16                ZK16
      CHARACTER*24                          ZK24
      CHARACTER*32                                    ZK32
      CHARACTER*80                                              ZK80
      COMMON  / KVARJE / ZK8(1) , ZK16(1) , ZK24(1) , ZK32(1) , ZK80(1)
C --------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ---------------------
C
      CHARACTER*2  K2BID
      INTEGER  KPG,N,I,J,IRET
      INTEGER  NNO,NPGBIS,DDLT,DDLD,NDIMB,INO
      INTEGER  JCOOPG,JDFD2,JGANO,JCOORS,IDFDE,IVF,IPOIDS,IDEPL
      INTEGER  NBSIGM,NBSIG,NNOPS
      LOGICAL  GRDEPL
      REAL*8   F(3,3),EPS(6),BASLOG(9)
      REAL*8   FE(4),REPERE(7),INSTAN,LSNG,LSTG
      REAL*8   XG(NDIM),XE(NDIM),FF(NNOP)
      REAL*8   D(6,6),ZERO,S
      REAL*8   DFDI(NNOP,NDIM),DGDGL(4,3)
      REAL*8   GRAD(3,3)
C
C--------------------------------------------------------------------
C
C     ATTENTION, DEPL ET VECTU SONT ICI DIMENSIONNÉS DE TELLE SORTE
C     QU'ILS NE PRENNENT PAS EN COMPTE LES DDL SUR LES NOEUDS MILIEU

C     NOMBRE DE DDL DE DEPLACEMENT À CHAQUE NOEUD SOMMET
      DDLD=NDIM*(1+NFH+NFE)

C     NOMBRE DE DDL TOTAL (DEPL+CONTACT) À CHAQUE NOEUD SOMMET
      DDLT=DDLD+DDLC


      K2BID  = '  '
      ZERO   = 0.0D0

      GRDEPL  = COMPOR(3) .EQ. 'GROT_GDEP'
C ---- NOMBRE DE CONTRAINTES ASSOCIE A L'ELEMENT
C      -----------------------------------------
      NBSIG = NBSIGM()

C ---- RECUPERATION DU CHAMP DE DEPLACEMENT SUR L'ELEMENT
C      --------------------------------------------------
      CALL JEVECH('PDEPLAR','L',IDEPL)
C
C     ADRESSE DES COORD DU SOUS ELT EN QUESTION
      CALL JEVEUO(COORSE,'L',JCOORS)
C
C       TE4-'XINT' : SCHÉMAS À 15 POINTS
      CALL ELREF5(ELRESE,'XINT',NDIMB,NNO,NNOPS,NPGBIS,IPOIDS,JCOOPG,
     &                  IVF,IDFDE,JDFD2,JGANO)

      CALL ASSERT(NPG.EQ.NPGBIS.AND.NDIM.EQ.NDIMB)

C-----------------------------------------------------------------------
C     BOUCLE SUR LES POINTS DE GAUSS DU SOUS-TÉTRA
      DO 100 KPG=1,NPG

C       COORDONNÉES DU PT DE GAUSS DANS LE REPÈRE RÉEL : XG
        CALL VECINI(NDIM,0.D0,XG)
        DO 110 I=1,NDIM
          DO 111 N=1,NNO
            XG(I)=XG(I)+ZR(IVF-1+NNO*(KPG-1)+N)*ZR(JCOORS-1+3*(N-1)+I)
 111      CONTINUE
 110    CONTINUE

C       JUSTE POUR CALCULER LES FF
        CALL REERE3(ELREFP,NNOP,IGEOM,XG,DEPL,GRDEPL,NDIM,HE,
     &              FISNO,NFISS,NFH,NFE,DDLT,FE,DGDGL,'NON',
     &              XE,FF,DFDI,F,EPS,GRAD)

        IF (NFE.GT.0) THEN
C         BASE LOCALE  ET LEVEL SETS AU POINT DE GAUSS
          CALL VECINI(9,0.D0,BASLOG)
          LSNG = 0.D0
          LSTG = 0.D0
          DO 113 INO=1,NNOP
            LSNG = LSNG + LSN(INO) * FF(INO)
            LSTG = LSTG + LST(INO) * FF(INO)
            DO 114 I=1,9
              BASLOG(I) = BASLOG(I) + BASLOC(9*(INO-1)+I) * FF(INO)
 114        CONTINUE
 113      CONTINUE
C
C         FONCTION D'ENRICHISSEMENT AU POINT DE GAUSS ET LEURS DÉRIVÉES
          CALL XCALFE(HE,LSNG,LSTG,BASLOG,FE,DGDGL,IRET)
C         ON A PAS PU CALCULER LES DERIVEES DES FONCTIONS SINGULIERES
C         CAR ON SE TROUVE SUR LE FOND DE FISSURE
          CALL ASSERT(IRET.NE.0)

        ENDIF

C       CALCUL DES DEFORMATIONS EPS
        CALL REERE3(ELREFP,NNOP,IGEOM,XG,DEPL,GRDEPL,NDIM,HE,
     &              FISNO,NFISS,NFH,NFE,DDLT,FE,DGDGL,'OUI',
     &              XE,FF,DFDI,F,EPS,GRAD)

C  --      CALCUL DE LA MATRICE DE HOOKE (LE MATERIAU POUVANT
C  --      ETRE ISOTROPE, ISOTROPE-TRANSVERSE OU ORTHOTROPE)
C          -------------------------------------------------
        CALL DMATMC('RIGI',K2BID,IMATE, INSTAN, '+', KPG, 1,
     +                REPERE, XG, NBSIG, D, .FALSE.)

C --- VECTEUR DES CONTRAINTES
C      ----------------------
      DO 30 I = 1, NBSIG 
C
          S = ZERO
C
          DO 40 J = 1, NBSIG
             S = S + EPS(J)*D(I,J)
  40      CONTINUE
C
          SIG(I,KPG) = S
  30  CONTINUE
  100 CONTINUE
      END
