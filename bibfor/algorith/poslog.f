      SUBROUTINE POSLOG(RESI,RIGI,TN,TP,FM,LGPG,VIP,NDIM,FP,G,DTDE,SIGM,
     &                  CPLAN,FAMI,MATE,INSTP,ANGMAS,GN,LAMB,LOGL,SIGP,
     &                  DSIDEP,PK2M,PK2,CODRET)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 10/09/2012   AUTEUR PROIX J-M.PROIX 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C TOLE CRP_21
C TOLE CRS_1404
C ----------------------------------------------------------------------
C     BUT:  POST TRAITEMENT GRANDES DEFORMATIONS 2D ET 3D LOG
C     SUIVANT ARTICLE MIEHE APEL LAMBRECHT CMAME 2002
C     CONFIGURATION LAGRANGIENNE
C ----------------------------------------------------------------------
C IN  RESI    : .TRUE. SI FULL_MECA/RAPH_MECA .FALSE. SI RIGI_MECA_TANG
C IN  RIGI    : .TRUE. SI FULL_MECA/RIGI_MECA_TANG
C IN  TN      : CONTRAINTES ASSOCIEES AUX DEF. LOGARITHMIQUES EN T-
C IN  TP      : CONTRAINTES ASSOCIEES AUX DEF. LOGARITHMIQUES EN T+
C IN  FM      : GRADIENT TRANSFORMATION EN T-
C IN  LGPG    : DIMENSION DU VECTEUR DES VAR. INTERNES POUR 1 PT GAUSS
C VAR VIP     : VARIABLES INTERNES EN T+
C IN  NDIM    : DIMENSION DE L'ESPACE
C IN  FP      : GRADIENT TRANSFORMATION EN T+
C IN  PES     : OPERATEUR DE TRANSFORMATION TN (OU TP) EN PK2
C IN  G       : NUMERO DU POINTS DE GAUSS
C IN  DTDE    : OPERATEUR TANGENT ISSU DE NMCOMP (6,6)
C IN  SIGM    : CONTRAINTES DE CAUCHY EN T-
C IN  GN      : TERMES UTILES AU CALCUL DE TL DANS POSLOG
C IN  FETA    : TERMES UTILES AU CALCUL DE TL DANS POSLOG
C IN  XI      : TERMES UTILES AU CALCUL DE TL DANS POSLOG
C IN  ME      : TERMES UTILES AU CALCUL DE TL DANS POSLOG
C OUT SIGP    : CONTRAINTES DE CAUCHY EN T+
C
      IMPLICIT NONE
      INTEGER NDIM,I,J,KL,G,LGPG,IVTN,MATE,CODRET
      CHARACTER*(*) FAMI
      REAL*8  DTDE(6,6),TRAV(6,6),TRAV2(6,6)
      REAL*8  PES(6,6),SIGM(2*NDIM),SIGP(2*NDIM),TP2(6),TN(6)
      REAL*8  FR(3,3),VIP(LGPG)
      REAL*8  DETF,DSIDEP(6,6),RAC2,INSTP,ANGMAS(*)
      REAL*8  PK2(6),TP(6),FP(3,3),FM(3,3),PK2M(6)
      REAL*8  TL(3,3,3,3),TLS(6,6),EPSE(4),D1(4,4)
      REAL*8  GN(3,3),FETA(4),XI(3,3),ME(3,3,3,3),LAMB(3),LOGL(3)
      LOGICAL RESI, RIGI, CPLAN
C ---------------------------------------------------------------------
C********************CONTRAINTE ET FORCES INTERIEURES******************
        
      RAC2=SQRT(2.D0)
      CODRET=0


C     CALCUL DES PRODUITS SYMETR. DE F PAR N
      IF (RESI) THEN
        CALL DCOPY(9,FP,1,FR,1)
      ELSE
        CALL DCOPY(9,FM,1,FR,1)
      ENDIF    
      
C     DETERMINANT DE LA MATRICE F A L INSTANT T+ 
      CALL LCDETF(NDIM,FR,DETF)

C     PERTINENCE DES GRANDEURS
      IF (DETF.LE.1.D-2 .OR. DETF.GT.1.D2) THEN
        CODRET = 1
        GOTO 9999
      END IF
      
C CORRECTION POUR LES CONTRAINTES PLANES
C NE FONCTIONNE QUE SI DET(F_PLAS)=1  SOIT DEF. PLAS. INCOMPRESSIBLES
C CF. COMP. METHODES FOR PLASTICITY - DE SOUZA-NIETO, PERIC, OWEN p.603

      IF (CPLAN) THEN
         CALL R8INIR(4,0.D0,EPSE,1)
         IF (RESI) THEN
            CALL D1MACP(FAMI,MATE,INSTP,'+',G,1,ANGMAS,D1)
            DO 10 I=1,4
            DO 10 J=1,4
               EPSE(I)=EPSE(I)+D1(I,J)*TP(J)
 10         CONTINUE
C           EN ELASTICITE ISTROPE
            EPSE(3)=D1(1,2)*(TP(1)+TP(2))
         ELSE
            CALL D1MACP(FAMI,MATE,INSTP,'-',G,1,ANGMAS,D1)
            DO 11 I=1,4
            DO 11 J=1,4
               EPSE(I)=EPSE(I)+D1(I,J)*TN(J)
 11         CONTINUE
C           EN ELASTICITE ISTROPE
            EPSE(3)=D1(1,2)*(TN(1)+TN(2))
         ENDIF
         DETF=EXP(EPSE(1)+EPSE(2)+EPSE(3))
      ENDIF

C ********************* TENSEUR DE PASSAGE DE T A PK2*******************
      
      CALL DEFLG2(GN,LAMB,LOGL,PES,FETA,XI,ME)

      
C *********************MATRICE TANGENTE(SYMETRIQUE)*********************
      IF (RIGI) THEN
      
C        POUR LA RIGIDITE GEOMETRIQUE : CALCUL AVEC LES PK2
         CALL R8INIR(6,0.D0,TP2,1) 
         IF (.NOT.RESI) THEN
            CALL PK2SIG(NDIM,FM,DETF,PK2M,SIGM,-1)
            DO 54 KL=4,2*NDIM
               PK2M(KL)=PK2M(KL)*RAC2
 54         CONTINUE
            CALL DCOPY(6,TN,1,TP2,1)
         ELSE
            CALL DCOPY(6,TP,1,TP2,1)
         ENDIF
 
         CALL DEFLG3(GN,FETA,XI,ME,TP2,TL)
         
         CALL SYMT46(TL,TLS)

         CALL R8INIR(36,0.D0,DSIDEP,1) 
         CALL LCTR2M(6, PES , TRAV )
         CALL PMAT(6,TRAV,DTDE,TRAV2)
         CALL PMAT(6,TRAV2,PES,DSIDEP)
         
         CALL DAXPY(36,1.D0,TLS,1,DSIDEP,1)
         
      END IF
      
      IF (RESI) THEN

C        TRANSFORMATION DU TENSEUR T EN PK2    
      
         CALL R8INIR(6,0.D0,PK2,1)
         DO 51 I=1,6
            DO 52 J=1,6
               PK2(I)=PK2(I)+TP(J)*PES(J,I)
 52         CONTINUE
 51      CONTINUE
C        CALCUL DES CONTRAINTES DE CAUCHY, CONVERSION LAGRANGE -> CAUCHY
         CALL PK2SIG(NDIM,FP,DETF,PK2,SIGP,1)

CC       --------------------------------
CC       pour gagner du temps : on stocke TP comme variable interne   
CC       --------------------------------
         IVTN=LGPG-6+1
         CALL DCOPY(2*NDIM,TP,1,VIP(IVTN),1)

      END IF

 9999 CONTINUE
 
      END
