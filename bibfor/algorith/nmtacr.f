      SUBROUTINE NMTACR(MODE,NDIMSI,MAT,SIGEL,VIM,EPM,DP,SP,XI,
     &                  F,G,FDS,GDS,FDP,GDP,FDX,GDX,DPMAX,SIG,TANG)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 20/03/2000   AUTEUR GJBHHEL E.LORENTZ 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================

      IMPLICIT NONE
      INTEGER MODE,NDIMSI
      REAL*8  MAT(14),SIGEL(NDIMSI),VIM(9),EPM(6),DP,SP,XI
      REAL*8  F,G,FDP,FDS,FDX,GDP,GDS,GDX,DPMAX,SIG(NDIMSI),TANG(6,6)


C ----------------------------------------------------------------------
C TAHERI :  EVALUATION DES CRITERES ET DERIVEES
C ----------------------------------------------------------------------
C IN  MODE   SELECTION DES TERMES A CALCULER
C             0 -> DPMAX
C             1 -> F,G
C             2 -> F, G, FDS, GDS, FDP, GDP
C             3 -> F, G, FDS, GDS, FDX, GDX
C             4 -> SIG
C             5 -> TANG (PLASTICITE CLASSIQUE)
C             6 -> TANG (PLASTICITE A DEUX SURFACES)
C IN  NDIMSI DIMENSION DES TENSEURS
C IN  MAT    TABLEAU DES CONSTANTES MATERIAUX
C IN  SIGEL  DEVIATEUR DES CONTRAINTES ELASTIQUES
C IN  VIM    VARIABLES INTERNES EN T-
C IN  EPM    DEFORMATION PLASTIQUE EN T-
C IN  DP     INCREMENT DE DEFORMATION PLASTIQUE CUMULEE
C IN  SP     CONTRAINTE DE PIC (SIGMA P)
C IN  XI     PILOTAGE DE EPN
C OUT F      VALEUR DU CRITERE DE PLASTICITE
C OUT G      VALEUR DU CRITERE DE CONTRAINTE MAXIMALE
C OUT FDS    DERIVEE DE F PAR RAPPORT A SIGMA P
C OUT GDS    DERIVEE DE G PAR RAPPORT A SIGMA P
C OUT FDP    DERIVEE DE F PAR RAPPORT A P
C OUT GDP    DERIVEE DE G PAR RAPPORT A P
C OUT FDX    DERIVEE DE F PAR RAPPORT A XI
C OUT GDX    DERIVEE DE G PAR RAPPORT A XI
C OUT DPMAX  BORNE SUPERIEURE POUR L'INCREMENT DE DEFO. PLAST.
C OUT SIG    CORRECTION DE CONTRAINTE (DEUXMU * DEP)
C OUT TANG   VARIATION :  DEP/SIGEL * PROJDEV
C ----------------------------------------------------------------------

      INTEGER N,I,J
      REAL*8  EPNDX(6),EPN(6)
      REAL*8  C,D,A(6),B(6),SE(6),S0(6),SEEQ
      REAL*8  V(6),SEMAX
      REAL*8  P,SEQ,M(6),MEQ,L(6),LEQ,Q
      REAL*8  TMP,CDP,DDP,SEDP(6),SEEQDP,S0DP(6),SEQDP
      REAL*8  MDP(6),MEQDP,QDP,LDP(6),LEQDP
      REAL*8  CDS,DDS,ADS(6),SEDS(6),SEEQDS,S0DS(6),SEQDS
      REAL*8  MDS(6),MEQDS,QDS,LDS(6),LEQDS
      REAL*8  ADX(6),BDX(6),SEDX(6),SEEQDX,S0DX(6),SEQDX
      REAL*8  MDX(6),MEQDX,LDX(6),LEQDX,QDX
      REAL*8  PBS0,SEQDE(6),MEQDE(6),QDE(6),FDE(6)
      REAL*8  PAS0,LEQDE(6),GDE(6)
      REAL*8  PDE(6),DET
      REAL*8  R8DOT



C -- CALCUL DES ELEMENTS COMMUNS

      P   = VIM(1) + DP
      TMP = EXP(- MAT(8) * P * (1.D0 - SP / MAT(11) ))
      C   = MAT(10) + MAT(9) * TMP
      D   = 1.D0    - MAT(6) * TMP
      DO 10 N=1,NDIMSI
        EPNDX(N) =   VIM(2+N) - EPM(N)
        B(N)     = - XI * EPNDX(N)
        EPN(N)   =   EPM(N) - B(N)
        A(N)     =   (MAT(11)-SP)*EPM(N) + SP*B(N)
        SE(N)    =   SIGEL(N) - C*A(N)
 10   CONTINUE
      SEEQ = SQRT(1.5D0*R8DOT(NDIMSI,SE,1,SE,1))


C -- CALCUL D'UNE BORNE POUR DP

      IF (MODE.EQ.0) THEN
        DO 15 N = 1,NDIMSI
          V(N) = SIGEL(N) - MAT(10) * A(N)
 15     CONTINUE
        SEMAX = SQRT(1.5D0*R8DOT(NDIMSI,V,1,V,1))
        IF (SEEQ.GT.SEMAX) SEMAX=SEEQ
        DPMAX = (SEMAX - D*MAT(4)) / 1.5D0 / (MAT(2)+MAT(11)*MAT(10))
        GOTO 9999
      END IF


C -- CALCUL DE SIGMA ET DES CRITERES F ET G

      IF (SEEQ.NE.0.D0) THEN
        DO 20 N=1,NDIMSI
          S0(N) = SE(N)/SEEQ
 20     CONTINUE
      ELSE
        DO 21 N=1,NDIMSI
          S0(N) = 0.D0
 21     CONTINUE
        S0(4) = 1.D0 / SQRT(1.5D0)
      END IF

      IF (MODE.EQ.4) THEN
        DO 23 N = 1,NDIMSI
          SIG(N) = 1.5D0*MAT(2) * DP * S0(N)
 23     CONTINUE
        GOTO 9999
      END IF

      DO 25 N = 1,NDIMSI
        M(N) = B(N) + 1.5D0*DP*S0(N)
        L(N) = A(N) + 1.5D0 * MAT(11) * DP * S0(N)
 25   CONTINUE

      SEQ = SEEQ - 1.5D0 * (MAT(2) + MAT(11)*C) * DP
      MEQ = SQRT(1.5D0*R8DOT(NDIMSI,M,1,M,1))
      LEQ = SQRT(1.5D0*R8DOT(NDIMSI,L,1,L,1))
      Q   = MAT(4) + MAT(7) * MEQ**MAT(5)

      F = SEQ - D*Q
      IF (DP.NE.0.D0) F = F - MAT(13) * DP**MAT(12) * P**MAT(14)
      G = C*LEQ + D*Q  - SP
      IF (MODE.EQ.1) GOTO 9999


C -- DERIVEES DES CRITERES PAR RAPPORT A SIGMA PIC

      TMP = MAT(8)*P/MAT(11) * EXP(-MAT(8)*P*(1-SP/MAT(11)))
      CDS =   MAT(9)*TMP
      DDS = - MAT(6)*TMP

      DO 30 N = 1,NDIMSI
        ADS(N)  = - EPN(N)
        SEDS(N) = - CDS*A(N) - C*ADS(N)
 30   CONTINUE
      SEEQDS = 1.5D0*R8DOT(NDIMSI,SE,1,SEDS,1)/SEEQ
      SEQDS  = SEEQDS - 1.5D0*MAT(11)*CDS*DP

      DO 40 N = 1,NDIMSI
        S0DS(N) = (SEDS(N)*SEQ - SEEQDS*SE(N)) / SEEQ**2
        MDS(N)  = 1.5D0 * DP * S0DS(N)
        LDS(N)  = ADS(N) + 1.5D0*MAT(11)*DP*S0DS(N)
 40   CONTINUE
      MEQDS = 1.5D0 * R8DOT(NDIMSI,M,1,MDS,1) / MEQ
      LEQDS = 1.5D0 * R8DOT(NDIMSI,L,1,LDS,1) / LEQ
      QDS   = MAT(7) * MAT(5) * MEQ**(MAT(5)-1) * MEQDS

      FDS = SEQDS - DDS*Q - D*QDS
      GDS = CDS*LEQ + C*LEQDS + DDS*Q + D*QDS - 1.D0


C -- DERIVEE DES CRITERES PAR RAPPORT A P

      IF (MODE.EQ.2 .OR. MODE.GE.5) THEN
        TMP = - MAT(8)*(1-SP/MAT(11)) * EXP(-MAT(8)*P*(1-SP/MAT(11)))
        CDP =   MAT(9)*TMP
        DDP = - MAT(6)*TMP

        DO 50 N = 1,NDIMSI
          SEDP(N) = - CDP*A(N)
 50     CONTINUE
        SEEQDP = 1.5D0*R8DOT(NDIMSI,SE,1,SEDP,1)/SEEQ
        SEQDP  = SEEQDP - 1.5D0 * (MAT(2) + MAT(11)*C + MAT(11)*CDP*DP)

        DO 60 N = 1,NDIMSI
          S0DP(N) = (SEDP(N)*SEEQ - SE(N)*SEEQDP) / SEEQ**2
          MDP(N)  = 1.5D0 * (S0(N)+DP*S0DP(N))
          LDP(N)  = MAT(11) * MDP(N)
 60     CONTINUE
        MEQDP = 1.5D0 * R8DOT(NDIMSI,M,1,MDP,1)/MEQ
        LEQDP = 1.5D0 * R8DOT(NDIMSI,L,1,LDP,1)/LEQ
        QDP = MAT(7) * MAT(5) * MEQ**(MAT(5)-1) * MEQDP

        FDP = SEQDP - DDP*Q - D*QDP - MAT(13) * (MAT(14)*DP+MAT(12)*P)
     &        * P**(MAT(14)-1) * DP**(MAT(12)-1)
        GDP = CDP*LEQ + C*LEQDP + DDP*Q + D*QDP

        IF (MODE.EQ.2) GOTO 9999
      END IF


C -- DERIVEES PAR RT A SIGEL ET CONSTRUCTION DE LA MATRICE TANGENTE
      IF (MODE.EQ.5 .OR. MODE.EQ.6) THEN
        TMP  = 1.5D0/MEQ * 1.5D0*DP/SEEQ
        PBS0 = 1.5D0 * R8DOT(NDIMSI,B,1,S0,1)
        DO 62 N = 1,NDIMSI
          SEQDE(N) = 1.5D0*S0(N)
          MEQDE(N) = TMP * (B(N) - PBS0*S0(N))
          QDE(N)   = MAT(7) * MAT(5) * MEQ**(MAT(5)-1) * MEQDE(N)
          FDE(N)   = SEQDE(N) - D*QDE(N)
 62     CONTINUE

        IF (MODE.EQ.6) THEN
          TMP  = 1.5D0/LEQ * 1.5D0*MAT(11)*DP/SEEQ
          PAS0 = 1.5D0 * R8DOT(NDIMSI,A,1,S0,1)
          DO 64 N = 1,NDIMSI
            LEQDE(N) = TMP * (A(N) - PAS0*S0(N))
            GDE(N)   = C*LEQDE(N) + D*QDE(N)
 64       CONTINUE
        END IF

        IF (MODE.EQ.5) THEN
          DO 66 N = 1,NDIMSI
            PDE(N) = - FDE(N)/FDP
 66       CONTINUE
        ELSE
          DET = FDP*GDS - GDP*FDS
          DO 68 N = 1,NDIMSI
            PDE(N) = (FDS*GDE(N) - GDS*FDE(N)) / DET
 68       CONTINUE
        END IF

        TMP = 1.5D0*DP/SEEQ
        DO 70 I = 1,NDIMSI
          DO 71 J = 1,NDIMSI
            TANG(I,J) = 1.5D0*(S0(I)*PDE(J) - TMP*S0(I)*S0(J))
 71       CONTINUE
 70     CONTINUE
        DO 72 N = 1,NDIMSI
          TANG(N,N) = TANG(N,N) + TMP
 72     CONTINUE
        DO 73 I = 1,3
          DO 74 J = 1,3
            TANG(I,J) = TANG(I,J) - TMP/3.D0
 74       CONTINUE
 73     CONTINUE

        GOTO 9999
      END IF


C -- DERIVEES DES CRITERES PAR RAPPORT A XI

      IF (MODE.EQ.3) THEN
        DO 80 N = 1,NDIMSI
          ADX(N)  = - SP * EPNDX(N)
          BDX(N)  = - EPNDX(N)
          SEDX(N) = - C * ADX(N)
 80     CONTINUE

        SEEQDX = 1.5D0*R8DOT(NDIMSI,SE,1,SEDX,1)/SEEQ
        SEQDX  = SEEQDX

        DO 90 N = 1,NDIMSI
          S0DX(N) = (SEDX(N)*SEQ - SEEQDX*SE(N)) / SEEQ**2
          MDX(N)  = BDX(N) + 1.5D0 * DP * S0DX(N)
          LDX(N)  = ADX(N) + 1.5D0 * MAT(11) * DP * S0DX(N)
 90     CONTINUE
        MEQDX = 1.5D0 * R8DOT(NDIMSI,M,1,MDX,1) / MEQ
        LEQDX = 1.5D0 * R8DOT(NDIMSI,L,1,LDX,1) / LEQ
        QDX   = MAT(7) * MAT(5) * MEQ**(MAT(5)-1) * MEQDX

        FDX = SEQDX - D*QDX
        GDX = C*LEQDX + D*QDX

        GOTO 9999
      END IF


 9999 CONTINUE
      END
