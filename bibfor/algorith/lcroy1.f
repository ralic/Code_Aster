      REAL*8 FUNCTION LCROY1 ()
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 16/10/2007   AUTEUR SALMONA L.SALMONA 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE ADBHHVV V.CANO
      IMPLICIT NONE

C ********************************************************************
C *       INTEGRATION DE LA LOI DE ROUSSELIER LOCAL                  *
C *  CALCUL DE BORNES INF ET SUP DE LA FONCTION SEUIL(Y) QUAND S(0)>0*
C *  ET RESOLUTION DE SEUIL(Y)=0                                     *
C *  PAR UNE METHODE DE NEWTON AVEC BORNES CONTROLEES ET DICHOTOMIE  *
C *  - LA BORNE INFERIEURE EST TELLE QUE:                            *
C *    2*MU*EQTR-R(PM+DPINF)=3*MU*DPINF      => ON EN DEDUIT DPINF   *
C *    YINF*EXP(YINF)=K*FONC*DPINF/SIG1      => ON EN DEDUIT YINF    *
C *  - LA BORNE SUPERIEURE EST TELLE QUE:                            *
C *    YSUP*EXP(YSUP) =K*FONC*(2*MU*EQTR-S(YINF))/3*MU*SIG1          *
C ********************************************************************

C OUT  Y   : VALEUR DE Y TEL QUE SEUIL(Y)=0

C ----------------------------------------------------------------------
C ----------------------------------------------------------------------
C  COMMON LOI DE COMPORTEMENT ROUSSELIER

      INTEGER ITEMAX, JPROLP, JVALEP, NBVALP
      REAL*8  PREC,YOUNG,NU,SIGY,SIG1,ROUSD,F0,FCR,ACCE
      REAL*8  PM,RPM,FONC,FCD,DFCDDJ,DPMAXI
      COMMON /LCROU/ PREC,YOUNG,NU,SIGY,SIG1,ROUSD,F0,FCR,ACCE,
     &               PM,RPM,FONC,FCD,DFCDDJ,DPMAXI,
     &               ITEMAX, JPROLP, JVALEP, NBVALP
C ----------------------------------------------------------------------
C  COMMON GRANDES DEFORMATIONS CANO-LORENTZ

      INTEGER IND1(6),IND2(6)
      REAL*8  KR(6),RAC2,RC(6)
      REAL*8  LAMBDA,MU,DEUXMU,UNK,TROISK,COTHER
      REAL*8  JM,DJ,JP,DJDF(3,3)
      REAL*8  ETR(6),DVETR(6),EQETR,TRETR,DETRDF(6,3,3)
      REAL*8  DTAUDE(6,6)

      COMMON /GDCLC/
     &          IND1,IND2,KR,RAC2,RC,
     &          LAMBDA,MU,DEUXMU,UNK,TROISK,COTHER,
     &          JM,DJ,JP,DJDF,
     &          ETR,DVETR,EQETR,TRETR,DETRDF,
     &          DTAUDE
C
C ----------------------------------------------------------------------
      INTEGER ITER
      REAL*8  SEUIL, DSEUIL, S
      REAL*8  Y,DP,YINF,YSUP,T,RP,PENTE,AIRE
      REAL*8  R8BID, LCROTY

C 1 - CALCUL DU MINORANT

      IF ( DEUXMU*EQETR-RPM .LE. 0) THEN

C LE TERME DE TRACE N'EST PAS NEGLIGEABLE

        YINF = 0
      ELSE

C RESOLUTION DE L'EQUATION SANS LE TERME DE TRACE
C LCROTY RESOUD UNE EQUATION DU TYPE Y*EXP(Y)=CONSTANTE

        CALL RCFONC('E','TRACTION',JPROLP,JVALEP,NBVALP,R8BID,YOUNG,
     &                      NU,PM,RP,PENTE,AIRE,2.D0*MU*EQETR,DP)
        YINF = LCROTY(DP*UNK*FONC/SIG1, PREC, ITEMAX)
      END IF

C 2 - CALCUL DU MAJORANT
C LCROTY RESOUD UNE EQUATION DU TYPE Y*EXP(Y)=CONSTANTE

      CALL LCROFG (YINF, DP, S, SEUIL, DSEUIL)
      IF (SEUIL.LT.0.D0) YINF=0.D0
      CALL LCROFG (YINF, DP, S, SEUIL, DSEUIL)

      T = (DEUXMU*EQETR-S)/(3.D0*MU)
      YSUP =  LCROTY(T*UNK*FONC/SIG1, PREC, ITEMAX)
      CALL LCROFG (YSUP, DP, S, SEUIL, DSEUIL)

C 3 - RESOLUTION PAR UNE METHODE DE NEWTON ENTRE LES BORNES

      Y = YSUP
      DO 10 ITER = 1, ITEMAX
        IF ( ABS(SEUIL)/SIGY .LE. PREC ) GOTO 100

        Y = Y - SEUIL/DSEUIL
        IF (Y.LE.YINF .OR. Y.GE.YSUP)  Y=(YINF+YSUP)/2

        CALL LCROFG (Y, DP, S, SEUIL, DSEUIL)

        IF (SEUIL.GE.0) YINF = Y
        IF (SEUIL.LE.0) YSUP = Y

 10   CONTINUE
      CALL U2MESS('F','ALGORITH3_55')


 100  CONTINUE
      LCROY1 = Y
      END
