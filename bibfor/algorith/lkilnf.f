        SUBROUTINE LKILNF(NVI,VIND,NMAT,MATERF,DT,SIGD,
     &                  NR,YD,YF,DEPS,VINF)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 26/03/2012   AUTEUR PROIX J-M.PROIX 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
        IMPLICIT NONE
C RESPOSABLE FOUCAULT A.FOUCAULT
C ----------------------------------------------------------------
C   POST-TRAITEMENTS SPECIFIQUES AU MODELE LETK
C
C   CORRESPONDANCE ENTRE LES VARIABLES INTERNES ET LES EQUATIONS
C          DU SYSTEME DIFFERENTIEL APRES INTEGRATION
C   VINF(1) = XIP (=YF(NDT+2)
C   VINF(2) = GAMMAP (=VIND(2)+YF(NDT+1)*G_II
C   VINF(3) = XIVP (=YF(NDT+3)
C   VINF(4) = GAMMAVP (=VIND(4)+DGAMMAVP)
C   VINF(5) = 0 OU 1 (CONTRACTANCE/DILATANCE)
C   VINF(6) = 0 OU 1 (INDICATEUR DE VISCOPLASTICITE)
C   VINF(7) = 0 OU 1 (INDICATEUR PLASTICITE)
C
C ----------------------------------------------------------------
C  IN
C     NVI    : NOMBRE DE VARIABLES INTERNES
C     VIND   : VARIABLE INTERNES A T
C     NMAT   : DIMENSION TABLEAU MATERIAU
C     MATERF : COEF MATERIAU A T+DT
C     DT     : INCREMENT DE TEMPS
C     SIGD   : ETAT DE CONTRAINTES A T
C     NR     : DIMENSION VECTEUR INCONNUES (YF/DY)
C     YD     : INCONNUES DU COMPORTEMENT INTEGRES A T
C     YF     : INCONNUES DU COMPORTEMENT INTEGRES A T+DT
C     DEPS   : INCREMENT DE DEFORMATIONS
C  OUT
C     VINF   :  VARIABLES INTERNES A T+DT
C       ----------------------------------------------------------------
        INTEGER  VAL,NDT,NVI,NMAT,NDI,NR
        REAL*8   MATERF(NMAT,2)
        REAL*8   YD(*),VIND(*),DT,DEPS(6)
        REAL*8   YF(*),VINF(*),SIGD(6)
C
        INTEGER  RETCOM,I
        REAL*8   DEVSIG(6),I1,XIVMAX,XIPPIC,UCRIV,SEUILV
        REAL*8   DEPSV(6),DGAMV,SEUILP,UCRIP
        REAL*8   VARV,ZERO,SEUIVM,DHDS(6),DS2HDS(6)
        REAL*8   PARAEP(3),VARPL(4),DFDSP(6),LKBPRI,BPRIMP
        REAL*8   SIGT(6)
        REAL*8   VECNP(6),GP(6),DEVGII,DEUX,TROIS,UN

        PARAMETER       (ZERO  =  0.D0 )
        PARAMETER       (UN    =  1.D0 )
        PARAMETER       (DEUX  =  2.D0 )
        PARAMETER       (TROIS =  3.D0 )

        COMMON /TDIM/   NDT  , NDI
C     ------------------------------------------------------------------
C --- REMPLISSAGE DIRECT DE VINF(1) ET VINF(3)      
        VINF(1) = MAX(YF(NDT+2),ZERO)     
        VINF(3) = MAX(YF(NDT+3),ZERO)     

C --------------------------------------------------------------------
C --- PASSAGE EN CONVENTION MECANIQUE DES SOLS
C --------------------------------------------------------------------
        DO 10 I = 1, NDT
          SIGT(I) = -YF(I)
  10    CONTINUE
C ----------------------------------------------------------------------
C --- VARIABLES LOCALES TEMPORAIRES
C ----------------------------------------------------------------------
C --- CONSTRUCTION TENSEUR DEVIATOIRE DES CONTRAINTES ET 1ER INVARIANT
        CALL LCDEVI(SIGT,DEVSIG)
        I1 = SIGT(1)+SIGT(2)+SIGT(3)

C --- DONNEES MATERIAU : VALEUR MAX DE XIV; XI_PIC 
        XIVMAX = MATERF(20,2)
        XIPPIC = MATERF(18,2)

C ----------------------------------------------------------------------
C --- I) - BUT : CALCUL DE LA DEFORMATION VISQUEUSE -DEPSV- ET DU 
C ---      PARAMETRE D ECROUISSAGE VISQUEUX -DGAMV-
C ----------------------------------------------------------------------
C --- I-1) INDICATEUR SUR ANGLE DE DILATANCE VISQUEUX PSI -> VAL = 0
        VAL = 0

C --- I-2) VARIABLE D'ECROUISSAGE VISQUEUSE VINTR = YF(NDT+3)
C --- I-3) CALCUL SEUIL VISQUEUX PAR RAPPORT A YF(1:6)=SIGF -> SEUILV
C --- I-3-1)  XIT   = YF(NDT+3)
        CALL LKCRIV(YF(NDT+3),I1,DEVSIG,VINF,NMAT,MATERF,UCRIV,SEUILV)
        IF(SEUILV.GE.ZERO)THEN
          CALL LKDGDE(VAL,YF(NDT+3),DT,SEUILV,UCRIV,I1,DEVSIG,VINF,NMAT,
     &              MATERF,DEPSV,DGAMV,RETCOM)
          VINF(4) = VIND(4)+DGAMV
C --- INDICATEUR DE VISCO-PLASTICITE
          VINF(6) = UN
        ELSE
          VINF(4) = VIND(4)
          VINF(6) = ZERO
        ENDIF
C ----------------------------------------------------------------------
C --- II) - BUT : CALCUL DE LA DEFORMATION PLASTIQUE -DEPSP- ET DU 
C ---       PARAMETRE D ECROUISSAGE PLASTIQUE -DGAMP-
C ----------------------------------------------------------------------
C --- II-1) CALCUL FONCTION SEUIL PLASTIQUE EN YF
        SEUILP = ZERO

        CALL LKCRIP(I1,DEVSIG,VINF,NMAT,MATERF,UCRIP,SEUILP)

C --- II-2)SI YF(NDT+1) > 0 ALORS PLASTICITE A PRENDRE EN COMPTE 
        IF(YF(NDT+1).GT.ZERO)THEN

C --- INDICATEUR DE PLASTICITE
          VINF(7) = UN

C --- II-2-A-1) INDICATEUR ANGLE DE DILATANCE PLASTIQUE PSI -> 0 OU 1
          IF(YF(NDT+2).LE.XIPPIC)THEN
            VAL = 0
          ELSE
            VAL = 1
          ENDIF

C --- II-2-A-2) INDICATEUR CONTRACTANCE OU DILATANCE -> VARV = 0 OU 1
C --- II-2-A-2)-1) CALCUL POSITION YF PAR RAPPORT SEUIL VISQUEUX MAX
          CALL LKCRIV(XIVMAX,I1,DEVSIG,VINF,NMAT,MATERF,UCRIV,SEUIVM)

C --- II-2-B-2)-2) TEST SUR SEUIL >0 OU <0 POUR DEFINIR VARV
          IF(SEUIVM.LE.ZERO)THEN
            VARV = 0
          ELSE
            VARV = 1
          ENDIF
          VINF(5) = VARV

C --- II-2-A-3) CALCUL DE DF/DSIG 
          CALL LKDHDS(NMAT,MATERF,I1,DEVSIG,DHDS,RETCOM)
          CALL LKDS2H(NMAT,MATERF,I1,DEVSIG,DHDS,DS2HDS,RETCOM)
          CALL LKVARP(VINF, NMAT, MATERF, PARAEP)
          CALL LKVACP(NMAT, MATERF,PARAEP, VARPL)
          CALL LKDFDS(NMAT,MATERF,DEVSIG,PARAEP,VARPL,DS2HDS,
     &              UCRIP,DFDSP)

C --- II-2-A-4) CALCUL DE G 
          BPRIMP = LKBPRI (VAL,VINF,NMAT,MATERF,PARAEP,I1,DEVSIG)
          CALL LKCALN(DEVSIG, BPRIMP, VECNP, RETCOM)
          CALL LKCALG(DFDSP,VECNP,GP,DEVGII)

          VINF(2) = YF(NDT+1)*DEVGII*SQRT(DEUX/TROIS)+VIND(2)

        ELSE
C --- II-2-B) PAS DE PLASTICITE A PRENDRE EN COMPTE - DLAMBAP = 0.D0
          VINF(2) = VIND(2)
        ENDIF

        END
