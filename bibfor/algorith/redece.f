        SUBROUTINE REDECE ( FAMI,KPG,KSP,NDIM,TYPMOD,IMATE,COMPOR,CRIT,
     &                      INSTAM,INSTAP,
     &                      NEPS,EPSDT,DEPST,NSIG,SIGD,VIND, OPTION,
     &                      ANGMAS,NWKIN,WKIN,
     &                      CP,NUMLC,TEMPD,TEMPF,TREF,
     &                      SIGF,VINF,NDSDE,DSDE,NWKOUT,WKOUT,RETCOM)
        IMPLICIT NONE
C       ================================================================
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 26/09/2011   AUTEUR PROIX J-M.PROIX 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C TOLE CRP_21
C TOLE CRS_1404
C RESPONSABLE JMBHH01 J.M.PROIX

C ======================================================================
C     INTEGRATION DES LOIS DE COMPORTEMENT NON LINEAIRE POUR LES
C     ELEMENTS ISOPARAMETRIQUES EN PETITES OU GRANDES DEFORMATIONS
C ======================================================================
C ROUTINE DE REDECOUPAGE LOCAL DU PAS dE TEMPS
C ----------------------------------------------------------------------
C       - SI CRIT(5) = -N  EN CAS DE NON-CONVERGENCE LOCALE ON EFFECTUE
C                          UN REDECOUPAGE DU PAS DE TEMPS EN N PALIERS
C                          L ORDRE D EXECUTION ETANT REMONTE EN ARGUMENT
C                          DANS REDECE, APPELE PAR NMCOMPOR AVANT PLASTI
C       - SI CRIT(5) = -1,0,1  PAS DE REDECOUPAGE DU PAS DE TEMPS
C       - SI CRIT(5) = +N  ON EFFECTUE UN REDECOUPAGE DU PAS DE TEMPS
C                          EN N PALIERS A CHAQUE APPEL DE REDECE
C                          LE PREMIER APPEL DE PLASTI SERT A
C                          L'INITIALISATION DE NVI
C       SI APRES REDECOUPAGE ON ABOUTIT A UN CAS DE NON CONVERGENCE, ON
C       REDECOUPE A NOUVEAU LE PAS DE TEMPS, EN 2*N PALIERS

C    ATTENTION  VIND    VARIABLES INTERNES A T MODIFIEES SI REDECOUPAGE

C ======================================================================
C     ARGUMENTS
C ======================================================================
C
C IN  FAMI,KPG,KSP  : FAMILLE ET NUMERO DU (SOUS)POINT DE GAUSS
C     NDIM    : DIMENSION DE L'ESPACE
C               3 : 3D , 2 : D_PLAN ,AXIS OU  C_PLAN
C     TYPMOD  : TYPE DE MODELISATION
C     IMATE   : ADRESSE DU MATERIAU CODE
C     COMPOR  : COMPORTEMENT :  (1) = TYPE DE RELATION COMPORTEMENT
C                               (2) = NB VARIABLES INTERNES / PG
C                               (3) = HYPOTHESE SUR LES DEFORMATIONS
C                               (4) etc... (voir grandeur COMPOR)
C     CRIT    : CRITERES DE CONVERGENCE LOCAUX (voir grandeur CARCRI)
C     INSTAM  : INSTANT DU CALCUL PRECEDENT
C     INSTAP  : INSTANT DU CALCUL
C     NEPS    : NOMBRE DE CMP DE EPSM ET DEPS (SUIVANT MODELISATION)
C     EPSM    : DEFORMATIONS A L'INSTANT DU CALCUL PRECEDENT
C     DEPS    : INCREMENT DE DEFORMATION TOTALE :
C                DEPS(T) = DEPS(MECANIQUE(T)) + DEPS(DILATATION(T))
C     NSIG    : NOMBRE DE CMP DE SIGM ET SIGP (SUIVANT MODELISATION)
C     SIGM    : CONTRAINTES A L'INSTANT DU CALCUL PRECEDENT
C     VIM     : VARIABLES INTERNES A L'INSTANT DU CALCUL PRECEDENT
C     OPTION  : OPTION DEMANDEE : RIGI_MECA_TANG , FULL_MECA , RAPH_MECA
C     ANGMAS  : LES TROIS ANGLES DU MOT_CLEF MASSIF (AFFE_CARA_ELEM),
C               + UN REEL QUI VAUT 0 SI NAUTIQUIES OU 2 SI EULER
C               + LES 3 ANGLES D'EULER
C     NWKIN   : DIMENSION DE WKIN 
C     WKIN    : TABLEAU DE TRAVAIL EN ENTREE(SUIVANT MODELISATION)
C     CP      : LOGIQUE = VRAI EN CONTRAINTES PLANES DEBORST
C     NUMLC   : NUMERO DE LOI DE COMPORTEMENT ISSUE DU CATALOGUE DE LC
C     TEMPD,TEMPF,TREF : TEMPERATURES SI L'APPEL PROVIENT DE CALCME

C OUT SIGP    : CONTRAINTES A L'INSTANT ACTUEL
C VAR VIP     : VARIABLES INTERNES
C                IN  : ESTIMATION (ITERATION PRECEDENTE OU LAG. AUGM.)
C                OUT : EN T+
C     NDSDE   : DIMENSION DE DSIDEP
C     DSIDEP  : OPERATEUR TANGENT DSIG/DEPS OU DSIG/DF
C     NWKOUT  : DIMENSION DE WKOUT
C     WKOUT   : TABLEAU DE TRAVAIL EN SORTIE (SUIVANT MODELISATION)
C     CODRET  : CODE RETOUR LOI DE COMPORMENT :
C               CODRET=0 : TOUT VA BIEN
C               CODRET=1 : ECHEC DANS L'INTEGRATION DE LA LOI
C               CODRET=3 : SIZZ NON NUL (CONTRAINTES PLANES DEBORST)
C
C PRECISIONS :
C -----------
C  LES TENSEURS ET MATRICES SONT RANGES DANS L'ORDRE :
C         XX YY ZZ SQRT(2)*XY SQRT(2)*XZ SQRT(2)*YZ

C -SI DEFORMATION = SIMO_MIEHE  
C   EPSM(3,3)    GRADIENT DE LA TRANSFORMATION EN T-
C   DEPS(3,3)    GRADIENT DE LA TRANSFORMATION DE T- A T+

C  OUTPUT SI RESI (RAPH_MECA, FULL_MECA_*)
C   VIP      VARIABLES INTERNES EN T+
C   SIGP(6)  CONTRAINTE DE KIRCHHOFF EN T+ RANGES DANS L'ORDRE
C         XX YY ZZ SQRT(2)*XY SQRT(2)*XZ SQRT(2)*YZ

C  OUTPUT SI RIGI (RIGI_MECA_*, FULL_MECA_*)
C   DSIDEP(6,3,3) MATRICE TANGENTE D(TAU)/D(FD) * (FD)T
C                 (AVEC LES RACINES DE 2)
C
C -SINON (DEFORMATION = PETIT OU PETIT_REAC OU GDEF_...)
C   EPSM(6), DEPS(6)  SONT LES DEFORMATIONS (LINEARISEES OU GREEN OU ..)
C
C ----------------------------------------------------------------------

        INTEGER         IMATE,NDIM,NDT,NDI,NVI,KPG,KSP,NUMLC
        INTEGER         NEPS,NSIG,NWKIN,NWKOUT,NDSDE
C
        REAL*8          CRIT(*), ANGMAS(*)
        REAL*8          INSTAM,     INSTAP,    TEMPD,   TEMPF  , TREF
        REAL*8          WKIN(NWKIN),WKOUT(NWKOUT)
        REAL*8          EPSDT(NEPS),  DEPST(NEPS)
        REAL*8          SIGD(NSIG),   SIGF(NSIG)
        REAL*8          VIND(*),   VINF(*)
        REAL*8          DSDE(NDSDE)
C
        CHARACTER*16    COMPOR(*),     OPTION
        CHARACTER*8     TYPMOD(*)
        CHARACTER*(*)   FAMI
        LOGICAL         CP
C
C       ----------------------------------------------------------------
C       VARIABLES LOCALES POUR LE REDECOUPAGE DU PAS DE TEMPS
C               TD      INSTANT T
C               TF      INSTANT T+DT
C               TEMD    TEMPERATURE A T
C               TEMF    TEMPERATURE A T+DT
C               EPS     DEFORMATION TOTALE A T
C               DEPS    INCREMENT DE DEFORMATION TOTALE
C               SD      CONTRAINTE A T
C               VD      VARIABLES INTERNES A T    + INDICATEUR ETAT T
C               DSDELO MATRICE DE COMPORTEMENT TANGENT A T+DT OU T
C               NPAL            NOMBRE DE PALIER POUR LE REDECOUPAGE
C               ICOMP           COMPORTEUR POUR LE REDECOUPAGE DU PAS DE
C                                    TEMPS
C               RETURN1 EN CAS DE NON CONVERGENCE LOCALE
C       ----------------------------------------------------------------
C
        INTEGER         ICOMP,        NPAL,      IPAL
        INTEGER         IRTET,     K
        INTEGER         RETCOM
        REAL*8          EPS(NEPS),       DEPS(NEPS),   SD(NSIG)
        REAL*8          DSDELO(NDSDE)
        REAL*8          DELTAT,TD,TF
C       ----------------------------------------------------------------
C       COMMONS POUR VARIABLES DE COMMANDE : CAII17 ET CARR01
        INTEGER NFPGMX
        PARAMETER (NFPGMX=10)
        INTEGER NFPG,JFPGL,DECALA(NFPGMX),KM,KP,KR,IREDEC
        COMMON /CAII17/NFPG,JFPGL,DECALA,KM,KP,KR,IREDEC
        REAL*8 TIMED1,TIMEF1,TD1,TF1
        COMMON /CARR01/TIMED1,TIMEF1,TD1,TF1
C       ----------------------------------------------------------------
C       ----------------------------------------------------------------
        COMMON /TDIM/   NDT  , NDI
C       ----------------------------------------------------------------
C       ----------------------------------------------------------------
C       -- POUR LES VARIABLES DE COMMANDE :
        IREDEC=1
        TIMED1=INSTAM
        TIMEF1=INSTAP
        TD1=INSTAM
        TF1=INSTAP


        IPAL  =  INT(CRIT(5))
        RETCOM=0
        IRTET=0
C       CORRECTION JMP : POURQUOI REDECOUPER POUR RIGI_MECA_TANG ?
        IF (OPTION.EQ.'RIGI_MECA_TANG') IPAL=0

        READ (COMPOR(2),'(I16)') NVI
C
C IPAL = 0  1 OU -1 -> PAS DE REDECOUPAGE DU PAS DE TEMPS
C
        IF ( IPAL .EQ. 0 .OR. IPAL .EQ. 1 .OR. IPAL .EQ. -1 ) THEN
           IPAL  = 0
           NPAL  = 0
           ICOMP = 2
C
C IPAL < -1 -> REDECOUPAGE DU PAS DE TEMPS EN CAS DE NON CONVERGENCE
C
        ELSEIF ( IPAL .LT. -1 ) THEN
           NPAL  = -IPAL
           ICOMP = 0
C
C IPAL > 1 -> REDECOUPAGE IMPOSE DU PAS DE TEMPS
C
        ELSEIF ( IPAL .GT.  1 ) THEN
           NPAL  = IPAL
           ICOMP = -1
        ENDIF
C
        CALL LC0000 ( FAMI,KPG,KSP, NDIM,TYPMOD, IMATE,COMPOR,CRIT,
     &                INSTAM, INSTAP, NEPS,EPSDT,DEPST, NSIG,SIGD, VIND,
     &                OPTION,ANGMAS,NWKIN,WKIN,
     &                CP,NUMLC,TEMPD,TEMPF,TREF,
     &                SIGF,VINF,NDSDE,DSDE,ICOMP,NVI,NWKOUT,WKOUT,IRTET)

        IF ( IRTET.GT.0 ) GOTO (1,2), IRTET
C
C -->   IPAL > 0 --> REDECOUPAGE IMPOSE DU PAS DE TEMPS
C -->   REDECOUPAGE IMPOSE ==>  RETURN DANS PLASTI APRES RECHERCHE
C       DES CARACTERISTIQUES DU MATERIAU A (T) ET (T+DT)
        IF ( IPAL  .LE.   0 ) GOTO 9999
        IF ( ICOMP .EQ.  -1 ) ICOMP = 0
C
        IF ( OPTION .EQ. 'RIGI_MECA_TANG' ) GOTO 9999
C
C
C --    CAS DE NON CONVERGENCE LOCALE / REDECOUPAGE DU PAS DE TEMPS
C
 1      CONTINUE
C
        IF ( NPAL .EQ. 0 ) THEN
           GOTO 2
        ELSE
           IF ((TYPMOD(2).EQ.'GRADEPSI').OR.
     &         (TYPMOD(2).EQ.'GRADVARI')) THEN
              CALL U2MESK('A','COMPOR2_10',1,TYPMOD(2))
           ENDIF
        ENDIF
C
        IF ( ICOMP .GT. 3 ) THEN
           CALL U2MESS('A','ALGORITH10_35')
           GOTO 2
        ENDIF
C
        IF ( ICOMP .GE. 1 ) NPAL = 2 * NPAL
        ICOMP = ICOMP + 1
C
        DO 124 K=1,NPAL
C --       INITIALISATION DES VARIABLES POUR LE REDECOUPAGE DU PAS
           IF ( K .EQ. 1 ) THEN
                TD = INSTAM
                TD1 = TD
                DELTAT = (INSTAP - INSTAM) / NPAL
                TF = TD + DELTAT
                TF1=TF
                IF ( OPTION .EQ. 'RIGI_MECA_TANG'
     &          .OR. OPTION .EQ. 'FULL_MECA' ) THEN
                    CALL R8INIR(NDSDE,0.D0,DSDE,1)
                ENDIF
                CALL LCEQVE ( EPSDT     , EPS            )
                CALL LCEQVE ( DEPST     , DEPS           )
                CALL LCPRSV ( 1.D0/NPAL , DEPS    , DEPS )
                CALL LCEQVN ( NDT       , SIGD    , SD   )
C
C --        REACTUALISATION DES VARIABLES POUR L INCREMENT SUIVANT
            ELSE IF ( K .GT. 1 ) THEN
                TD = TF
                TD1=TD
                TF = TF + DELTAT
                TF1=TF
                CALL LCSOVE ( EPS     , DEPS    , EPS  )
                IF ( OPTION .NE. 'RIGI_MECA_TANG' ) THEN
                    CALL LCEQVN ( NDT     , SIGF    , SD   )
                    CALL LCEQVN ( NVI     , VINF    , VIND   )
                ENDIF
            ENDIF
C
C
            CALL LC0000 ( FAMI,KPG,KSP,NDIM,TYPMOD,IMATE,COMPOR,CRIT,
     &               TD, TF, NEPS,EPS,DEPS, NSIG,SD, VIND,
     &               OPTION, ANGMAS, NWKIN, WKIN,
     &               CP,NUMLC,TEMPD,TEMPF,TREF,
     &         SIGF, VINF, NDSDE,DSDELO, ICOMP, NVI, NWKOUT,WKOUT,IRTET)

            IF ( IRTET.GT.0 ) GOTO (1,2), IRTET
C
            IF ( OPTION .EQ. 'RIGI_MECA_TANG'
     &               .OR. OPTION .EQ. 'FULL_MECA' ) THEN
                CALL LCPRSM ( 1.D0/NPAL , DSDELO , DSDELO   )
                CALL LCSOMA ( DSDE    , DSDELO , DSDE      )
            ENDIF
C
 124    CONTINUE
        GOTO 9999
C
   2    CONTINUE
        RETCOM = 1
        GO TO 9999
C
 9999   CONTINUE
        END
