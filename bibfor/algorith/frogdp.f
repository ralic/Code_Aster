      SUBROUTINE FROGDP(SDSTAT,RESOCO,NUMEDD,MATASS,RESIGR)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 11/06/2012   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT     NONE
      REAL*8       RESIGR
      CHARACTER*24 SDSTAT
      CHARACTER*24 RESOCO
      CHARACTER*19 MATASS
      CHARACTER*14 NUMEDD
C
C ----------------------------------------------------------------------
C
C ROUTINE CONTACT (METHODES DISCRETES - RESOLUTION)
C
C ALGO. POUR CONTACT    : PENALISATION
C ALGO. POUR FROTTEMENT : PENALISATION
C
C ----------------------------------------------------------------------
C
C
C RESO. DE : C.DU + KC ACT.AC.DU + KG AGT.AG.DU = F - KG AGT.AG (E-U)
C            AC. (U+DU)      <= E  (= POUR LES LIAISONS ACTIVES)
C
C AVEC E = JEU COURANT (CORRESPONDANT A U/I/N)
C
C     AC = MATRICE DE CONTACT
C
C    AG  = MATRICE DE FROTTEMENT POUR LES NOEUDS GLISSANTS
C
C      C = ( K  BT ) MATRICE DE RIGIDITE INCLUANT LES LAGRANGE
C          ( B  0  )
C
C      U = ( DEPL )
C          ( LAM  )
C
C      F = ( DL  ) DANS LA PHASE DE PREDICTION
C          ( DUD )
C
C      F = ( L - QT.SIG - BT.LAM  ) AU COURS D'UNE ITERATION DE NEWTON
C          (           0          )
C
C IN  SDSTAT : SD STATISTIQUES
C IN  RESOCO : SD DE TRAITEMENT NUMERIQUE DU CONTACT
C IN  NUMEDD : NUME_DDL
C IN  MATASS : NOM DE LA MATRICE DU PREMIER MEMBRE ASSEMBLEE
C IN  RESIGR : RESI_GLOB_RELA
C ON UTILISE UNIQUEMENT LE VECTEUR AFMU CAR LES DONNEES DE ATMU SONT
C NECESSAIRE POUR LE CALCUL DE LA MATRICE TANGENTE QUI SE FAIT
C A L'AIDE DU VECTEUR AFMU
C
C --------------- DEBUT DECLARATIONS NORMALISEES JEVEUX ---------------
C
      INTEGER            ZI
      COMMON  / IVARJE / ZI(1)
      REAL*8             ZR
      COMMON  / RVARJE / ZR(1)
      COMPLEX*16         ZC
      COMMON  / CVARJE / ZC(1)
      LOGICAL            ZL
      COMMON  / LVARJE / ZL(1)
      CHARACTER*8        ZK8
      CHARACTER*16                ZK16
      CHARACTER*24                          ZK24
      CHARACTER*32                                    ZK32
      CHARACTER*80                                              ZK80
      COMMON  / KVARJE / ZK8(1) , ZK16(1) , ZK24(1) , ZK32(1) , ZK80(1)
C
C -------------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ----------------
C
      INTEGER      IFM,NIV
      INTEGER      NMULT
      INTEGER      IEQ
      INTEGER      NEQ,NBLIAC,NBLIAI,NDIM
      INTEGER      LMAT
      INTEGER      CFDISD
      INTEGER      LMAF1,ITER
      INTEGER      NESMAX
      CHARACTER*14 NUMEF1,NUMEF2,NUTEMP
      CHARACTER*19 MAF1,MAF2,MATEMP,MACT
      CHARACTER*14 NUMECF
      CHARACTER*19 MATRCF,FRO1,FRO2
      CHARACTER*19 ATMU,AFMU
      INTEGER      JATMU,JAFMU
      CHARACTER*19 DEPL0
      INTEGER      JDEPL0
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
      CALL INFDBG('CONTACT',IFM,NIV)
C
C --- AFFICHAGE
C
      IF (NIV.GE.2) THEN
        WRITE(IFM,*) '<CONTACT><CALC> ALGO_CONTACT   : PENALISATION'
        WRITE(IFM,*) '<CONTACT><CALC> ALGO_FROTTEMENT: PENALISATION'
      ENDIF
C
C --- LECTURE DES STRUCTURES DE DONNEES DE CONTACT
C
      ATMU   = RESOCO(1:14)//'.ATMU'
      AFMU   = RESOCO(1:14)//'.AFMU'
      CALL JEVEUO(ATMU,  'E',JATMU)
      CALL JEVEUO(AFMU , 'E',JAFMU)
C
C --- MATRICES DE FROTTEMENT
C
      MAF1   = '&&FROGDP.MAF1'
      NUMEF1 = '&&FROGDP.NUF1'
      FRO1   = RESOCO(1:14)//'.FRO1'
      NUMEF2 = '&&FROGDP.NUF2'
      MAF2   = '&&FROGDP.MAF2'
      FRO2   = RESOCO(1:14)//'.FRO2'
C
C --- ACCES AUX CHAMPS DE TRAVAIL
C --- DEPL0: INCREMENT DE DEPLACEMENT CUMULE DEPUIS DEBUT
C ---        DU PAS DE TEMPS SANS CORRECTION DU CONTACT
C
      DEPL0  = RESOCO(1:14)//'.DEP0'
      CALL JEVEUO(DEPL0 (1:19)//'.VALE','L',JDEPL0)
C
C --- RECUPERATION DU DESCRIPTEUR DE LA MATRICE GLOBALE
C
      CALL JEVEUO(MATASS//'.&INT','E',LMAT)
C
C --- INITIALISATIONS DES VARIABLES
C
      NBLIAI = CFDISD(RESOCO,'NBLIAI')
      NEQ    = CFDISD(RESOCO,'NEQ'   )
      NDIM   = CFDISD(RESOCO,'NDIM'  )
      NESMAX = CFDISD(RESOCO,'NESMAX')
      NBLIAC = CFDISD(RESOCO,'NBLIAC')
      ITER   = 1
C
C --- CREATION DU SECOND MEMBRE ATMU = -E_N.[Ac]T.{JEU}
C
      CALL CFCPES(RESOCO,JATMU )
C
      IF (NBLIAC.EQ.0) THEN
        GOTO 999
      ENDIF
C
C --- CALCUL DES COEFFICIENTS DE LAGRANGE MU POUR LE FROTTEMENT
C
      CALL CFFPFO(RESOCO,NBLIAI,NBLIAC,NDIM  )
C
C --- CALCUL DE LA MATRICE DE CONTACT PENALISEE "ELEMENTAIRE" [E_N*AcT]
C
      CALL CFCPEM(RESOCO,NBLIAI)
C
C --- CALCUL DE LA MATRICE DE CONTACT PENALISEE "GLOBALE" [E_N*AcT*Ac]
C
      MACT   = '&&FROGDP.MACT'
      CALL CFCPMA(RESOCO,NEQ   ,NBLIAI,NUMEDD,MACT  )
C
C --- CREATION DE LA MATRICE FRO1 = E_T*AaT
C
      CALL CFFPM1(RESOCO,NBLIAI,NDIM  ,NESMAX)
C
C --- CREATION DE LA MATRICE MAF1 = E_T*AaT*Aa
C --- CETTE MATRICE NE SERT QU'AU CALCUL DU SECOND MEMBRE
C
      NMULT  = NDIM - 1
      CALL CFMATA(RESOCO,NEQ   ,NBLIAI,NMULT ,NUMEDD,
     &            FRO1  ,NUMEF1,MAF1   )
C
C --- RECUPERATION DU SECOND MEMBRE E_T*AaT*Aa * DELTA -> AFMU
C
      CALL MTDSCR(MAF1)
      CALL JEVEUO(MAF1//'.&INT','L',LMAF1 )
      CALL MRMULT('ZERO',LMAF1,ZR(JDEPL0),ZR(JAFMU),1,.TRUE.)
C
C --- CREATION DE FRO2 = E_T*AT
C
      CALL CFFPM2(RESOCO,RESIGR,NBLIAI,NBLIAC,NDIM  )
C
C --- CREATION DE LA SECONDE PARTIE DE LA MATRICE DE FROTTEMENT MAF2
C
      NMULT  = 1
      CALL CFMATA(RESOCO,NEQ   ,NBLIAI,NMULT ,NUMEDD,
     &            FRO2  ,NUMEF2,MAF2   )
C
C --- CALCUL DE LA MATRICE TANGENTE MAFROT = MACT+MAF1+MAF2
C
      MATEMP = '&&FROGDP.MATP'
      NUTEMP = '&&FROGDP.NUTP'
      MATRCF = RESOCO(1:14)//'.MATR'
      NUMECF = '&&FROGDP.NUFR'
      CALL CFFROT(MAF1,'-',MAF2  ,MATEMP,NUTEMP)
      CALL CFFROT(MACT,'+',MATEMP,MATRCF,NUMECF)
C
C --- CALCUL DES FORCES DE CONTACT (AT.MU) ET FROTTEMENT (AF.MU)
C
      DO 350 IEQ = 1,NEQ
        ZR(JAFMU-1+IEQ) = ZR(JAFMU-1+IEQ) + ZR(JATMU-1+IEQ)
        ZR(JATMU-1+IEQ) = 0.D0
 350  CONTINUE

 999  CONTINUE
C
C --- ETAT DES VARIABLES DE CONTROLE DU CONTACT
C
      CALL CFECRD(RESOCO,'NBLIAC',NBLIAC)
C
C --- SAUVEGARDE DES INFOS DE DIAGNOSTIC
C
      CALL NMRVAI(SDSTAT,'CTCD_ALGO_ITER','E',ITER  )
      CALL NMRVAI(SDSTAT,'CONT_NBLIAC'   ,'E',NBLIAC)
C
      CALL JEDEMA()
C
C ======================================================================
C
      END
