       SUBROUTINE CFADH2(RESOCO,DEFICO,NOMA  ,INDIC ,NBLIAC,
     &                   NBLIAI,AJLIAI,SPLIAI,LLF   )
C 
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 13/06/2012   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT     NONE
      INCLUDE 'jeveux.h'
      CHARACTER*24 RESOCO,DEFICO
      CHARACTER*8  NOMA
      INTEGER      INDIC
      INTEGER      AJLIAI,SPLIAI,NBLIAI
      INTEGER      NBLIAC,LLF
C      
C ----------------------------------------------------------------------
C
C ROUTINE CONTACT (METHODES DISCRETES - RESOLUTION)
C
C VERIFICATION QUE LES LIAISONS SONT BIEN ADHERENTES - VERSION 2D
C
C ----------------------------------------------------------------------
C
C
C IN  DEFICO : SD DE DEFINITION DU CONTACT (ISSUE D'AFFE_CHAR_MECA)
C IN  RESOCO : SD DE TRAITEMENT NUMERIQUE DU CONTACT
C IN  NOMA   : NOM DU MAILLAGE
C OUT INDIC  :+1 ON A RAJOUTE UNE LIAISON
C             -1 ON A ENLEVE UNE LIAISON
C IN  NBLIAI : NOMBRE DE LIAISONS DE CONTACT
C I/O NBLIAC : NOMBRE DE LIAISONS ACTIVES
C I/O AJLIAI : INDICE DANS LA LISTE DES LIAISONS ACTIVES DE LA DERNIERE
C              LIAISON CORRECTE DU CALCUL
C              DE LA MATRICE DE CONTACT ACM1AT
C I/O SPLIAI : INDICE DANS LA LISTE DES LIAISONS ACTIVES DE LA DERNIERE
C              LIAISON AYANT ETE CALCULEE POUR LE VECTEUR CM1A
C I/O LLF    : NOMBRE DE LIAISONS DE FROTTEMENT
C
C
C
C
      INTEGER      BTOTAL,LLF1,LLF2
      INTEGER      ILIAI,ILIAI2,ILIAC,ILIAC2
      INTEGER      ICOMPT
      INTEGER      COMPT0,COMPTS,COMPTN
      CHARACTER*24 SPLF0
      INTEGER      JSPLF0
      REAL*8       COEFFF,LAMBDF,LAMBDC,XQUOT
      CHARACTER*1  TYPESP
      CHARACTER*2  TYPLIA,TYPLI2,TYPEC0,TYPEF0
      CHARACTER*19 LIAC,TYPL,MU
      INTEGER      JLIAC,JTYPL,JMU
      CHARACTER*24 TACFIN
      INTEGER      JTACF
      INTEGER      CFMMVD,ZTACF      
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
C
C --- INITIALISATIONS
C
      TYPESP = 'S'
      TYPEC0 = 'C0'
      TYPEF0 = 'F0'
      COMPT0 = 0
      COMPTS = 0
      COMPTN = 0
      LLF1   = 0
      LLF2   = 0
      SPLF0  = '&&CFADH2.SUPLF0'
      BTOTAL = NBLIAC + LLF + LLF1 + LLF2
C
C --- LECTURE DES STRUCTURES DE DONNEES DE CONTACT
C
      LIAC   = RESOCO(1:14)//'.LIAC'
      TYPL   = RESOCO(1:14)//'.TYPL'
      MU     = RESOCO(1:14)//'.MU'
      TACFIN = RESOCO(1:14)//'.TACFIN'      
      CALL JEVEUO(LIAC,  'L',JLIAC )
      CALL JEVEUO(TYPL  ,'L',JTYPL )
      CALL JEVEUO(MU,    'E',JMU   )
      CALL JEVEUO(TACFIN,'L',JTACF )
      ZTACF  = CFMMVD('ZTACF')       
C
C --- VECTEUR POUR STOCKER LES LIAISONS (GLISSANTES) A SUPPRIMER
C
      CALL WKVECT(SPLF0,'V V I',LLF,JSPLF0)
C
C --- DETECTION DES LIAISONS EVENTUELLEMENT GLISSANTES
C   
      DO 300 ILIAC = 1, BTOTAL
        COMPTN = 0
        ILIAI  = ZI(JLIAC-1+ILIAC)
        TYPLIA = ZK8(JTYPL-1+ILIAC)(1:2)
C 
C ----- CAS D'UNE LIAISON DE FROTTEMENT
C
        IF (TYPLIA.EQ.TYPEF0) THEN
          COMPT0 = COMPT0 + 1
          COEFFF = ZR(JTACF+ZTACF*(ILIAI-1)+0)
          LAMBDF = ZR(JMU+NBLIAC+COMPT0-1)
C
C ------- DECALAGE DE LA LIAISON DE CONTACT ASSOCIEE
C
          DO 310 ILIAC2 = 1, ILIAC - 1
            TYPLI2 = ZK8(JTYPL+ILIAC2-1)(1:2)
            IF (TYPLI2.EQ.TYPEC0) THEN
              COMPTN = COMPTN + 1
              ILIAI2 = ZI(JLIAC+ILIAC2-1)
              IF (ILIAI.EQ.ILIAI2) THEN
                GOTO 312
              ENDIF
            ENDIF
 310      CONTINUE
          CALL ASSERT(.FALSE.)
        ENDIF
        GOTO 300
 312    CONTINUE
C
C ----- PRESSION DE CONTACT
C
        LAMBDC = ZR(JMU+COMPTN-1)
        IF ( LAMBDC.GT.0.D0 ) THEN
          XQUOT  = ABS(LAMBDF)/LAMBDC
        ELSE
          XQUOT  = 0.D0
        ENDIF
C
C ----- LA LIAISON EST GLISSANTE ?
C
        IF ( ABS(XQUOT).GE.COEFFF ) THEN
C
C ------- OUI
C
          COMPTS                   = COMPTS + 1
          ZI(JSPLF0-1+COMPTS)      = ILIAC
          ZR(JMU+3*NBLIAI+ILIAI-1) = (LAMBDF/ABS(LAMBDF)) * COEFFF
        ELSE
C
C ------- NON
C
          ZR(JMU+NBLIAC+COMPT0-COMPTS-1) = LAMBDF
          ZR(JMU+3*NBLIAI+ILIAI-1) = 0.D0
        ENDIF
 300  CONTINUE
C
C --- SUPPRESSION DES LIAISONS QUI SONT EN FAIT GLISSANTES
C 
      DO 160 ICOMPT  = 1, COMPTS
        ILIAC  = ZI(JSPLF0+COMPTS-ICOMPT)
        ILIAI  = ZI(JLIAC-1+ILIAC)
        CALL CFTABL(INDIC ,NBLIAC,AJLIAI,SPLIAI,LLF   ,
     &              LLF1  ,LLF2  ,RESOCO,TYPESP,ILIAC ,
     &              ILIAI ,TYPEF0)
        CALL CFIMP2(DEFICO,RESOCO,NOMA  ,ILIAI ,'F3'  ,
     &              'GLI')
 160  CONTINUE
C
      CALL JEDETR(SPLF0)
      CALL JEDEMA()
C 
      END
