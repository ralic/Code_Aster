       SUBROUTINE NIFN3G(NNO1  , NNO2  , NPG   , IPOIDS, IVF1  , IVF2  ,
     &                   IDFDE1 ,  DFDI, GEOM  ,  SIG   ,
     &                   DEPLM,GONFLM, FINTU , FINTA )
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 29/09/2006   AUTEUR VABHHTS J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2003  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================

       IMPLICIT NONE
       INTEGER       NNO1,NNO2,NPG,IPOIDS,IVF1,IVF2,IDFDE1
       REAL*8        GEOM(3,NNO1),DFDI(NNO1,3)
       REAL*8        SIG(7,NPG)
       REAL*8        FINTU(3,20), FINTA(2,8)
       REAL*8        DEPLM(3,NNO1), GONFLM(2,NNO2)

C......................................................................
C
C     BUT:  CALCUL  DE L'OPTION FORC_NODA EN QUASI INCOMPRESSIBLE
C                  DEFORMATION = 'SIMO_MIEHE'
C     APPELEE PAR  TE0481F
C......................................................................
C IN  NNO1    : NOMBRE DE NOEUDS DE L'ELEMENT LIES AUX DEPLACEMENTS
C IN  NNO2    : NOMBRE DE NOEUDS DE L'ELEMENT LIES A LA PRESSION
C IN  NPG     : NOMBRE DE POINTS DE GAUSS
C IN  POIDSG  : POIDS DES POINTS DE GAUSS
C IN  VFF1    : VALEUR  DES FONCTIONS DE FORME LIES AUX DEPLACEMENTS
C IN  VFF2    : VALEUR  DES FONCTIONS DE FORME LIES A LA PRESSION
C IN  DFDE1   : DERIVEE DES FONCTIONS DE FORME ELEMENT DE REFERENCE
C IN  DFDN1   : DERIVEE DES FONCTIONS DE FORME ELEMENT DE REFERENCE
C IN  DFDK1   : DERIVEE DES FONCTIONS DE FORME ELEMENT DE REFERENCE
C IN  GEOM    : COORDONEES DES NOEUDS
C IN  SIG     : CONTRAINTES A L'INSTANT PRECEDENT
C IN  DEPLM   : DEPLACEMENTS A L'INSTANT PRECEDENT
C IN  GONFLM  : VARIABLES LIEES AU GONFLEMENT A L'INSTANT PRECEDENT
C OUT DFDI    : DERIVEE DES FONCTIONS DE FORME  AU DERNIER PT DE GAUSS
C OUT FINTU   : FORCES INTERIEURES ASSOCIEES AU DEPLACEMENT
C OUT FINTA   : FORCES INTERIEURES ASSOCIEES A LA PRESSION ET GONFLEMENT
C......................................................................
C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ---------------------
      INTEGER            ZI
      COMMON  / IVARJE / ZI(1)
      REAL*8             ZR
      COMMON  / RVARJE / ZR(1)
      COMPLEX*16         ZC
      COMMON  / CVARJE / ZC(1)
      LOGICAL            ZL
      COMMON  / LVARJE / ZL(1)
      CHARACTER*8        ZK8
      CHARACTER*16                ZK16
      CHARACTER*24                          ZK24
      CHARACTER*32                                    ZK32
      CHARACTER*80                                              ZK80
      COMMON  / KVARJE / ZK8(1) , ZK16(1) , ZK24(1) , ZK32(1) , ZK80(1)
C --------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ---------------------
      LOGICAL      GRAND, AXI
      INTEGER      KPG,N,I,IND(3,3)
      REAL*8       TMP, RBID,POIDS,POIDS0,GM,JM
      REAL*8       GEOMM(3,27),EPSBID(6),FM(3,3),FBID(3,3),SIGDF(3,27)
      DATA    IND / 1, 4, 5,
     &              4, 2, 6,
     &              5, 6, 3 /
C --------------------------------------------------------------------
C - PRE REQUIS
      IF (NNO1 .GT. 20) CALL U2MESS('F','ALGORITH6_54')

C - INITIALISATION
      CALL R8INIR(60, 0.D0, FINTU,1)
      CALL R8INIR(16, 0.D0, FINTA,1)
      GRAND = .TRUE.
      AXI   = .FALSE.
C    REACTUALISATION DE LA GEOMETRIE
      DO 5 N = 1,NNO1
        DO 6 I = 1,3
          GEOMM(I,N) = GEOM(I,N) + DEPLM(I,N)
 6      CONTINUE
 5    CONTINUE

C - CALCUL POUR CHAQUE POINT DE GAUSS

      DO 800 KPG = 1,NPG
C - CALCUL DU GONFLEMENT
        GM = 0.D0
        DO 1 N = 1, NNO2
          GM = GM + ZR(IVF2+N+(KPG-1)*NNO2-1)*GONFLM(2,N)
 1      CONTINUE

C      CALCUL DES ELEMENTS GEOMETRIQUES
C   CALCUL DU POIDS DU PT DE GAUSS CONFIG INITIALE ET FM
        CALL NMGEOM(3,NNO1,AXI,GRAND,GEOM,KPG,IPOIDS,
     &              IVF1,IDFDE1,DEPLM,POIDS0,DFDI,
     &              FM,EPSBID,RBID)

C     CALCUL DE DFDI SUR LA CONFIGURATION ACTUELLE
        CALL NMGEOM(3,NNO1,AXI,GRAND,GEOMM,KPG,IPOIDS,
     &              IVF1,IDFDE1,DEPLM,POIDS,DFDI,
     &              FBID,EPSBID,RBID)

C   CALCUL DU GRADIENT DE LA TRANSFORMATION POUR LA LOI DE COMPORTEMENT
      JM=FM(1,1)*(FM(2,2)*FM(3,3)-FM(2,3)*FM(3,2))
     &  -FM(2,1)*(FM(1,2)*FM(3,3)-FM(1,3)*FM(3,2))
     &  +FM(3,1)*(FM(1,2)*FM(2,3)-FM(1,3)*FM(2,2))

C        CALCUL DE FINT_U
        DO 500 N = 1,NNO1
          DO 510 I = 1,3
            SIGDF(I,N) =  SIG(IND(I,1),KPG)*DFDI(N,1)
     &                 + SIG(IND(I,2),KPG)*DFDI(N,2)
     &                 + SIG(IND(I,3),KPG)*DFDI(N,3)

 510      CONTINUE
 500    CONTINUE
        DO 3 N=1,NNO1
          DO 2 I=1,3
            FINTU(I,N) = FINTU(I,N) + POIDS * SIGDF(I,N)
 2        CONTINUE
 3      CONTINUE

C        CALCUL DE FINT_P ET FINT_G
        DO  4 N = 1, NNO2
          TMP = (JM - 1.D0 - GM)*ZR(IVF2+N+(KPG-1)*NNO2-1)
          FINTA(1,N) = FINTA(1,N) + TMP*POIDS0

          TMP = SIG(7,KPG)*ZR(IVF2+N+(KPG-1)*NNO2-1)
          FINTA(2,N) = FINTA(2,N) + TMP*POIDS0
 4      CONTINUE

 800  CONTINUE
      END
