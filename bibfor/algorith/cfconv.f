      SUBROUTINE CFCONV(MAILLA,NUMEDD,SDIMPR,RESOCO,SOLALG,
     &                  MAXREL,CTCGEO)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 23/09/2008   AUTEUR ABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2005  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE MABBAS M.ABBAS
C
      IMPLICIT     NONE
      CHARACTER*8  MAILLA
      CHARACTER*24 RESOCO,NUMEDD,SDIMPR
      LOGICAL      CTCGEO,MAXREL
      CHARACTER*19 SOLALG(*)
C      
C ----------------------------------------------------------------------
C
C ROUTINE CONTACT (METHODES DISCRETES)
C
C CONVERGENCE LIEE A LA REACTUALISATION GEOMETRIQUE DU CONTACT
C
C ----------------------------------------------------------------------
C
C
C IN  RESOCO : SD POUR LA RESOLUTION DU CONTACT
C IN  MAILLA : NOM DU MAILLAGE
C IN  NUMEDD : NUMEROTATION NUME_DDL
C IN  SDIMPR : SD AFFICHAGE
C IN  SOLALG : VARIABLE CHAPEAU POUR INCREMENTS SOLUTIONS
C IN  MAXREL : .TRUE. SI CRITERE RESI_GLOB_RELA ET CHARGEMENT = 0,
C                     ON UTILISE RESI_GLOB_MAXI
C OUT CTCGEO : VAUT .TRUE. SI BOUCLE A ARRETER
C
C --------------- DEBUT DECLARATIONS NORMALISEES JEVEUX ----------------
C
      CHARACTER*32       JEXNUM
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C -------------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ----------------
C
      INTEGER      JDEPDE,II,IRET,NUMNO1,NUMNO2,NEQ,IBID
      INTEGER      GEOITE,INCR
      REAL*8       AUTONO,TEMP1,TEMP2,R8PREM,GEOTOL,RMIN,R8BID
      CHARACTER*8  NOMNOE,K8BID
      LOGICAL      PREMIE,ALARME
      INTEGER      VECONT(2)
      CHARACTER*19 NMCHEX,DEPDEL
      CHARACTER*24 MAXDEP,CIREAC,CLREAC,AUTOC1,AUTOC2 
      INTEGER      JMAXDE,JCIREA,JCLREA,JAUTO1,JAUTO2  
      CHARACTER*16 GEONOE
      REAL*8       GEOVAL
      LOGICAL      GEOERR            
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()   
C
C --- DECOMPACTION VARIABLES CHAPEAUX
C         
      DEPDEL = NMCHEX(SOLALG,'SOLALG','DEPDEL')
C
C --- ACCES OBJETS
C          
      AUTOC1 = RESOCO(1:14)//'.REA1'
      AUTOC2 = RESOCO(1:14)//'.REA2' 
      CIREAC = RESOCO(1:14)//'.REAI' 
      CLREAC = RESOCO(1:14)//'.REAL'             
      MAXDEP = RESOCO(1:14)//'.MAXD' 
      CALL JEVEUO(DEPDEL(1:19)//'.VALE','L',JDEPDE)             
      CALL JEVEUO(AUTOC1(1:19)//'.VALE','E',JAUTO1)
      CALL JEVEUO(AUTOC2(1:19)//'.VALE','E',JAUTO2)          
      CALL JEVEUO(CIREAC,'E',JCIREA)  
      CALL JEVEUO(MAXDEP,'E',JMAXDE)
      CALL JEVEUO(CLREAC,'E',JCLREA)      
      VECONT(1) = ZI(JCIREA+1-1) 
      VECONT(2) = ZI(JCIREA+2-1) 
      
C
C --- TOLERANCE POUR REACTUALISATION GEOMETRIQUE AUTOMATIQUE
C --- NOMBRE MAXI D'ITERATIONS DE REACTUALISATION GEOMETRIQUE EN AUTO
C
      GEOTOL   = 5.0D-2
      GEOITE   = 99
      GEOERR   = .FALSE.
      CTCGEO   = .TRUE.
      PREMIE   = .FALSE.
      ALARME   = .FALSE.      
      GEONOE   = ' '
      TEMP1    = 0.D0
      TEMP2    = 0.D0
      INCR     = 1
C      
      CALL DISMOI('F','NB_EQUA',NUMEDD,'NUME_DDL',NEQ,K8BID,IRET)       
C
C --- CONVERGENCE GEOMETRIQUE MAIS ATTENTE POINT FIXE CONTACT
C
      VECONT(2) = VECONT(2) + 1
      CALL CFITER(RESOCO,'E','GEOM',INCR,R8BID  )

      IF (VECONT(2).GT.GEOITE) THEN
        CALL U2MESS('F','ALGORITH_81')
      ENDIF
C
C --- CALCUL DU DEPLACEMENT 
C
      DO 10 II = 1,NEQ
        ZR(JAUTO2-1+II) = ZR(JAUTO2-1+II) + ZR(JAUTO1-1+II)
        ZR(JAUTO1-1+II) = ZR(JDEPDE-1+II) - ZR(JAUTO2-1+II)
   10 CONTINUE
C
C --- CALCUL DU MAX DE LA NORME DU DEPLACEMENT (SAUF LAGRANGES)
C
      CALL CNOMAX(AUTOC1,TEMP1,NUMNO1)
      CALL CNOMAX(AUTOC2,TEMP2,NUMNO2)
C
C --- STOCKAGE DU MAX DE LA NORME DU DEPL
C
      IF (ZR(JMAXDE).LT.0.D0) THEN
        ZR(JMAXDE-1+1) = TEMP2
        RMIN           = R8PREM()
      ELSE 
        ZR(JMAXDE-1+1) = MAX(ZR(JMAXDE-1+1),TEMP2)
        RMIN           = 1.D-6*ZR(JMAXDE-1+1)
      ENDIF
C
C --- VALEUR DE DEPL. MAX MESURE
C
      IF (TEMP2.LE.RMIN) THEN
        IF (TEMP2.EQ.0.D0) THEN
          AUTONO = 10.0D0*GEOTOL
          PREMIE = .TRUE.
        ELSE
          AUTONO = 1.D-1*GEOTOL
        ENDIF
      ELSE
        AUTONO = TEMP1/TEMP2
      ENDIF
C
C --- INFORMATIONS: NOM DU NOEUD ET VALEUR REACTUALISATION
C
      IF (NUMNO2.EQ.0) THEN
        NOMNOE = ' '
      ELSE
        CALL JENUNO(JEXNUM(MAILLA//'.NOMNOE',NUMNO2),NOMNOE)
      ENDIF
      GEONOE = NOMNOE//'        '
      GEOVAL = AUTONO
C
C --- CORRESPOND A REAC_GEOM = AUTOMATIQUE
C
      IF (VECONT(1).LT.0) THEN

        IF (AUTONO.LT.GEOTOL) THEN
          CTCGEO   = .TRUE.
        ELSE
          CTCGEO   = .FALSE.
        END IF
C
C --- CORRESPOND A REAC_GEOM = SANS
C
      ELSE IF (VECONT(1).EQ.0) THEN
        IF (AUTONO.GE.GEOTOL) THEN
          ALARME = .TRUE.
        ENDIF
        CTCGEO   = .TRUE.
      ELSE
C
C --- CORRESPOND A REAC_GEOM = CONTROLE
C
        IF (VECONT(2).EQ.VECONT(1)) THEN
          CTCGEO   = .TRUE.
          IF (AUTONO.GE.GEOTOL) THEN
            ALARME = .TRUE.
          ENDIF
        ELSE
          CTCGEO   = .FALSE.
        END IF
      END IF
C
C --- SI ON DEPASSE LA TOLERANCE DE 5% DE REAC_GEOM ET SI ON N'EST
C --- PAS AU PREMIER PAS DE TEMPS OU QU'ON N'A PAS DE CORPS RIGIDE
C
      IF (ALARME.AND..NOT.PREMIE) THEN
        GEOERR = .TRUE.
      ELSE
        GEOERR = .FALSE.
      ENDIF
C
C --- SAUVEGARDE
C        
      ZI(JCIREA+1-1) = VECONT(1)
      ZI(JCIREA+2-1) = VECONT(2)  
C
C --- AFFICHAGE
C
      CALL IMPSDR(SDIMPR,'CTCD_NOEU',GEONOE,R8BID,IBID)
      CALL IMPSDR(SDIMPR,'CTCD_GEOM',' ',GEOVAL,IBID)
      IF (GEOERR) THEN
        CALL U2MESS('A','MECANONLINE2_96')
      ENDIF  
C
 999  CONTINUE

      ZL(JCLREA+1-1) = .NOT.CTCGEO
      ZL(JCLREA+4-1) = CTCGEO
       
C
      CALL JEDEMA()
      END
