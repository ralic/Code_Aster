      SUBROUTINE NMVPCA(ICODMA,COMPOR,
     &                   TYPPAR,INST,TEMP,BL2,
     &                   COEVIL,COECYR,COEEPR)
C ----------------------------------------------------------------------
      IMPLICIT NONE
      
      INTEGER      ICODMA
      CHARACTER*2  TYPPAR,BL2
      CHARACTER*16 COMPOR
      REAL*8      INST,TEMP
      INTEGER    NBCVIL, NBCCYR,NBCEPR
      PARAMETER (NBCVIL=5, NBCCYR=3, NBCEPR=3)
      REAL*8     COEVIL(NBCVIL)
      REAL*8     COECYR(NBCCYR),COEEPR(NBCEPR)

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 15/06/2004   AUTEUR MABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2004  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C
C     RECUPERATION DES CARACTERISTIQUES DU MATERIAU POUR LES ELEMENTS
C     DE POUTRE DANS LE CAS DES COMPORTEMENTS VISCO AVEC IRRADIATION
C ----------------------------------------------------------------------
C
C IN  ICODMA : ADRESSE DU MATERIAU CODE
C IN  NOMCOMP: NOM DU COMPORTEMENT SCRUTE
C IN  TYPPAR : TYPE DE PARAMETRE SCRUTE
C               '  ' (DEUX BLANCS): PAS DE PARAMETRES
C               'T ' (T/BLANC):     TEMPS MAIS PAS DE TEMPERATURE
C               ' T' (BLANC/T):     TEMPERATURE MAIS PAS DE TEMPS
C               'TT' (T/T):         TEMPERATURE ET TEMPS
C IN  INST   : INSTANT CONSIDERE
C IN  TEMP   : TEMPERATURE CONSIDEREE
C IN  BL2    : CODE POUR RCVALA
C OUT COEVIL : COEFFICIENTS POUR LA LOI DE VISC_IRRA_LOG
C OUT COECYR : COEFFICIENTS POUR LA LOI DE ZIRC_CYRA2
C OUT COEEPR : COEFFICIENTS POUR LA LOI DE ZIRC_EPRI
C
C *************** DECLARATION DES VARIABLES LOCALES ********************
C
      INTEGER      NBPAR, NBRES, K
      REAL*8       VALPAR(2)
      CHARACTER*8  NOMPAR(2)
      REAL*8       VALVIL(NBCVIL)
      REAL*8       VALCYR(NBCCYR),VALEPR(NBCEPR)
      CHARACTER*2  CODVIL(NBCVIL)
      CHARACTER*2  CODCYR(NBCCYR),CODEPR(NBCEPR)
      CHARACTER*8  NOMVIL(NBCVIL) 
      CHARACTER*8  NOMCYR(NBCCYR),NOMEPR(NBCEPR)
C
C *********** FIN DES DECLARATIONS DES VARIABLES LOCALES ***************
C
C ****************************** DATA  *********************************
C

      DATA NOMVIL / 'A', 'B', 'CSTE_TPS', 'ENER_ACT', 'FLUX_PHI'/
      DATA NOMCYR / 'EPSI_FAB', 'TEMP_RECUIT', 'FLUX_PHI'/
      DATA NOMEPR / 'FLUX_PHI', 'R_P', 'THETA_MAX'/

     
C
C ********************* DEBUT DE LA SUBROUTINE *************************
C
C --- INITIALISATIONS
C

      CALL R8INIR (NBCVIL,0.D0,COEVIL,1)
      CALL R8INIR (NBCCYR,0.D0,COECYR,1)
      CALL R8INIR (NBCEPR,0.D0,COEEPR,1)

      CALL R8INIR (NBCVIL,0.D0,VALVIL,1) 
      CALL R8INIR (NBCCYR,0.D0,VALCYR,1) 
      CALL R8INIR (NBCEPR,0.D0,VALEPR,1) 

      DO 1 K = 1,NBCVIL
        CODVIL(K) = 'NN'
 1    CONTINUE
      DO 2 K = 1,NBCCYR
        CODCYR(K) = 'NN'
 2    CONTINUE
      DO 3 K = 1,NBCEPR
        CODEPR(K) = 'NN'
 3    CONTINUE

      
C INIT DES PARAMETRES
      NBPAR  = 0
      NOMPAR(1) = ' '
      NOMPAR(2) = ' '
      IF (TYPPAR(1:1).EQ.'T') THEN
       NBPAR = NBPAR +1
       NOMPAR(1) = 'INST'
       VALPAR(1) = INST
      ENDIF
      IF (TYPPAR(2:2).EQ.'T') THEN
       NBPAR = NBPAR +1
       NOMPAR(2) = 'TEMP'
       VALPAR(2) = TEMP
      ENDIF
C LECTURE


      IF (COMPOR(1:10).EQ.'ZIRC_CYRA2') THEN
        NBRES = NBCCYR
        CALL RCVALA(ICODMA,' ','ZIRC_CYRA2',NBPAR,NOMPAR,VALPAR,
     &              NBRES,NOMCYR(1),VALCYR(1),CODCYR(1), BL2 )
      ELSE IF (COMPOR(1:9).EQ.'ZIRC_EPRI')THEN
        NBRES = NBCEPR
        CALL RCVALA(ICODMA,' ','ZIRC_EPRI',NBPAR,NOMPAR,VALPAR,
     &              NBRES,NOMEPR(1),VALEPR(1),CODEPR(1), BL2 )
      ELSE IF (COMPOR(1:10).EQ.'VISC_IRRA_') THEN
        NBRES = NBCVIL
        CALL RCVALA(ICODMA,' ','VISC_IRRA_',NBPAR,NOMPAR,VALPAR,
     &              NBRES,NOMVIL(1),VALVIL(1),CODVIL(1), BL2 )
      ELSE
         CALL UTMESS ('F','NMVPCA',
     &  'COMPORTEMENT DE FLUAGE SOUS IRRADIATION INCONNU')
      ENDIF


C          
      IF (CODVIL(1).EQ.'OK'.AND.
     &    CODCYR(1).NE.'OK'.AND.
     &    CODEPR(1).NE.'OK') THEN      
         CALL R8COPY (NBCVIL,VALVIL,1,COEVIL,1)
   
      ELSEIF (CODVIL(1).NE.'OK'.AND.
     &    CODCYR(1).EQ.'OK'.AND.
     &    CODEPR(1).NE.'OK') THEN    
         CALL R8COPY (NBCCYR,VALCYR,1,COECYR,1)
      ELSEIF (CODVIL(1).NE.'OK'.AND.
     &    CODCYR(1).NE.'OK'.AND.
     &    CODEPR(1).EQ.'OK') THEN    
         CALL R8COPY (NBCEPR,VALEPR,1,COEEPR,1)   
      ELSE
         CALL UTMESS ('F','NMVPCA',
     &  'DEFINITION MULTIPLE DU COMPORTEMENT POUR UN ELEMENT DE POUTRE')
      ENDIF
      END
