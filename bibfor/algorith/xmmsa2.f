      SUBROUTINE XMMSA2(NDIM ,IPGF  ,IMATE ,SAUT ,ND   ,
     &                  TAU1 ,TAU2  ,COHES ,JOB  ,RELA ,
     &                  ALPHA,DSIDEP,SIGMA ,PP   ,DNOR ,
     &                  DTANG,P     ,AM)
      IMPLICIT NONE
      INTEGER     NDIM,IPGF,IMATE
      REAL*8      SAUT(3),AM(3),PP(3,3),DSIDEP(6,6)
      REAL*8      TAU1(3),TAU2(3),ND(3)
      REAL*8      ALPHA,P(3,3)
      REAL*8      DTANG(3),DNOR(3),COHES,RELA
      CHARACTER*8 JOB
C ----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 26/04/2011   AUTEUR DELMAS J.DELMAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C
C ROUTINE CONTACT (METHODE XFEM HPP - CALCUL ELEM.)
C
C --- CALCUL DU SAUT DE DEPLACEMENT EQUIVALENT [[UEG]]
C
C ----------------------------------------------------------------------
C
C IN  NDIM   : DIMENSION DE L'ESPACE
C IN  IPGF   : NUMÉRO DU POINTS DE GAUSS
C IN  IMATE  : ADRESSE DE LA SD MATERIAU
C IN  SAUT   : SAUT DE DEPLACEMENT
C IN  ND     : NORMALE À LA FACETTE ORIENTÉE DE ESCL -> MAIT
C                 AU POINT DE GAUSS
C IN  TAU1   : TANGENTE A LA FACETTE AU POINT DE GAUSS
C IN  TAU2   : TANGENTE A LA FACETTE AU POINT DE GAUSS
C IN  COHES  : VARIABLE INTERNE COHESIVE
C IN  JOB    : 'SAUT_EQ', 'MATRICE' OU 'VECTEUR'
C IN  RELA   : LOI COHESIVE 1:CZM_EXP_REG 2:CZM_LIN_REG
C OUT ALPHA  : SAUT DE DEPLACEMENT EQUIVALENT
C OUT DSIDEP : MATRICE TANGENTE DE CONTACT PENALISE ET DE FISSURATION
C OUT SIGMA  : CONTRAINTE
C OUT PP     : ND X ND
C OUT DNOR   : SAUT DEPLACEMENT NORMAL DANS LA BASE FIXE
C OUT DTANG  : SAUT DEPLACEMENT TANGENTIEL DANS LA BASE FIXE
C OUT P      : MATRICE DE PROJECTION SUR LE PLAN TANGENT
C OUT AM     : SAUT INSTANT - BASE LOCALE : AM(1) = SAUT NORMAL
C                                           AM(2) = SAUT TANGENTIEL
C
C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX --------------------
C
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)

C --------- FIN  DECLARATIONS  NORMALISEES  JEVEUX --------------------
C
      INTEGER      I,K

      REAL*8       VIM(9),VIP(9)
      REAL*8       AM2D(2),DAM2D(2),DSID2D(6,6),DAM(3)
      REAL*8       SIGMA(6),SQRNOR,SQRTAN,R8PREM,EPS

      CHARACTER*16 OPTION
C
C ----------------------------------------------------------------------
C
      CALL VECINI(3,0.D0,AM)
      CALL VECINI(3,0.D0,DAM)
      CALL MATINI(3,3,0.D0,PP)
      CALL MATINI(3,3,0.D0,P)
      CALL MATINI(6,6,0.D0,DSIDEP)
      CALL MATINI(6,6,0.D0,DSID2D)
      CALL VECINI(6,0.D0,SIGMA)
      CALL VECINI(9,0.D0,VIM)
      CALL VECINI(9,0.D0,VIP)
      CALL VECINI(3,0.D0,DTANG)
      CALL VECINI(3,0.D0,DNOR)
C
      CALL XMAFR1(3,ND,P)
C
C --- CALCUL DU SAUT DE DEPLACEMENT AM EN BASE LOCALE
C
      DO 218 I=1,NDIM
        DTANG(I) = 0.D0
        DNOR(I) = 0.D0
        DO 219 K=1,NDIM
          PP(I,K)=ND(I)*ND(K)
          DTANG(I)=DTANG(I)+P(I,K)*SAUT(K)
          DNOR(I) = DNOR(I) +PP(I,K)*SAUT(K)
 219    CONTINUE
C
C --- L'INTERPENETRATION CORRESPOND A SAUT<0 DANS LCEJEX
C
        AM(1) = AM(1) - DNOR(I)*ND(I)
        AM(2) = AM(2) - DTANG(I)*TAU1(I)
        AM(3) = AM(3) - DTANG(I)*TAU2(I)
 218  CONTINUE
C
C --- CALCUL DU SAUT DE DEPLACEMENT EQUIVALENT ALPHA
C
      SQRTAN=0.D0
      DO 214 I=2,NDIM
         SQRTAN=SQRTAN+AM(I)**2
214   CONTINUE
      EPS=R8PREM()
      SQRNOR=(MAX(AM(1),EPS))**2
      ALPHA = SQRT(SQRNOR+SQRTAN)
C
C --- CALCUL VECTEUR ET MATRICE TANGENTE EN BASE LOCALE
C
      IF(JOB.NE.'SAUT_EQ') THEN
         VIM(1)=COHES
         OPTION='FULL_MECA'
C
C VIM = VARIABLES INTERNES UTILISEES DANS LCEJEX
C.............VIM(1): SEUIL, PLUS GRANDE NORME DU SAUT
C
         IF (NDIM.EQ.2) THEN
           AM2D(1)=AM(1)
           AM2D(2)=AM(2)
           DAM2D(1)=DAM(1)
           DAM2D(2)=DAM(2)
           IF(RELA.EQ.1.D0) THEN
             CALL LCEJEX('RIGI',IPGF,1,2,IMATE,OPTION,
     &                    AM2D,DAM2D,SIGMA,DSID2D,VIM,VIP)
           ELSE IF(RELA.EQ.2.D0) THEN
             CALL LCEJLI('RIGI',IPGF,1,2,IMATE,OPTION,
     &                    AM2D,DAM2D,SIGMA,DSID2D,VIM,VIP)
           ENDIF
           DSIDEP(1,1)=DSID2D(1,1)
           DSIDEP(1,2)=DSID2D(1,2)
           DSIDEP(2,1)=DSID2D(2,1)
           DSIDEP(2,2)=DSID2D(2,2)
         ELSE IF (NDIM.EQ.3) THEN
           IF(RELA.EQ.1.D0) THEN
             CALL LCEJEX('RIGI',IPGF,1,3,IMATE,OPTION,
     &                    AM,DAM,SIGMA,DSIDEP,VIM,VIP)
           ELSE IF(RELA.EQ.2.D0) THEN
             CALL LCEJLI('RIGI',IPGF,1,3,IMATE,OPTION,
     &                    AM,DAM,SIGMA,DSIDEP,VIM,VIP)
           ENDIF
         ENDIF
C
      ENDIF
      END
