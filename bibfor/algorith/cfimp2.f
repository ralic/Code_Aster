      SUBROUTINE CFIMP2(DEFICO,RESOCO,NOMA  ,ILIAI ,TYPLIA,
     &                  TYPEOU)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGORITH  DATE 13/06/2012   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT     NONE
      INCLUDE 'jeveux.h'
      CHARACTER*8  NOMA
      CHARACTER*24 DEFICO,RESOCO      
      INTEGER      ILIAI
      CHARACTER*2  TYPLIA
      CHARACTER*3  TYPEOU
C      
C ----------------------------------------------------------------------
C
C ROUTINE CONTACT (METHODE DISCRETE - APPARIEMENT - UTILITAIRE)
C
C IMPRESSION DE L'ACTIVATION/DESACTIVATION DE LA LIAISON ESCLAVE/MAITRE
C
C ----------------------------------------------------------------------
C
C
C IN  DEFICO : SD DE DEFINITION DU CONTACT
C IN  RESOCO : SD DE TRAITEMENT NUMERIQUE DU CONTACT
C IN  NOMA   : NOM DU MAILLAGE
C IN  ILIAI  : NUMERO DE LA LIAISON (INDICE DANS LE TABLEAU GLOBAL DE
C              TOUTE LES LIAISONS POSSIBLES -APPARIEES-)
C IN  TYPLIA : TYPE DE LA LIAISON
C                'C0': CONTACT
C                'F0': FROTTEMENT SUIVANT LES DEUX DIRECTIONS
C                       SIMULTANEES (3D)
C                'F1': FROTTEMENT SUIVANT LA PREMIERE DIRECTION (3D)
C                'F2': FROTTEMENT SUIVANT LA SECONDE DIRECTION (3D)
C                'F3': FROTTEMENT (2D)
C IN  TYPEOU : LIEU OU L'OPERATION A ETE FAITE
C                'ACT' : ACTIVATION LIAISON DE CONTACT
C                'LIB' : DESACTIVATION LIAISON DE CONTACT
C                'NEG' : SUPPRESSION D'UNE LIAISON A PRESSION NEGATIVE
C                'GLI' : SUPPRESSION D'UNE LIAISON GLISSANTE
C                'ADH' : AJOUT D'UNE LIAISON ADHERENTE
C                'PIV' : SUPPRESSION D'UNE LIAISON A PIVOT NUL 
C                'ALJ' : ALARME LORSQU'UN JEU DEPASSE LA VALEUR SEUIL
C                         DANS LE CAS DU CONTACT GLISSIERE
C                'SIN' : AFFICHAGE DE LA LIAISON PROVOQUANT UNE MATRICE
C                         DE CONTACT SINGULIERE
C                'AGC' : ALARME LORSQUE LE NOMBRE D'ITERATIONS MAX
C                         DU GCP EST DEPASSE
C
C
C
C
      INTEGER      IFM,NIV
      CHARACTER*24 NUMLIA
      INTEGER      JNUMLI 
      INTEGER      IP,IZONE
      INTEGER      ENTAPP
      CHARACTER*8  NOMAPP
      CHARACTER*16 NOMNOE
      INTEGER      TYPAPP
      CHARACTER*16 ETALIA
      CHARACTER*4  TYPE2      
      CHARACTER*19 SDAPPA
      CHARACTER*38 NOMLIA
      REAL*8       CFDISR,ALJEU
      CHARACTER*10 TYPLI
      INTEGER      CFDISD,NDIM
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
      CALL INFDBG('CONTACT',IFM,NIV)
C
C --- ACCES SD CONTACT
C
      NUMLIA = RESOCO(1:14)//'.NUMLIA' 
      CALL JEVEUO(NUMLIA,'L',JNUMLI)
C
C --- INITIALISATIONS
C 
      ALJEU  = CFDISR(DEFICO,'ALARME_JEU'  )
      NOMLIA = ' '
      ETALIA = ' '
      NDIM   = CFDISD(RESOCO,'NDIM'  )
C
C --- SD APPARIEMENT
C
      SDAPPA = RESOCO(1:14)//'.APPA'
C
      IF (NIV.GE.2) THEN
C
C ----- POINT DE CONTACT
C      
        IP     = ZI(JNUMLI+4*(ILIAI-1)+1-1)
C
C ----- NOM DU NOEUD ESCLAVE
C
        CALL APNOMP(SDAPPA,IP    ,NOMNOE)          
C
C ----- INFOS APPARIEMENT
C
        CALL APINFI(SDAPPA,'APPARI_TYPE'  ,IP    ,TYPAPP )
        CALL APINFI(SDAPPA,'APPARI_ENTITE',IP    ,ENTAPP )
        CALL APINFI(SDAPPA,'APPARI_ZONE'  ,IP    ,IZONE  )    
C
C ----- NOM ET TYPE DU MAITRE
C
        CALL CFNOAP(NOMA  ,DEFICO,TYPAPP,ENTAPP,NOMAPP,
     &              TYPE2 )
C
C ----- NOM DE LA LIAISON
C
        WRITE (NOMLIA,100) ILIAI,'( ',NOMNOE,TYPE2,NOMAPP,'): '
C
C ----- TYPE LIAISON
C
        TYPLI  = 'CONT.     '
        IF (TYPLIA.EQ.'C0') THEN
          TYPLI = 'CONT.     '
        ELSE IF (TYPLIA.EQ.'F0') THEN
          IF (NDIM.EQ.3) THEN
            TYPLI = 'ADHE. 1&2 '
          ELSE
            TYPLI = 'ADHE.     '
          ENDIF
        ELSE IF (TYPLIA.EQ.'F1') THEN
          TYPLI = 'ADHE. 1   '
        ELSE IF (TYPLIA.EQ.'F2') THEN
          TYPLI = 'ADHE. 2   '
        ENDIF
C
C ----- AFFICHAGE LIAISON
C
        IF (TYPEOU.EQ.'ACT') THEN
          ETALIA = ' CONT - AJOUTE  '
          WRITE (IFM,1002) NOMLIA,ETALIA
          
        ELSEIF (TYPEOU.EQ.'LIB') THEN
          ETALIA = ' CONT - SUPPRIME'
          WRITE (IFM,1002) NOMLIA,ETALIA   
          
        ELSE IF (TYPEOU.EQ.'NEG') THEN
          ETALIA = ' PRES. NEGATIVE '
          WRITE (IFM,1000) NOMLIA,ETALIA,' - TYPE ',TYPLI

        ELSE IF (TYPEOU.EQ.'PIV') THEN
          ETALIA = ' PIVOT NUL      '
          WRITE (IFM,1000) NOMLIA,ETALIA,' - TYPE ',TYPLI

        ELSE IF (TYPEOU.EQ.'GLI') THEN
          ETALIA = ' GLIS - SUPPRIME'
          WRITE (IFM,1000) NOMLIA,ETALIA,' - TYPE ',TYPLI
          
        ELSE IF (TYPEOU.EQ.'ADH') THEN
          ETALIA = ' ADHE - AJOUTE  '
          WRITE (IFM,1000) NOMLIA,ETALIA,' - TYPE ',TYPLI
          
        ELSE IF (TYPEOU.EQ.'ALJ') THEN
          ETALIA = ' DECOLLE DU JEU '
          WRITE (IFM,1001) NOMLIA,ETALIA,ALJEU
          
        ELSE IF (TYPEOU.EQ.'AGC') THEN
          ETALIA = ' INTERPENETRE   '
          WRITE (IFM,1002) NOMLIA,ETALIA
          
        ELSE
          CALL ASSERT(.FALSE.)
        ENDIF
      ENDIF
C
  100 FORMAT (I5,A2,A16,A4,A8,A3)
 1000 FORMAT (' <CONTACT><CALC> LIAISON ',A38,A16,A8,A10)
 1001 FORMAT (' <CONTACT><CALC> LIAISON ',A38,A16,E12.3)
 1002 FORMAT (' <CONTACT><CALC> LIAISON ',A38,A16) 
C
      CALL JEDEMA()     
C      
      END
