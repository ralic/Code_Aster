      SUBROUTINE OP5903(NBOCCI,COMPOR)
      IMPLICIT NONE
      INTEGER        NBOCCI
      CHARACTER*8    COMPOR
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 19/12/2012   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE FLEJOU J.L.FLEJOU
C ======================================================================
C
C     COMMANDE:  DEFI_COMPOR MOT-CLE MULTIFIBRE
C
      INCLUDE 'jeveux.h'

      CHARACTER*32 JEXNUM,JEXNOM
      INTEGER        IARG,IDBOR,IMI,IMK,IOCC,IRETT
      INTEGER        IBID,NBG,NBGMAX,IMG,IG,IG1,JNFG,IAFF
      INTEGER        NBVF,NBV,ICP,NBKIT,NBNVI(2),NCOMEL,NUMLC
      CHARACTER*1    K1BID
      CHARACTER*8    MATERI,SDGF,K8BID,MATOR
      CHARACTER*16   NOMREL,ALGO1D,NOMKIT(2),LCOMEL(5),COMCOD
      CHARACTER*16   COMCO2,TEXTE(2),MOCLEF
      CHARACTER*24   VNBFIG,RNOMGF,KGROUP
C
      CALL JEMARQ()

C     ON RECUPERE LES RENSEIGNEMENTS DANS LA SD_GROUP_FIBRE :
C        NOMS DE TOUS LES GROUPES
C        NB MAXI DE GROUPES
C        NB DE FIBRES PAR GROUPE
      NBVF=0
      MOCLEF='MULTIFIBRE'
C
      CALL GETVID(' ','GEOM_FIBRE',0,IARG,1,SDGF,IBID)
      VNBFIG = SDGF//'.NB_FIBRE_GROUPE'
      RNOMGF = SDGF//'.NOMS_GROUPES'
      CALL JEVEUO(VNBFIG,'L',JNFG)
      CALL JELIRA(VNBFIG,'LONMAX',NBGMAX,K1BID)
      CALL WKVECT(COMPOR//'.CPRK','G V K24',6*NBGMAX+1,IMK)
      CALL WKVECT('&&OP0059.NOMS_GROUPES', 'V V K24',NBGMAX,IMG)
      CALL WKVECT('&&OP0059.VERIF_AFFECT', 'V V I',NBGMAX,IAFF)
      DO 50 IG=1,NBGMAX
         ZI(IAFF-1+IG) = 0
50    CONTINUE
      IDBOR = 0
C
      DO 25 IOCC=1,NBOCCI
         CALL GETVTX(MOCLEF,'GROUP_FIBRE',IOCC,IARG,0,K8BID,NBG)
         NBG=-NBG
         CALL GETVTX(MOCLEF,'GROUP_FIBRE',IOCC,IARG,NBG,
     &               ZK24(IMG),IBID)
         CALL GETVID(MOCLEF,'MATER',IOCC,IARG,1,MATERI,IBID)
         CALL GETVTX(MOCLEF,'RELATION',IOCC,IARG,1,NOMREL,IBID)
         NCOMEL = 1
         LCOMEL(NCOMEL) = NOMREL
C        AFFECTATION ALGO_1D ANALYTIQUE OU DEBORST
         ALGO1D = 'ANALYTIQUE'
         CALL LCCREE(NCOMEL, LCOMEL, COMCO2)
         CALL LCTEST(COMCO2,'MODELISATION','1D',IRETT)
         IF (IRETT.EQ.0) THEN
            TEXTE(1) = '1D'
            TEXTE(2) = NOMREL
            CALL U2MESG('I','COMPOR1_48',2,TEXTE,0,0,0,0.D0)
            ALGO1D='DEBORST'
            IDBOR = IDBOR+1
         ENDIF
C
C        POUR COMPORTEMENTS KIT_DDI A COMPLETER
         CALL NMDOKI(MOCLEF,' ',NOMREL,IOCC,2,NBKIT,NOMKIT,
     &               NBNVI,NCOMEL,LCOMEL,NUMLC,NBV)
C        APPEL A LCINFO POUR RECUPERER LE NOMBRE DE VARIABLES INTERNES
         CALL LCCREE(NCOMEL, LCOMEL, COMCOD)
         CALL LCINFO(COMCOD, NUMLC, NBV)
C
         DO 27 IG=1,NBG
C           NUMERO CORRESPONDANT AU NOM
            CALL JENONU(JEXNOM(RNOMGF,ZK24(IMG+IG-1)),IG1)
            IF (IG1.EQ.0) THEN
               CALL U2MESK('F','MODELISA8_8',1,ZK24(IMG+IG-1))
            ENDIF
            ICP=IMK-1+(IG1-1)*6
            ZK24(ICP+1) = ZK24(IMG+IG-1)
            ZK24(ICP+2) = MATERI
            ZK24(ICP+3) = NOMREL
            ZK24(ICP+4) = ALGO1D
            ZK24(ICP+5) = 'VIDE'
            WRITE(ZK24(ICP+6),'(I24)') ZI(JNFG-1+IG1)
            ZI(IAFF-1+IG1) = 1
27       CONTINUE
C        ON MET À JOUR LE NOMBRE DE VARIABLES INTERNES MAXI
         NBVF=MAX(NBVF,NBV)
25    CONTINUE
C
C     VERIFICATION DE L'UTILISATION DE COMP_1D
      IF (NBOCCI.GT.1) THEN
         IF (IDBOR.GE.1) CALL U2MESS('F','COMPOR1_15')
      ENDIF
C     VERIF TOUT AFFECTE AU MOINS UNE FOIS
C     ON MARQUE PAR VIDE LES GROUPES NON AFFECTES
      DO 51 IG=1,NBGMAX
         IF(ZI(IAFF-1+IG).EQ.0)THEN
            CALL JENUNO(JEXNUM(RNOMGF,IG),KGROUP)
            ICP=IMK-1+(IG-1)*6
            ZK24(ICP+1) = KGROUP
            ZK24(ICP+2) = 'VIDE'
         ENDIF
51    CONTINUE
C
C     ON RECUPERE LE NOM DU MATERIAU POUR LA TORSION, MIS A LA FIN
      CALL GETVID(' ','MATER_SECT',0,IARG,1,MATOR,IBID)
      ZK24(IMK-1+NBGMAX*6+1)=MATOR
      CALL WKVECT(COMPOR//'.CPRI', 'G V I',3,IMI)
C     TYPE 3 = MULTIFIBRE
      ZI(IMI)   = 3
      ZI(IMI+1) = NBVF
      ZI(IMI+2) = NBGMAX
C
      CALL JEDEMA()
      END
