      SUBROUTINE CALIAG(FONREZ,CHARGZ)
C ======================================================================
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.

C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.

C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
      IMPLICIT REAL*8 (A-H,O-Z)
      CHARACTER*(*) FONREZ,CHARGZ
C ----------------------------------------------------------------------
C MODIF MODELISA  DATE 10/07/2007   AUTEUR PELLET J.PELLET 

C     CREER LES CARTES CHAR.CHME.CMULT ET CHAR.CHME.CIMPO
C          ET REMPLIR LIGRCH, POUR LE MOT-CLE LIAISON_GROUP

C IN  : FONREE : 'REEL' OU 'FONC'
C IN  : CHARGE : NOM UTILISATEUR DU RESULTAT DE CHARGE
C-----------------------------------------------------------------------
C     ----------- COMMUNS NORMALISES  JEVEUX  --------------------------
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
      CHARACTER*32 JEXNOM,JEXNUM
C---------------- FIN COMMUNS NORMALISES  JEVEUX  ----------------------

      INTEGER I,J,K,IRET,IER,IOCC,IBID,IFM,NIV,NMOCL
      INTEGER VALI(2)
      PARAMETER (NMOCL=300)
      REAL*8 RBID,BETA
      COMPLEX*16 BETAC
      LOGICAL EXISDG,DNOR
      CHARACTER*2 TYPLAG
      CHARACTER*4 FONREE,TYPCOE
      CHARACTER*7 TYPCHA
      CHARACTER*8 NOMNO1,NOMNO2,CHARGE,NOMG,NOMA,MOD,K8BID,NOMDEP,KBETA,
     &            CMP,NOMCMP(NMOCL)
      CHARACTER*16 MOTFAC,MCGREX,MCEX
      CHARACTER*19 PREFIX,LIGRMO,LISREL
      CHARACTER*24 CONI,CONR,NOMDD1,NOMDD2,COEF1,COEF2,LISIN1,LISIN2
      CHARACTER*123 TEXTE
C ----------------------------------------------------------------------

      CALL JEMARQ()
      MOTFAC = 'LIAISON_GROUP'
      CALL GETFAC(MOTFAC,NLIAG)
      IF (NLIAG.EQ.0) GO TO 170

      FONREE = FONREZ
      CHARGE = CHARGZ

      TYPCOE = 'REEL'
      IF (FONREE.EQ.'COMP') TYPCOE = 'COMP'

      LISREL = '&&CALIAG.RLLISTE'
      LISIN1 = '&&CALIAG.LISNO1'
      LISIN2 = '&&CALIAG.LISNO2'
      BETAC = (1.0D0,0.0D0)
      NOMDEP = 'DEPL'
      TYPLAG = '12'

C --- MODELE ASSOCIE AU LIGREL DE CHARGE ---

      CALL DISMOI('F','NOM_MODELE',CHARGE(1:8),'CHARGE',IBID,MOD,IER)

C ---  LIGREL DU MODELE ---

      LIGRMO = MOD(1:8)//'.MODELE'

C --- MAILLAGE ASSOCIE AU MODELE ---

      CALL JEVEUO(LIGRMO//'.LGRF','L',JNOMA)
      NOMA = ZK8(JNOMA)

      MCGREX = 'SANS_GROUP_NO'
      MCEX = 'SANS_NOEUD'
      PREFIX = CHARGE//'.LIAG.COUPL'
      CONI = PREFIX//'.CONI'
      CALL JECREC(CONI,'V V I','NU','DISPERSE','VARIABLE',NLIAG)
      CONR = PREFIX//'.CONR'
      CALL JECREC(CONR,'V V R','NU','DISPERSE','VARIABLE',NLIAG)
      NOMDD1 = PREFIX(1:13)//'.NOMDDL1'
      CALL JECREC(NOMDD1,'V V K8','NU','DISPERSE','VARIABLE',NLIAG)
      NOMDD2 = PREFIX(1:13)//'.NOMDDL2'
      CALL JECREC(NOMDD2,'V V K8','NU','DISPERSE','VARIABLE',NLIAG)
      COEF1 = PREFIX(1:13)//'.CMULT1'
      CALL JECREC(COEF1,'V V R','NU','DISPERSE','VARIABLE',NLIAG)
      COEF2 = PREFIX(1:13)//'.CMULT2'
      CALL JECREC(COEF2,'V V R','NU','DISPERSE','VARIABLE',NLIAG)

      DO 30 IOCC = 1,NLIAG

C --- LECTURE DES MOTS CLES GROUP_MA_1 OU 2 OU MAILLE_1 OU 2 OU ---
C --- GROUP_NO_1 OU 2 OU NOEUD_1 OU 2)                          ---

        CALL CALEMN(MOTFAC,NOMA,IOCC,LISIN1,LONLI1,LISIN2,LONLI2)

C --- CONSTRUCTION DES VIS A VIS DES LISTES DE NOEUDS         ---
C --- LISIN1 ET LISIN2 DANS L'OJB CONI(IOCC)                  ---

        CALL CALINN(PREFIX,NOMA,MOTFAC,IOCC,LISIN1,LONLI1,LISIN2,LONLI2,
     &              MOD)

C --- LECTURE DES MOTS CLES SANS_GROUP_NO ET SANS_NOEUD ---
C --- MISE A JOUR DE CONI ET CONR SI IL EXISTE          ---

        CALL CAEXNO(CONI,NOMA,MOTFAC,MCGREX,MCEX,IOCC)
        CALL JEVEUO(JEXNUM(CONI,IOCC),'L',IDCONI)
        NBNO = ZI(IDCONI)
        CALL JEEXIN(JEXNUM(CONR,IOCC),IRET)
        IF (IRET.EQ.0) THEN
          DNOR = .FALSE.
        ELSE
          DNOR = .TRUE.
        END IF

C --- LECTURE DES DDLS IMPOSES SUR LA LISTE 1 ---

        CALL GETVTX(MOTFAC,'DDL_1',IOCC,1,0,K8BID,NDDL1)
        NDDL1 = -NDDL1

C --- LECTURE DES COEF. MULT. ASSOCIES AUX DDLS IMPOSES ---
C --- SUR LA LISTE 1                                    ---

        CALL GETVR8(MOTFAC,'COEF_MULT_1',IOCC,1,0,RBID,NMULT1)
        NMULT1 = -NMULT1
        IF (NDDL1.NE.NMULT1) THEN
        VALI (1) = NDDL1
        VALI (2) = NMULT1
          CALL U2MESG('F', 'MODELISA8_43',0,' ',2,VALI,0,0.D0)
        END IF

        CALL JECROC(JEXNUM(NOMDD1,IOCC))
        CALL JEECRA(JEXNUM(NOMDD1,IOCC),'LONMAX',NDDL1,' ')
        CALL JEVEUO(JEXNUM(NOMDD1,IOCC),'E',IDDL1)
        CALL JEECRA(JEXNUM(NOMDD1,IOCC),'LONUTI',NDDL1,' ')
        CALL GETVTX(MOTFAC,'DDL_1',IOCC,1,NDDL1,ZK8(IDDL1),NDDL1)
        DO 10 K = 1,NDDL1
          CALL LXCAPS(ZK8(IDDL1-1+K))
          CALL LXCADR(ZK8(IDDL1-1+K))
   10   CONTINUE

        CALL JECROC(JEXNUM(COEF1,IOCC))
        CALL JEECRA(JEXNUM(COEF1,IOCC),'LONMAX',NDDL1,' ')
        CALL JEVEUO(JEXNUM(COEF1,IOCC),'E',IMULT1)
        CALL GETVR8(MOTFAC,'COEF_MULT_1',IOCC,1,NDDL1,ZR(IMULT1),NDDL1)

C --- CAS DE DNOR : ON VA GENERE UNE LIAISON SUR DX,DY DZ POUR ---
C --- CHAQUE COUPLE DE LA LIST(UN GREL PAR COUPLE)             ---

        IF ((NDDL1.EQ.1) .AND. (ZK8(IDDL1).EQ.'DNOR')) THEN
          IF (.NOT.DNOR) THEN
            CALL U2MESS('F','MODELISA2_94')
          ELSE
          END IF
        ELSE
        END IF

C --- LECTURE DES DDLS IMPOSES SUR LA LISTE 2 ---

        CALL GETVTX(MOTFAC,'DDL_2',IOCC,1,0,K8BID,NDDL2)
        NDDL2 = -NDDL2

C --- LECTURE DES COEF. MULT. ASSOCIES AUX DDLS IMPOSES ---
C --- SUR LA LISTE 2                                    ---

        CALL GETVR8(MOTFAC,'COEF_MULT_2',IOCC,1,0,RBID,NMULT2)
        NMULT2 = -NMULT2
        IF (NDDL2.NE.NMULT2) THEN
        VALI (1) = NDDL2
        VALI (2) = NMULT2
          CALL U2MESG('F', 'MODELISA8_44',0,' ',2,VALI,0,0.D0)
        END IF

        CALL JECROC(JEXNUM(NOMDD2,IOCC))
        CALL JEECRA(JEXNUM(NOMDD2,IOCC),'LONMAX',NDDL2,' ')
        CALL JEVEUO(JEXNUM(NOMDD2,IOCC),'E',IDDL2)
        CALL JEECRA(JEXNUM(NOMDD2,IOCC),'LONUTI',NDDL2,' ')
        CALL GETVTX(MOTFAC,'DDL_2',IOCC,1,NDDL2,ZK8(IDDL2),NDDL2)
        DO 20 K = 1,NDDL2
          CALL LXCAPS(ZK8(IDDL2-1+K))
          CALL LXCADR(ZK8(IDDL2-1+K))
   20   CONTINUE

        CALL JECROC(JEXNUM(COEF2,IOCC))
        CALL JEECRA(JEXNUM(COEF2,IOCC),'LONMAX',NDDL2,' ')
        CALL JEVEUO(JEXNUM(COEF2,IOCC),'E',IMULT2)
        CALL GETVR8(MOTFAC,'COEF_MULT_2',IOCC,1,NDDL2,ZR(IMULT2),NDDL2)
        IF ((NDDL2.EQ.1) .AND. (ZK8(IDDL2).EQ.'DNOR')) THEN
          IF (.NOT.DNOR) THEN
            CALL U2MESS('F','MODELISA2_94')
          ELSE
          END IF
        ELSE
        END IF
   30 CONTINUE

C --- TYPE DE LA CHARGE ---

      CALL DISMOI('F','TYPE_CHARGE',CHARGE(1:8),'CHARGE',IBID,TYPCHA,
     &            IER)

      IF (TYPCHA(1:2).EQ.'TH') THEN
        NOMG = 'TEMP_R'
      ELSE
        NOMG = 'DEPL_R'
      END IF

C --- NOMBRE D'ENTIERS CODES ASSOCIE A LA GRANDEUR ---

      CALL DISMOI('F','NB_EC',NOMG,'GRANDEUR',NBEC,K8BID,IER)
      IF (NBEC.GT.10) THEN
        CALL U2MESK('F','MODELISA2_87',1,NOMG)
      END IF

      CALL JEVEUO(JEXNOM('&CATA.GD.NOMCMP',NOMG),'L',INOM)
      CALL JELIRA(JEXNOM('&CATA.GD.NOMCMP',NOMG),'LONMAX',NBCMP,K8BID)
      NDDLA = NBCMP - 1
      IF (NDDLA.GT.NMOCL) THEN
        VALI (1) = NMOCL
        VALI (2) = NDDLA
        CALL U2MESG('F', 'MODELISA8_29',0,' ',2,VALI,0,0.D0)
      END IF
      DO 40 I = 1,NDDLA
        NOMCMP(I) = ZK8(INOM-1+I)
   40 CONTINUE

      CALL JEVEUO(LIGRMO//'.PRNM','L',JPRNM)

C --- CREATION ET AFFECTATION DES RELATIONS A LA LISTE DE ---
C --- RELATIONS                                           ---

      ICMPZ = INDIK8(NOMCMP,'DZ',1,NDDLA)

      DO 160 IOCC = 1,NLIAG
        IF (FONREE.EQ.'REEL') THEN
          CALL GETVR8(MOTFAC,'COEF_IMPO',IOCC,1,1,BETA,NB)
        ELSE
          CALL GETVID(MOTFAC,'COEF_IMPO',IOCC,1,1,KBETA,NB)
        END IF
        CALL JEVEUO(JEXNUM(CONI,IOCC),'L',IDCONI)
C --- NOMBRE DE NOEUDS DE CHACUNES DES LISTES EN VIS A VIS
        NBNO = ZI(IDCONI)

        CALL JEVEUO(JEXNUM(NOMDD1,IOCC),'L',IDDL1)
        CALL JEVEUO(JEXNUM(NOMDD2,IOCC),'L',IDDL2)
        CALL JEVEUO(JEXNUM(COEF1,IOCC),'L',IDCO1)
        CALL JEVEUO(JEXNUM(COEF2,IOCC),'L',IDCO2)

C --- NOMBRE DE DDL IMPOSES POUR LES NOEUDS DE LA 1ERE LISTE ---

        CALL JELIRA(JEXNUM(NOMDD1,IOCC),'LONUTI',NDDL1,K8BID)

C --- NOMBRE DE DDL IMPOSES POUR LES NOEUDS DE LA 2EME LISTE ---

        CALL JELIRA(JEXNUM(NOMDD2,IOCC),'LONUTI',NDDL2,K8BID)

        IDMAX = 3* (NDDL1+NDDL2)

C ---  ALLOCATION D'UN TABLEAU BIDON POUR AFRELA ---

        CALL WKVECT('&&CALIAG.COEMUC','V V C',IDMAX,JCMUC)

C ---  ALLOCATION DU TABLEAU DES NOMS DES NOEUDS DE LA RELATION ---

        CALL WKVECT('&&CALIAG.NOMNOE','V V K8',IDMAX,IDNOMN)

C ---  ALLOCATION DU TABLEAU DES NOMS DES DDLS DE LA RELATION ---

        CALL WKVECT('&&CALIAG.NOMDDL','V V K8',IDMAX,IDNOMD)

C ---  ALLOCATION DU TABLEAU DES COEFFICIENTS DE LA RELATION ---

        CALL WKVECT('&&CALIAG.COEF','V V R',IDMAX,IDCOEF)

C ---  ALLOCATION DU TABLEAU DES DIRECTIONS DES COMPOSANTES ---
C ---  DE LA RELATION                                       ---

        CALL WKVECT('&&CALIAG.DIRECT','V V R',3*IDMAX,IDIREC)

C ---  ALLOCATION DU TABLEAU DE LA DIMENSION DU PROBLEME  ---
C ---  RELATIVE A CHAQUE COMPOSANTE DE LA RELATION        ---

        CALL WKVECT('&&CALIAG.DIMENSION','V V I',IDMAX,IDIMEN)

C ---  ALLOCATION DU TABLEAU DES DIMENSIONS DES VECTEURS NORMAUX ---
C ---  EN CHAQUE NOEUD POUR TOUTES LES RELATIONS                 ---

        CALL WKVECT('&&CALIAG.NBNOR','V V I',2*NBNO,IDNBN)

C ---  AFFECTATION DE CE VECTEUR ---

        DO 90 J = 1,NBNO
          INO1 = ZI(IDCONI+2* (J-1)+1)
          INO2 = ZI(IDCONI+2* (J-1)+2)

          IEXCM1 = 0
          IEXCM2 = 0
          DO 50 IEC = 1,NBEC
            IF (ZI(JPRNM-1+ (INO1-1)*NBEC+IEC).NE.0) THEN
              IEXCM1 = 1
              GO TO 60
            END IF
   50     CONTINUE
   60     CONTINUE

          DO 70 IEC = 1,NBEC
            IF (ZI(JPRNM-1+ (INO2-1)*NBEC+IEC).NE.0) THEN
              IEXCM2 = 1
              GO TO 80
            END IF
   70     CONTINUE
   80     CONTINUE
          IDG1 = JPRNM - 1 + (INO1-1)*NBEC + 1
          IDG2 = JPRNM - 1 + (INO2-1)*NBEC + 1

          IF (IEXCM1.EQ.0) THEN
            TEXTE(1:9) = 'LE NOEUD '
            TEXTE(18:80) = ' NE FAIT PAS PARTIE DU MODELE. '//MOTFAC//
     &                     ' IMPOSSIBLE'
            CALL JENUNO(JEXNUM(NOMA//'.NOMNOE',INO1),NOMNO1)
            WRITE (TEXTE(10:17),'(A)') NOMNO1
            CALL U2MESK('F','JEVEUX_1',1,TEXTE)
          END IF
          ZI(IDNBN-1+2* (J-1)+1) = 3
          IF ((ICMPZ.EQ.0) .OR. (.NOT.EXISDG(ZI(IDG1),ICMPZ))) THEN
            ZI(IDNBN-1+2* (J-1)+1) = 2
          END IF

          IF (IEXCM2.EQ.0) THEN
            TEXTE(1:9) = 'LE NOEUD '
            TEXTE(18:80) = ' NE FAIT PAS PARTIE DU MODELE. '//MOTFAC//
     &                     ' IMPOSSIBLE'
            CALL JENUNO(JEXNUM(NOMA//'.NOMNOE',INO2),NOMNO2)
            WRITE (TEXTE(10:17),'(A)') NOMNO2
            CALL U2MESK('F','JEVEUX_1',1,TEXTE)
          END IF
          ZI(IDNBN-1+2* (J-1)+2) = 3
          IF ((ICMPZ.EQ.0) .OR. (.NOT.EXISDG(ZI(IDG2),ICMPZ))) THEN
            ZI(IDNBN-1+2* (J-1)+2) = 2
          END IF
   90   CONTINUE

C ---  AFFECTATION DES RELATIONS ---

        DO 140 J = 1,NBNO
          K = 0

C --- PREMIER NOEUD DE LA RELATION ---

          INO1 = ZI(IDCONI+2* (J-1)+1)
          CALL JENUNO(JEXNUM(NOMA//'.NOMNOE',INO1),NOMNO1)
          CMP = ZK8(IDDL1)
          IF (CMP.EQ.'DNOR') THEN
            CALL JEVEUO(JEXNUM(CONR,IOCC),'L',IDCONR)
            IDIM = ZI(IDNBN-1+2* (J-1)+1)
            K = K + 1
            ZR(IDCOEF+K-1) = ZR(IDCO1)
            ZK8(IDNOMN+K-1) = NOMNO1
            ZK8(IDNOMD+K-1) = NOMDEP
            ZI(IDIMEN+K-1) = IDIM
            DO 100 I = 1,IDIM
              ZR(IDIREC+3* (K-1)+I-1) = ZR(IDCONR-1+ (2*3+1)* (J-1)+I)
C     +                                ZR(IDCONR-1+(2*IDIM+1)*(J-1)+I)
  100       CONTINUE
          ELSE
            DO 110 I = 1,NDDL1
              K = K + 1
              ZK8(IDNOMN+K-1) = NOMNO1
              ZK8(IDNOMD+K-1) = ZK8(IDDL1+I-1)
              ZR(IDCOEF+K-1) = ZR(IDCO1+I-1)
  110       CONTINUE
          END IF

C --- DEUXIEME NOEUD DE LA RELATION ---

          INO2 = ZI(IDCONI+2* (J-1)+2)
          CALL JENUNO(JEXNUM(NOMA//'.NOMNOE',INO2),NOMNO2)
          CMP = ZK8(IDDL2)
          IF (CMP.EQ.'DNOR') THEN
            CALL JEVEUO(JEXNUM(CONR,IOCC),'L',IDCONR)
            IDIM = ZI(IDNBN-1+2* (J-1)+2)
            K = K + 1
            ZR(IDCOEF+K-1) = ZR(IDCO2)
            ZK8(IDNOMN+K-1) = NOMNO2
            ZK8(IDNOMD+K-1) = NOMDEP
            ZI(IDIMEN+K-1) = IDIM
            DO 120 I = 1,IDIM
              ZR(IDIREC+3* (K-1)+I-1) = ZR(IDCONR-1+ (2*3+1)* (J-1)+3+I)
C     +                          ZR(IDCONR-1+(2*IDIM+1)*(J-1)+IDIM+I)
  120       CONTINUE
          ELSE
            DO 130 I = 1,NDDL2
              K = K + 1
              ZK8(IDNOMN+K-1) = NOMNO2
              ZK8(IDNOMD+K-1) = ZK8(IDDL2+I-1)
              ZR(IDCOEF+K-1) = ZR(IDCO2+I-1)
  130       CONTINUE
          END IF

C --- NOMBRE DE TERMES DE LA RELATION ---

          NBTERM = K

C --- AFFECTATION DE LA RELATION ---

          CALL AFRELA(ZR(IDCOEF),ZC(JCMUC),ZK8(IDNOMD),ZK8(IDNOMN),
     &                ZI(IDIMEN),ZR(IDIREC),NBTERM,BETA,BETAC,KBETA,
     &                TYPCOE,FONREE,TYPLAG,0.D0,LISREL)

C --- FIN DE LA BOUCLE SUR LES RELATIONS                       ---
C --- (I.E. LES COUPLES DE NOEUDS EN VIS A VIS POUR LE MOT-CLE ---
C --- LIAISON-GROUP COURANT)                                   ---

  140   CONTINUE

C --- IMPRESSION DES COUPLES DE NOEUDS EN VIS-A-VIS ---

        CALL INFNIV(IFM,NIV)
        IF (NIV.EQ.2) THEN
          WRITE (IFM,'(1X)')
          WRITE (IFM,'(1X,A,I5,A)') 'LISTE DES NOEUDS EN VIS-A-VIS A '//
     &      'L''OCCURENCE: ',IOCC,' DE LIAISON_GROUP :'
          DO 150 J = 1,NBNO
            INO1 = ZI(IDCONI+2* (J-1)+1)
            CALL JENUNO(JEXNUM(NOMA//'.NOMNOE',INO1),NOMNO1)
            INO2 = ZI(IDCONI+2* (J-1)+2)
            CALL JENUNO(JEXNUM(NOMA//'.NOMNOE',INO2),NOMNO2)
            WRITE (IFM,1000) NOMNO1,NOMNO2
  150     CONTINUE
        END IF

C --- DESTRUCTION DES TABLEAUX DE TRAVAIL  ---

        CALL JEDETR('&&CALIAG.COEMUC')
        CALL JEDETR('&&CALIAG.NOMNOE')
        CALL JEDETR('&&CALIAG.NOMDDL')
        CALL JEDETR('&&CALIAG.COEF')
        CALL JEDETR('&&CALIAG.DIRECT')
        CALL JEDETR('&&CALIAG.DIMENSION')
        CALL JEDETR('&&CALIAG.NBNOR')
        CALL JEDETR(LISIN1)
        CALL JEDETR(LISIN2)

C --- FIN DE LA BOUCLE SUR LES OCCURENCES DU MOT-CLE  ---
C --- LIAISON-GROUP                                   ---

  160 CONTINUE

C --- AFFECTATION DE LA LISTE DE RELATIONS A LA CHARGE  ---

      CALL AFLRCH(LISREL,CHARGE)


  170 CONTINUE
      CALL JEDEMA()
 1000 FORMAT (3X,A8,1X,'<--->',1X,A8)
      END
