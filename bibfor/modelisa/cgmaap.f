      SUBROUTINE CGMAAP ( MOFAZ, IOCC, NOMAZ, LISMAZ, NBMA )
      IMPLICIT   NONE
      INCLUDE 'jeveux.h'

      CHARACTER*32 JEXNUM,JEXATR
      INTEGER             IOCC, NBMA
      CHARACTER*(*)       MOFAZ, NOMAZ, LISMAZ
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 09/11/2012   AUTEUR DELMAS J.DELMAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C
C       CGMAAP -- TRAITEMENT DE L'OPTION 'APPUI'
C                 DU MOT FACTEUR CREA_GROUP_MA DE
C                 LA COMMANDE DEFI_GROUP
C
C -------------------------------------------------------
C  MOFAZ         - IN    - K16  - : MOT FACTEUR 'CREA_GROUP_MA'
C  IOCC          - IN    - I    - : NUMERO D'OCCURENCE DU MOT-FACTEUR
C  NOMAZ         - IN    - K8   - : NOM DU MAILLAGE
C  LISMAZ        - JXVAR - K24  - : NOM DE LA LISTE DE MAILLES OBTENUES
C                                   SUIVANT LE TYPE D'APPUI
C                                   (VOIR CI-DESSOUS)
C  NBMA          - OUT   -  I   - : LONGUEUR DE CETTE LISTE
C -------------------------------------------------------
C
C    TYPE D'APPUI:
C      - 'AU_MOINS_UN': UNE MAILLE EST RETENUE SI L'UN DE SES NOEUDS
C              EST DANS LA LISTE DES NOEUDS FOURNIS.
C      - 'TOUT': UNE MAILLE EST RETENUE SI TOUS SES NOEUDS
C              SONT DANS LA LISTE DES NOEUDS FOURNIS.
C      - 'SOMMET': UNE MAILLE EST RETENUE SI TOUS SES NOEUDS SOMMETS
C              SONT DANS LA LISTE DES NOEUDS FOURNIS.
C      - 'MAJORITE': UNE MAILLE EST RETENUE SI PLUS DE LA MOITIE DE
C             SES NOEUDS SONT DANS LA LISTE DES NOEUDS FOURNIS.
C
C
      INTEGER        NBMALA, I, J, JMALA, JCO, IACNX, ILCNX, II, NBMC
      INTEGER        JNOEU,NBNO,NNO, JLMAS, IDLIST, IMA, N1
      INTEGER        ITYP, JNNMA, NUNO,J1,K,NBNOT,IER
      CHARACTER*8    NOMA, K8BID, MOTCLE(2), TYMOCL(2),TYPMA
      CHARACTER*16   MOTFAC,TYPAPP
      CHARACTER*24   LISTRV,LISMAI,MESNOE,LISMAS,LNNMA
      INTEGER      IARG

C     -----------------------------------------------------------------
C
      CALL JEMARQ()
C
C --- INITIALISATIONS :
C     ================
      NBMALA=0
      MOTFAC    = MOFAZ
      NOMA      = NOMAZ
      LISMAI    = LISMAZ
      LISTRV    = '&&CGMAAS.MAILLES_LACHE'
      MESNOE    = '&&CGMAAL.NOEUDS'
      LISMAS    = '&&CGMAAS.MAILLES_STRICT'
      LNNMA     = '&&CGMAAL.NBNO_MA'

C --  ON RECUPERE LES MAILLES DU TYPE D'APPUI 'AU_MOINS_UN'
C     (COMMMUN A TOUT TYPE D'APPUI)
      CALL CGMAAL(MOTFAC, IOCC, NOMA, LISTRV, NBMALA)
C
C --  RECUPERATION DU TYPE D'APPUI :
      CALL GETVTX('CREA_GROUP_MA','TYPE_APPUI',IOCC,IARG,1,TYPAPP,N1)
      CALL JEVEUO(LISTRV,'L',JMALA)
C
C --- TYPE D'APPUI = 'AU_MOINS_UN'
C     ============================
      IF(TYPAPP.EQ.'AU_MOINS_UN')THEN
         NBMA=NBMALA
         JLMAS=JMALA
         GOTO 333
      ELSE
        CALL WKVECT(LISMAS,'V V I',NBMALA,JLMAS)
      ENDIF
C
C --- TYPE D'APPUI = 'TOUT' ET 'SOMMET'
C     ======================================
C
C --  RECUPERATIONS DES NOEUDS FOURNIS PAR L'UTILISATEUR
C     --------------------------------------------------
      NBMC = 2
      MOTCLE(1) = 'NOEUD'
      TYMOCL(1) = 'NOEUD'
      MOTCLE(2) = 'GROUP_NO'
      TYMOCL(2) = 'GROUP_NO'
      CALL RELIEM ( ' ', NOMA, 'NU_NOEUD', MOTFAC, IOCC, NBMC, MOTCLE,
     &                                          TYMOCL, MESNOE, NBNO )
      CALL JEVEUO ( MESNOE, 'L', J1 )
      CALL DISMOI('F','NB_NO_MAILLA',NOMA,'MAILLAGE',NBNOT,K8BID,IER)
      CALL WKVECT('&&CGMAAP.NOEUDS','V V I',NBNOT,JNOEU)
      DO 1, K=1,NBNO
        NUNO=ZI(J1-1+K)
        ZI(JNOEU-1+NUNO)=1
1     CONTINUE

      CALL JEVEUO(NOMA//'.CONNEX','L',IACNX)
      CALL JEVEUO(JEXATR(NOMA//'.CONNEX','LONCUM'),'L',ILCNX)
      CALL WKVECT(LNNMA,'V V I',NBMALA,JNNMA)


      IF(TYPAPP.EQ.'SOMMET')THEN
C     --  ON DETERMINE LE NOMBRE DE NOEUDS 'SOMMET' POUR CHAQUE MAILLE
        CALL JEVEUO ( NOMA//'.TYPMAIL', 'L', ITYP )

        DO 10 I=1,NBMALA
          IMA=ZI(JMALA+I-1)
          CALL JENUNO(JEXNUM('&CATA.TM.NOMTM',ZI(ITYP+IMA-1)),TYPMA)

          IF (TYPMA(1:3).EQ.'POI' )THEN
             ZI(JNNMA+I-1)=1
          ELSE IF (TYPMA(1:3).EQ.'SEG' )THEN
             ZI(JNNMA+I-1)=2
          ELSE IF (TYPMA(1:4).EQ.'QUAD' )THEN
             ZI(JNNMA+I-1)=4
          ELSE IF (TYPMA(1:4).EQ.'TRIA' )THEN
             ZI(JNNMA+I-1)=3
          ELSE IF (TYPMA(1:5).EQ.'TETRA')THEN
              ZI(JNNMA+I-1)=4
          ELSE IF(TYPMA(1:5).EQ.'PENTA')THEN
              ZI(JNNMA+I-1)=6
          ELSE IF(TYPMA(1:5).EQ.'PYRAM')THEN
              ZI(JNNMA+I-1)=5
          ELSE IF(TYPMA(1:4).EQ.'HEXA' )THEN
              ZI(JNNMA+I-1)=8
          ENDIF
 10     CONTINUE

      ELSEIF(TYPAPP.EQ.'TOUT'.OR.TYPAPP.EQ.'MAJORITE')THEN
        DO 20 I=1,NBMALA
          IMA=ZI(JMALA+I-1)
          ZI(JNNMA+I-1)=ZI(ILCNX+IMA)-ZI(ILCNX+IMA-1)
 20     CONTINUE
      ELSE
        CALL ASSERT(.FALSE.)
      ENDIF

C --  ON FILTRE LES MAILLES SUIVANT LES CRITERES RELATIFS
C     A CHAQUE TYPE D'APPUI:
C     ---------------------------------------------------
      NBMA=0
      DO 30 I=1,NBMALA
         NNO=ZI(JNNMA+I-1)
         IF (NNO.EQ.0) GOTO 30

         IMA=ZI(JMALA+I-1)
         JCO=IACNX+ZI(ILCNX+IMA-1)-1
         II=0
         DO 40 J=1,NNO
           NUNO=ZI(JCO+J-1)
           IF (ZI(JNOEU-1+NUNO).EQ.1)  II=II+1
 40      CONTINUE

         IF(TYPAPP.EQ.'TOUT'.OR.TYPAPP.EQ.'SOMMET')THEN
           IF(II.EQ.NNO)THEN
             NBMA=NBMA+1
             ZI(JLMAS+NBMA-1)=IMA
           ENDIF
         ELSE IF(TYPAPP.EQ.'MAJORITE')THEN
           IF(II.GE.(NNO+1)/2)THEN
             NBMA=NBMA+1
             ZI(JLMAS+NBMA-1)=IMA
           ENDIF
         ENDIF
 30    CONTINUE
C
C --- CREATION ET REMPLISSAGE DU VECTEUR DE SORTIE
C     --------------------------------------------
 333  CONTINUE
      IF (NBMA.GT.0) THEN
        CALL WKVECT ( LISMAI, 'V V I', NBMA, IDLIST )
        DO 50 I = 1 , NBMA
          ZI(IDLIST+I-1) = ZI(JLMAS+I-1)
 50     CONTINUE
      ENDIF
C
C --- FIN
C     ===
      CALL JEDETR ( LISTRV )
      CALL JEDETR ( LISMAS )
      CALL JEDETR ( MESNOE )
      CALL JEDETR ( LNNMA  )
      CALL JEDETR('&&CGMAAP.NOEUDS')
C
      CALL JEDEMA()
C
      END
