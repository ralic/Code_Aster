      SUBROUTINE DIMECO(CHAR  ,NDIM  ,NZOCO ,NSUCO ,NMACO ,
     &                  NNOCO )     
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 14/09/2010   AUTEUR ABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2004  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C 
      IMPLICIT     NONE
      CHARACTER*8  CHAR
      INTEGER      NDIM
      INTEGER      NZOCO
      INTEGER      NSUCO
      INTEGER      NMACO
      INTEGER      NNOCO
C      
C ----------------------------------------------------------------------
C
C ROUTINE CONTACT (METHODES MAILLEES - LECTURE DONNEES)
C
C CONSTRUCTION DU VECTEUR D'INFORMATION SUR LES LONGUEURS
C      
C ----------------------------------------------------------------------
C
C
C IN  CHAR   : NOM UTILISATEUR DU CONCEPT DE CHARGE
C IN  NOMA   : NOM DU MAILLAGE
C IN  NDIM   : DIMENSION DU PROBLEME
C IN  NZOCO  : NOMBRE DE ZONES DE CONTACT
C IN  NSUCO  : NOMBRE TOTAL DE SURFACES DE CONTACT
C IN  NMACO  : NOMBRE TOTAL DE MAILLES DES SURFACES
C IN  NNOCO  : NOMBRE TOTAL DE NOEUDS DES SURFACES
C
C -------------- DEBUT DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C ---------------- FIN DECLARATIONS NORMALISEES JEVEUX ----------------
C
      CHARACTER*24 NDIMCO
      INTEGER      JDIM
      INTEGER      NTMANO
      INTEGER      NTPC,NTPT
      INTEGER      IZONE,IBID,IMAM,IMAE
      INTEGER      MMINFI
      INTEGER      NBNOE,NBNOM,NBMAE,NBMAM
      INTEGER      NTNOE,NTNOM,NTMAE,NTMAM
      INTEGER      NBNOEC,NBNOMC,NBMAEC,NBMAMC
      INTEGER      NTNOEC,NTNOMC,NTMAEC,NTMAMC
      INTEGER      NBPT,NBPC
      INTEGER      NNOMAE,NNOMAM
      INTEGER      JDECME,JDECMM,POSMAE,POSMAM
      CHARACTER*24 DEFICO      
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
C
C --- INITIALISATIONS
C
      DEFICO = CHAR(1:8)//'.CONTACT'      
C 
C --- ACCES AUX STRUCTURES DE DONNEES DE CONTACT
C 
      NDIMCO = DEFICO(1:16)//'.NDIMCO'    
      CALL JEVEUO(NDIMCO,'E',JDIM)       
C      
C --- DIMENSION DE L'ESPACE
C      
      IF (NDIM.GT.3) THEN
        CALL U2MESS('A','CONTACT_84')
        IF (NDIM.EQ.1003) THEN
          NDIM     = 3
        ELSE IF (NDIM.EQ.1002) THEN
          NDIM     = 2
        ELSEIF (NDIM.EQ.23) THEN
          NDIM     = 2  
        ELSE
          CALL ASSERT(.FALSE.)
        ENDIF  
      ENDIF
C 
C --- TABLEAU CONTENANT LES LONGUEURS DES DIFFERENTS VECTEURS 
C --- ET LE NOMBRE DE NOEUDS ESCLAVES MAXIMUM POUR CHAQUE ZONE 
C
      ZI(JDIM+1-1)  = NDIM
      ZI(JDIM+2-1)  = NZOCO
      ZI(JDIM+3-1)  = NSUCO
      ZI(JDIM+4-1)  = NMACO
      ZI(JDIM+5-1)  = NNOCO
      ZI(JDIM+6-1)  = 0
      ZI(JDIM+7-1)  = 0

C 
C --- CALCUL DU NOMBRE TOTAL DE NOEUDS/MAILLES ESCLAVES/MAITRES
C
      NTNOE = 0
      NTMAE = 0
      NTNOM = 0
      NTMAM = 0      
      DO 20 IZONE = 1,NZOCO
        NBMAE  = MMINFI(DEFICO,'NBMAE'      ,IZONE )
        NBNOE  = MMINFI(DEFICO,'NBNOE'      ,IZONE ) 
        NBMAM  = MMINFI(DEFICO,'NBMAM'      ,IZONE )
        NBNOM  = MMINFI(DEFICO,'NBNOM'      ,IZONE )                
        NTNOE  = NTNOE + NBNOE
        NTMAE  = NTMAE + NBMAE
        NTNOM  = NTNOM + NBNOM
        NTMAM  = NTMAM + NBMAM        
   20 CONTINUE  
C 
      ZI(JDIM+8 -1)  = NTNOE
      ZI(JDIM+9 -1)  = NTMAE
      ZI(JDIM+10-1)  = NTNOM
      ZI(JDIM+11-1)  = NTMAM   
C 
C --- CALCUL DU NOMBRE TOTAL DE NOEUDS/MAILLES ESCL/MAIT AVEC CALCUL
C
      NTNOEC = 0
      NTMAEC = 0
      NTNOMC = 0
      NTMAMC = 0      
      DO 21 IZONE = 1,NZOCO
        NBMAEC  = MMINFI(DEFICO,'NBMAEC'     ,IZONE )
        NBNOEC  = MMINFI(DEFICO,'NBNOEC'     ,IZONE ) 
        NBMAMC  = MMINFI(DEFICO,'NBMAMC'     ,IZONE )
        NBNOMC  = MMINFI(DEFICO,'NBNOMC'     ,IZONE )                
        NTNOEC  = NTNOEC + NBNOEC
        NTMAEC  = NTMAEC + NBMAEC
        NTNOMC  = NTNOMC + NBNOMC
        NTMAMC  = NTMAMC + NBMAMC        
   21 CONTINUE  
C 
      ZI(JDIM+12-1) = NTNOEC
      ZI(JDIM+13-1) = NTMAEC
      ZI(JDIM+14-1) = NTNOMC
      ZI(JDIM+15-1) = NTMAMC      
C
C --- NOMBRE TOTAL DE POINTS DE CONTACT
C
      NTPT  = 0
      DO 41 IZONE = 1,NZOCO
        NBPT   = MMINFI(DEFICO,'NBPT'       ,IZONE )       
        NTPT   = NTPT + NBPT
   41 CONTINUE
C 
      ZI(JDIM+16-1) = NTPT
C
C --- NOMBRE TOTAL DE POINTS DE CONTACT EFFECTIF (CALCUL)
C
      NTPC  = 0
      DO 42 IZONE = 1,NZOCO
        NBPC   = MMINFI(DEFICO,'NBPC'       ,IZONE )       
        NTPC   = NTPC + NBPC
   42 CONTINUE
C 
      ZI(JDIM+17-1) = NTPC       
C
C --- NOMBRE TOTAL DE NOEUD AUX ELEMENTS (ELNO)
C
      NTMANO = 0
      DO 30 IZONE = 1,NZOCO  
        JDECME = MMINFI(DEFICO,'JDECME',IZONE )
        NBMAE  = MMINFI(DEFICO,'NBMAE' ,IZONE )
        DO 31 IMAE = 1,NBMAE
          POSMAE = JDECME + IMAE 
          CALL CFNBEN(DEFICO,POSMAE,'CONNEX',NNOMAE,IBID  )
          NTMANO = NTMANO + NNOMAE
 31     CONTINUE
        JDECMM = MMINFI(DEFICO,'JDECMM',IZONE )
        NBMAM  = MMINFI(DEFICO,'NBMAM' ,IZONE )
        DO 32 IMAM = 1,NBMAM
          POSMAM = JDECMM + IMAM 
          CALL CFNBEN(DEFICO,POSMAM,'CONNEX',NNOMAM ,IBID  )
          NTMANO = NTMANO + NNOMAM
 32     CONTINUE 
 30   CONTINUE    
C
      ZI(JDIM+18-1) = NTMANO      
C
      CALL JEDEMA()
      END
