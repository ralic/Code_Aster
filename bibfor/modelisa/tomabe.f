      SUBROUTINE TOMABE(CHMAT,NMABET,NBMABE,MAILLA,NBNOMA,
     &                  MAIL2D,NBNOBE,NUNOBE,XFLU,XRET)
      IMPLICIT NONE
C-----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 18/09/2012   AUTEUR LADIER A.LADIER 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C-----------------------------------------------------------------------
C  DESCRIPTION : CARACTERISATION DE LA TOPOLOGIE DE LA STRUCTURE BETON
C  -----------   ET RECUPERATION DES CARACTERISTIQUES DU MATERIAU
C                CONSTITUTIF
C                APPELANT : OP0180 , OPERATEUR DEFI_CABLE_BP
C
C  IN     : CHMAT  : CHARACTER*8 , SCALAIRE
C                    NOM DU CONCEPT CHAM_MATER ASSOCIE A L'ETUDE
C  IN     : NMABET : CHARACTER*24 ,
C                    OBJET CONTENANT LES MAILLES BETON
C  IN     : NBMABE : INTEGER , SCALAIRE
C                    NOMBRE DE MAILLE BETON
C  IN     : MAILLA : CHARACTER*8 , SCALAIRE
C                    NOM DU CONCEPT MAILLAGE ASSOCIE A L'ETUDE
C  IN     : NBNOMA : INTEGER , SCALAIRE
C                    NOMBRE TOTAL DE NOEUDS DU MAILLAGE
C  OUT    : MAIL2D : LOGICAL , SCALAIRE
C                    INDICATEUR BOOLEEN CARACTERISANT LA TOPOLOGIE DE LA
C                    STRUCTURE BETON
C                    SI ( MAIL2D ) : REPRESENTATION PAR DES MAILLES 2D
C                    SINON         : REPRESENTATION PAR DES MAILLES 3D
C  OUT    : NBNOBE : INTEGER , SCALAIRE
C                    NOMBRE DE NOEUDS APPARTENANT A LA STRUCTURE BETON
C  IN     : NUNOBE : CHARACTER*19 , SCALAIRE
C                    NOM D'UN VECTEUR D'ENTIERS POUR STOCKAGE DES
C                    NUMEROS DES NOEUDS APPARTENANT A LA STRUCTURE BETON
C  OUT    : XFLU   : REAL*8 , SCALAIRE
C                    VALEUR DU TAUX DE PERTE DE TENSION PAR FLUAGE DU
C                    BETON, EN % DE LA TENSION INITIALE
C  OUT    : XRET   : REAL*8 , SCALAIRE
C                    VALEUR DU TAUX DE PERTE DE TENSION PAR RETRAIT DU
C                    BETON, EN % DE LA TENSION INITIALE
C  N.B. LE VECTEUR NUNOBE EST REMPLI LORS DU PASSAGE DANS LA ROUTINE
C       TOMABE, APRES AJUSTEMENT DE SA DIMENSION A NBNOBE
C
C-------------------   DECLARATION DES VARIABLES   ---------------------
C
      INCLUDE 'jeveux.h'
C
C ARGUMENTS
C ---------
      CHARACTER*8   CHMAT,  MAILLA
      INTEGER       NBNOMA, NBNOBE, NBMABE
      LOGICAL       MAIL2D
      CHARACTER*19  NUNOBE
      REAL*8        XFLU, XRET
      CHARACTER*24  NMABET
C
C VARIABLES LOCALES
C -----------------
      INTEGER       IAS, ICSTE, IDECAL, IMAIL, INO, IRET, JCONX, JNCOCH,
     &              JNUMAB, JNUNOB, JPTMA, JTYMA, JVALK, JVALR, NBCONX,
     &              NBCSTE, NTYMA, NUMAIL, NUMNOE,
     &              JCESD, JCESL, JCESV, IAD
C
      INTEGER       NTRI3, NTRI6, NQUA4, NQUA8, NQUA9,
     &              NTET4, NTET10, NPYR5, NPYR13, NPEN6, NPEN15,
     &              NHEX8, NHEX20, NHEX27
C
      LOGICAL       MAIL3D, TROUV1, TROUV2
      CHARACTER*1   K1B
      CHARACTER*3   K3MAI
      CHARACTER*8   BETON
      CHARACTER*19  CARTE, NOMRC, CHSMAT, CARTEZ,CHTMP
      CHARACTER*24  CAPTMA, CAVALK, CONXMA, RCVALK, RCVALR,
     &              TYMAMA
C
      CHARACTER*8   BPELB(2)
      REAL*8        CRITE
      DATA          BPELB  /'PERT_FLU','PERT_RET'/
C
C-------------------   DEBUT DU CODE EXECUTABLE    ---------------------
C
      CALL JEMARQ()
C
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
C 1   CARACTERISATION DE LA TOPOLOGIE DE LA STRUCTURE BETON :
C     REPRESENTATION PAR DES MAILLES 2D OU 3D
C     VERIFICATION DU TYPE DE CHAQUE MAILLE
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
C
C 1.1 ACCES AUX OBJETS DU CONCEPT MAILLAGE
C ---
      CONXMA = MAILLA//'.CONNEX'
      CALL JEVEUO(NMABET,'L',JNUMAB)
      TYMAMA = MAILLA//'.TYPMAIL'
      CALL JEVEUO(TYMAMA,'L',JTYMA)
C
C 1.2 SAISIE DES TYPES DE MAILLES ACCEPTABLES POUR UNE REPRESENTATION 2D
C --- ET VERIFICATION
C
      CALL JENONU(JEXNOM('&CATA.TM.NOMTM','TRIA3'),NTRI3)
      CALL JENONU(JEXNOM('&CATA.TM.NOMTM','TRIA6'),NTRI6)
      CALL JENONU(JEXNOM('&CATA.TM.NOMTM','QUAD4'),NQUA4)
      CALL JENONU(JEXNOM('&CATA.TM.NOMTM','QUAD8'),NQUA8)
      CALL JENONU(JEXNOM('&CATA.TM.NOMTM','QUAD9'),NQUA9)
      MAIL2D = .TRUE.
      DO 10 IMAIL = 1, NBMABE
         NUMAIL = ZI(JNUMAB+IMAIL-1)
         NTYMA = ZI(JTYMA+NUMAIL-1)
         IF ( (NTYMA.NE.NTRI3) .AND. (NTYMA.NE.NTRI6) .AND.
     &        (NTYMA.NE.NQUA4) .AND. (NTYMA.NE.NQUA8) .AND.
     &        (NTYMA.NE.NQUA9) ) THEN
            MAIL2D = .FALSE.
            GO TO 11
         ENDIF
  10  CONTINUE
  11  CONTINUE
C
C 1.3 SAISIE DES TYPES DE MAILLES ACCEPTABLES POUR UNE REPRESENTATION 3D
C --- ET VERIFICATION
C
      IF ( MAIL2D ) THEN
         MAIL3D = .FALSE.
      ELSE
         CALL JENONU(JEXNOM('&CATA.TM.NOMTM','TETRA4' ),NTET4 )
         CALL JENONU(JEXNOM('&CATA.TM.NOMTM','TETRA10'),NTET10)
         CALL JENONU(JEXNOM('&CATA.TM.NOMTM','PYRAM5' ),NPYR5 )
         CALL JENONU(JEXNOM('&CATA.TM.NOMTM','PYRAM13'),NPYR13)
         CALL JENONU(JEXNOM('&CATA.TM.NOMTM','PENTA6' ),NPEN6 )
         CALL JENONU(JEXNOM('&CATA.TM.NOMTM','PENTA15'),NPEN15)
         CALL JENONU(JEXNOM('&CATA.TM.NOMTM','HEXA8'  ),NHEX8 )
         CALL JENONU(JEXNOM('&CATA.TM.NOMTM','HEXA20' ),NHEX20)
         CALL JENONU(JEXNOM('&CATA.TM.NOMTM','HEXA27' ),NHEX27)
         MAIL3D = .TRUE.
         DO 20 IMAIL = 1, NBMABE
            NUMAIL = ZI(JNUMAB+IMAIL-1)
            NTYMA = ZI(JTYMA+NUMAIL-1)
            IF ( (NTYMA.NE.NTET4) .AND. (NTYMA.NE.NTET10) .AND.
     &           (NTYMA.NE.NPYR5) .AND. (NTYMA.NE.NPYR13) .AND.
     &           (NTYMA.NE.NPEN6) .AND. (NTYMA.NE.NPEN15) .AND.
     &           (NTYMA.NE.NHEX8) .AND. (NTYMA.NE.NHEX20) .AND.
     &                                  (NTYMA.NE.NHEX27) ) THEN
               MAIL3D = .FALSE.
               GO TO 21
            ENDIF
  20     CONTINUE
  21     CONTINUE
      ENDIF
C
C 1.4 SORTIE EN ERREUR FATALE SI REPRESENTATION NON ACCEPTABLE
C ---
      IF ( (.NOT.MAIL2D) .AND. (.NOT.MAIL3D) )
     &   CALL U2MESS('F','MODELISA7_46')
C
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
C 2   DETERMINATION DES NOEUDS APPARTENANT A LA STRUCTURE BETON
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
C
C 2.1 CREATION D'UN OBJET DE TRAVAIL
C ---
      CALL WKVECT('&&TOMABE.NOE_COCHES','V V I',NBNOMA,JNCOCH)
      DO 30 INO = 1, NBNOMA
         ZI(JNCOCH+INO-1) = 0
  30  CONTINUE
C
C 2.2 LECTURE DES CONNECTIVITES DES MAILLES
C ---
      DO 40 IMAIL = 1, NBMABE
         NUMAIL = ZI(JNUMAB+IMAIL-1)
         CALL JELIRA(JEXNUM(CONXMA,NUMAIL),'LONMAX',NBCONX,K1B)
         CALL JEVEUO(JEXNUM(CONXMA,NUMAIL),'L',JCONX)
         DO 41 INO = 1, NBCONX
            NUMNOE = ZI(JCONX+INO-1)
            ZI(JNCOCH+NUMNOE-1) = ZI(JNCOCH+NUMNOE-1) + 1
  41     CONTINUE
  40  CONTINUE
C
C 2.3 DECOMPTE DES NOEUDS ET RELEVE DE LEUR NUMERO
C ---
      NBNOBE = 0
      DO 50 INO = 1, NBNOMA
         IF ( ZI(JNCOCH+INO-1).GT.0 ) NBNOBE = NBNOBE + 1
  50  CONTINUE
C
      CALL JEECRA(NUNOBE,'LONUTI',NBNOBE,' ')
      CALL JEVEUO(NUNOBE,'E',JNUNOB)
      IDECAL = 0
      DO 60 INO = 1, NBNOMA
         IF ( ZI(JNCOCH+INO-1).GT.0 ) THEN
            IDECAL = IDECAL + 1
            ZI(JNUNOB+IDECAL-1) = INO
         ENDIF
  60  CONTINUE
C
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
C 4   RECUPERATION DU MATERIAU CONSTITUTIF DE LA STRUCTURE BETON
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
C
      CARTE = CHMAT//'.CHAMP_MAT '
      CAVALK = CARTE//'.VALE'
      CAPTMA = CARTE//'.PTMA'
      CALL JEVEUO(CAVALK,'L',JVALK)
      CALL JEVEUO(CAPTMA,'L',JPTMA)
C
      NUMAIL = ZI(JNUMAB)
      IAS = ZI(JPTMA+NUMAIL-1)
      IF ( IAS.EQ.0 ) THEN
         WRITE(K3MAI,'(I3)') NUMAIL
         CALL U2MESK('F','MODELISA7_47',1,K3MAI)
      ENDIF
C


C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
C 5.  RECUPERATION DES CARACTERISTIQUES DU MATERIAU CONSTITUTIF
C     DE LA STRUCTURE BETON ET VERIFICATION DE LA COMPATIBILITE DES
C     MATERIAUX BETON.
C     ( LA LOI BPEL_BETON DOIT ETRE LA MEME POUR TOUTES LES MAILLES )
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


C     TRANSFORMATION DU CHAM_MATER EN CHAM_ELEM_S POUR TRAITEMENT

      CHSMAT='&&TOMABE.NOM_MATER'
      CARTEZ='&&TOMABE.CARTE'
      CHTMP='&&TOMABE.BID'

      CALL COPISD(' ','V',CARTE,CARTEZ)
      CALL CARCES(CARTEZ,'ELEM',CHTMP,'V',CHSMAT,'A',IRET)
      CALL JEVEUO(CHSMAT//'.CESD','L',JCESD)
      CALL JEVEUO(CHSMAT//'.CESL','L',JCESL)
      CALL JEVEUO(CHSMAT//'.CESV','L',JCESV)

      NUMAIL = ZI(JNUMAB+1-1)
      CALL CESEXI('C',JCESD,JCESL,NUMAIL,1,1,1,IAD)

C.... RELATION DE COMPORTEMENT <BPEL_BETON>

      BETON=ZK8(JCESV-1+IAD)
      NOMRC =  BETON//'.BPEL_BETON'
      RCVALK = NOMRC//'.VALK'
      CALL JEEXIN(RCVALK,IRET)
      IF ( IRET.EQ.0 )
     &   CALL U2MESS('F','MODELISA7_48')
      RCVALR = NOMRC//'.VALR'
      CALL JEVEUO(RCVALK,'L',JVALK)
      CALL JEVEUO(RCVALR,'L',JVALR)
      CALL JELIRA(RCVALR,'LONMAX',NBCSTE,K1B)
      TROUV1 = .FALSE.
      TROUV2 = .FALSE.
      DO 150 ICSTE = 1, NBCSTE
         IF ( ZK8(JVALK+ICSTE-1).EQ.BPELB(1) ) THEN
            TROUV1 = .TRUE.
            XFLU = ZR(JVALR+ICSTE-1)
         ENDIF
         IF ( ZK8(JVALK+ICSTE-1).EQ.BPELB(2) ) THEN
            TROUV2 = .TRUE.
            XRET = ZR(JVALR+ICSTE-1)
         ENDIF
         IF ( TROUV1 .AND. TROUV2 ) GO TO 151
  150 CONTINUE

  151 CONTINUE

C CRITERE DE COMPARAISON DES VALEURS MATERIAUX INTRODUITES
C PAR L UTILISATEUR DANS LA RELATION BPEL_BETON QUI DOIT ETRE
C UNIQUE A TOUT LE BETON
      CRITE=1.D-07

      IF ( NBMABE.GT.1 ) THEN
         DO 200 IMAIL = 2, NBMABE
            NUMAIL = ZI(JNUMAB+IMAIL-1)
            CALL CESEXI('C',JCESD,JCESL,NUMAIL,1,1,1,IAD)
                 BETON=ZK8(JCESV-1+IAD)
            NOMRC =  BETON//'.BPEL_BETON'
            RCVALK = NOMRC//'.VALK'
            RCVALR = NOMRC//'.VALR'
            CALL JEVEUO(RCVALK,'L',JVALK)
            CALL JEVEUO(RCVALR,'L',JVALR)
            CALL JELIRA(RCVALR,'LONMAX',NBCSTE,K1B)

            IAS = ZI(JPTMA+NUMAIL-1)
            IF ( IAS.EQ.0 ) THEN
               WRITE(K3MAI,'(I3)') NUMAIL
               CALL U2MESK('F','MODELISA7_47',1,K3MAI)
            ENDIF

            DO 250 ICSTE = 1, NBCSTE

              IF ( ZK8(JVALK+ICSTE-1).EQ.BPELB(1) ) THEN
                IF ( ABS(XFLU).LT.CRITE ) THEN
                  IF (ABS(XFLU-ZR(JVALR+ICSTE-1)).GT.CRITE) THEN
                    CALL U2MESS('F','MODELISA7_49')
                  ENDIF
                ELSE
                  IF (ABS((XFLU-ZR(JVALR+ICSTE-1))/XFLU).GT.CRITE) THEN
                    CALL U2MESS('F','MODELISA7_50')
                  ENDIF
                ENDIF
              ELSE IF ( ZK8(JVALK+ICSTE-1).EQ.BPELB(2) ) THEN
                IF ( ABS(XRET).LT.CRITE ) THEN
                  IF (ABS(XRET-ZR(JVALR+ICSTE-1)).GT.CRITE) THEN
                    CALL U2MESS('F','MODELISA7_51')
                  ENDIF
                ELSE
                  IF (ABS((XRET-ZR(JVALR+ICSTE-1))/XRET).GT.CRITE) THEN
                    CALL U2MESS('F','MODELISA7_49')
                  ENDIF
                ENDIF
              ENDIF
  250       CONTINUE
  200    CONTINUE
      ENDIF

      IF ( .NOT. ( TROUV1 .AND. TROUV2 ) )
     &   CALL U2MESS('F','MODELISA7_52')
      IF ( ( XFLU.LT.0.0D0 ) .OR. ( XRET.LT.0.0D0 ) .OR.
     &     ( XFLU+XRET.GT.1.0D0 ) )
     &   CALL U2MESS('F','MODELISA7_53')
C
C --- MENAGE
      CALL DETRSD('CHAM_ELEM_S',CHSMAT)
      CALL DETRSD('CARTE','&&TOMABE.CARTE')
      CALL DETRSD('CHAM_ELEM_S',CHTMP)
      CALL JEDEMA()
C
C --- FIN DE TOMABE.
      END
