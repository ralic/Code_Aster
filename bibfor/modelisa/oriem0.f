      SUBROUTINE ORIEM0(TYPE,NOMA,COOR,LINO1,NBNO1,LINO2,NBNO2,LINO3,
     &                  NBNO3,IER,IMAI)
      IMPLICIT   NONE
C RESPONSABLE PELLET J.PELLET
      INTEGER LINO1(*),NBNO1,LINO2(*),NBNO2,LINO3(*),NBNO3
      INTEGER IER,IMAI
      REAL*8 COOR(*)
      CHARACTER*8 TYPE
      CHARACTER*(*) NOMA
C.======================================================================
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 08/10/2012   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C BUT :  DETERMINER LA POSITION RELATIVE DE 2 MAILLES "VOLUMIQUES"
C        (LINO1 ET LINO2) PAR RAPPORT A UNE MAILLE DE "PEAU" (LINO3)
C        ON SUPPOSE QUE LES NOEUDS DE LA MAILLE DE PEAU APPARTIENNENT
C        AUX 2 MAILLES VOLUMIQUES.
C ======================================================================
C IN  : TYPE   : TYPE DE LA MAILLE DE PEAU (TRIA3, QUAD4, ...)
C IN  : NOMA   : NOM DU MAILLAGE
C IN  : LINO1  : LISTE DES NOEUDS DE LA MAILLE 1 (3D)
C IN  : NBNO1  : NB DE NOEUDS DE LINO1
C IN  : LINO2  : LISTE DES NOEUDS DE LA MAILLE 2  (3D)
C IN  : NBNO2  : NB DE NOEUDS DE LINO2
C IN  : LINO3  : LISTE DES NOEUDS DE LA MAILLE DE PEAU (2.5D)
C IN  : NBNO3  : NB DE NOEUDS DE DE LA MAILLE DE PEAU
C IN  : TYPE   : TYPE DE LA MAILLE DE PEAU (SEG,QUAD,..)
C OUT : IER    : = 0  MAILLE 1 ET MAILLES 2 DU MEME COTE (PAR RAPPORT
C                     A LA MAILLE DE PEAU)
C                = 1  SINON
C OUT : IMAI   : = /0 /1 /2
C          SI IER =0 :  IMAI = 0
C          SINON:  IMAI = 1 OU 2
C              IMAI EST LE NUMERO DE LA MAILLE QUI A LA MEME NORMALE
C              SORTANTE QUE LA MAILLE DE PEAU.
C.========================= DEBUT DES DECLARATIONS ====================
C
      INTEGER INO,N1,N2,N3,IC,K,INDIIS,INDI
      REAL*8 X1,X2,NOR1(3),N1N2(3),N1N3(3),PS1,PS2,DDOT
C
C ========================= DEBUT DU CODE EXECUTABLE ==================
C
      CALL JEMARQ()
      CALL ASSERT(TYPE(1:4).EQ.'TRIA' .OR. TYPE(1:4).EQ.'QUAD')

C --- INITIALISATIONS :
C     ---------------


C --- VERIFICATION DE LA POSITION DES MAILLES 1 ET 2
C     PAR RAPPORT A LA MAILLE DE PEAU :
C     -----------------------------------------------

C     DEFINITION DE LA NORMALE DE LA MAILLE DE PEAU: NOR1
      N1=LINO3(1)
      N2=LINO3(2)
      N3=LINO3(3)
      DO 10 IC=1,3
        N1N2(IC)=COOR(3*(N2-1)+IC)-COOR(3*(N1-1)+IC)
        N1N3(IC)=COOR(3*(N3-1)+IC)-COOR(3*(N1-1)+IC)
   10 CONTINUE
      CALL PROVEC(N1N2,N1N3,NOR1)


C     POSITION DE LA MAILLE 1 PAR RAPPORT A LA MAILLE DE PEAU
      DO 30 INO=1,NBNO1
        INDI=INDIIS(LINO3(1),LINO1(INO),1,NBNO3)
C        -- ON CHERCHE UN NOEUD DE LINO1 (N2) QUI NE SOIT PAS UN NOEUD
C           DE LA PEAU :
        IF (INDI.EQ.0) THEN
          N2=LINO1(INO)
          N1=LINO3(1)
          DO 20 IC=1,3
            N1N2(IC)=COOR(3*(N2-1)+IC)-COOR(3*(N1-1)+IC)
   20     CONTINUE
C          -- PS1 > 0 <=> LA NORMALE DE LA PEAU EST ORIENTEE COMME LA
C                         LA NORMALE EXTERIEURE DE LA MAILLE 1
          PS1=DDOT(3,N1N2,1,NOR1,1)
          GOTO 40

        ENDIF
   30 CONTINUE
      CALL ASSERT(.FALSE.)
   40 CONTINUE


C     POSITION DE LA MAILLE 2 PAR RAPPORT A LA MAILLE DE PEAU
      DO 60 INO=1,NBNO2
        INDI=INDIIS(LINO3(1),LINO2(INO),1,NBNO2)
        IF (INDI.EQ.0) THEN
          N2=LINO2(INO)
          N1=LINO3(1)
          DO 50 IC=1,3
            N1N2(IC)=COOR(3*(N2-1)+IC)-COOR(3*(N1-1)+IC)
   50     CONTINUE
          PS2=DDOT(3,N1N2,1,NOR1,1)
          GOTO 70

        ENDIF
   60 CONTINUE
      CALL ASSERT(.FALSE.)
   70 CONTINUE


C     LES MAILLES 1 ET 2 SONT ELLES DU MEME COTE PAR RAPPORT
C     A LA MAILLE DE PEAU ?
C     -------------------------------------------------------
      CALL ASSERT(PS1.NE.0.D0 .AND. PS2.NE.0.D0)
      IER=1
      IF ((PS1.GT.0.AND.PS2.GT.0) .OR. (PS1.LT.0.AND.PS2.LT.0))IER=0

      IMAI=0
      IF (IER.NE.0) THEN
        IF (PS1.LT.0) THEN
          IMAI=1
        ELSE
          IMAI=2
        ENDIF
      ENDIF

      CALL JEDEMA()

      END
