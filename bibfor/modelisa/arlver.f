      SUBROUTINE ARLVER(MODELE,LGMA  ,NBGMA,NOMSD,MODEL,
     &                  CINE  ,DIMVAR)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 12/02/2008   AUTEUR ABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2002  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C
C
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT NONE
      CHARACTER*8   MODELE
      CHARACTER*8   LGMA(*)
      INTEGER       NBGMA
      CHARACTER*10  NOMSD
      CHARACTER*8   MODEL
      CHARACTER*8   CINE
      INTEGER       DIMVAR
C
C ----------------------------------------------------------------------
C
C ROUTINE ARLEQUIN
C
C FILTRE, REGROUPEMENT, VERIFICATION GROUPES DE MAILLES
C
C ----------------------------------------------------------------------
C
C
C IN  MODELE : NOM DU MODELE
C IN  LGMA   : NOMS DES GROUPES DE MAILLES
C IN  NOMSD  : SD DOMAINE
C I/O NBGMA  : EN ENTREE -> NOMBRE DE GROUPES DE MAILLES
C              EN SORTIE -> NOMBRE DE MAILLES DANS .GROUPEMA
C OUT MODEL  : MODELISATION ASSOCIEE AUX MAILLES
C                '3D','AXIS', 'CPLAN' OU 'DPLAN'
C OUT CINE   :  CINEMATIQUE ASSOCIEE AUX MAILLES
C                'SOLIDE' OU 'COQUE'
C                UN POUR CHAQUE GROUPE + ZONE DE COLLAGE
C OUT DIMVAR : DIMENSION DE LA VARIETE POUR LES DEUX GROUPES
C                SI SOLIDE : DIMENSION DE L'ESPACE
C                SI COQUE  : DIMENSION DE L'ESPACE - 1
C
C SD DE SORTIE
C NOM.GROUPEMA : LISTE TRIEE DES MAILLES DU DOMAINE NOM (MA1,MA2,...)
C
C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ---------------------
C
      CHARACTER*32       JEXNUM , JEXNOM
      INTEGER            ZI
      COMMON  / IVARJE / ZI(1)
      REAL*8             ZR
      COMMON  / RVARJE / ZR(1)
      COMPLEX*16         ZC
      COMMON  / CVARJE / ZC(1)
      LOGICAL            ZL
      COMMON  / LVARJE / ZL(1)
      CHARACTER*8        ZK8
      CHARACTER*16                ZK16
      CHARACTER*24                          ZK24
      CHARACTER*32                                    ZK32
      CHARACTER*80                                              ZK80
      COMMON  / KVARJE / ZK8(1) , ZK16(1) , ZK24(1) , ZK32(1) , ZK80(1)
C
C --- FIN DECLARATIONS NORMALISEES JEVEUX ------------------------------
C
      CHARACTER*8  NOMA,K8BID,NOMMAI
      CHARACTER*16 NOMTE,MODTE,CINTE
      INTEGER      NBMA,NTOT,NBLIGR,NUMA,NINIT
      INTEGER      ICOMPT,IGMA,IMA,ILIGR,IRET
      INTEGER      JMAIL,JREPE,JCOMPT,JTE,JGMA,JGROUP,JTYEL
      LOGICAL      ELTOK,ARLELT
      INTEGER      IFM,NIV
C
C ---------------------------------------------------------------------
C
      CALL JEMARQ()
      CALL INFNIV(IFM,NIV)
C
C --- INITIALISATIONS
C
      NINIT = 0
      CINTE = ' '
      MODTE = ' '
C
C --- ACCES MODELE
C
      CALL JEVEUO(MODELE(1:8)//'.MODELE    .LGRF','L',JMAIL)
      CALL JEVEUO(MODELE(1:8)//'.MODELE    .REPE','L',JREPE)
      CALL JELIRA(MODELE(1:8)//'.MODELE    .LIEL','NMAXOC',NBLIGR,K8BID)
      CALL JEVEUO(MODELE(1:8)//'.MAILLE','L',JTYEL)
C
C --- NOM DU MAILLAGE
C
      NOMA = ZK8(JMAIL)
C
C --- ALLOCATION OBJETS TEMPORAIRES
C
      CALL WKVECT('&&ARLVER.COMPTEUR','V V I',NBLIGR,JCOMPT)
      CALL WKVECT('&&ARLVER.TE','V V I',NBLIGR,JTE)
C
C --- COMPTER LE NOMBRE DE MAILLES AFFECTEES A CHAQUE TYPE D'ELEMENT
C --- DONNER LE NUMERO ABSOLU DU NUMERO D'ELEMENT AFFECTE A CHAQUE TYPE
C
      DO 20 IGMA = 1, NBGMA
        CALL JEEXIN(JEXNOM(NOMA(1:8)//'.GROUPEMA',LGMA(IGMA)),IRET)
        IF (IRET.EQ.0) THEN
          CALL U2MESG ('F','ARLEQUIN_2',1,LGMA(IGMA),0,0,0,0.D0)
        ENDIF
        CALL JEVEUO(JEXNOM(NOMA(1:8)//'.GROUPEMA',LGMA(IGMA)),'L',JGMA)
        CALL JELIRA(JEXNOM(NOMA(1:8)//'.GROUPEMA',LGMA(IGMA)),'LONMAX',
     &              NBMA,K8BID)
        NINIT = NINIT + NBMA
        DO 20 IMA = 1, NBMA
          NUMA  = ZI(JGMA-1+IMA)
          ILIGR = ZI(JREPE+2*(NUMA-1))
          IF (ILIGR.NE.0) THEN
            IF (ZI(JCOMPT-1+ILIGR).EQ.0) THEN
              ZI(JTE-1+ILIGR) = ZI(JTYEL-1+NUMA)
            ENDIF
            ZI(JCOMPT-1+ILIGR) = ZI(JCOMPT-1+ILIGR) + 1
          ENDIF
 20   CONTINUE
C
C --- VERIFICATION COHERENCE MODELISATION / CINEMATIQUE
C
      NTOT = 0
      DO 30 ILIGR = 1, NBLIGR
        IF (ZI(JCOMPT-1+ILIGR).NE.0) THEN
          CALL JENUNO(JEXNUM('&CATA.TE.NOMTE',ZI(JTE-1+ILIGR)),NOMTE)
          ELTOK = ARLELT(NOMTE,MODTE,CINTE)
          IF (.NOT.ELTOK) THEN
            ZI(JCOMPT-1+ILIGR) = 0
            GOTO 30
          ELSE
            IF (NTOT.EQ.0) THEN
              MODEL = MODTE
              CINE  = CINTE
            ELSE
              IF (MODTE.NE.MODEL) THEN
                CALL U2MESS('F','ARLEQUIN_3')
              ENDIF
              IF (CINTE.NE.CINE) THEN
                CALL U2MESS('F','ARLEQUIN_4')
              ENDIF
            ENDIF
            NTOT = NTOT + ZI(JCOMPT-1+ILIGR)
          ENDIF
        ENDIF
 30   CONTINUE
C
C --- AUCUNE MAILLE DU GROUPE N'EST UTILISABLE
C
      IF (NTOT.EQ.0) THEN
        CALL U2MESS('F','ARLEQUIN_5')
      ENDIF
C
C --- DIMENSION DE LA VARIETE
C
      IF (MODEL(1:2).EQ.'3D') THEN
        DIMVAR = 3
      ELSE
        DIMVAR = 2
      ENDIF
      IF (CINE(1:5).EQ.'COQUE') THEN
        DIMVAR = DIMVAR - 1
      ENDIF
C
      IF (NIV.GE.2) THEN
        WRITE(IFM,*) '<ARLEQUIN> ... NOMBRE DE MAILLES INITIALES : ',
     &                NINIT
        WRITE(IFM,*) '<ARLEQUIN> ... TRI DES MAILLES VALIDES...'
      ENDIF
C
C --- ALLOCATION .GROUPEMA
C
      CALL WKVECT(NOMSD//'.GROUPEMA','V V I',NTOT,JGROUP)
C
C --- COPIE DES MAILLES VALIDES
C
      ICOMPT = 0
      DO 50 IGMA = 1, NBGMA
        CALL JEVEUO(JEXNOM(NOMA//'.GROUPEMA',LGMA(IGMA)),'L',JGMA)
        CALL JELIRA(JEXNOM(NOMA//'.GROUPEMA',LGMA(IGMA)),'LONMAX',
     &              NBMA,ZK8)
        DO 50 IMA = 1, NBMA
          NUMA  = ZI(JGMA-1+IMA)
          ILIGR = ZI(JREPE+2*(NUMA-1))
          CALL JENUNO(JEXNUM('&CATA.TE.NOMTE',ZI(JTE-1+ILIGR)),NOMTE)
          CALL JENUNO(JEXNUM(NOMA//'.NOMMAI',NUMA),NOMMAI)
          IF ((ILIGR.NE.0).AND.(ZI(JCOMPT-1+ILIGR).NE.0)) THEN
            ZI(JGROUP+ICOMPT) = NUMA
            ICOMPT = ICOMPT + 1
          ENDIF
 50   CONTINUE
C
      IF (ICOMPT.NE.NTOT) THEN
        CALL ASSERT(.FALSE.)
      ENDIF
      NBGMA = NTOT
      CALL TRI(ZI(JGROUP),ZI(JGROUP),0,NBGMA)
C
      IF (NIV.GE.2) THEN
        WRITE(IFM,*) '<ARLEQUIN> ... MODELISATION              : ',
     &                MODEL
        WRITE(IFM,*) '<ARLEQUIN> ... CINEMATIQUE               : ',
     &                CINE
        WRITE(IFM,*) '<ARLEQUIN> ... NOMBRE DE MAILLES FINALES : ',
     &                NTOT
      ENDIF
C
C --- MENAGE
C
      CALL JEDETR('&&ARLVER.COMPTEUR')
      CALL JEDETR('&&ARLVER.TE')
C
      CALL JEDEMA()

      END
