      SUBROUTINE ARLAS3(DIME  ,LCARA ,NDML1   ,NDML2   ,PTMT     ,
     &                  CNML2   ,NORM  ,MTEL    ,MTMO)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 13/10/2009   AUTEUR CAO B.CAO 
C ======================================================================
C COPYRIGHT (C) 1991 - 2002  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C
C
C ======================================================================
C RESPONSABLE MEUNIER S.MEUNIER
C
      IMPLICIT NONE
      INTEGER  DIME,NDML1,NDML2
      INTEGER  PTMT(NDML2/2,*)
      INTEGER  CNML2(*)
      REAL*8   LCARA
      REAL*8   NORM(DIME,*)
      REAL*8   MTEL(*),MTMO(*)
C
C ----------------------------------------------------------------------
C
C ROUTINE ARLEQUIN
C
C GESTION DES RELATIONS LINEAIRES
C ASSEMBLAGE MATRICE ELEMENTAIRE ARLEQUIN
C   MAILLE SOLIDE / MAILLE COQUE
C
C ----------------------------------------------------------------------
C
C
C IN  DIME   : DIMENSION DE L'ESPACE
C IN  LCARA  : LONGUEUR CARACTERISTIQUE POUR TERME DE COUPLAGE
C IN  NDML1  : NOMBRE DE NOEUDS MAILLE 1
C IN  NDML2  : NOMBRE DE NOEUDS MAILLE 2
C IN  PTMT   : POINTEURS DANS C (CF ARLAS0)
C IN  CNML2  : CONNECTIVITE MAILLE 2
C IN  NORM   : NORMALES LISSEES POUR COQUE (CF LISNOR)
C IN  MTEL   : MATRICES ELEMENTAIRES (CF ARLTE)
C I/O MTMO      : MATRICE MORSE (CF ARLFC*)
C
C MATRICE PONCTUELLE DANS MTMO : (X1.X2, X1.Y2, [X1.Z2], Y1.X2, ...,
C                 (N X X1.*2).1, (N X X1.*2).2, ..., (N X Y1.*2).1, ...)
C
C ----------------------------------------------------------------------
C
      INTEGER NOECOQ(2,9),NDML12,VLTER,VLTER2,VLTERR
      INTEGER NBNO,IN2,IAUX,JAUX,KAUX,JND1,JND2,PMAT,PMAT0,PMAT1,
     &        PMAT2,LGNM,LGNM1,LGNM2
      REAL*8  TRMT1,TRMT2,VLR1,VLR2,MTMO1(9),VLMT2(9),MTMO2(9)
C
C ----------------------------------------------------------------------
C
      IF (DIME.EQ.2) THEN
        VLTER2 = 4
        VLTERR = 2
        VLTER = 6
      ELSE IF (DIME.EQ.3) THEN
        VLTER2 = 9
        VLTERR = 9
        VLTER = 18
      ELSE
        CALL ASSERT(.FALSE.)
      ENDIF

      NBNO = NDML2/2
      NDML12 = NDML1*NDML2

C --- ASSEMBLAGE DE LA MATRICE ELEMENTAIRE MTEL

      PMAT0 = -NDML2

      CALL NOCOQU(DIME,NBNO,NOECOQ)

      DO 10 IAUX = 1, NDML1

        PMAT0 = PMAT0 + NDML2

        DO 10 JAUX = 1, NBNO

          LGNM = 1 + VLTER*(PTMT(JAUX,IAUX)-1)

          IN2 = CNML2(JAUX)

          JND1 = NOECOQ(2,JAUX)
          JND2 = NOECOQ(1,JAUX)

          PMAT1 = PMAT0 + JND1
          PMAT2 = PMAT0 + JND2

          LGNM1 = NDML12 + VLTER2*(PMAT1-1)
          LGNM2 = NDML12 + VLTER2*(PMAT2-1)

C ------- CALCUL DE LA TRACE DE MTEL+ ET MTEL-

          TRMT1 = 0.D0
          TRMT2 = 0.D0

          PMAT = 1
          DO 20 KAUX = 1, DIME
            TRMT1 = TRMT1 + MTEL(LGNM1+PMAT)
            TRMT2 = TRMT2 + MTEL(LGNM2+PMAT)
            PMAT = PMAT + DIME + 1
 20       CONTINUE

C ------- CALCUL DES MATRICES PONCTUELLES MTMO1 ET MTMO2

          DO 30 KAUX = 1, VLTER2
            LGNM1 = LGNM1 + 1
            LGNM2 = LGNM2 + 1
            MTMO1(KAUX) = LCARA*(MTEL(LGNM1)+MTEL(LGNM2))
            VLMT2(KAUX) = LCARA*(MTEL(LGNM1)-MTEL(LGNM2))
 30       CONTINUE

          VLR1 = MTEL(PMAT1) + MTEL(PMAT2) + LCARA*(TRMT1+TRMT2)
          VLR2 = MTEL(PMAT1) - MTEL(PMAT2) + LCARA*(TRMT1-TRMT2)

          PMAT = 1
          DO 40 KAUX = 1, DIME
            MTMO1(PMAT) = MTMO1(PMAT) + VLR1
            VLMT2(PMAT) = VLMT2(PMAT) + VLR2
            PMAT = PMAT + DIME + 1
 40       CONTINUE

          IF (DIME.EQ.2) THEN

            VLR1 = NORM(1,IN2)
            VLR2 = NORM(2,IN2)

            MTMO2(1) = VLR1*VLMT2(2) - VLR2*VLMT2(1)
            MTMO2(2) = VLR1*VLMT2(4) - VLR2*VLMT2(3)

          ELSE

            PMAT = 1
            DO 50 KAUX = 1, DIME
              CALL PROVEC(NORM(1,IN2),VLMT2(PMAT),MTMO2(PMAT))
              PMAT = PMAT + DIME
 50         CONTINUE

          ENDIF

C ------- ASSEMBLAGE DES MATRICES PONCTUELLES MTMO1 ET MTMO2

          DO 60 KAUX = 1, VLTER2
            MTMO(LGNM) = MTMO(LGNM) + MTMO1(KAUX)
            LGNM = LGNM + 1
 60       CONTINUE

          DO 70 KAUX = 1, VLTERR
            MTMO(LGNM) = MTMO(LGNM) + MTMO2(KAUX)
            LGNM = LGNM + 1
 70       CONTINUE

 10   CONTINUE

      END
