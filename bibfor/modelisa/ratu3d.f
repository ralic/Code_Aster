      SUBROUTINE RATU3D(IPRNO,LONLIS,KLISNO,NOEPOU,NOMA,LIGREL,MOD,CARA,
     &                  NUMDDL,TYPLAG,LISREL,COORIG,SECTIO)
      IMPLICIT NONE
      INTEGER LONLIS,IPRNO(*)
      CHARACTER*2 TYPLAG
      CHARACTER*8 KLISNO(LONLIS),NOEPOU,NOMA,CARA,MOD
      CHARACTER*14 NUMDDL
      CHARACTER*19 LIGREL,LISREL
      REAL*8 COORIG(3),RAYON
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 08/11/2010   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.

C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.

C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C -------------------------------------------------------
C     RACCORD 3D_TUYAU PAR DES RELATIONS LINEAIRES
C     ECRITURE DES RELATIONS SUR LES DDLS DE FOURIER

C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ------
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C --------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ------
      INTEGER NBCMP,NBMODE,IRAYO,IBID
      PARAMETER (NBMODE=3,NBCMP=6* (NBMODE-1))
      CHARACTER*1 K1BID
      CHARACTER*8 NOCMP(NBCMP),LPAIN(5),LPAOUT(6),NOMDDL(4)
      CHARACTER*24 LCHIN(5),LCHOUT(6),VALECH
      REAL*8 RBID,COEF(4),EG1(3),EG2(3),EG3(3),SECTIO
      COMPLEX*16 CBID
      INTEGER IAREFE,IMOD,INFO,IER,IFM
      INTEGER NBCOEF,IDEC,IUNIFI

      CALL JEMARQ()
      CALL INFNIV(IFM,INFO)


C     APPEL DE L OPTION CARA_SECT_POU3R AFIN DE CALCULER LE RAYON
C     MOYEN DE LA SECTION ANNULAIRE DE LA PARTIE 3D
C     L APPEL A MESOMM DONNE L INTEGRALE SURFACIQUE DU RAYON

      LPAIN(1) = 'PGEOMER'
      LCHIN(1) = NOMA//'.COORDO'
      LPAIN(2) = 'PORIGIN'
      LCHIN(2) = '&&RAPO3D.CAORIGE'
      LPAOUT(1) = 'PRAYONM'
      LCHOUT(1) = '&&RAPO3D.PRAYO'

      CALL CALCUL('S','CARA_SECT_POU3R',LIGREL,2,LCHIN,LPAIN,1,LCHOUT,
     &            LPAOUT,'V','OUI')

      CALL WKVECT('&&RATU3D.RAYON_RACCORD','V V R',1,IRAYO)
      CALL MESOMM(LCHOUT(1),1,IBID,ZR(IRAYO),CBID,0,IBID)
      RAYON = ZR(IRAYO+1-1)/SECTIO


C     CREATION D'UNE CARTE CONTENANT LE POINT P ORIGINE DE PHI

      CALL RAORFI(NOMA,LIGREL,NOEPOU,CARA,COORIG,EG1,EG2,
     &            EG3,'&&RATU3D',RAYON)

C --- DETERMINATION DE 3 LISTES  DE VECTEURS PAR ELEMENT PRENANT
C --- LEURS VALEURS AUX NOEUDS DES ELEMENTS. CES VALEURS SONT :
C     VECT_COEF_UM
C --- SOMME/S_ELEMENT(COS(M.PHI).P(1,J).NI)DS
C --- SOMME/S_ELEMENT(SIN(M.PHI).P(1,J).NI)DS
C     VECT_COEF_VM
C --- SOMME/S_ELEMENT(COS(M.PHI).P(2,J).NI)DS
C --- SOMME/S_ELEMENT(SIN(M.PHI).P(2,J).NI)DS
C     VECT_COEF_WM
C --- SOMME/S_ELEMENT(COS(M.PHI).P(3,J).NI)DS
C --- SOMME/S_ELEMENT(SIN(M.PHI).P(3,J).NI)DS

C --- OU P EST LA MATRICE DE PASSAGE DU REPERE GLOBAL AU REPERE
C --- (E1,E2,E3) DEFINI SUR LE BORD ORIENTE DE LA SURFACE
C     ------------------------------
      LPAIN(1) = 'PGEOMER'
      LCHIN(1) = NOMA//'.COORDO'
      LPAIN(2) = 'PORIGIN'
      LCHIN(2) = '&&RAPO3D.CAORIGE'
      LPAIN(3) = 'PCAORIE'
      LCHIN(3) = '&&RATU3D.CAXE_TUY'
      LPAIN(4) = 'PORIGFI'
      LCHIN(4) = '&&RATU3D.CAORIFI'
      LPAIN(5) = 'PNUMMOD'
      LCHIN(5) = '&&RATU3D.NUME_MODE'
      LPAOUT(1) = 'PVECTU1'
      LCHOUT(1) = '&&RATU3D.COSF_UM'
      LPAOUT(2) = 'PVECTU2'
      LCHOUT(2) = '&&RATU3D.COSF_VM'
      LPAOUT(3) = 'PVECTU3'
      LCHOUT(3) = '&&RATU3D.COSF_WM'
      LPAOUT(4) = 'PVECTU4'
      LCHOUT(4) = '&&RATU3D.SINF_UM'
      LPAOUT(5) = 'PVECTU5'
      LCHOUT(5) = '&&RATU3D.SINF_VM'
      LPAOUT(6) = 'PVECTU6'
      LCHOUT(6) = '&&RATU3D.SINF_WM'

C --- CREATION DES .RERR DES VECTEURS EN SORTIE DE CALCUL

      CALL MEMARE('V','&&RATU3D',MOD,' ',' ','CHAR_MECA')

C     RELATIONS ENTRE LES NOEUDS DE SURFACE ET LE NOEUD POUTRE DDL WO

      IMOD = 0
      CALL MECACT('V',LCHIN(5),'LIGREL',LIGREL,'NUMMOD',1,'NUM',IMOD,
     &            RBID,CBID,K1BID)
      CALL CALCUL('S','CARA_SECT_POUT5',LIGREL,5,LCHIN,LPAIN,6,LCHOUT,
     &            LPAOUT,'V','OUI')
      CALL JEDETR('&&RATU3D           .RELR')
      CALL REAJRE('&&RATU3D',LCHOUT(3),'V')
      CALL ASSVEC('V','CH_DEPL_3',1,'&&RATU3D           .RELR',
     &            1.D0,NUMDDL, ' ','ZERO',1)
      VALECH = 'CH_DEPL_3          .VALE'
      NBCOEF = 1
      IDEC = 0
      NOMDDL(1) = 'WO'
      COEF(1) = -1.D0*SECTIO
      CALL AFRETU(IPRNO,LONLIS,KLISNO,NOEPOU,NOMA,VALECH,NBCOEF,IDEC,
     &            COEF,NOMDDL,TYPLAG,LISREL)
C        FIN IMOD=0

C   RELATIONS ENTRE LES NOEUDS DE COQUE ET LE NOEUD POUTRE
C   DDL UIM, UOM, VIM, VOM, WIM, WOM, M VARIANT DE 2 A NBMODE

      NOCMP(1) = 'UI2'
      NOCMP(2) = 'VI2'
      NOCMP(3) = 'WI2'
      NOCMP(4) = 'UO2'
      NOCMP(5) = 'VO2'
      NOCMP(6) = 'WO2'
      NOCMP(7) = 'UI3'
      NOCMP(8) = 'VI3'
      NOCMP(9) = 'WI3'
      NOCMP(10) = 'UO3'
      NOCMP(11) = 'VO3'
      NOCMP(12) = 'WO3'

      DO 10 IMOD = 1,NBMODE
        IF (INFO.EQ.2) THEN
          WRITE (IFM,*) 'RELATIONS SUR LE MODE ',IMOD
        END IF
        CALL MECACT('V',LCHIN(5),'LIGREL',LIGREL,'NUMMOD',1,'NUM',IMOD,
     &              RBID,CBID,K1BID)
        CALL CALCUL('S','CARA_SECT_POUT5',LIGREL,5,LCHIN,LPAIN,6,LCHOUT,
     &              LPAOUT,'V','OUI')

C     RELATIONS ENTRE LES NOEUDS DE SURFACE ET LE NOEUD POUTRE DDL UIM

        CALL JEDETR('&&RATU3D           .RELR')
        CALL REAJRE('&&RATU3D',LCHOUT(1),'V')
        CALL ASSVEC('V','CH_DEPL_1',1,'&&RATU3D           .RELR',
     &               1.D0,NUMDDL,' ','ZERO',1)
        VALECH = 'CH_DEPL_1          .VALE'
        IDEC = 0
        IF (IMOD.NE.1) THEN
          NBCOEF = 1
          NOMDDL(1) = NOCMP(6* (IMOD-2)+1)
          COEF(1) = -SECTIO/2.D0
          CALL AFRETU(IPRNO,LONLIS,KLISNO,NOEPOU,NOMA,VALECH,NBCOEF,
     &                IDEC,COEF,NOMDDL,TYPLAG,LISREL)

        END IF

C     RELATIONS ENTRE LES NOEUDS DE SURFACE ET LE NOEUD POUTRE DDL UOM

        CALL JEDETR('&&RATU3D           .RELR')
        CALL REAJRE('&&RATU3D',LCHOUT(4),'V')
        CALL ASSVEC('V','CH_DEPL_4',1,'&&RATU3D           .RELR',
     &              1.D0,NUMDDL,' ','ZERO',1)
        VALECH = 'CH_DEPL_4          .VALE'
        IDEC = 0
        IF (IMOD.NE.1) THEN
          NBCOEF = 1
          NOMDDL(1) = NOCMP(6* (IMOD-2)+4)
          COEF(1) = -SECTIO/2.D0
          CALL AFRETU(IPRNO,LONLIS,KLISNO,NOEPOU,NOMA,VALECH,NBCOEF,
     &                IDEC,COEF,NOMDDL,TYPLAG,LISREL)
        END IF

C     RELATIONS ENTRE LES NOEUDS DE SURFACE ET LE NOEUD POUTRE DDL VOM

        CALL JEDETR('&&RATU3D           .RELR')
        CALL REAJRE('&&RATU3D',LCHOUT(2),'V')
        CALL ASSVEC('V','CH_DEPL_2',1,'&&RATU3D           .RELR',
     &              1.D0,NUMDDL,' ','ZERO',1)
        VALECH = 'CH_DEPL_2          .VALE'
        IDEC = 0
        IF (IMOD.NE.1) THEN
          NBCOEF = 1
          NOMDDL(1) = NOCMP(6* (IMOD-2)+5)
          COEF(1) = -SECTIO/2.D0
          CALL AFRETU(IPRNO,LONLIS,KLISNO,NOEPOU,NOMA,VALECH,NBCOEF,
     &                IDEC,COEF,NOMDDL,TYPLAG,LISREL)
        END IF

C     RELATIONS ENTRE LES NOEUDS DE SURFACE ET LE NOEUD POUTRE DDL VIM

        CALL JEDETR('&&RATU3D           .RELR')
        CALL REAJRE('&&RATU3D',LCHOUT(5),'V')
        CALL ASSVEC('V','CH_DEPL_5',1,'&&RATU3D           .RELR',
     &               1.D0,NUMDDL,' ','ZERO',1)
        VALECH = 'CH_DEPL_5          .VALE'
        IDEC = 0
        IF (IMOD.NE.1) THEN
          NBCOEF = 1
          NOMDDL(1) = NOCMP(6* (IMOD-2)+2)
          COEF(1) = -SECTIO/2.D0
          CALL AFRETU(IPRNO,LONLIS,KLISNO,NOEPOU,NOMA,VALECH,NBCOEF,
     &                IDEC,COEF,NOMDDL,TYPLAG,LISREL)
        END IF

C     RELATIONS ENTRE LES NOEUDS DE SURFACE ET LE NOEUD POUTRE DDL WIM
C     OU SI IMOD=1 LE DDL DZ DANS REPERE LOCAL DU TUYAU ET WI1
C     IDEC=0 SIGNIFIE QUE ON UTILISE LES TERMES EN COS(M*PHI)

        CALL JEDETR('&&RATU3D           .RELR')
        CALL REAJRE('&&RATU3D',LCHOUT(3),'V')
        CALL ASSVEC('V','CH_DEPL_3',1,'&&RATU3D           .RELR',
     &               1.D0,NUMDDL,' ','ZERO',1)
        VALECH = 'CH_DEPL_3          .VALE'
        IDEC = 0

        IF (IMOD.EQ.1) THEN
          NBCOEF = 4
          NOMDDL(1) = 'DX'
          NOMDDL(2) = 'DY'
          NOMDDL(3) = 'DZ'
          NOMDDL(4) = 'WI1'
          COEF(1) = EG3(1)*SECTIO/2.D0
          COEF(2) = EG3(2)*SECTIO/2.D0
          COEF(3) = EG3(3)*SECTIO/2.D0
          COEF(4) = -SECTIO/2.D0
        ELSE
C         IF (IMOD.NE.1) THEN
          NBCOEF = 1
          NOMDDL(1) = NOCMP(6* (IMOD-2)+3)
          COEF(1) = -SECTIO/2.D0
        END IF
        CALL AFRETU(IPRNO,LONLIS,KLISNO,NOEPOU,NOMA,VALECH,NBCOEF,IDEC,
     &              COEF,NOMDDL,TYPLAG,LISREL)
C         ENDIF

C     RELATIONS ENTRE LES NOEUDS DE SURFACE ET LE NOEUD POUTRE DDL WOM
C     OU SI IMOD=1 LE DDL DY DANS REPERE LOCAL DU TUYAU ET WO1

        CALL JEDETR('&&RATU3D           .RELR')
        CALL REAJRE('&&RATU3D',LCHOUT(6),'V')
        CALL ASSVEC('V','CH_DEPL_6',1,'&&RATU3D           .RELR',
     &               1.D0,NUMDDL,' ','ZERO',1)
        VALECH = 'CH_DEPL_6          .VALE'
        IDEC = 0
        IF (IMOD.EQ.1) THEN
          NBCOEF = 4
          NOMDDL(1) = 'DX'
          NOMDDL(2) = 'DY'
          NOMDDL(3) = 'DZ'
          NOMDDL(4) = 'WO1'
          COEF(1) = EG2(1)*SECTIO/2.D0
          COEF(2) = EG2(2)*SECTIO/2.D0
          COEF(3) = EG2(3)*SECTIO/2.D0
          COEF(4) = -SECTIO/2.D0
        ELSE
C         IF (IMOD.NE.1) THEN
          NBCOEF = 1
          NOMDDL(1) = NOCMP(6* (IMOD-2)+6)
          COEF(1) = -SECTIO/2.D0
        END IF
        CALL AFRETU(IPRNO,LONLIS,KLISNO,NOEPOU,NOMA,VALECH,NBCOEF,IDEC,
     &              COEF,NOMDDL,TYPLAG,LISREL)
C         ENDIF

C     FIN DE LA BOUCLE SUR LES MODES

   10 CONTINUE

C --- DESTRUCTION DES OBJETS DE TRAVAIL

      CALL JEDETC('V','&&RATU3D',1)
      CALL JEDETC('V','CH_DEPL_1',1)
      CALL JEDETC('V','CH_DEPL_2',1)
      CALL JEDETC('V','CH_DEPL_3',1)
      CALL JEDETC('V','CH_DEPL_4',1)
      CALL JEDETC('V','CH_DEPL_5',1)
      CALL JEDETC('V','CH_DEPL_6',1)

      CALL JEDEMA()
      END
