      SUBROUTINE DEFFEN(BASE,NUOR,IMODI,NBMR,NBM,IAXE,LONG,NBNFEN,NOFE,
     &                  DISCFE,NBP1,NBP2,DISCFF,DEFM)
      IMPLICIT REAL*8 (A-H,O-Z)
C-----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 19/02/2008   AUTEUR MACOCCO K.MACOCCO 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C-----------------------------------------------------------------------
C     EXTRACTION DES COMPOSANTES DES DEFORMEES MODALES SUR LA
C     FENETRE EXCITEE, DANS LES DEUX DIRECTIONS ORTHOGONALES A LA
C     POUTRE. INTERPOLATION SUR LA DISCRETISATION DES FONCTIONS DE FORME
C     ASSOCIEES A L'EXCITATION.
C     APPELANT : SPECFF
C-----------------------------------------------------------------------
C IN  : BASE   : NOM DU CONCEPT MELASFLU
C IN  : NUOR   : NUMEROS D'ORDRE DES MODES DU CONCEPT MELASFLU
C IN  : IMODI  : INDICE DU PREMIER MODE PRIS EN COMPTE
C IN  : NBMR   : NOMBRE DE MODES PRIS EN COMPTE
C IN  : NBM    : NOMBRE DE MODES DU CONCEPT MELASFLU
C IN  : IAXE   : ENTIER DEFINISSANT L'AXE DIRECTEUR
C       IAXE = 1 L'AXE DIRECTEUR EST L'AXE DES X DU REPERE GLOBAL
C       IAXE = 2 L'AXE DIRECTEUR EST L'AXE DES Y DU REPERE GLOBAL
C       IAXE = 3 L'AXE DIRECTEUR EST L'AXE DES Z DU REPERE GLOBAL
C IN  : LONG   : VALEUR DE LA LONGUEUR EXCITEE
C IN  : NBNFEN : NOMBRE DE NOEUDS APPARTENANT A LA FENETRE EXCITEE
C IN  : NOFE   : LISTE DES NUMEROS DES NOEUDS APPARTENANT A LA
C                FENETRE EXCITEE
C IN  : DISCFE : LISTE DES VALEURS DE LA COORDONNEE D'ESPACE SUR LA
C                POUTRE POUR LES NOEUDS APPARTENANT A LA FENETRE
C                EXCITEE. CES VALEURS ONT ETE PREALABLEMENT TRANSLATEES
C                AFIN DE LES SUPERPOSER AU DOMAINE DE DEFINITION DES
C                FONCTIONS DE FORME ASSOCIEES A L'EXCITATION.
C IN  : NBP1   : NOMBRE DE POINTS DE DISCRETISATION DES FONCTIONS DE
C                FORME SUR L'INTERVALLE 0,L
C IN  : NBP2   : NOMBRE DE POINTS DE DISCRETISATION DES FONCTIONS DE
C                FORME SUR L'INTERVALLE L,2L
C IN  : DISCFF : DISCRETISATION DES FONCTIONS DE FORME SUR 0,2L
C OUT : DEFM   : TABLEAU DES DEFORMEES MODALES
C
C REMARQUE
C
C  DEFM EST UN TABLEAU DE DIMENSION (NBP,NBMR).
C
C  DANS CHAQUE COLONNE (I.E. POUR CHAQUE MODE PRIS EN COMPTE), LES
C  NBP1 PREMIERES VALEURS DONNENT LA COMPOSANTE DE LA DEFORMEE DANS
C  LA PREMIERE DIRECTION ORTHOGONALE A LA POUTRE. LES NBP2 SUIVANTES
C  DONNENT LA COMPOSANTE DANS LA DEUXIEME DIRECTION ORTHOGONALE A LA
C  POUTRE.
C
C  DANS UN PREMIER TEMPS, CES VALEURS SONT EXTRAITES SUR LA
C  DISCRETISATION DISCFE PUIS SONT INTERPOLEES SUR LA DISCRETISATION
C  DES FONCTIONS DE FORME DISCFF. LES VALEURS INTERPOLEES SONT ENSUITE
C  RANGEES DANS LE TABLEAU DEFM.
C
C     ----- DEBUT COMMUNS NORMALISES  JEVEUX  --------------------------
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C     -----  FIN  COMMUNS NORMALISES  JEVEUX  --------------------------
C
      INTEGER      IMODI,NBMR,NBM,NUOR(NBM)
      INTEGER      IAXE,NBNFEN,NOFE(NBNFEN),NBP1,NBP2
      REAL*8       LONG,DISCFE(NBNFEN),DISCFF(NBP1+NBP2)
      REAL*8       DEFM(NBP1+NBP2,NBMR)
      CHARACTER*19 BASE
C
      CHARACTER*8  NOMPAR
      CHARACTER*24 CHVALE
C
C-----------------------------------------------------------------------
      CALL JEMARQ()
C
C --- 1.INITIALISATIONS
C
      IMODF = IMODI + NBMR - 1
      IV = 1
      IF (IAXE.EQ.1) THEN
        IDIR1 = 2
        IDIR2 = 3
        NOMPAR = 'X'
      ELSE IF (IAXE.EQ.2) THEN
        IDIR1 = 3
        IDIR2 = 1
        NOMPAR = 'Y'
      ELSE
        IDIR1 = 1
        IDIR2 = 2
        NOMPAR = 'Z'
      ENDIF
C
C --- 2.CREATION DE VECTEURS DE TRAVAIL POUR FOINTR
C
C --- 2.1.CREATION D'UN CHPROL
C
      CALL WKVECT('&&DEFFEN.TEMP.PROL','V V K24',6,IPROL)
      ZK24(IPROL)   = 'FONCTION'
      ZK24(IPROL+1) = 'LIN LIN '
      ZK24(IPROL+2) =  NOMPAR
      ZK24(IPROL+3) = 'TOUTRESU'
      ZK24(IPROL+4) = 'CC      '
      ZK24(IPROL+5) = '&&DEFFEN.TEMP.PR'
C
C --- 2.2.CREATION D'UNE DISCRETISATION TRANSLATEE SUR LA FENETRE
C ---     EXCITEE, UTILE POUR L'INTERPOLATION SUR L,2L
C
      CALL WKVECT('&&DEFFEN.TEMP.FEN2','V V R',NBNFEN,IFEN2)
      DO 10 J = 1,NBNFEN
        ZR(IFEN2+J-1) = DISCFE(J) + LONG
  10  CONTINUE
C
C --- 3.EXTRACTION DES COMPOSANTES DES DEFORMEES SUR LA DISCRETISATION
C ---   DE LA FENETRE EXCITEE ET INTERPOLATION SUR LA DISCRETISATION DES
C ---   FONCTIONS DE FORME
C
      CALL WKVECT('&&DEFFEN.TEMP.VALE1','V V R',NBNFEN,IVALE1)
      CALL WKVECT('&&DEFFEN.TEMP.VALE2','V V R',NBNFEN,IVALE2)
C
      DO 20 IMOD = IMODI,IMODF
C
        WRITE(CHVALE,'(A8,A5,2I3.3,A5)') BASE(1:8),'.C01.',NUOR(IMOD),
     &                                   IV,'.VALE'
        CALL JEVEUO(CHVALE,'L',IVALE)
C
C-------EXTRACTION
C
        DO 40 J = 1,NBNFEN
          NUMNOE = NOFE(J)
          ZR(IVALE1+J-1) = ZR(IVALE+6*(NUMNOE-1)+IDIR1-1)
          ZR(IVALE2+J-1) = ZR(IVALE+6*(NUMNOE-1)+IDIR2-1)
  40    CONTINUE
        CALL JELIBE(CHVALE)
C
C-------INTERPOLATION
C
        IMR = IMOD - IMODI + 1
        CALL FOINTR(' ',ZK24(IPROL),NBNFEN,DISCFE,ZR(IVALE1),NBP1,
     &              DISCFF,DEFM(1,IMR),IER1)
        CALL FOINTR(' ',ZK24(IPROL),NBNFEN,ZR(IFEN2),ZR(IVALE2),NBP2,
     &              DISCFF(NBP1+1),DEFM(NBP1+1,IMR),IER2)
        IF (IER1.NE.0 .OR. IER2.NE.0) THEN
          CALL U2MESS('F','MODELISA4_39')
        ENDIF
C
  20  CONTINUE
C
      CALL JEDETR('&&DEFFEN.TEMP.PROL')
      CALL JEDETR('&&DEFFEN.TEMP.FEN2')
      CALL JEDETR('&&DEFFEN.TEMP.VALE1')
      CALL JEDETR('&&DEFFEN.TEMP.VALE2')
      CALL JEDEMA()
      END
