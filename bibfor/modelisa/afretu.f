      SUBROUTINE AFRETU (IPRNO,LONLIS,KLISNO,NOEPOU,NOMA,VALE1,NBCOEF,
     &                   IDEC,COEF,NOMDDL,TYPLAG,LISREL)
      IMPLICIT NONE
      INTEGER LONLIS,IPRNO(*),IDEC,NBCOEF
      REAL*8 COEF(NBCOEF)
      CHARACTER*2 TYPLAG
      CHARACTER*8 KLISNO(LONLIS),NOEPOU,NOMA,NOMDDL(NBCOEF)
      CHARACTER*24 VALE1
      CHARACTER*19  LISREL
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 17/05/2000   AUTEUR DURAND C.DURAND 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C -------------------------------------------------------
C     RACCORD (COQUE OU 3D)_TUYAU : UNE RELATION LINEAIRE
C
C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ------
      CHARACTER*32       JEXNOM
      INTEGER            ZI
      COMMON  / IVARJE / ZI(1)
      REAL*8             ZR
      COMMON  / RVARJE / ZR(1)
      COMPLEX*16         ZC
      COMMON  / CVARJE / ZC(1)
      LOGICAL            ZL
      COMMON  / LVARJE / ZL(1)
      CHARACTER*8        ZK8
      CHARACTER*16                ZK16
      CHARACTER*24                          ZK24
      CHARACTER*32                                    ZK32
      CHARACTER*80                                              ZK80
      COMMON  / KVARJE /ZK8(1),ZK16(1),ZK24(1),ZK32(1), ZK80(1)
C --------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ------
      INTEGER       INO,IVAL,IDCH1,NBTERM,I,NBEC,IER
      INTEGER JLISNO,JLISCR,JLISCC,JLISDI,JLISDM,JLISDL
      CHARACTER*8   BETAF,K8BID
      CHARACTER*16  MOTFAC
      CHARACTER*24  NOEUMA
      REAL*8        BETA
      COMPLEX*16    BETAC
C
      CALL JEMARQ()
C
      MOTFAC = 'LIAISON_ELEM'
      BETAF =  '&FOZERO'
      BETA  = 0.0D0
      BETAC = (0.0D0,0.0D0)
      NOEUMA = NOMA//'.NOMNOE'
      MOTFAC = 'LIAISON_ELEM'
      CALL JEVEUO(VALE1,'L',IDCH1)
      CALL DISMOI('F','NB_EC','DEPL_R','GRANDEUR',NBEC,K8BID,IER)
      IF (NBEC.GT.10) THEN
         CALL UTMESS('F','AFRETU',
     &                   'LE DESCRIPTEUR_GRANDEUR DES DEPLACEMENTS'//
     &                    ' NE TIENT PAS SUR DIX ENTIERS CODES')
      ENDIF
C
C --- CREATION DES TABLEAUX DE TRAVAIL NECESSAIRES A L'AFFECTATION
C --- NBTERM : MAJORANT DU NOMBRE DE TERMES DANS UNE RELATION
C
      NBTERM = 3*LONLIS+NBCOEF
C ---     VECTEUR DU NOM DES NOEUDS
      CALL WKVECT ('&&AFRETU.LISNO','V V K8',NBTERM,JLISNO)
C ---     VECTEUR DU NOM DES DDLS
      CALL WKVECT ('&&AFRETU.LISDDL','V V K8',NBTERM,JLISDL)
C ---     VECTEUR DES COEFFICIENTS REELS
      CALL WKVECT ('&&AFRETU.COER','V V R',NBTERM,JLISCR)
C ---     VECTEUR DES COEFFICIENTS COMPLEXES
      CALL WKVECT ('&&AFRETU.COEC','V V C',NBTERM,JLISCC)
C ---     VECTEUR DES DIRECTIONS DES DDLS A CONTRAINDRE
      CALL WKVECT ('&&AFRETU.DIRECT','V V R',3*NBTERM,JLISDI)
C ---     VECTEUR DES DIMENSIONS DE CES DIRECTIONS
      CALL WKVECT ('&&AFRETU.DIME','V V I',NBTERM,JLISDM)
C
C        RELATIONS ENTRE LES NOEUDS DE COQUE ET LE NOEUD NOEPOU
C        DE TUYAU SUR LE DDL NOMDDL
C
         DO 120 I=1, LONLIS
            CALL JENONU(JEXNOM(NOEUMA,KLISNO(I)),INO)
C           ADRESSE DE LA PREMIERE CMP DU NOEUD INO DANS LES CHAMNO
            IVAL = IPRNO((INO-1)*(NBEC+2)+1)
C
            ZK8(JLISNO+3*(I-1)+1-1) = KLISNO(I)
            ZK8(JLISNO+3*(I-1)+2-1) = KLISNO(I)
            ZK8(JLISNO+3*(I-1)+3-1) = KLISNO(I)
C
            ZK8(JLISDL+3*(I-1)+1-1) = 'DX'
            ZK8(JLISDL+3*(I-1)+2-1) = 'DY'
            ZK8(JLISDL+3*(I-1)+3-1) = 'DZ'
C
C RACCORD  3D_TUYAU : IDEC=0 DANS TOUS LES APPELS A AFRETU
C RACCORD COQ_TUYAU : IDEC=0 OU 3 DANS LES APPELS A AFRETU
C
            ZR(JLISCR+3*(I-1)+1-1)  =  ZR(IDCH1+IVAL-1+IDEC+0)
            ZR(JLISCR+3*(I-1)+2-1)  =  ZR(IDCH1+IVAL-1+IDEC+1)
            ZR(JLISCR+3*(I-1)+3-1)  =  ZR(IDCH1+IVAL-1+IDEC+2)
 120     CONTINUE
C
         DO 121 I=1, NBCOEF
            ZK8(JLISNO+3*LONLIS+I-1) = NOEPOU
            ZK8(JLISDL+3*LONLIS+I-1) = NOMDDL(I)
            ZR(JLISCR+3*LONLIS+I-1)  = COEF(I)
 121     CONTINUE
C
         CALL AFRELA (ZR(JLISCR), ZC(JLISCC), ZK8(JLISDL),
     +             ZK8(JLISNO), ZI(JLISDM), ZR(JLISDI),
     +             NBTERM, BETA, BETAC, BETAF, 'REEL',
     +             'REEL', TYPLAG, LISREL)
         CALL IMPREL(MOTFAC,NBTERM,ZR(JLISCR),ZK8(JLISDL),ZK8(JLISNO),
     +               BETA)
C
      CALL JEDETC('V','&&AFRETU',1)
      CALL JEDEMA()
      END
