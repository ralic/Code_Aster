      SUBROUTINE CARAUN(CHAR  ,MOTFAC,NZOCU ,NBGDCU,COEFCU,
     &                  COMPCU,MULTCU,NTCMP )
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 22/12/2009   AUTEUR ABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2006  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C
      IMPLICIT      NONE
      CHARACTER*8   CHAR
      CHARACTER*16  MOTFAC
      INTEGER       NZOCU,NTCMP
      CHARACTER*24  NBGDCU
      CHARACTER*24  COEFCU
      CHARACTER*24  COMPCU
      CHARACTER*24  MULTCU
C     
C ----------------------------------------------------------------------
C
C ROUTINE LIAISON_UNILATERALE (LECTURE)
C
C LIRE LES CARACTERISTIQUES DE LA LIAISON UNILATERALE
C      
C ----------------------------------------------------------------------
C
C
C IN  CHAR   : NOM DU CONCEPT CHARGE
C IN  MOTFAC : MOT_CLEF FACTEUR POUR LIAISON UNILATERALE
C IN  NZOCU  : NOMBRE DE ZONES DE LIAISON_UNILATERALE
C IN  NBGDCU : NOM JEVEUX DE LA SD INFOS POINTEURS GRANDEURS
C       ZI(JNBGD+IOCC-1): INDICE DEBUT DANS LISTE DES NOMS DES GRANDEURS
C                       POUR ZONE IOCC
C       ZI(JNBGD+IOCC) - ZI(JNBGD+IOCC-1): NOMBRE DE GRANDEURS DE LA
C                       ZONE IOCC
C IN  COEFCU : NOM JEVEUX DE LA SD CONTENANT LES COEFFICIENTS DES
C              GRANDEURS DE MEMBRE DE DROITE
C              VECTEUR TYPE ZR OU ZK8 SUIVANT FONREE
C       Z*(JCOEF+IOCC-1): VALEUR OU NOM FONCTION DU MEMBRE DE DROITE
C IN  COMPCU : NOM JEVEUX DE LA SD CONTENANT LES GRANDEURS DU MEMBRE
C              DE GAUCHE
C              LONGUEUR = ZI(JDUME+3)
C              INDEXE PAR NBGDCU:
C       ZI(JNBGD+IOCC-1): INDEX DEBUT POUR ZONE IOCC
C       ZI(JDUME+2*(IOCC-1)+5) = ZI(JNBGD+IOCC)-ZI(JNBGD+IOCC-1):
C                         NOMBRE GRANDEURS A GAUCHE POUR ZONE IOCC
C       ZK8(JCMPG-1+INDEX+ICMP-1): NOM ICMP-EME GRANDEUR
C IN  MULTCU : NOM JEVEUX DE LA SD CONTENANT LES COEF DU MEMBRE
C              DE GAUCHE
C              VECTEUR TYPE ZR OU ZK8 SUIVANT FONREE
C              MEME ACCES QUE COMPCU
C OUT NTCMP  : NOMBRE TOTAL DE COMPOSANTES SUR TOUTES LES ZONES
C
C
C -------------- DEBUT DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C ---------------- FIN DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER       ZMAX
      PARAMETER     (ZMAX = 30)
      CHARACTER*8   CMPGD(ZMAX),K8BID,CCOEF,CCMULT(ZMAX)
      INTEGER       NOC,NBCMP,NBCMUL
      INTEGER       IZONE,ICMP,IFORM
      CHARACTER*24  DEFICO
      CHARACTER*24  PARACI
      INTEGER       JPARCI
      INTEGER       JCOEF,JNBGD,JCMPG,JMULT
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
C
C --- INITIALISATIONS
C
      IFORM  = 4
      NTCMP  = 0
      DEFICO = CHAR(1:8)//'.CONTACT'
C 
C --- LECTURE DES STRUCTURES DE DONNEES DE CONTACT
C    
      PARACI = DEFICO(1:16)//'.PARACI'
      CALL JEVEUO(PARACI,'E',JPARCI)           
C
C --- LA FORMULATION (UNIQUE !)
C  
      ZI(JPARCI+4-1) = IFORM
C
C --- METHODE: CONTRAINTES ACTIVES 
C
      ZI(JPARCI+17-1) = 0
C
C --- PARAMETRES 
C
      ZI(JPARCI+3-1) = 10
C
C --- MEMBRE DE DROITE DE L'INEGALITE
C
      COEFCU = '&&CARAUN.COEFCU'
      CALL WKVECT(COEFCU,'V V K8',NZOCU,JCOEF)
      DO 1001 IZONE=1,NZOCU
        CALL GETVID(MOTFAC,'COEF_IMPO',IZONE,1,1,CCOEF,NOC)
        ZK8(JCOEF-1+IZONE) = CCOEF
 1001 CONTINUE
C
C --- MEMBRE DE GAUCHE DE L'INEGALITE
C --- COMPTAGE PREALABLE DU NOMBRE DE DDLS
C
      CALL WKVECT(NBGDCU,'V V I',NZOCU+1  ,JNBGD )
      ZI(JNBGD) = 1
      NTCMP     = 0
      DO 1002 IZONE=1,NZOCU
        CALL GETVTX(MOTFAC,'NOM_CMP'  ,IZONE,1,1,K8BID,NBCMP)
        CALL GETVID(MOTFAC,'COEF_MULT',IZONE,1,1,K8BID,NBCMUL)
        IF (NBCMP.NE.NBCMUL) THEN
          CALL U2MESS('F','UNILATER_42')
        ENDIF
        NBCMP  = ABS(NBCMP)
        NBCMUL = ABS(NBCMUL)
        NTCMP  = NTCMP + NBCMP
        IF (NTCMP.GT.ZMAX) THEN
          CALL U2MESS('F','UNILATER_43')
        ENDIF
        ZI(JNBGD+IZONE)        = ZI(JNBGD+IZONE-1) + NBCMP
 1002 CONTINUE
C
C --- MEMBRE DE GAUCHE DE L'INEGALITE: CREATION DES OBJETS
C
      CALL WKVECT(COMPCU,'V V K8',NTCMP ,JCMPG)
      CALL WKVECT(MULTCU,'V V K8',NTCMP ,JMULT)
C
C --- MEMBRE DE GAUCHE DE L'INEGALITE: REMPLISSAGE DES OBJETS
C
      DO 2000 IZONE=1,NZOCU
        NBCMP = ZI(JNBGD+IZONE) - ZI(JNBGD+IZONE-1)
        CALL GETVTX(MOTFAC,'NOM_CMP'  ,IZONE,1,NBCMP,CMPGD ,NOC)
        CALL GETVID(MOTFAC,'COEF_MULT',IZONE,1,NBCMP,CCMULT,NOC)
        DO 11 ICMP = 1,NBCMP
          ZK8(JCMPG-1+ZI(JNBGD+IZONE-1)+ICMP-1) = CMPGD(ICMP)
          ZK8(JMULT-1+ZI(JNBGD+IZONE-1)+ICMP-1) = CCMULT(ICMP)
   11   CONTINUE
 2000 CONTINUE
C 
      CALL JEDEMA()
C
      END
