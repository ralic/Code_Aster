      SUBROUTINE PROJKN(MAILLA,X3DCA,LNUMA,LICNX,
     &                  NUMAIL,NBCNX,CXMA,XYZMA,NORMAL,
     &                  ITRIA,IPROJ,EXCENT)
      IMPLICIT NONE
C-----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 09/11/2012   AUTEUR DELMAS J.DELMAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C-----------------------------------------------------------------------
C  DESCRIPTION : PROJECTION D'UN NOEUD CABLE SUR LE NOEUD BETON LE PLUS
C  -----------   PROCHE
C                APPELANT : PROJCA
C
C  IN     : MAILLA : CHARACTER*8 , SCALAIRE
C                    NOM DU CONCEPT MAILLAGE ASSOCIE A L'ETUDE
C  IN     : X3DCA  : REAL*8 , VECTEUR DE DIMENSION 3
C                    COORDONNEES DU NOEUD CABLE CONSIDERE
C  IN     : LNUMA  : CHARACTER*19 , SCALAIRE
C                    NOM D'UN VECTEUR D'ENTIERS CONTENANT LES NUMEROS
C                    DES MAILLES AUXQUELLES APPARTIENT LE NOEUD BETON
C                    LE PLUS PROCHE DU NOEUD CABLE
C  IN     : LICNX  : CHARACTER*19 , SCALAIRE
C                    NOM D'UN VECTEUR D'ENTIERS CONTENANT LES RANGS DU
C                    NOEUD BETON LE PLUS PROCHE DANS LES TABLES DE
C                    CONNECTIVITE DES MAILLES AUXQUELLES IL APPARTIENT
C  OUT    : NUMAIL : INTEGER , SCALAIRE
C                    NUMERO DE LA PREMIERE MAILLE A LAQUELLE APPARTIENT
C                    LE NOEUD BETON LE PLUS PROCHE
C  OUT    : NBCNX  : INTEGER , SCALAIRE
C                    NOMBRE DE NOEUDS DE LA PREMIERE MAILLE A LAQUELLE
C                    APPARTIENT LE NOEUD BETON LE PLUS PROCHE
C  OUT    : CXMA   : INTEGER , VECTEUR DE DIMENSION AU PLUS NNOMAX
C                    NUMEROS DES NOEUDS DE LA PREMIERE MAILLE A LAQUELLE
C                    APPARTIENT LE NOEUD BETON LE PLUS PROCHE
C                    (TABLE DE CONNECTIVITE)
C  OUT    : XYZMA  : REAL*8 , TABLEAU DE DIMENSIONS (3,NNOMAX)
C                    TABLEAU DES COORDONNEES DES NOEUDS DE LA PREMIERE
C                    MAILLE A LAQUELLE APPARTIENT LE NOEUD BETON LE
C                    PLUS PROCHE
C  OUT    : NORMAL : REAL*8 , VECTEUR DE DIMENSION 3
C                    COORDONNEES DANS LE REPERE GLOBAL DU VECTEUR
C                    ---------->          ---------->
C                    NOEBE,NOECA / DNRM2(NOEBE,NOECA)
C  OUT    : ITRIA  : INTEGER , SCALAIRE
C                    INDICATEUR DU SOUS-DOMAINE AUQUEL APPARTIENT LE
C                    NOEUD BETON LE PLUS PROCHE :
C                    ITRIA = 1 : TRIANGLE 1-2-3
C                    ITRIA = 2 : TRIANGLE 3-4-1
C  OUT    : IPROJ  : INTEGER , SCALAIRE
C                    INDICE DE PROJECTION
C                    IPROJ =  2  CAR LE POINT PROJETE COINCIDE AVEC LE
C                                NOEUD BETON LE PLUS PROCHE
C  OUT    : EXCENT : REAL*8 , SCALAIRE
C                    DISTANCE DU NOEUD CABLE AU NOEUD BETON LE PLUS
C                    PROCHE
C
C-------------------   DECLARATION DES VARIABLES   ---------------------
C
C
C ARGUMENTS
C ---------
      INCLUDE 'jeveux.h'

      CHARACTER*32 JEXNUM
      CHARACTER*8   MAILLA
      CHARACTER*19  LNUMA, LICNX
      INTEGER       NUMAIL, NBCNX, CXMA(*), ITRIA, IPROJ
      REAL*8        X3DCA(*), XYZMA(3,*), NORMAL(*), EXCENT
C
C VARIABLES LOCALES
C -----------------
      INTEGER       ICNX, INOMA, JCOOR, JCXMA, JLICNX, JLNUMA, NOE
      REAL*8        EPSG, NRM2
      CHARACTER*1   K1B
      CHARACTER*24  CONXMA, COORNO
C
      REAL*8        DNRM2, R8PREM
C
C-------------------   DEBUT DU CODE EXECUTABLE    ---------------------
C
      CALL MATFPE(-1)
C
      CALL JEMARQ()
C
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
C 1   ACCES AUX OBJETS UTILES - INITIALISATIONS
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
C
      CONXMA = MAILLA//'.CONNEX'
      COORNO = MAILLA//'.COORDO    .VALE'
      CALL JEVEUO(COORNO,'L',JCOOR)
C
      CALL JEVEUO(LNUMA,'L',JLNUMA)
      CALL JEVEUO(LICNX,'L',JLICNX)
C
      EPSG = 1.0D+08 * R8PREM()
C
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
C 2   PROJECTION DU NOEUD CABLE SUR LE NOEUD BETON LE PLUS PROCHE
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
C
      NUMAIL = ZI(JLNUMA)
      ICNX   = ZI(JLICNX)
C
C 2.1 RECUPERATION DES INFORMATIONS CARACTERISANT LA TOPOLOGIE
C --- DE LA PREMIERE MAILLE A LAQUELLE APPARTIENT
C     LE NOEUD BETON LE PLUS PROCHE
C
      CALL JELIRA(JEXNUM(CONXMA,NUMAIL),'LONMAX',NBCNX,K1B)
      CALL JEVEUO(JEXNUM(CONXMA,NUMAIL),'L',JCXMA)
C.... AFFECTATION DE ITRIA
      IF ( (NBCNX.EQ.3).OR.(NBCNX.EQ.6) ) THEN
         ITRIA = 1
      ELSE
         IF ( (ICNX.EQ.4).OR.(ICNX.EQ.7).OR.(ICNX.EQ.8) ) THEN
            ITRIA = 2
         ELSE
            ITRIA = 1
         ENDIF
      ENDIF
C
C 2.2 RECUPERATION DES NUMEROS ET DES COORDONNEES DES NOEUDS
C --- DE LA PREMIERE MAILLE A LAQUELLE APPARTIENT
C     LE NOEUD BETON LE PLUS PROCHE
C
      DO 10 INOMA = 1, NBCNX
         NOE = ZI(JCXMA+INOMA-1)
         CXMA(INOMA) = NOE
         XYZMA(1,INOMA) = ZR(JCOOR+3*(NOE-1)  )
         XYZMA(2,INOMA) = ZR(JCOOR+3*(NOE-1)+1)
         XYZMA(3,INOMA) = ZR(JCOOR+3*(NOE-1)+2)
  10  CONTINUE
C
C 2.3 PROJECTION DU NOEUD CABLE SUR LE NOEUD BETON LE PLUS PROCHE
C ---
      NORMAL(1) = X3DCA(1) - XYZMA(1,ICNX)
      NORMAL(2) = X3DCA(2) - XYZMA(2,ICNX)
      NORMAL(3) = X3DCA(3) - XYZMA(3,ICNX)
      EXCENT = DNRM2(3,NORMAL(1),1)
      NRM2 = DBLE(MAX(DNRM2(3,X3DCA(1),1),DNRM2(3,XYZMA(1,ICNX),1)))
      IF ( NRM2.EQ.0.0D0 ) NRM2 = 1.0D0
      IF ( DBLE(ABS(EXCENT))/NRM2.LT.EPSG ) THEN
         EXCENT = 0.0D0
         CALL R8INIR(3,0.0D0,NORMAL(1),1)
      ELSE
         CALL DSCAL(3,1.0D0/EXCENT,NORMAL(1),1)
      ENDIF
C.... AFFECTATION DE IPROJ
      IPROJ = 2
C
      CALL JEDEMA()
C
C --- FIN DE PROJKN.
      CALL MATFPE(1)
C
      END
