      SUBROUTINE LRMMMA ( FID, NOMAMD, NDIM, NBMAIL, NBNOMA,
     &                    NBTYP, TYPGEO, NOMTYP, NNOTYP, RENUMD,
     &                    NMATYP,
     &                    NOMMAI, CONNEX, TYPMAI,
     &                    PREFIX,
     &                    INFMED, MODNUM, NUMNOA )
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 14/09/2010   AUTEUR REZETTE C.REZETTE 
C ======================================================================
C COPYRIGHT (C) 1991 - 2002  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE GNICOLAS G.NICOLAS
C-----------------------------------------------------------------------
C     LECTURE DU MAILLAGE -  FORMAT MED - LES MAILLES
C     -    -     -                  -         --
C-----------------------------------------------------------------------
C     ENTREES :
C       FID    : IDENTIFIANT DU FICHIER MED
C       NOMAMD : NOM DU MAILLAGE MED
C       NDIM   : DIMENSION DU PROBLEME
C       NBMAIL : NOMBRE DE MAILLES DU MAILLAGE
C       NBNOMA : NOMBRE CUMULE DE NOEUDS PAR MAILLE
C       NBTYP  : NOMBRE DE TYPES POSSIBLES POUR MED
C       TYPGEO : TYPE MED POUR CHAQUE MAILLE
C       NNOTYP : NOMBRE DE NOEUDS POUR CHAQUE TYPE DE MAILLES
C       NOMTYP : NOM DES TYPES POUR CHAQUE MAILLE
C       RENUMD : RENUMEROTATION DES TYPES ENTRE MED ET ASTER
C       NMATYP : NOMBRE DE MAILLES PAR TYPE
C       PREFIX : PREFIXE POUR LES TABLEAUX DES RENUMEROTATIONS
C                A UTILISER PLUS TARD
C       MODNUM : INDICATEUR SI LA SPECIFICATION DE NUMEROTATION DES
C                NOEUDS DES MAILLES EST DIFFERENTES ENTRE ASTER ET MED:
C                     MODNUM = 0 : NUMEROTATION IDENTIQUE
C                     MODNUM = 1 : NUMEROTATION DIFFERENTE
C       NUMNOA : TABLEAU DE CORRESPONDANCE DES NOEUDS MED/ASTER.
C                NUMNOA(ITYP,J) : NUMERO DANS MED DU J IEME NOEUD
C                DE LA MAILLE DE TYPE ITYP DE ASTER
C
C     SORTIES:
C       NOMMAI : NOM DES MAILLES
C       CONNEX : CONNECTIVITE
C       TYPMAI : TYPE DES MAILLES
C-----------------------------------------------------------------------
C
      IMPLICIT NONE
C
      INTEGER NTYMAX
      PARAMETER (NTYMAX = 54)
C
C 0.1. ==> ARGUMENTS
C
      INTEGER FID
      INTEGER NDIM
      INTEGER NBMAIL, NBNOMA
      INTEGER NBTYP
      INTEGER NMATYP(NTYMAX), NUMNOA(NTYMAX,*), MODNUM(NTYMAX)
      INTEGER NNOTYP(NTYMAX), TYPGEO(NTYMAX)
      INTEGER RENUMD(*)
      INTEGER INFMED
C
      CHARACTER*6 PREFIX
      CHARACTER*8 NOMTYP(*)
      CHARACTER*24 CONNEX, NOMMAI, TYPMAI
      CHARACTER*(*)  NOMAMD
C
C 0.2. ==> COMMUNS
C
C     ----- DEBUT COMMUNS NORMALISES  JEVEUX  --------------------------
      INTEGER            ZI
      COMMON  / IVARJE / ZI(1)
      REAL*8             ZR
      COMMON  / RVARJE / ZR(1)
      COMPLEX*16         ZC
      COMMON  / CVARJE / ZC(1)
      LOGICAL            ZL
      COMMON  / LVARJE / ZL(1)
      CHARACTER*8        ZK8
      CHARACTER*16                ZK16
      CHARACTER*24                          ZK24
      CHARACTER*32                                    ZK32
      CHARACTER*80                                              ZK80
      COMMON  / KVARJE / ZK8(1) , ZK16(1) , ZK24(1) , ZK32(1) , ZK80(1)
      CHARACTER*32       JEXNOM, JEXNUM
C     -----  FIN  COMMUNS NORMALISES  JEVEUX  --------------------------
C
C 0.3. ==> VARIABLES LOCALES
C
      CHARACTER*6 NOMPRO
      PARAMETER ( NOMPRO = 'LRMMMA' )
C
      INTEGER EDMAIL
      INTEGER VALI(2)
      PARAMETER (EDMAIL=0)
      INTEGER EDFUIN
      PARAMETER (EDFUIN=0)
      INTEGER EDNODA
      PARAMETER (EDNODA=0)
C
      INTEGER CODRET
      INTEGER IMA, IDEC
      INTEGER IAUX, JAUX
      INTEGER IMATYP, ITYP, LETYPE
      INTEGER JCNXMA, JMATYP
      INTEGER JNOMMA, JTYPMA
      INTEGER CODE
      INTEGER JNOMTY(NTYMAX), JNUMTY(NTYMAX), JCXTYP(NTYMAX)
      INTEGER TABAUX(1)
      INTEGER NROMAI
      INTEGER IFM, NIVINF
C
      CHARACTER*15 SAUX15
      CHARACTER*8 SAUX08
C
C     ------------------------------------------------------------------
      CALL JEMARQ ( )
C
      CALL INFNIV ( IFM, NIVINF )
C
C====
C 1. ALLOCATIONS
C====
C 1.1. ==> BASE GLOBALE: OBJETS NOMS / CONNECTIVITE / TYPE DES MAILLES
C
      CALL WKVECT (TYPMAI,'G V I', NBMAIL,JTYPMA)
C
C 1.2. ==> CREATION DE PLUSIEURS VECTEURS PAR TYPE DE MAILLE PRESENT :
C              UN VECTEUR CONTENANT LES NOMS DES MAILLES/TYPE
C           +  UN VECTEUR CONTENANT LES NUMEROS DES MAILLES/TYPE
C           +  UN VECTEUR CONTENANT LA CONNECTIVITE DES MAILLE/TYPE
C              (CONNECTIVITE = NOEUDS + UNE VALEUR BIDON(0) SI BESOIN)
C
      CALL WKVECT ('&&'//NOMPRO//'.NOMMAI','V V K8',NBMAIL,JNOMMA)
      CALL WKVECT ('&&'//NOMPRO//'.IMATYP','V V I', NBMAIL*2,JMATYP)
C
      DO 12 , ITYP = 1 , NTYMAX
C
        IF ( NMATYP(ITYP).NE.0 ) THEN
C
          CALL WKVECT ('&&'//NOMPRO//'.NOM.'//NOMTYP(ITYP),'V V K16',
     &                  NMATYP(ITYP),JNOMTY(ITYP))
          CALL WKVECT ('&&'//PREFIX//'.NUM.'//NOMTYP(ITYP),'V V I',
     &                  NMATYP(ITYP),JNUMTY(ITYP))
          IAUX = NMATYP(ITYP) * NNOTYP(ITYP)
          CALL WKVECT ('&&'//NOMPRO//'.CNX.'//NOMTYP(ITYP),'V V I',
     &                  IAUX,JCXTYP(ITYP) )
C
        ENDIF
C
   12 CONTINUE
C
C====
C 2. LECTURE
C    ON PARCOURT TOUS LES TYPES POSSIBLES POUR MED ET ON DECLENCHE LES
C    LECTURES SI DES MAILLES DE CE TYPE SONT PRESENTES DANS LE MAILLAGE
C    REMARQUE : GRACE A LA RENUMEROTATION, ON PARCOURT LES TYPES DE
C    MAILLES DANS L'ORDRE CROISSANT DE LEUR TYPE MED. CE N'EST PAS
C    OBLIGATOIRE SI ON DISPOSE DES TABLEAUX DE NUMEROTATION DES MAILLES.
C    MAIS QUAND CES TABLEAUX SONT ABSENTS, LES CONVENTIONS MED PRECISENT
C    QUE LA NUMEROTATION IMPLICITE SE FAIT DANS CET ORDRE. DONC ON
C    LE FAIT !
C====
C
      NROMAI = 1
      DO 21 , LETYPE = 1 , NBTYP
C
C 2.0. ==> PASSAGE DU NUMERO DE TYPE MED AU NUMERO DE TYPE ASTER
C
        ITYP = RENUMD(LETYPE)
C
        IF ( INFMED.GE.3 ) THEN
          WRITE (IFM,2001) NOMTYP(ITYP), NMATYP(ITYP)
        ENDIF
 2001 FORMAT('TYPE ',A8,' : ',I10,' MAILLES')
C
        IF ( NMATYP(ITYP).NE.0 ) THEN
C
C 2.1. ==> LE NUMERO DES MAILLES
C          ON NE TIENT PAS COMPTE DE LA NUMEROTATION DES MAILLES
C          PRESENTE DANS LE FICHIER MED: ON NUMEROTE LES MAILLES
C          SELON LEUR ORDRE D'APPARITION.
C
          DO 211 , JAUX = 1 , NMATYP(ITYP)
            ZI(JNUMTY(ITYP)+JAUX-1) = NROMAI
            NROMAI = NROMAI + 1
  211     CONTINUE

C
C 2.2. ==> LE NOM DES MAILLES
C          SI LE FICHIER NE CONTIENT PAS DE NOMMAGE DES MAILLES, ON LEUR
C          DONNE UN NOM PAR DEFAUT FORME AVEC LE PREFIXE 'M' SUIVI DE
C          LEUR NUMERO
C
          CALL EFNOML ( FID, NOMAMD, ZK16(JNOMTY(ITYP)), NMATYP(ITYP),
     &                  EDMAIL, TYPGEO(ITYP), CODRET )
C
          IF ( CODRET.NE.0 ) THEN
            IF ( INFMED.GE.3 ) THEN
              CALL U2MESK('I','MED_19',1,NOMTYP(ITYP))
            ENDIF
            IF (NBMAIL .GE. 10000000) THEN
C           + DE 10 MILLIONS DE MAILLES (AU TOTAL), ON PASSE EN BASE 36
               DO 221 , IAUX = 1, NMATYP(ITYP)
                 CODE = ZI(JNUMTY(ITYP)+IAUX-1)
                 CALL CODLET(CODE,'G',SAUX15)
                 ZK16(JNOMTY(ITYP)+IAUX-1) = 'M'//SAUX15
  221          CONTINUE
            ELSE
C           MOINS DE 10 MILLIONS DE MAILLES, ON RESTE EN BASE 10
               DO 222 , IAUX = 1, NMATYP(ITYP)
                 CODE = ZI(JNUMTY(ITYP)+IAUX-1)
                 CALL CODENT(CODE,'G',SAUX15)
                 ZK16(JNOMTY(ITYP)+IAUX-1) = 'M'//SAUX15
  222          CONTINUE
            ENDIF
            CODRET = 0
          ENDIF
C
C 2.3. ==> LES CONNECTIVITES
C          LA CONNECTIVITE EST FOURNIE EN STOCKANT TOUS LES NOEUDS A
C          LA SUITE POUR UNE MAILLE DONNEE.
C          C'EST CE QUE MED APPELLE LE MODE ENTRELACE
C
          CALL EFCONL ( FID, NOMAMD, NDIM, ZI(JCXTYP(ITYP)), EDFUIN,
     &                  TABAUX, 0,
     &                  EDMAIL, TYPGEO(ITYP), EDNODA, CODRET )
          IF ( CODRET.NE.0 ) THEN
            SAUX08='EFCONL  '
            CALL U2MESG('F','DVP_97',1,SAUX08,1,CODRET,0,0.D0)
          ENDIF
C
          DO 231 , IMATYP = 1 , NMATYP(ITYP)
            IMA = ZI(JNUMTY(ITYP)+IMATYP-1)
            ZK8(JNOMMA+IMA-1) = ZK16(JNOMTY(ITYP)+IMATYP-1)(1:8)
            ZI (JTYPMA+IMA-1) = ITYP
            IDEC   = (IMA-1)*2
            ZI(JMATYP+IDEC)   = ITYP
            ZI(JMATYP+IDEC+1) = IMATYP
  231     CONTINUE
C
        ENDIF
C
 21   CONTINUE
C
C====
C 3. CREATION OBJET CONNECTIVITE DES MAILLES DANS LE BON ORDRE
C====
C
      CALL JECREC (CONNEX,'G V I','NU','CONTIG','VARIABLE',NBMAIL)
      CALL JEECRA (CONNEX,'LONT',NBNOMA,' ')
C
      DO 31 , IMA = 1 , NBMAIL
C
        IDEC = (IMA-1)*2
        ITYP = ZI(JMATYP+IDEC)
        CALL JEECRA(JEXNUM(CONNEX,IMA),'LONMAX',NNOTYP(ITYP),' ')
        CALL JEVEUO(JEXNUM(CONNEX,IMA),'E',JCNXMA)
C
        IMATYP = ZI(JMATYP+IDEC+1)
        IDEC = (IMATYP-1)*NNOTYP(ITYP)
C
C       POUR LES TYPES DE MAILLE DONT LA NUMEROTATION DES NOEUDS
C       ENTRE ASTER ET MED EST IDENTIQUE:
        IF ( MODNUM(ITYP).EQ.0 ) THEN
          DO 311 , JAUX = 1 , NNOTYP(ITYP)
            ZI(JCNXMA+JAUX-1) = ZI(JCXTYP(ITYP)+JAUX-1+IDEC)
 311      CONTINUE
C
C       SINON (CF LRMTYP POUR CONNAITRE LA CORRESPONDANCE DES
C       NOEUDS LOCAUX ENTRE ASTER ET MED)
        ELSE
          DO 312 , JAUX = 1 , NNOTYP(ITYP)
            ZI(JCNXMA+JAUX-1) =
     &         ZI(JCXTYP(ITYP)+NUMNOA(ITYP,JAUX)-1+IDEC)
 312      CONTINUE
        ENDIF
C
   31 CONTINUE
C
C====
C 4. CREATION OBJET NOMS DES MAILLES DANS LE BON ORDRE
C====
C
      CALL JECREO (NOMMAI,'G N K8')
      CALL JEECRA (NOMMAI,'NOMMAX',NBMAIL,' ')
C
      DO 41 , IAUX = 1 , NBMAIL
        CALL JECROC (JEXNOM(NOMMAI,ZK8(JNOMMA+IAUX-1)))
   41 CONTINUE
C
C====
C 5. LA FIN
C====
C
      CALL JEDETC ('V','&&'//NOMPRO,1)
C
      CALL JEDEMA ( )
C
      END
