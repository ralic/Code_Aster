      SUBROUTINE POINCO(CHAR  ,MOTFAC,NOMA  ,NZOCO ,ORDSTC,
     &                  INDQUA,NSUCO ,NMACO ,NNOCO ,NNOQUA,
     &                  NTRAV)
C     
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 30/04/2007   AUTEUR ABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT     NONE
      CHARACTER*8  CHAR
      CHARACTER*16 MOTFAC
      CHARACTER*8  NOMA
      INTEGER      NZOCO
      INTEGER      ORDSTC
      INTEGER      INDQUA
      INTEGER      NSUCO
      INTEGER      NMACO
      INTEGER      NNOCO
      INTEGER      NNOQUA
      INTEGER      NTRAV
C      
C ----------------------------------------------------------------------
C
C ROUTINE CONTACT (METHODES MAILLEES - LECTURE DONNEES)
C
C DETERMINATION DU NOMBRE DE ZONES DE CONTACT ET DU NOMBRE TOTAL DE
C MAILLES ET DE NOEUDS DE CONTACT. 
C REMPLISSAGE DES POINTEURS ASSOCIES: PZONE,PSURMA,PSURNO,PNOQUA
C      
C ----------------------------------------------------------------------
C
C 
C IN  CHAR   : NOM UTILISATEUR DU CONCEPT DE CHARGE
C IN  MOTFAC : MOT-CLE FACTEUR
C IN  NOMA   : NOM DU MAILLAGE
C IN  NZOCO  : NOMBRE DE ZONES DE CONTACT
C IN  ORDSTC : ORDRE DE STOCKAGE DES ZONES MAITRES ET ESCLAVES
C              0: MAITRES PUIS ESCLAVES
C              1: ESCLAVES PUIS MAITRES
C IN  INDQUA : VAUT 1 LORSQUE L'ON DOIT IGNORER LES NOEUDS MILIEUX DANS
C              NBNOEL (PENALISATION SUR LE CONTACT OU METHODE CONTINUE)
C OUT NSUCO  : NOMBRE TOTAL DE SURFACES DE CONTACT
C OUT NMACO  : NOMBRE TOTAL DE MAILLES DES SURFACES DE CONTACT
C OUT NNOCO  : NOMBRE TOTAL DE NOEUDS DES SURFACES DE CONTACT
C OUT NNOQUA : NOMBRE TOTAL DE NOEUDS QUADRATIQUES DES SURFACES DE
C              CONTACT
C OUT NTRAV  : NOMBRE MAXIMUM DE GROUPES ET DE MAILLES IMPLIQUES DANS
C              LE CONTACT (TOUTES ZONES CONFONDUES)
C
C -------------- DEBUT DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER            ZI
      COMMON  / IVARJE / ZI(1)
      REAL*8             ZR
      COMMON  / RVARJE / ZR(1)
      COMPLEX*16         ZC
      COMMON  / CVARJE / ZC(1)
      LOGICAL            ZL
      COMMON  / LVARJE / ZL(1)
      CHARACTER*8        ZK8
      CHARACTER*16                ZK16
      CHARACTER*24                          ZK24
      CHARACTER*32                                    ZK32
      CHARACTER*80                                              ZK80
      COMMON  / KVARJE / ZK8(1) , ZK16(1) , ZK24(1) , ZK32(1) , ZK80(1)
C
C ---------------- FIN DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER      IOC,ISY,IWRITE,IREAD,ISYME
      INTEGER      NZOCP,NGTOT,NSYME
      CHARACTER*24 PZONE,PSURMA,PSURNO,PNOQUA
      INTEGER      JZONE,JSUMA,JSUNO,JNOQUA
      CHARACTER*24 SYMECO
      INTEGER      JSYME
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
C 
C --- ACCES AUX STRUCTURES DE DONNEES DE CONTACT
C 
      PZONE  = CHAR(1:8)//'.CONTACT.PZONECO'
      PSURMA = CHAR(1:8)//'.CONTACT.PSUMACO'
      PSURNO = CHAR(1:8)//'.CONTACT.PSUNOCO'
      PNOQUA = CHAR(1:8)//'.CONTACT.PNOEUQU'
      SYMECO = CHAR(1:8)//'.CONTACT.SYMECO'
C
      CALL JEVEUO(SYMECO,'L',JSYME)
C
C --- INITIALISATIONS
C  
      NSYME  = ZI(JSYME) 
      NSUCO  = 0    
      NTRAV  = 0 
      NZOCP  = NZOCO - NSYME
      NMACO  = 0  
      NNOCO  = 0  
      NNOQUA = 0 
C
C --- DETERMINATION DU NOMBRE DE GROUPES DE MAILLES
C --- ET DE MAILLES PAR ZONE POUR TOUTES LES ZONES DE CONTACT
C --- REMPLISSAGE DU POINTEUR ASSOCIE JZONE.
C
      CALL WKVECT (PZONE,'G V I',NZOCO+1,JZONE)
      DO 2 IOC = 1,NZOCP
        IREAD  = IOC
        IWRITE = IOC
        CALL NBZOCO(CHAR  ,MOTFAC,NOMA  ,IREAD ,IWRITE ,
     &              JZONE ,NSUCO ,NGTOT )
        NTRAV = MAX(NTRAV,NGTOT)
  2   CONTINUE
C
C --- ON BOUCLE SUR LES ZONES PRINCIPALES MAIS ON AGIT SUR LES ZONES
C     SYMETRIQUES
C --- POUR ZONES DE CONTACT SYMETRIQUES
C ---   IREAD: OCCURRENCE DU MOT-CLEF CONTACT SYMETRIQUE
C ---   IWRITE: OCCURRENCE DANS LA SD DE LA ZONE DE CONTACT SYMETRIQUE
C
      IF (NSYME.GT.0) THEN
        ISYME = 0
        DO 3 IOC = 1,NZOCP
          IREAD = IOC
          DO 4 ISY = 1,NSYME
            IF (ZI(JSYME+ISY).EQ.IOC) THEN
              ISYME  = ISYME +1
              IWRITE = NZOCP+ISYME
              CALL NBZOCO(CHAR  ,MOTFAC,NOMA  ,IREAD ,IWRITE,
     &                    JZONE ,NSUCO ,NGTOT)
            ENDIF
  4       CONTINUE
  3     CONTINUE
        IF (ISYME.NE.NSYME)  CALL U2MESS('F','MODELISA6_28')
        IF (IWRITE.NE.NZOCO) CALL U2MESS('F','MODELISA6_28')
      ENDIF
C
C --- DETERMINATION DU NOMBRE DE MAILLES, DE NOEUDS DE CONTACT ET
C --- DE NOEUDS DE CONTACT QUADRATIQUES POUR TOUTES LES SURFACES
C --- DE CONTACT
C --- REMPLISSAGE DES POINTEURS ASSOCIES JSUMA,JSUNO,JNOQUA
C
      CALL WKVECT(PSURMA,'G V I',NSUCO+1,JSUMA )
      CALL WKVECT(PSURNO,'G V I',NSUCO+1,JSUNO )
      CALL WKVECT(PNOQUA,'G V I',NSUCO+1,JNOQUA)
C 
      IWRITE = 0
      DO 5 IOC = 1,NZOCP
        IREAD  = IOC
        CALL NBSUCO(CHAR  ,MOTFAC,NOMA  ,IREAD ,IWRITE,
     &              JSUMA ,JSUNO ,JNOQUA,NTRAV ,ORDSTC,
     &              INDQUA)
  5   CONTINUE
C
      IF (NSYME.GT.0) THEN
         ISYME = 0
         DO 6 IOC = 1,NZOCP
            IREAD = IOC
            DO 7 ISY = 1,NSYME
               IF (ZI(JSYME+ISY) .EQ.IOC) THEN
                  ISYME = ISYME +1
                  CALL NBSUCO(CHAR  ,MOTFAC,NOMA  ,IREAD ,IWRITE,
     &                        JSUMA ,JSUNO ,JNOQUA,NTRAV ,ORDSTC,
     &                        INDQUA)
               ENDIF
  7         CONTINUE
  6      CONTINUE
         IF (ISYME.NE.NSYME) CALL U2MESS('F','MODELISA6_29')
      ENDIF
C
C --- NMACO EST LE NOMBRE TOTAL DE MAILLES DE CONTACT
C --- NNOCO EST LE NOMBRE TOTAL DE NOEUDS DE CONTACT
C --- NNOQUA EST LE NOMBRE TOTAL DE NOEUDS QUADRATIQUES DE CONTACT
C
      NMACO  = ZI(JSUMA  + NSUCO)
      NNOCO  = ZI(JSUNO  + NSUCO)
      NNOQUA = ZI(JNOQUA + NSUCO)
C
      CALL JEDEMA()
      END
