      SUBROUTINE CREAUN(CHAR  ,NOMA  ,NOMO  ,NZOCU ,NNOCU ,
     &                  LISNOE,POINOE,NBGDCU,COEFCU,COMPCU,
     &                  MULTCU)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 09/11/2012   AUTEUR DELMAS J.DELMAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C
      IMPLICIT      NONE
      INCLUDE 'jeveux.h'

      CHARACTER*32 JEXNUM
      CHARACTER*8   CHAR
      CHARACTER*8   NOMA
      CHARACTER*8   NOMO
      INTEGER       NZOCU,NNOCU
      CHARACTER*24  LISNOE
      CHARACTER*24  POINOE
      CHARACTER*24  NBGDCU
      CHARACTER*24  COEFCU
      CHARACTER*24  COMPCU
      CHARACTER*24  MULTCU
C
C ----------------------------------------------------------------------
C
C ROUTINE LIAISON_UNILATERALE (CREATION SD)
C
C CONSTRUCTION FINALE DES VECTEURS ON OUBLIE LE CONCEPT DE ZONES
C
C ----------------------------------------------------------------------
C
C
C IN  CHAR   : NOM DU CONCEPT CHARGE
C IN  NOMA   : NOM DU MAILLAGE
C IN  NOMO   : NOM DU MODELE
C IN  NZOCU  : NOMBRE DE ZONES
C IN  NNOCU  : NOMBRE DE NOEUDS
C IN  POINOE : NOM DE L'OBJET CONTENANT LE VECTEUR D'INDIRECTION
C               DES NOEUDS
C IN  LISNOE : NOM DE L'OBJET CONTENANT LES NOEUDS
C IN  NBGDCU : NOM JEVEUX DE LA SD INFOS POINTEURS GRANDEURS DU MEMBRE
C              DE GAUCHE
C IN  COEFCU : NOM JEVEUX DE LA SD CONTENANT LES VALEURS DU MEMBRE
C              DE DROITE
C IN  COMPCU : NOM JEVEUX DE LA SD CONTENANT LES GRANDEURS DU MEMBRE
C              DE GAUCHE
C IN  MULTCU : NOM JEVEUX DE LA SD CONTENANT LES COEFFICIENTS DU MEMBRE
C              DE GAUCHE
C
C
C
C
C
       INTEGER        NBGAU
       CHARACTER*24   DEFICU
       INTEGER        JMULT,JNOE,JPOI,JNBGD
       INTEGER        JCOED,JCOEG,JCOMPG,JCOEF,JNCMP
       INTEGER        INO,ICMP,IZONE
       CHARACTER*24   NOEUCU,NOEUMA
       CHARACTER*24   VALK(2)
       INTEGER        JNOEU,JINDIR
       INTEGER        NUMND,EXIST,NBSUP
       INTEGER        NBNO,NBCMP
       INTEGER        JDEBCP,JDEBND
       CHARACTER*8    CMP,K8BLA,NOMNO
       INTEGER        CPTD,NCMPG,CPTND
       CHARACTER*24   CMPGCU,NDIMCU,COEGCU,COEDCU,POINCU
       INTEGER        JCMPG,JDIM,JCOEFG,JCOEFD,JPOIN
       INTEGER        IFM,NIV
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
      CALL INFNIV(IFM,NIV)
C
C --- INITIALISATIONS
C
      DEFICU = CHAR(1:8)//'.UNILATE'
      NOEUMA = NOMA // '.NOMNOE'
      K8BLA  = ' '
      CALL JEVEUO(MULTCU,'L',JMULT)
      CALL JEVEUO(POINOE,'L',JPOI )
      CALL JEVEUO(LISNOE,'L',JNOE )
      CALL JEVEUO(NBGDCU,'L',JNBGD)
      CALL JEVEUO(COMPCU,'L',JNCMP)
      CALL JEVEUO(COEFCU,'L',JCOEF)
C
C --- CALCUL DU NOMBRE TOTAL DE GRANDEURS A GAUCHE
C
      NBGAU = 0
      DO 20 IZONE = 1,NZOCU
        NBNO   = ZI(JPOI+IZONE) - ZI(JPOI+IZONE-1)
        NBCMP  = ZI(JNBGD+IZONE) - ZI(JNBGD+IZONE-1)
        NBGAU  = NBGAU + NBNO*NBCMP
  20  CONTINUE
C
C --- CREATION DES VECTEURS DEFINITIFS
C
      NOEUCU = DEFICU(1:16)//'.LISNOE'
      CALL WKVECT(NOEUCU,'G V I',NNOCU ,JNOEU)
C
C --- CREATION DES VECTEURS TEMPORAIRES
C
      CALL WKVECT('&&CREAUN.INDIR','V V I' ,NNOCU+1,JINDIR)
      CALL WKVECT('&&CREAUN.CMPG' ,'V V K8',NBGAU,JCOMPG)
      CALL WKVECT('&&CREAUN.COEFG','V V K8',NBGAU,JCOEG)
      CALL WKVECT('&&CREAUN.COEFD','V V K8',NNOCU,JCOED)
      ZI(JINDIR) = 1
C
C ---
C
      CPTND  = 1
      CPTD   = 1
      NCMPG  = 1
C
      DO 1000 IZONE = 1,NZOCU
C
        NBNO   = ZI(JPOI+IZONE) - ZI(JPOI+IZONE-1)
        JDEBND = ZI(JPOI+IZONE-1)
        NBCMP  = ZI(JNBGD+IZONE) - ZI(JNBGD+IZONE-1)
        JDEBCP = ZI(JNBGD+IZONE-1)
C
        DO 2000 INO = 1,NBNO

          NUMND = ZI(JNOE-1+JDEBND+INO-1)
          NBSUP = 0

          DO 3000 ICMP = 1,NBCMP

            CMP = ZK8(JNCMP-1+JDEBCP+ICMP-1)

            CALL EXISCP(CMP   ,K8BLA ,NOMO  ,1     ,'NUM' ,
     &                  K8BLA ,NUMND ,EXIST )

            IF (EXIST.EQ.1) THEN
              IF (NIV.GE.2) THEN
                CALL JENUNO(JEXNUM(NOEUMA,NUMND),NOMNO)
                VALK (1) = NOMNO
                VALK (2) = CMP
                CALL U2MESK('I','UNILATER_58',2,VALK)
              END IF
              ZK8(JCOMPG-1+NCMPG) = CMP
              ZK8(JCOEG-1+NCMPG) = ZK8(JMULT-1+JDEBCP+ICMP-1)
              NCMPG  = NCMPG  + 1
            ELSE
              NBSUP = NBSUP + 1
              CALL JENUNO(JEXNUM(NOEUMA,NUMND),NOMNO)
              VALK (1) = NOMNO
              VALK (2) = CMP
              CALL U2MESK('I', 'UNILATER_75',2,VALK)
            ENDIF

 3000     CONTINUE

          ZI(JNOEU-1+CPTND)  = NUMND
          ZK8(JCOED-1+CPTD)  = ZK8(JCOEF+IZONE-1)
          ZI(JINDIR+CPTND)   = ZI(JINDIR+CPTND-1) + NBCMP - NBSUP

          CPTD  = CPTD  + 1
          CPTND = CPTND + 1

 2000   CONTINUE
 1000 CONTINUE

      CPTD   = CPTD  - 1
      CPTND  = CPTND - 1
      NCMPG  = NCMPG  - 1

      CALL ASSERT(CPTD.EQ.NNOCU)
      CALL ASSERT(CPTND.EQ.NNOCU)
C
C --- QUELQUES INFOS DIMENSIONS
C
      NDIMCU = DEFICU(1:16)//'.NDIMCU'
      CALL JEVEUO(NDIMCU,'E',JDIM)
      ZI(JDIM)   =  NNOCU
      ZI(JDIM+1) =  NCMPG
C
C --- LISTE DES POINTEURS DES NOEUDS
C
      POINCU = DEFICU(1:16)//'.POINOE'
      CALL WKVECT(POINCU,'G V I',NNOCU+1,JPOIN)
      DO 4000 INO = 1,NNOCU+1
        ZI(JPOIN-1+INO) = ZI(JINDIR-1+INO)
 4000 CONTINUE
C
C --- LISTE DES NOMS DE COMPOSANTES A GAUCHE
C
      CMPGCU = DEFICU(1:16)//'.CMPGCU'
      CALL WKVECT(CMPGCU,'G V K8',NCMPG,JCMPG)
      DO 4001 ICMP = 1,NCMPG
        ZK8(JCMPG-1+ICMP) = ZK8(JCOMPG-1+ICMP)
 4001 CONTINUE
C
C --- LISTE DES COEFFICIENTS A DROITE ET A GAUCHE
C
      COEGCU = DEFICU(1:16)//'.COEFG'
      COEDCU = DEFICU(1:16)//'.COEFD'
      CALL WKVECT(COEGCU,'G V K8',NCMPG,JCOEFG)
      CALL WKVECT(COEDCU,'G V K8',NNOCU,JCOEFD)

      DO 4003 ICMP = 1,NCMPG
        ZK8(JCOEFG-1+ICMP) = ZK8(JCOEG-1+ICMP)
 4003 CONTINUE

      DO 4004 ICMP = 1,CPTD
        ZK8(JCOEFD-1+ICMP) = ZK8(JCOED-1+ICMP)
 4004 CONTINUE
C
C --- NETTOYAGE
C
      CALL JEDETR('&&CREAUN.INDIR')
      CALL JEDETR('&&CREAUN.CMPG')
      CALL JEDETR('&&CREAUN.COEFG')
      CALL JEDETR('&&CREAUN.COEFD')
C
C ======================================================================
      CALL JEDEMA()
C
      END
