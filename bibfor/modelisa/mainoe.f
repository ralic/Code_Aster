      SUBROUTINE MAINOE(NOMA,NBM,LIMANU,NUOUNO,NBNO,OBJ)
      IMPLICIT   NONE
      INCLUDE 'jeveux.h'

      CHARACTER*32 JEXNUM,JEXATR
      INTEGER LIMANU(*),NBM
      CHARACTER*24 OBJ
      CHARACTER*8 NOMA
      CHARACTER*2 NUOUNO
C-----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 09/11/2012   AUTEUR DELMAS J.DELMAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE REZETTE C.REZETTE

C BUT : FAIRE LA LISTE DES NOEUDS PORTES PAR LES MAILLES DE LIMANU
C       (SANS DOUBLONS)

C ARGUMENTS D'ENTREE:
C      NOMA : NOM DU MAILLAGE
C      NBM  : NOMBRE DE MAILLES DANS LA LISTE.
C    LIMANU : LISTE DES NUMEROS DE MAILLE
C    NUOUNO : /'NU' : ON VEUT LA LISTE DES NUMEROS DE NOEUDS
C             /'NO' : ON VEUT LA LISTE DES NOMS DE NOEUDS
C     OBJ   : NOM DE L'OBJET QUI CONTIENDRA LES NOEUDS (V I OU V K8)

C ARGUMENTS DE SORTIE:
C      NBNO : NOMBRE DE NOEUDS TROUVES
C      OBJ  : OBJ EST ALLOUE ET REMPLI
C-----------------------------------------------------------------------
      INTEGER IER,NBNOT,JCONX1,JCONX2,K,JLISNO,IMA,NBNO,KNO,NUNO
      INTEGER JRESU,ICO
      CHARACTER*8 NONO
      CHARACTER*1 KBID
C     ------------------------------------------------------------------

      CALL JEMARQ()
      CALL ASSERT(NUOUNO.EQ.'NO' .OR. NUOUNO.EQ.'NU')

      CALL DISMOI('F','NB_NO_MAILLA',NOMA,'MAILLAGE',NBNOT,KBID,IER)
      CALL ASSERT(NBNOT.GT.0)
      CALL JEVEUO(NOMA//'.CONNEX','L',JCONX1)
      CALL JEVEUO(JEXATR(NOMA//'.CONNEX','LONCUM'),'L',JCONX2)


C     -- ON "COCHE" DANS .LISNO LES NOEUDS CONCERNES PAR LES MAILLES:
C     ----------------------------------------------------------------
      CALL JEDETR('&&MAINOE.LISNO')
      CALL WKVECT('&&MAINOE.LISNO','V V I',NBNOT,JLISNO)
      DO 20,K = 1,NBM
        IMA = LIMANU(K)
        NBNO = ZI(JCONX2-1+IMA+1) - ZI(JCONX2-1+IMA)
        DO 10,KNO = 1,NBNO
          NUNO = ZI(JCONX1-1+ZI(JCONX2-1+IMA)+KNO-1)
          ZI(JLISNO-1+NUNO) = 1
   10   CONTINUE
   20 CONTINUE

C     -- ON COMPTE LES RETENUS  :
      NBNO = 0
      DO 30,K = 1,NBNOT
        IF (ZI(JLISNO-1+K).EQ.1) NBNO = NBNO + 1
   30 CONTINUE
      CALL ASSERT(NBNO.GT.0)

C     -- ALLOCATION ET REMPLISSAGE DE OBJ :
C     --------------------------------------
      IF (NUOUNO.EQ.'NU') THEN
        CALL WKVECT(OBJ,'V V I',NBNO,JRESU)

      ELSE
        CALL WKVECT(OBJ,'V V K8',NBNO,JRESU)
      END IF

      ICO = 0
      DO 40,K = 1,NBNOT
        IF (ZI(JLISNO-1+K).EQ.0) GO TO 40
        ICO = ICO + 1
        IF (NUOUNO.EQ.'NU') THEN
          ZI(JRESU-1+ICO) = K
        ELSE
          CALL JENUNO(JEXNUM(NOMA//'.NOMNOE',K),NONO)
          ZK8(JRESU-1+ICO) = NONO
        END IF
   40 CONTINUE

      CALL JEDEMA()
      END
