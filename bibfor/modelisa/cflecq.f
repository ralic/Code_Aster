      SUBROUTINE CFLECQ(IFORM ,NOMA  ,NOMO  ,DEFICO,NSUCO  ,
     &                  NNOCO0,LISTNO,POINSN,NNOCO )
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 20/04/2010   AUTEUR DESOZA T.DESOZA 
C ======================================================================
C COPYRIGHT (C) 1991 - 2008  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT     NONE
      CHARACTER*8  NOMO,NOMA
      INTEGER      IFORM,NSUCO
      INTEGER      NNOCO0,NNOCO
      CHARACTER*24 DEFICO
      CHARACTER*24 LISTNO
      CHARACTER*24 POINSN
C
C ----------------------------------------------------------------------
C
C ROUTINE CONTACT (METHODES MAILLEES - LECTURE DONNEES - ELIMINATION)
C
C CREATION D'UNE LISTE DES NOEUDS MILIEUX DES FACES DES COQUES_3D
C
C ----------------------------------------------------------------------
C
C
C IN  IFORM  : FORMULATION DISCRETE (1) OU CONTINUE (2)
C IN  NOMA   : NOM DU MAILLAGE
C IN  NOMO   : NOM DU MODELE
C IN  DEFICO : NOM SD CONTACT DEFINITION
C IN  NSUCO  : NOMBRE TOTAL DE SURFACES DE CONTACT
C IN  NNOCO0 : NOMBRE TOTAL DE NOEUDS DES SURFACES
C OUT POINSN : POINTEUR MISE A JOUR POUR PSURNO
C OUT LISTNO : LISTE DES NOEUDS RESTANTES (LONGUEUR NNOCO)
C OUT NNOCO  : NOMBRE DE NOEUDS AU FINAL
C
C -------------- DEBUT DECLARATIONS NORMALISEES JEVEUX -----------------
C
      CHARACTER*32 JEXNUM
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C ---------------- FIN DECLARATIONS NORMALISEES JEVEUX -----------------
C
      CHARACTER*24 CONTNO,CONTMA
      INTEGER      JNOCO,JMACO
      INTEGER      JINDNO,JELINO
      INTEGER      JDECNO,JNO
      INTEGER      JDECMA
      INTEGER      ISUCO,I,INO,K,IMA,NUTYP,NOEUMI
      INTEGER      ELIMNO,NBMA,NBNO
      INTEGER      IATYMA,ITYPMA
      INTEGER      NUMMAI,NUMNOE
      CHARACTER*8  NOMTM,NOMMAI,NOMNOE
      LOGICAL      LCOQUE
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
C
C --- CREATION DES VECTEURS DE TRAVAIL TEMPORAIRES
C
      CALL WKVECT('&&CFLECQ.INDINO','V V I',NNOCO  ,JINDNO)
      CALL WKVECT(POINSN,'V V I',NSUCO+1,JELINO)
C
C --- ACCES AUX STRUCTURES DE DONNEES DE CONTACT
C
      CONTNO = DEFICO(1:16)//'.NOEUCO'
      CONTMA = DEFICO(1:16)//'.MAILCO'
      CALL JEVEUO(CONTMA,'L',JMACO)
      CALL JEVEUO(CONTNO,'L',JNOCO)
C
C --- INITIALISATIONS
C
      ELIMNO = 0
      CALL JEVEUO(NOMA(1:8)//'.TYPMAIL','L',IATYMA)
C
C --- REPERAGE DES NOEUDS MILIEUX POUR CHAQUE SURFACE
C
      DO 110 ISUCO = 1,NSUCO

        CALL CFNBSF(DEFICO,ISUCO ,'MAIL',NBMA  ,JDECMA)
        ZI(JELINO+ISUCO) = ZI(JELINO+ISUCO-1)

        DO 90 IMA = 1,NBMA

C         --- NUMERO MAILLE COURANTE

          NUMMAI = ZI(JMACO+JDECMA+IMA-1)

C         --- TYPE MAILLE COURANTE

          ITYPMA = IATYMA - 1 + NUMMAI
          NUTYP  = ZI(ITYPMA)
          CALL JENUNO(JEXNUM('&CATA.TM.NOMTM',NUTYP),NOMTM)
          CALL JENUNO(JEXNUM(NOMA//'.NOMMAI',NUMMAI),NOMMAI)

          IF  (NOMTM(1:5).EQ.'QUAD9') THEN
            CALL ISCOQU(NOMO  ,NUMMAI,LCOQUE)
            NOEUMI = 9
          ELSEIF  (NOMTM(1:5).EQ.'TRIA7') THEN
            CALL ISCOQU(NOMO  ,NUMMAI,LCOQUE)
            NOEUMI = 7
          ELSE
            LCOQUE = .FALSE.
          ENDIF

C         --- NUMERO ABSOLU DU NOEUD VISE

          IF (LCOQUE) THEN
            CALL JEVEUO(JEXNUM(NOMA//'.CONNEX',NUMMAI),'L',JDECNO)
            NUMNOE = ZI(JDECNO+NOEUMI-1)
            CALL JENUNO(JEXNUM(NOMA//'.NOMNOE',NUMNOE),NOMNOE)
          ENDIF

C         --- ELIMINATION NOEUD MILIEU

          IF (LCOQUE) THEN
            CALL CFNBSF(DEFICO,ISUCO ,'NOEU',NBNO ,JDECNO)
            DO 20 I = 1,NBNO
              INO = ZI(JNOCO+JDECNO+I-1)
              IF (INO.EQ.NUMNOE) THEN
                ZI(JINDNO+JDECNO+I-1) = 1
                ZI(JELINO+ISUCO)      = ZI(JELINO+ISUCO) + 1
                ELIMNO                = ELIMNO + 1
                GOTO 90
              ENDIF
  20        CONTINUE
          ENDIF

   90   CONTINUE


  110 CONTINUE
C
C --- COQUE_3D NON UTILISABLE EN FORMULATION CONTINUE
C
      IF ((IFORM.EQ.2).AND.(ELIMNO.GT.0)) THEN
        CALL U2MESS('F','CONTACT_94')
      ENDIF
C
C --- RECOPIE DES NOEUDS NON ELIMINES DANS TABLEAU DE TRAVAIL
C
      NNOCO  = NNOCO0 - ELIMNO
      CALL WKVECT(LISTNO,'V V I',NNOCO,JNO)
C
C --- TRAITEMENT DES NOEUDS
C
      K = 0
      DO 120 I = 1,NNOCO0
        IF (ZI(JINDNO+I-1).EQ.0) THEN
          K = K + 1
          ZI(JNO+K-1) = ZI(JNOCO+I-1)
        ENDIF
  120 CONTINUE
      CALL ASSERT(K.EQ.NNOCO)
C
C --- DESTRUCTION DES VECTEURS DE TRAVAIL TEMPORAIRES
C
      CALL JEDETR('&&CFLECQ.INDINO')
C
      CALL JEDEMA()
      END
