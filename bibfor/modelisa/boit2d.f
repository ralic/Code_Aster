      SUBROUTINE BOIT2D(MAIL  ,TYPMAI,NGRMA ,NORM  ,PRECBO,
     &                  NOMBOI)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 09/01/2007   AUTEUR ABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2007  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT NONE
      CHARACTER*8   MAIL
      CHARACTER*16  TYPMAI
      CHARACTER*16  NOMBOI
      CHARACTER*10  NORM       
      CHARACTER*19  NGRMA
      REAL*8        PRECBO
C      
C ----------------------------------------------------------------------
C
C CONSTRUCTION DE BOITES ENGLOBANTES POUR UN GROUPE DE MAILLES
C
C AIGUILLAGE PRINCIPAL POUR MAILLES 2D
C
C ----------------------------------------------------------------------
C
C
C IN  MAIL   : NOM DU MAILLAGE
C IN  NORM   : NORMALES LISSEES POUR LES COQUES
C IN  TYPMAI : SD CONTENANT NOM DES TYPES ELEMENTS (&&CATA.NOMTM)
C IN  NGRMA  : LISTE DES MAILLES A METTRE EN BOITE
C IN  PRECBO : PRECISION RELATIVE POUR AGRANDIR LA BOITE ENGLOBANTE
C              MAILLE 
C I/O NOMBOI : SD BOITE
C
C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ---------------------
C
      CHARACTER*32       JEXATR
      INTEGER            ZI
      COMMON  / IVARJE / ZI(1)
      REAL*8             ZR
      COMMON  / RVARJE / ZR(1)
      COMPLEX*16         ZC
      COMMON  / CVARJE / ZC(1)
      LOGICAL            ZL
      COMMON  / LVARJE / ZL(1)
      CHARACTER*8        ZK8
      CHARACTER*16                ZK16
      CHARACTER*24                          ZK24
      CHARACTER*32                                    ZK32
      CHARACTER*80                                              ZK80
      COMMON  / KVARJE / ZK8(1) , ZK16(1) , ZK24(1) , ZK32(1) , ZK80(1)
C      
C --- FIN DECLARATIONS NORMALISEES JEVEUX ------------------------------
C
      INTEGER      IMA,IPAN,NUMA,IDIME,L,IRET
      CHARACTER*8  TYPEMA,K8BID
      INTEGER      JGRMA,JTYPMA,JTYPMM,JNORM
      INTEGER      JDCSOM,JDCPAN,JDCMMA,JDCMMG,JDCDIM
      INTEGER      IFM,NIV
      INTEGER      LCPAN,LCSOM
      INTEGER      NPAN,NSOM,NAREQ,NMA,NDIME
      INTEGER      LCOQUE   
      INTEGER      JCONX,JLONG,JCOORD 
      INTEGER      JBSOM,JBPAN,JMNMX,JMMGL,JDIME,JBOIH 
      REAL*8       DIMEH,LONG,SURF,PREC
      REAL*8       CNOEUD(81)
      INTEGER      NOEPAN(60),NOAR(36),PANNOE(24),PAAR(24)
      INTEGER      NBPAN,NBSOM,NBAREQ
C      
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
      CALL INFNIV(IFM,NIV)      
C
C --- INITIALISATIONS
C
      CALL JEVEUO(TYPMAI,'L',JTYPMM)
      LCPAN  = 1
      LCSOM  = 1
      JDCSOM = 0
      JDCPAN = 0
      JDCMMA = 0
      JDCDIM = 4
      NDIME  = 2
C
C --- LECTURE DONNEES MAILLAGE
C
      CALL JEVEUO(MAIL(1:8)//'.TYPMAIL','L',JTYPMA)
      CALL JEVEUO(MAIL(1:8)//'.CONNEX','L',JCONX)
      CALL JEVEUO(JEXATR(MAIL(1:8)//'.CONNEX','LONCUM'),'L',JLONG)
      CALL JEVEUO(MAIL(1:8)//'.COORDO    .VALE','L',JCOORD)    
C
C --- LECTURE DONNEES NORMALES
C  
      CALL JEEXIN(NORM,IRET)
      IF (IRET.NE.0) THEN
         CALL JEVEUO(NORM,'L',JNORM)   
      ENDIF               
C
C --- LECTURE DONNEES BOITES APPARIEMENT
C         
      CALL JEVEUO(NOMBOI(1:16)//'.SOMMET','E',JBSOM)
      CALL JEVEUO(NOMBOI(1:16)//'.PAN'   ,'E',JBPAN)
      CALL JEVEUO(NOMBOI(1:16)//'.MINMAX','E',JMNMX)
      CALL JEVEUO(NOMBOI(1:16)//'.MMGLOB','E',JMMGL)
      CALL JEVEUO(NOMBOI(1:16)//'.DIME'  ,'E',JDIME)
      CALL JEVEUO(NOMBOI(1:16)//'.H'     ,'E',JBOIH)
C
C --- LECTURE DONNEES GROUPE DE MAILLES
C      
      CALL JEVEUO(NGRMA,'L',JGRMA)
      CALL JELIRA(NGRMA,'LONMAX',NMA,K8BID)
C
C --- QUELQUES VERIFICATIONS
C      
      IF (NMA.NE.ZI(JDIME+1)) THEN
        CALL ASSERT(.FALSE.)
      ENDIF
      IF (ZI(JDIME).NE.NDIME) THEN
        CALL ASSERT(.FALSE.)
      ENDIF                      
C
      DO 40 IMA = 1, NMA
                                   
C --- CARACTERISTIQUES DE LA MAILLE
        NUMA   = ZI(JGRMA + IMA - 1)
        TYPEMA = ZK8(JTYPMM+ZI(JTYPMA-1+NUMA)-1)
        
C --- EXTENSION COQUE 1D EN MAILLE 2D (UN SEUL CAS: SEG3 -> QUAD6)
        CALL TMACOQ(TYPEMA,NDIME,LCOQUE)
   
C --- NOMBRE DE PANS DE LA MAILLE
        NPAN = NBPAN(TYPEMA)
        
C --- NOMBRE DE SOMMETS DE LA MAILLE
        NSOM = NBSOM(TYPEMA)         

C --- COORDONNEES DES NOEUDS DEFINISSANT LES PANS DE LA MAILLE
        CALL NOPAN(TYPEMA,NOEPAN)
C
        IF (TYPEMA(1:4).EQ.'TRIA') THEN             
          IF (TYPEMA(5:5).EQ.'3') THEN    

C --- COORDONNEES DES SOMMETS
            CALL COSOLI(NUMA,ZI(JCONX),ZI(JLONG),ZR(JCOORD),NDIME,
     &                  ZR(JBSOM+JDCSOM)) 
     
C --- MISE EN BOITE
            CALL BOITEL(ZR(JBSOM+JDCSOM),TYPEMA,NOEPAN,NPAN  ,NSOM  ,
     &                  NDIME,ZR(JMNMX+JDCMMA),ZR(JBPAN+JDCPAN))
         
          ELSE IF (TYPEMA(5:5).EQ.'6') THEN
          
C --- COORDONNEES DES SOMMETS
            CALL COSOLI(NUMA,ZI(JCONX),ZI(JLONG),ZR(JCOORD),NDIME,
     &                  CNOEUD)

C --- MISE EN BOITE COMME SI MAILLE LINEAIRE     
            CALL BOITEL(CNOEUD,TYPEMA,NOEPAN,NPAN  ,NSOM  ,
     &                  NDIME ,ZR(JMNMX+JDCMMA),ZR(JBPAN+JDCPAN))

C --- NOMBRE D'ARETES QUADRATIQUES 
            NAREQ = NBAREQ(TYPEMA)

C --- INDICES DES NOEUDS DEFINISSANT LES ARETES QUADRATIQUES
            CALL NOAREQ(TYPEMA,NOAR)

C --- INDICES DES PANS TOUCHANT LES ARETES QUADRATIQUES
            CALL PANARQ(TYPEMA,PAAR)

C --- CORRECTION DE LA MISE EN BOITE POUR ARETES QUADRATIQUES 
            CALL BOITEA(CNOEUD,NOAR  ,PAAR  ,NAREQ  ,NPAN  ,
     &                  NDIME ,ZR(JMNMX+JDCMMA),ZR(JBPAN+JDCPAN))

C --- INDICES DES PANS TOUCHANT LES NOEUDS PRINCIPAUX     
            CALL PANNO (TYPEMA,PANNOE) 

C --- COORDONNEES DES SOMMETS                
            CALL SOMMET(PANNOE,NDIME,ZR(JBPAN+JDCPAN),NSOM  ,
     &                  ZR(JBSOM+JDCSOM))
          ELSE
            WRITE(6,*) 'TYPEMA: ',TYPEMA
            CALL ASSERT(.FALSE.)
          ENDIF
C --- TROIS SOMMETS EN 2D: +6
          JDCSOM = JDCSOM + 6
C          
        ELSE IF (TYPEMA(1:4).EQ.'QUAD') THEN           
          IF (TYPEMA(5:5).EQ.'4') THEN
C --- COORDONNEES DES SOMMETS
            CALL COSOLI(NUMA,ZI(JCONX),ZI(JLONG),ZR(JCOORD),NDIME,
     &                  ZR(JBSOM+JDCSOM)) 
     
C --- MISE EN BOITE   
            CALL BOITEL(ZR(JBSOM+JDCSOM),TYPEMA,NOEPAN,NPAN  ,NSOM  ,
     &                  NDIME,ZR(JMNMX+JDCMMA),ZR(JBPAN+JDCPAN))
          
          ELSE IF ((TYPEMA(5:5).EQ.'6').OR.
     &             (TYPEMA(5:5).EQ.'8').OR.
     &             (TYPEMA(5:5).EQ.'9'))  THEN
     
C --- COORDONNEES DES SOMMETS
     
            IF (TYPEMA(5:5).EQ.'6') THEN
              CALL COCOQU(
     &             NUMA,ZI(JCONX),ZI(JLONG),ZR(JCOORD),'VARIABLE',
     &             0.D0,ZR(JNORM),NDIME,CNOEUD)            
            ELSE
              CALL COSOLI(NUMA,ZI(JCONX),ZI(JLONG),ZR(JCOORD),NDIME,
     &                    CNOEUD)            
            ENDIF

C --- MISE EN BOITE COMME SI MAILLE LINEAIRE  
            CALL BOITEL(CNOEUD,TYPEMA,NOEPAN,NPAN  ,NSOM  ,
     &                  NDIME,ZR(JMNMX+JDCMMA),ZR(JBPAN+JDCPAN))   

C --- NOMBRE D'ARETES QUADRATIQUES 
            NAREQ = NBAREQ(TYPEMA)

C --- INDICES DES NOEUDS DEFINISSANT LES ARETES QUADRATIQUES
            CALL NOAREQ(TYPEMA,NOAR)

C --- INDICES DES PANS TOUCHANT LES ARETES QUADRATIQUES
            CALL PANARQ(TYPEMA,PAAR)

C --- CORRECTION DE LA MISE EN BOITE POUR ARETES QUADRATIQUES
            CALL BOITEA(CNOEUD,NOAR  ,PAAR  ,NAREQ  ,NPAN  ,
     &                  NDIME,ZR(JMNMX+JDCMMA),ZR(JBPAN+JDCPAN))

C --- INDICES DES PANS TOUCHANT LES NOEUDS PRINCIPAUX     
            CALL PANNO (TYPEMA,PANNOE) 

C --- COORDONNEES DES SOMMETS                
            CALL SOMMET(PANNOE,NDIME,ZR(JBPAN+JDCPAN),NSOM  ,
     &                  ZR(JBSOM+JDCSOM))
          ELSE
            WRITE(6,*) 'TYPEMA: ',TYPEMA,IMA,NUMA
            CALL ASSERT(.FALSE.)
          ENDIF
C --- QUATRE SOMMETS EN 2D: +8          
          JDCSOM = JDCSOM + 8
        ELSE
          WRITE(6,*) 'TYPEMA: ',TYPEMA
          CALL ASSERT(.FALSE.)        
        ENDIF
C
C --- CALCUL DE LA BOITE GLOBALE
C
        SURF   = 1.D0
        JDCMMG = 0
        DO 50 IDIME = 1,NDIME
          LONG  = ZR(JMNMX+JDCMMA+1)-ZR(JMNMX+JDCMMA)
          SURF  = SURF*LONG
          PREC  = PRECBO*LONG
          ZR(JMNMX+JDCMMA)   = ZR(JMNMX+JDCMMA)   - PREC
          ZR(JMNMX+JDCMMA+1) = ZR(JMNMX+JDCMMA+1) + PREC
          IF (ZR(JMNMX+JDCMMA).LT.ZR(JMMGL+JDCMMG)) THEN
            ZR(JMMGL+JDCMMG) = ZR(JMNMX+JDCMMA)
          ENDIF   
          IF (ZR(JMNMX+JDCMMA+1).GT.ZR(JMMGL+JDCMMG+1)) THEN
            ZR(JMMGL+JDCMMG+1) = ZR(JMNMX+JDCMMA+1)
          ENDIF 
          JDCMMA = JDCMMA + 2   
          JDCMMG = JDCMMG + 2
 50     CONTINUE
C
C --- EQUATIONS DES PANS:
C ---  * MISE A L'ECHELLE PAR LA SURFACE DE LA BOITE
C ---  * CALCUL DES DEUX BOITES (ENGLOBANT ET INSCRIT) PAR LA PRECISION
C
        DO 60 IPAN = 1, NPAN
          DO 70 L = 1, 4
            ZR(JBPAN+JDCPAN) = ZR(JBPAN+JDCPAN)/SURF
            JDCPAN = JDCPAN + 1
 70       CONTINUE
          ZR(JBPAN+JDCPAN-2) = ZR(JBPAN+JDCPAN-2) - PRECBO
          ZR(JBPAN+JDCPAN-1) = ZR(JBPAN+JDCPAN-1) + PRECBO
 60     CONTINUE
C
C --- INFORMATIONS SUR DIMENSIONS
C
        LCPAN = LCPAN + NPAN
        LCSOM = LCSOM + NSOM
        ZI(JDIME+JDCDIM)   = LCPAN
        ZI(JDIME+JDCDIM+1) = LCSOM
        JDCDIM = JDCDIM + 2
C
C --- DIMENSION CARACTERISTIQUE DE LA BOITE
C
        DIMEH = SQRT(SURF)
        ZR(JBOIH+IMA-1) = DIMEH

 40   CONTINUE
C
      CALL JEDEMA()
      END
