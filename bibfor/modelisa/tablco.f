      SUBROUTINE TABLCO(CHAR  ,NOMA  ,NZOCO ,NSUCO ,NMACO ,
     &                  NNOCO ,NMANO ,NNOMA ,NMAMA)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 23/09/2008   AUTEUR ABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C TOLE CRP_21
C
      IMPLICIT     NONE
      CHARACTER*8  CHAR
      CHARACTER*8  NOMA
      INTEGER      NZOCO
      INTEGER      NSUCO
      INTEGER      NMACO
      INTEGER      NNOCO
      INTEGER      NMANO
      INTEGER      NNOMA
      INTEGER      NMAMA
C
C ----------------------------------------------------------------------
C
C ROUTINE CONTACT (METHODES MAILLEES - LECTURE DONNEES)
C
C CONSTRUCTION DES CONNECTIVITES INVERSES
C
C ----------------------------------------------------------------------
C
C
C CONSTRUCTION DU TABLEAU INVERSE DONNANT POUR CHAQUE NOEUD DE CONTACT
C LA LISTE DES MAILLES QUI LE CONTIENNENT AU SEIN DE LA MEME SURFACE.
C CONSTRUCTION DU TABLEAU DIRECT DONNANT POUR CHAQUE MAILLE DE CONTACT
C LA LISTE DES NOEUDS QU'ELLE CONTIENT AU SEIN DE LA MEME SURFACE.
C
C IN  CHAR   : NOM UTILISATEUR DU CONCEPT DE CHARGE
C IN  NOMA   : NOM DU MAILLAGE
C IN  NZOCO  : NOMBRE DE ZONES DE CONTACT
C IN  NSUCO  : NOMBRE TOTAL DE SURFACES DE CONTACT
C IN  NMACO  : NOMBRE TOTAL DE MAILLES DES SURFACES
C IN  NNOCO  : NOMBRE TOTAL DE NOEUDS DES SURFACES
C OUT NMANO  : DIMENSION DU TABLEAU INVERSE NOEUDS->MAILLES
C OUT NNOMA  : DIMENSION DU TABLEAU DIRECT  MAILLES->NOEUDS
C OUT NMAMA  : DIMENSION DU TABLEAU DONNANT POUR CHAQUE MAILLE DE
C              CONTACT LA LISTE
C              DES MAILLES DE CONTACT DE LA MEME SURFACE ADJACENTE
C              (ON STOCKE LA POSITION DANS CONTMA, PAS LE NUMERO ABSOLU)
C
C -------------- DEBUT DECLARATIONS NORMALISEES JEVEUX -----------------
C
      CHARACTER*32 JEXATR
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C ---------------- FIN DECLARATIONS NORMALISEES JEVEUX ----------------
C
      INTEGER      NBNOM,ILMACO,NUMGLM,IAMACO
      INTEGER      IMA,IMA2,INO,NBID,I,NO,IZONE,I1,I2,IZOCO
      INTEGER      NUMNO,NUMA
      INTEGER      NMAX,NBNO,NBMA
      INTEGER      ISUCO,JDECNO,JDECMA,IDECMA
      INTEGER      JTRAV,JTRAV2,INC,LONG,JPROC

      INTEGER      JJNO1,NBNO1,NO1
      CHARACTER*24 DEFICO
      CHARACTER*8  K8BID
      CHARACTER*24 PZONE,PSURMA,PSURNO,CONTMA,CONTNO
      INTEGER      JZONE,JSUMA,JSUNO,JMACO,JNOCO,JMANO,JPOMA
      CHARACTER*24 MANOCO,PMANO,NOMACO,PNOMA,MAMACO,PMAMA,NOZOCO
      INTEGER      JNOMA,JPONO,JMAMA,JPOIN,JZOCO,NNN,IAINVE,ILINVE
      INTEGER      NUMALO
      CHARACTER*19 CONINV
C
C ----------------------------------------------------------------------
C
C     FONCTION "FORMULE" D'ACCES AU NOMBRE DE NEOUDS DES MAILLES
      NBNOM(IMA)=ZI(ILMACO+IMA)-ZI(ILMACO-1+IMA)

C     FONCTION "FORMULE" D'ACCES A LA CONNECTIVITE DES MAILLES
      NUMGLM(IMA,INO)=ZI(IAMACO-1+ZI(ILMACO+IMA-1)+INO-1)
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
C
C --- INITIALISATIONS
C
      DEFICO = CHAR(1:8)//'.CONTACT'  
C 
C --- LECTURE DES STRUCTURES DE DONNEES DE CONTACT
C      
      PZONE  = DEFICO(1:16)//'.PZONECO'
      PSURMA = DEFICO(1:16)//'.PSUMACO'
      PSURNO = DEFICO(1:16)//'.PSUNOCO'
      CONTMA = DEFICO(1:16)//'.MAILCO'
      CONTNO = DEFICO(1:16)//'.NOEUCO'
      CALL JEVEUO(PZONE, 'L',JZONE)
      CALL JEVEUO(PSURMA,'L',JSUMA)
      CALL JEVEUO(PSURNO,'L',JSUNO)
      CALL JEVEUO(CONTMA,'L',JMACO)
      CALL JEVEUO(CONTNO,'L',JNOCO)
C
C --- CONNECTIVITES INVERSES
C
      CONINV='&&TABLCO.CONINV'
      CALL CNCINV(NOMA,ZI(JMACO),NMACO,'V',CONINV)
      CALL JEVEUO(NOMA//'.CONNEX','L',IAMACO)
      CALL JEVEUO(JEXATR(NOMA//'.CONNEX','LONCUM'),'L',ILMACO)

      NMAX = MAX(NNOCO,NMACO)
      NBID = 20*NMAX

      CALL WKVECT('&&TABLCO.TRAV','V V I',NNOCO,JTRAV)
      CALL WKVECT('&&TABLCO.TRAV2','V V I',NMACO,JTRAV2)

C ======================================================================
C TABLEAU MANOCO : TABLEAU INVERSE NOEUDS->MAILLES
C                  LISTE DES MAILLES DE CONTACT AUXQUELLES APPARTIENT
C                  CHAQUE NOEUD DE CONTACT, POUR CHAQUE SURFACE.
C TABLEAU NOMACO : TABLEAU DIRECT MAILLES->NOEUDS
C                  LISTE DES NOEUDS DE CHAQUE MAILLE DE CONTACT.
C TABLEAU MAMACO : TABLEAU DONNANT POUR CHAQUE MAILLE DE CONTACT LA
C                  LISTE
C                  DES MAILLES DE CONTACT DE LA MEME SURFACE ADJACENTES
C                  (ON STOCKE LA POSITION DANS CONTMA, PAS LE NUMERO
C                    ABSOLU)
C                  <-> POSITION DANS CONTMA DES MAILLES DE CONTACT
C                      ADJACENTES A UNE MAILLE DONNEE DANS LA MEME
C                      SURFACE.
C TABLEAU NOZOCO : TABLEAU DONNANT LE NUMERO DE ZONE POUR CHAQUE NOEUD
C                   DE CONTACT
C                  NUMERO DE LA ZONE A LAQUELLE APPARTIENT CHAQUE NOEUD-
C                  DE CONTACT.
C ======================================================================

      MANOCO = DEFICO(1:16)//'.MANOCO'
      PMANO  = DEFICO(1:16)//'.PMANOCO'
      NOMACO = DEFICO(1:16)//'.NOMACO'
      PNOMA  = DEFICO(1:16)//'.PNOMACO'
      MAMACO = DEFICO(1:16)//'.MAMACO'
      PMAMA  = DEFICO(1:16)//'.PMAMACO'
      NOZOCO = DEFICO(1:16)//'.NOZOCO'

C ======================================================================
C         REMPLISSAGE DU TABLEAU INVERSE NOEUDS->MAILLES MANOCO
C            ET CONSTRUCTION DU POINTEUR PAMANO CORRESPONDANT
C            CALCUL DE LA NORMALE EN CHAQUE NOEUD DE CONTACT
C                        BOUCLE SUR LES SURFACES
C ======================================================================

      CALL WKVECT(MANOCO,'G V I',NBID,JMANO)
      CALL WKVECT(PMANO,'G V I',NNOCO+1,JPOMA)
      ZI(JPOMA) = 0

      INC = 0
      LONG = NBID

      CALL JEVEUO(CONINV,'L',IAINVE)
      CALL JEVEUO(JEXATR(CONINV,'LONCUM'),'L',ILINVE)
      DO 40 ISUCO = 1,NSUCO

C ------- ADRESSES DE DEBUT DANS LES LISTES CONTNO ET CONTMA
C ------- ET NOMBRE DE NOEUDS ET MAILLES POUR LA SURFACE ISUCO

        JDECNO = ZI(JSUNO+ISUCO-1)
        JDECMA = ZI(JSUMA+ISUCO-1)
        NBNO   = ZI(JSUNO+ISUCO) - ZI(JSUNO+ISUCO-1)
        NBMA   = ZI(JSUMA+ISUCO) - ZI(JSUMA+ISUCO-1)

C ------- EXAMEN DES NOEUDS DE LA SURFACE
        DO 30 INO = 1,NBNO

          ZI(JTRAV+JDECNO+INO-1) = 0

C ----- NUMERO DU NOEUD

          NUMNO = ZI(JNOCO+JDECNO+INO-1)

C ----- EXAMEN DE TOUTES LES MAILLES DE LA SURFACE

          NNN=ZI(ILINVE+NUMNO)-ZI(ILINVE-1+NUMNO)
          DO 20 IMA = 1,NNN
            NUMALO=ZI(IAINVE-1+ZI(ILINVE-1+NUMNO)+IMA-1)
            IF (NUMALO.LE.ZI(JSUMA+ISUCO).AND.
     &          NUMALO.GT.ZI(JSUMA+ISUCO-1)) THEN
              INC=INC+1
              IF (INC.GT.LONG) THEN
                LONG = 2*LONG
                CALL JUVECA(MANOCO,LONG)
                CALL JEVEUO(MANOCO,'E',JMANO)
              END IF
              ZI(JMANO-1+INC)=NUMALO
              ZI(JTRAV+JDECNO+INO-1) = ZI(JTRAV+JDECNO+INO-1) + 1
            ENDIF
   20     CONTINUE

C ----- INCREMENTATION DU POINTEUR PMANO

          ZI(JPOMA+JDECNO+INO) = ZI(JPOMA+JDECNO+INO-1) +
     &                           ZI(JTRAV+JDECNO+INO-1)

   30   CONTINUE

   40 CONTINUE

C ======================================================================
C       VERIFICATION DE LA LONGUEUR DU TABLEAU MANOCO ET STOCKAGE
C ======================================================================

      NMANO = ZI(JPOMA+NNOCO)
      IF (NMANO.GT.LONG) THEN
        CALL ASSERT(.FALSE.)
      END IF
      CALL JEECRA(MANOCO,'LONUTI',NMANO,K8BID)

C ======================================================================
C         REMPLISSAGE DU TABLEAU DIRECT MAILLES->NOEUDS NOMACO
C            ET CONSTRUCTION DU POINTEUR PNOMA CORRESPONDANT
C                        BOUCLE SUR LES SURFACES
C ======================================================================

      CALL WKVECT(PNOMA,'G V I',NMACO+1,JPONO)
      ZI(JPONO) = 0

      LONG = NBID
      INC=0
      DO 70 ISUCO = 1,NSUCO
        JDECNO = ZI(JSUNO+ISUCO-1)
        NBNO = ZI(JSUNO+ISUCO) - ZI(JSUNO+ISUCO-1)
        JDECMA = ZI(JSUMA+ISUCO-1)
        DO 50 INO = 1,NBNO
          NUMNO = ZI(JNOCO+JDECNO+INO-1)
          NNN=ZI(ILINVE+NUMNO)-ZI(ILINVE-1+NUMNO)
          DO 60 IMA = 1,NNN
            NUMALO=ZI(IAINVE-1+ZI(ILINVE-1+NUMNO)+IMA-1)
            IF (NUMALO.LE.ZI(JSUMA+ISUCO).AND.
     &          NUMALO.GT.ZI(JSUMA+ISUCO-1)) THEN
              INC=INC+1
              IF (INC.GT.LONG) THEN
                LONG = 2*LONG
              END IF
            ENDIF
  60      CONTINUE
  50    CONTINUE
  70  CONTINUE

      CALL WKVECT(NOMACO,'G V I',LONG,JNOMA)

C ------- ADRESSES DE DEBUT DANS LES LISTES CONTNO ET CONTMA
C ------- ET NOMBRE DE NOEUDS ET MAILLES POUR LA SURFACE ISUCO

      INC=0
      DO 80 ISUCO = 1,NSUCO
        JDECNO = ZI(JSUNO+ISUCO-1)
        NBNO = ZI(JSUNO+ISUCO) - ZI(JSUNO+ISUCO-1)
        NBMA = ZI(JSUMA+ISUCO) - ZI(JSUMA+ISUCO-1)
        JDECMA = ZI(JSUMA+ISUCO-1)

C ------- EXAMEN DES MAILLES DE LA SURFACE

        DO 90 INO = 1,NBNO
          NUMNO = ZI(JNOCO+JDECNO+INO-1)
          NNN=ZI(ILINVE+NUMNO)-ZI(ILINVE-1+NUMNO)
          DO 100 IMA = 1,NNN
            NUMALO=ZI(IAINVE-1+ZI(ILINVE-1+NUMNO)+IMA-1)
            IF (NUMALO.LE.ZI(JSUMA+ISUCO).AND.
     &          NUMALO.GT.ZI(JSUMA+ISUCO-1)) THEN
              ZI(JTRAV2+NUMALO-1)=ZI(JTRAV2+NUMALO-1)+1
            ENDIF
  100     CONTINUE
  90    CONTINUE

        DO 110 IMA = 1,NBMA
            ZI(JPONO+JDECMA+IMA)= ZI(JPONO+JDECMA+IMA-1)+
     &                               ZI(JTRAV2+JDECMA+IMA-1)


C ----- NUMERO DE LA MAILLE ET ADRESSE DE SES NOEUDS

          NUMA = ZI(JMACO+JDECMA+IMA-1)

C --- COMPARAISON AVEC LES NOEUDS DE CONTACT DE LA SURFACE :
C --- ON STOCKERA LES NOEUDS DE LA MAILLE DANS L'ORDRE OU ILS
C --- APPARAISSENT, D'OU L'INVERSION DES BOUCLES 130 ET 140

          DO 130 I = 1,NBNOM(NUMA)
            NO=NUMGLM(NUMA,I)
            DO 140 INO = 1,NBNO
              NUMNO = ZI(JNOCO+JDECNO+INO-1)
              IF (NO.EQ.NUMNO) THEN
                INC = INC + 1
                ZI(JNOMA+INC-1) = JDECNO + INO
                GO TO 130
              END IF
  140       CONTINUE
  130     CONTINUE
  110   CONTINUE

  80  CONTINUE

C ======================================================================
C       VERIFICATION DE LA LONGUEUR DU TABLEAU NOMACO ET STOCKAGE
C ======================================================================

      NNOMA = ZI(JPONO+NMACO)
      IF (NNOMA.GT.LONG) THEN
        CALL ASSERT(.FALSE.)
      END IF
      CALL JEECRA(NOMACO,'LONUTI',NNOMA,K8BID)

C ======================================================================
C    CONSTRUCTION DU TABLEAU MAMACO DONNANT POUR CHAQUE MAILLE D'UNE
C     SURFACE DE CONTACT LA LISTE DES MAILLES DE CONTACT ADJACENTES
C      (IE AYANT AU MOINS UN NOEUD EN COMMUN) DANS LA MEME SURFACE
C ======================================================================

      CALL WKVECT(MAMACO,'G V I',NBID,JMAMA)
      CALL WKVECT(PMAMA,'G V I',NMACO+1,JPOIN)
      ZI(JPOIN) = 0
      INC = 0
      LONG = NBID
      DO 150 ISUCO = 1,NSUCO

C ------- ADRESSE DE DEBUT DE LA LISTE DES MAILLES DANS CONTMA ET NOMBRE

        JDECMA = ZI(JSUMA+ISUCO-1)
        NBMA = ZI(JSUMA+ISUCO) - ZI(JSUMA+ISUCO-1)

C ------- BOUCLE SUR LES MAILLES DE LA SURFACE ISUCO

        DO 160 IMA = 1,NBMA

          ZI(JTRAV2+JDECMA+IMA-1) = 0
          CALL WKVECT('&&TABLCO.PROCH','V V I',NBMA,JPROC)

C ----- ADRESSE DE RANGEMENT DES NOEUDS DE LA MAILLE, NOMBRE ET NUMEROS

          JJNO1 = ZI(JPONO+JDECMA+IMA-1)
          NBNO1 = ZI(JPONO+JDECMA+IMA) - ZI(JPONO+JDECMA+IMA-1)
          IF (NBNO1.GT.9) CALL ASSERT(.FALSE.)
          DO 170 INO = 1,NBNO1
            NO1 = ZI(JNOMA+JJNO1+INO-1)
            NUMNO = ZI(JNOCO+NO1-1)
            NNN=ZI(ILINVE+NUMNO)-ZI(ILINVE-1+NUMNO)
            DO 180 IMA2 = 1,NNN
              NUMALO=ZI(IAINVE-1+ZI(ILINVE-1+NUMNO)+IMA2-1)
              IDECMA=NUMALO-JDECMA
              IF (IDECMA.EQ.IMA) GOTO 180
              IF (NUMALO.LE.ZI(JSUMA+ISUCO).AND.
     &            NUMALO.GT.ZI(JSUMA+ISUCO-1)) THEN
                IF (ZI(JPROC-1+IDECMA).EQ.0) THEN
                 INC = INC + 1
                 IF (INC.GT.LONG) THEN
                   LONG = 2*LONG
                   CALL JUVECA(MAMACO,LONG)
                   CALL JEVEUO(MAMACO,'E',JMAMA)
                 END IF
                 ZI(JMAMA+INC-1) = NUMALO
                 ZI(JTRAV2+JDECMA+IMA-1) = ZI(JTRAV2+JDECMA+IMA-1) + 1
                 ZI(JPROC-1+IDECMA)=1
                ENDIF
              ENDIF
  180       CONTINUE
  170     CONTINUE


          ZI(JPOIN+JDECMA+IMA) = ZI(JPOIN+JDECMA+IMA-1) +
     &                           ZI(JTRAV2+JDECMA+IMA-1)
          CALL JEDETR('&&TABLCO.PROCH')
  160   CONTINUE
  150 CONTINUE
C ======================================================================
C       VERIFICATION DE LA LONGUEUR DU TABLEAU MAMACO ET STOCKAGE
C ======================================================================
      NMAMA = ZI(JPOIN+NMACO)
      IF (NMAMA.GT.LONG) THEN
        CALL ASSERT(.FALSE.)
      END IF
      CALL JEECRA(MAMACO,'LONUTI',NMAMA,K8BID)

C ======================================================================
C    CONSTRUCTION DU TABLEAU NOZOCO DONNANT POUR CHAQUE NOEUD DE CONTACT
C             LE NUMERO DE LA ZONE A LAQUELLE IL APPARTIENT
C ======================================================================

      CALL WKVECT(NOZOCO,'G V I',NNOCO,JZOCO)

      DO 210 ISUCO = 1,NSUCO
        DO 220 IZONE = 1,NZOCO
          I1 = ZI(JZONE+IZONE-1) + 1
          I2 = ZI(JZONE+IZONE)
          IF ((ISUCO.GE.I1) .AND. (ISUCO.LE.I2)) THEN
            IZOCO = IZONE
            GO TO 230
          END IF
  220   CONTINUE
  230   CONTINUE

        NBNO = ZI(JSUNO+ISUCO) - ZI(JSUNO+ISUCO-1)
        JDECNO = ZI(JSUNO+ISUCO-1)
        DO 240 INO = 1,NBNO
          ZI(JZOCO+JDECNO+INO-1) = IZOCO
  240   CONTINUE

  210 CONTINUE

      CALL JEDETR('&&TABLCO.TRAV')
      CALL JEDETR('&&TABLCO.TRAV2')
      CALL JEDETR(CONINV)
C ======================================================================
      CALL JEDEMA()
      END
