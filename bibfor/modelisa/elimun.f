      SUBROUTINE ELIMUN(CHAR,NOMA,NOMO,MOTFAC,NBOCC,
     &                  DIMECU,NBGDCU,CMPGCU,
     &                  NOPONO,NOLINO,
     &                  LISNOE,POINOE,
     &                  NBTOT,NBNOE)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 08/02/2008   AUTEUR MACOCCO K.MACOCCO 
C ======================================================================
C COPYRIGHT (C) 1991 - 2006  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C
      IMPLICIT      NONE
      CHARACTER*8   CHAR
      CHARACTER*8   NOMA
      CHARACTER*8   NOMO
      CHARACTER*16  MOTFAC
      INTEGER       NBOCC
      CHARACTER*24  DIMECU
      CHARACTER*24  NBGDCU
      CHARACTER*24  CMPGCU
      CHARACTER*24  NOPONO
      CHARACTER*24  NOLINO
      CHARACTER*24  LISNOE
      CHARACTER*24  POINOE
      INTEGER       NBTOT
      INTEGER       NBNOE
C
C ----------------------------------------------------------------------
C ROUTINE APPELEE PAR : CALIUN
C ----------------------------------------------------------------------
C
C TRAITEMENT DU MOT-FACTEUR 'LIAISON_UNILATERALE' DANS AFFE_CHAR_MECA.
C NETTOYAGE DE LA LISTE DES NOEUDS SUR LESQUELS S'APPLIQUENT LES
C CONDITIONS UNILATERALES:
C  - SUPPRESSION DES DOUBLONS ENTRE MAILLE, GROUP_MA, NOEUD,GROUP_NO
C  - SUPPRESSION DES NOEUDS DEFINIS DANS SANS_NOEUD ET SANS_GROUP_NO
C    -> CES DEUX TYPES DE NOEUDS VONT ETRE SUPPRIMES
C
C IN  CHAR   : NOM DU CONCEPT CHARGE
C IN  NOMA   : NOM DU MAILLAGE
C IN  NOMO   : NOM DU MODELE
C IN  MOTFAC : MOT-CLEF FACTEUR POUR LIAISON UNILATERALE
C IN  NBOCC  : NOMBRE D'OCCURRENCES MOT-CLEF FACTEUR
C IN  DIMECU : NOM JEVEUX DE LA SD INFOS GENERALES
C IN  NBGDCU : NOM JEVEUX DE LA SD INFOS POINTEURS GRANDEURS
C IN  CMPGCU : NOM JEVEUX DE LA SD CONTENANT LES GRANDEURS DU MEMBRE
C              DE GAUCHE
C IN  NOPONO : NOM DE L'OBJET CONTENANT LE VECTEUR D'INDIRECTION
C IN  NOLINO : NOM DE L'OBJET CONTENANT LA LISTE DES NOEUDS
C IN  POINOE : NOM DE L'OBJET CONTENANT LE VECTEUR D'INDIRECTION
C               DES NOEUDS APRES NETTOYAGE
C IN  LISNOE : NOM DE L'OBJET CONTENANT LES NOEUDS APRES NETTOYAGE
C IN  NBNOE  : NOMBRE DE NOEUDS DANS LA LISTE RESULTANTE
C                VAUT NBNOE = NBTOT-NBSUP
C IN  NBTOT  : NOMBRE DE TOTAL DE NOEUDS POUR TOUTES LES OCCURRENCES
C                AVANT NETTOYAGE
C OUT NBNOE  : NOMBRE DE TOTAL DE NOEUDS POUR TOUTES LES OCCURRENCES
C                APRES NETTOYAGE DES DOUBLONS
C
C -------------- DEBUT DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C ---------------- FIN DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER       JDEBUT,JUELIM,JDECAL,JDECAT
      INTEGER       NBELIM
      CHARACTER*1   K1BID
      CHARACTER*8   K8BID,CMP
      INTEGER       I,J,ICMP,IOCC,NUMNO1,NUMNO2
      INTEGER       NBNO,NBSUP,NB,NBCMP
      INTEGER       N1,N2,N3
      INTEGER       JNL,JNP,JPOI,JNOE
      CHARACTER*24  NELIM
      INTEGER       JELIM
      INTEGER       JNBGD,JCMPG,JDIM
      INTEGER       EXIST
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
C
      CALL JEVEUO(NOLINO,'E',JNL)
      CALL JEVEUO(NOPONO,'L',JNP)
      CALL JEVEUO(NBGDCU,'L',JNBGD)
      CALL JEVEUO(CMPGCU,'L',JCMPG)
      CALL JEVEUO(DIMECU,'E',JDIM)
      CALL JEVEUO(POINOE,'E',JPOI)
C
      NBNOE = 0
      NBSUP = 0
      K8BID = '        '
      NELIM = '&&ELIMUN.ELIM'
C
      ZI(JPOI) = 1
C
      DO 1000 IOCC=1,NBOCC
C
C --- VECTEUR CONTENANT LES NOEUDS POUR LA IOCC OCCURRENCE
C
        NBNO   = ZI(JNP+IOCC) - ZI(JNP+IOCC-1)
        JDEBUT = ZI(JNP+IOCC-1)
        N1     = 0
        N2     = 0
        N3     = 0
C
C --- ELIMINATION DES PURS DOUBLONS
C
        DO 10 I=1,NBNO
          NUMNO1 = ZI(JNL-2+JDEBUT+I)
          IF (NUMNO1.NE.0) THEN
            DO 11 J=I+1,NBNO
              NUMNO2 = ZI(JNL-2+JDEBUT+J)
              IF ((NUMNO1.EQ.NUMNO2).AND.(NUMNO2.NE.0)) THEN
                ZI(JNL-2+JDEBUT+J) = 0
                N1 = N1 + 1
              ENDIF
   11       CONTINUE
          ENDIF
   10   CONTINUE
C
C --- RECUPERATION INFOS SANS_NOEUD, SANS_GROUP_NO
C
        CALL PALINO(NOMA,MOTFAC,'SANS_GROUP_NO','SANS_NOEUD',IOCC,
     &              NELIM)
        CALL JEVEUO(NELIM,'L',JUELIM)
        NBELIM = ZI(JUELIM)
C
C --- ELIMINATION DES SANS_GROUP_NO, SANS_NOEUD
C
        CALL JELIRA(NELIM,'LONMAX',NBELIM,K1BID)
        CALL JEVEUO(NELIM,'E',JELIM)
        DO 20 I=1,NBELIM
          NUMNO1 = ZI(JUELIM-1+I)
          IF (NUMNO1.NE.0) THEN
            DO 21 J=1,NBNO
              NUMNO2 = ZI(JNL-2+JDEBUT+J)
              IF ((NUMNO1.EQ.NUMNO2).AND.(NUMNO2.NE.0)) THEN
                ZI(JNL-2+JDEBUT+J) = 0
                N2 = N2 + 1
              ENDIF
   21       CONTINUE
          ENDIF
   20   CONTINUE
C
C --- ELIMINATION DES NOEUDS NE COMPORTANT AUCUNE DES GRANDEURS
C
        NBCMP  = ZI(JDIM+2*(IOCC-1)+5)
        JDECAT = ZI(JNBGD+IOCC-1)
        DO 30 I=1,NBNO
          NUMNO1 = ZI(JNL-2+JDEBUT+I)
          IF (NUMNO1.NE.0) THEN
            NB = 0
            DO 31 ICMP=1,NBCMP
              CMP = ZK8(JCMPG-1+JDECAT+ICMP-1)
              CALL EXISCP(CMP,CHAR,NOMO,
     &                    1,'NUM',K8BID,NUMNO1,
     &                    EXIST)
              IF (EXIST.EQ.0) THEN
                NB = NB + 1
              ENDIF
   31       CONTINUE
            IF (NB.EQ.NBCMP) THEN
              ZI(JNL-2+JDEBUT+I) = 0
              N3 = N3 + 1
            ENDIF
          ENDIF
   30   CONTINUE
C
C --- NOMBRE DE NOEUDS A SUPPRIMER
C
        ZI(JDIM+2*(IOCC-1)+4) = NBNO - (N1 + N2 + N3)
        NBSUP                 = NBSUP + N1 + N2 + N3
C
C --- MAJ VECTEUR POINTEUR INDIRECT (POINOE)
C
        ZI(JPOI+IOCC) = ZI(JPOI+IOCC-1) + NBNO - (N1 + N2 + N3)
        IF (ZI(JDIM+2*(IOCC-1)+4).LE.0) THEN
          CALL U2MESS('F','MODELISA4_48')
        ENDIF
 1000 CONTINUE
C
C --- CREATION DU VECTEUR RESULTANT
C
      NBNOE = NBTOT - NBSUP
      CALL WKVECT(LISNOE,'V V I',NBNOE,JNOE)
      ZI(JDIM+2) = NBNOE
C
C --- ELIMINATION EFFECTIVE DES NOEUDS
C
      JDECAL = 0
      DO 50 IOCC = 1,NBOCC
        NBNO   = ZI(JNP+IOCC) - ZI(JNP+IOCC-1)
        JDEBUT = ZI(JNP+IOCC-1)
        DO 51 J=1,NBNO
          NUMNO1 = ZI(JNL-2+JDEBUT+J)
          IF (NUMNO1.NE.0) THEN
            CALL ASSERT (JDECAL.LE.NBNOE)
            ZI(JNOE+JDECAL) = NUMNO1
            JDECAL = JDECAL +1
          ENDIF
   51   CONTINUE
   50 CONTINUE
C
      CALL JEDETR(NELIM)
C
      CALL JEDEMA()

      END
