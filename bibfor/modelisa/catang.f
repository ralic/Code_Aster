      SUBROUTINE CATANG (NOMA,NBMA,NUMAIL,NBNO,NUNOEU,TANG)
      IMPLICIT NONE
      INCLUDE 'jeveux.h'
      INTEGER     NBMA,NUMAIL(NBMA),NBNO,NUNOEU(NBNO)
      REAL*8      TANG(3*NBNO)
      CHARACTER*8 NOMA
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 13/06/2012   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C
C     BUT: CALCULER LES VECTEURS TANGENTS AUX NOEUDS DES ARETES DES
C          ELEMENTS 3D (SEG2 OU SEG3)
C
C ARGUMENTS D'ENTREE:
C      NOMA    : NOM DU MAILLAGE
C      NBMA    : NOMBRE DE MAILLES
C      NUMAIL  : TABLEAU DES NUMEROS DES MAILLES (SEG2,SEG3)
C      NBNO    : NOMBRE DE NOEUDS
C      NUNOEU  : TABLEAU DES NUMEROS DE NOEUDS A PRENDRE EN COMPTE
C
C ARGUMENTS D'ENTREE/SORTIE:
C      TANG    : TABLEAU DES VECTEURS TANGENTS AUX NOEUDS
C
C ROUTINE APPELEE: CAAREI
C
      INTEGER I,IACNX1,ILCNX1,JCO,JTANMA,J,INO,NBNOMA,JCOOR,JNOEMA
      INTEGER JTANNO,IRET,K,INO1,INO2,INO3,INDIIS,JTYP,I1
      REAL*8  VALE1(3),VALE2(3),VALE3(3),VALE(3),VALU(3),VALV(3),NORM
      CHARACTER*8 NTYP,K8B
C
      CALL JEMARQ()
C
      CALL DISMOI('F','NB_NO_MAILLA',NOMA,'MAILLAGE',NBNOMA,K8B,IRET)
      CALL JEVEUO(NOMA//'.TYPMAIL','L',JTYP)
      CALL JEVEUO(NOMA//'.COORDO    .VALE','L',JCOOR)
      CALL JEVEUO(NOMA//'.CONNEX','L',IACNX1)
      CALL JEVEUO(JEXATR(NOMA//'.CONNEX','LONCUM'),'L',ILCNX1)
C
      CALL WKVECT('&&CATANG.TANG_MAIL','V V R',3*3*NBMA,JTANMA)
      CALL WKVECT('&&CATANG.NOEU_MAIL','V V I',3*NBMA,JNOEMA)
      CALL WKVECT('&&CATANG.TANG_NOEU','V V R',3*NBNOMA,JTANNO)
C
C --- 1.ON CONSTRUIT LES VECTEURS TANGENTS AUX NOEUDS PAR ELEMENT
C     (LES ELEMENTS SONT CEUX SPECIFIES PAR L'UTILISATEUR)
C     ON LES STOCKE DANS TANMA=ZR(JTANMA)
C             <-- NOEUD 1--> <-- NOEUD 2-->  <-- NOEUD 3-->
C     TANMA=( V1X1,V1Y1,V1Z1,V1X2,V1Y2,V1Z2,V1X3,V1Y3,V1Z3, <= MAILLE 1
C             V2X1,V2Y1,V2Z1,V2X2,V2Y2,V2Z2,V2X3,V2Y3,V2Z3, <= MAILLE 2
C             ............................................
C             VNX1,VNY1,VNZ1,VNX2,VNY2,VNZ2,VNX3,VNY3,VNZ3) <= MAILLE N
C     REMARQUE : LE NOMBRE MAX DE NOEUDS PAR MAILLE EST 3.
C     DANS LE CAS SEG2, LES VALEURS TANGENTIELLES DU NOEUD 3 NE SONT PAS
C     PRISE EN COMPTE, ON INITIALISE AVEC LA VALEUR 0.D0
C
      DO 10 I=1,NBMA
C
         CALL JENUNO(JEXNUM('&CATA.TM.NOMTM',ZI(JTYP+NUMAIL(I)-1)),NTYP)
         IF(NTYP(1:3).NE.'SEG')CALL ASSERT(.FALSE.)
C
         JCO=IACNX1-1+ZI(ILCNX1+NUMAIL(I)-1)
         INO1=ZI(JCO)
         INO2=ZI(JCO+1)
         DO 20 J=1,3
            VALE1(J)=ZR(JCOOR+3*(INO1-1)+J-1)
            VALE2(J)=ZR(JCOOR+3*(INO2-1)+J-1)
 20      CONTINUE
C
         IF(NTYP(1:4).EQ.'SEG2')THEN
C
            DO 25 J=1,3
              VALE(J)=VALE2(J)-VALE1(J)
 25         CONTINUE
            CALL NORMEV(VALE,NORM)
C
C            -- PREMIER NOEUD DE LA MAILLE J
             ZI(JNOEMA+3*(I-1))=INO1
             ZR(JTANMA+9*(I-1))  =VALE(1)
             ZR(JTANMA+9*(I-1)+1)=VALE(2)
             ZR(JTANMA+9*(I-1)+2)=VALE(3)
C            -- SECOND NOEUD DE LA MAILLE J
             ZI(JNOEMA+3*(I-1)+1)=INO2
             ZR(JTANMA+9*(I-1)+3)=VALE(1)
             ZR(JTANMA+9*(I-1)+4)=VALE(2)
             ZR(JTANMA+9*(I-1)+5)=VALE(3)
C            -- NOEUD MILIEU DE LA MAILLE J
             ZI(JNOEMA+3*(I-1)+2)=0
             ZR(JTANMA+9*(I-1)+6)=0.D0
             ZR(JTANMA+9*(I-1)+7)=0.D0
             ZR(JTANMA+9*(I-1)+8)=0.D0
C
         ELSEIF(NTYP(1:4).EQ.'SEG3')THEN
C
             INO3=ZI(JCO+2)
             DO 30 J=1,3
              VALE3(J)=ZR(JCOOR+3*(INO3-1)+J-1)
              VALU(J) =VALE3(J)-VALE1(J)
              VALV(J) =VALE2(J)-VALE3(J)
 30          CONTINUE
             CALL NORMEV(VALU,NORM)
             CALL NORMEV(VALV,NORM)
             DO 35 J=1,3
               VALE(J)=VALU(J)+VALV(J)
 35          CONTINUE
             CALL NORMEV(VALE,NORM)
C
C            -- PREMIER NOEUD DE LA MAILLE J
             ZI(JNOEMA+3*(I-1))=INO1
             ZR(JTANMA+9*(I-1))  =VALU(1)
             ZR(JTANMA+9*(I-1)+1)=VALU(2)
             ZR(JTANMA+9*(I-1)+2)=VALU(3)
C            -- SECOND NOEUD DE LA MAILLE J
             ZI(JNOEMA+3*(I-1)+1)=INO2
             ZR(JTANMA+9*(I-1)+3)=VALV(1)
             ZR(JTANMA+9*(I-1)+4)=VALV(2)
             ZR(JTANMA+9*(I-1)+5)=VALV(3)
C            -- NOEUD MILIEU DE LA MAILLE J
             ZI(JNOEMA+3*(I-1)+2)=INO3
             ZR(JTANMA+9*(I-1)+6)=VALE(1)
             ZR(JTANMA+9*(I-1)+7)=VALE(2)
             ZR(JTANMA+9*(I-1)+8)=VALE(3)

         ENDIF
 10   CONTINUE

C --- 2.ON CONSTRUIT LES VECTEURS TANGENTS AUX NOEUDS
C     ON LES STOCKE DANS TNNO= ZR(JTANNO) DIMENSIONNE
C     AU NOMBRE DE NOEUDS DU MAILLAGE.
C     TANNO=( V1X,V1Y,V1Z, <= NOEUD 1
C             V2X,V2Y,V2Z, <= NOEUD 2
C             ............,
C             VNX,VNY,VNZ )<= NOEUD N
C     REMARQUE : ViX=ViY=ViZ=0 SI LE NOEUD i NE FAIT PAS
C     PARTIE DES MAILLES FOURNIES PAR L'UTILISATEUR
C
      DO 40 J=1,3*NBNOMA
        ZR(JTANNO+J-1)=0.D0
 40   CONTINUE
      DO 50 J=1,NBMA
        DO 51 I=1,3
          INO=ZI(JNOEMA+3*(J-1)+I-1)
          IF(INO.GT.0)THEN
            DO 52 K=1,3
               ZR(JTANNO+3*(INO-1)+K-1)=ZR(JTANNO+3*(INO-1)+K-1)+
     &                                  ZR(JTANMA+9*(J-1)+3*(I-1)+K-1)
 52         CONTINUE
          ENDIF
 51     CONTINUE
 50   CONTINUE

C --- 3.ON FILTRE POUR NE CONSERVER QUE LES VECTEURS TANGENTS AUX NOEUDS
C     DEMANDES PAR L'UTILISATEUR (PRISE EN COMPTE DE SANS_XXXX)
C
      DO 55 J=1,3*NBNO
         TANG(J)=0.D0
 55   CONTINUE
      DO 60 J=1,NBMA
        DO 61 I=1,3
          INO=ZI(JNOEMA+3*(J-1)+I-1)
          I1= INDIIS(NUNOEU,INO,1,NBNO)
          IF(I1.GT.0)THEN
             VALE(1)=ZR(JTANNO+3*(INO-1))
             VALE(2)=ZR(JTANNO+3*(INO-1)+1)
             VALE(3)=ZR(JTANNO+3*(INO-1)+2)
             CALL NORMEV(VALE,NORM)
             DO 62 K=1,3
               TANG(3*(I1-1)+K)=VALE(K)
 62          CONTINUE
          ENDIF
 61     CONTINUE
 60   CONTINUE
C
      CALL JEDETR('&&CATANG.TANG_MAIL')
      CALL JEDETR('&&CATANG.NOEU_MAIL')
      CALL JEDETR('&&CATANG.TANG_NOEU')
C
      CALL JEDEMA()
      END
