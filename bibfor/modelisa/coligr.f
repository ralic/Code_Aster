      SUBROUTINE COLIGR (BASEZ, LIGR1Z, LIGR2Z, LIGOUZ)
      IMPLICIT REAL*8 (A-H,O-Z)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 25/03/2002   AUTEUR CIBHHGB G.BERTRAND 
C ======================================================================
C COPYRIGHT (C) 1991 - 2002  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C                                                                       
C                                                                       
C ======================================================================
C ------------------------------------------------------------
C     BUT : CONCATENATION DES LIGRELS LIGRE1 ET LIGRE2
C           DANS LE LIGREL LIGOUT QUI EST CREE SUR LA BASE BASEZ
C
C
C  ARGUMENT       E/S    TYPE          ROLE
C
C  BASEZ          IN      K1      NOM DE LA BASE
C  LIGRE1         IN      K19     NOM DU PREMIER LIGREL A CONCATENER 
C  LIGRE2         IN      K19     NOM DU SECOND LIGREL A CONCATENER 
C  LIGOUT         IN      K19     NOM DU LIGREL  = LIGRE1 + LIGRE2
C                 JXVAR        
C-------------------------------------------------------------
C
C ====================== DEBUT DES DECLARATIONS ========================
C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX -----------
      CHARACTER*32       JEXNUM , JEXNOM , JEXR8 , JEXATR
      INTEGER            ZI
      COMMON  / IVARJE / ZI(1)
      REAL*8             ZR
      COMMON  / RVARJE / ZR(1)
      COMPLEX*16         ZC
      COMMON  / CVARJE / ZC(1)
      LOGICAL            ZL
      COMMON  / LVARJE / ZL(1)
      CHARACTER*8        ZK8
      CHARACTER*16                ZK16
      CHARACTER*24                          ZK24
      CHARACTER*32                                    ZK32
      CHARACTER*80                                              ZK80
      COMMON  / KVARJE /ZK8(1),ZK16(1),ZK24(1),ZK32(1), ZK80(1)
C --------- FIN  DECLARATIONS  NORMALISEES  JEVEUX -----------
C
C ----- ARGUMENTS
      CHARACTER*(*) BASEZ, LIGR1Z, LIGR2Z, LIGOUZ
C ----- VARIABLES LOCALES -------------------------------
      CHARACTER*1  BASE
      CHARACTER*8  K8BID
      CHARACTER*19 LIGRE1, LIGRE2, LIGOUT
      CHARACTER*19 LIGR1W, LIGR2W, LIGREL
C ====================== DEBUT DU CODE EXECUTABLE ======================
C
      CALL JEMARQ()
C
C --- INITIALISATIONS :
C     ---------------
      BASE   = BASEZ
      LIGRE1 = LIGR1Z
      LIGRE2 = LIGR2Z
      LIGOUT = LIGOUZ
C
      LIGR1W = '&&COLIGR.LIGRE1.WORK'
      LIGR2W = '&&COLIGR.LIGRE2.WORK'
      LIGREL = 'LIGREL'
C
      CALL COPISD(LIGREL,'V',LIGRE1,LIGR1W)
      CALL COPISD(LIGREL,'V',LIGRE2,LIGR2W)
C
      CALL DETRSD(LIGREL,LIGRE1)
      CALL DETRSD(LIGREL,LIGRE2)
C
C --- ON VERIFIE QUE LE MAILLAGE EST LE MEME POUR LES 2 LIGRELS 
C --- A CONCATENER :
C     ------------
      CALL JEVEUO(LIGR1W//'.NOMA','L',IDNOM1)
      CALL JEVEUO(LIGR2W//'.NOMA','L',IDNOM2)
C
      IF (ZK8(IDNOM1).NE.ZK8(IDNOM2)) THEN
        CALL UTMESS('F','COLIGR','LES LIGRELS A CONCATENER NE '//
     +              'REFERENCENT PAS LE MEME MAILLAGE.')
      ENDIF
C
C --- AFFECTATION DU NOM DU MAILLAGE AU LIGREL RESULTANT :
C     --------------------------------------------------
      CALL WKVECT(LIGOUT//'.NOMA',BASE//' V K8',1,IDNOMA)
      ZK8(IDNOMA) = ZK8(IDNOM1)
C
C --- AFFECTATION DU NOMBRE DE NOEUDS AU LIGREL RESULTANT :
C     ---------------------------------------------------
      CALL JEVEUO(LIGR1W//'.NBNO','L',IDNBN1)
      CALL JEVEUO(LIGR2W//'.NBNO','L',IDNBN2)
      CALL WKVECT(LIGOUT//'.NBNO',BASE//' V I',1,IDNBNO)
      ZI(IDNBNO) = ZI(IDNBN1) + ZI(IDNBN2)
C
C --- AFFECTATION DU .LGNS AU LIGREL RESULTANT :
C     ----------------------------------------
      CALL JEEXIN(LIGR1W//'.LGNS',IRET1)
      CALL JEEXIN(LIGR2W//'.LGNS',IRET2)
      IF (IRET1.EQ.0.AND.IRET2.NE.0) THEN
        CALL JEVEUO(LIGR2W//'.LGNS','L',IDLGN2)
        CALL WKVECT(LIGOUT//'.LGNS',BASE//' V I',ZI(IDNBN2),IDLGOU)
        DO 10 I = 1, ZI(IDNBN2)
          ZI(IDLGOU+I-1) = ZI(IDLGN2+I-1)
  10    CONTINUE
      ELSEIF (IRET1.NE.0.AND.IRET2.EQ.0) THEN
        CALL JEVEUO(LIGR1W//'.LGNS','L',IDLGN1)
        CALL WKVECT(LIGOUT//'.LGNS',BASE//' V I',ZI(IDNBN1),IDLGOU)
        DO 20 I = 1, ZI(IDNBN1)
          ZI(IDLGOU+I-1) = ZI(IDLGN1+I-1)
  20    CONTINUE
      ELSEIF (IRET1.NE.0.AND.IRET2.NE.0) THEN
        CALL JEVEUO(LIGR1W//'.LGNS','L',IDLGN1)
        CALL JEVEUO(LIGR2W//'.LGNS','L',IDLGN2)
        CALL WKVECT(LIGOUT//'.LGNS',BASE//' V I',ZI(IDNBN1)+ZI(IDNBN2),
     +              IDLGOU)
        DO 30 I = 1, ZI(IDNBN1)
          ZI(IDLGOU+I-1) = ZI(IDLGN1+I-1)
  30    CONTINUE
        DO 40 I = 1, ZI(IDNBN2)
          ZI(IDLGOU+ZI(IDNBN1)+I-1) = ZI(IDLGN2+I-1)
  40    CONTINUE
      ENDIF
C
C --- CONCATENATION DES .LIEL :
C     -----------------------
      CALL JELIRA(LIGR1W//'.LIEL','NUTIOC',NBOBJ1,K8BID)
      CALL JELIRA(LIGR2W//'.LIEL','NUTIOC',NBOBJ2,K8BID)
C
      CALL JELIRA(LIGR1W//'.LIEL','LONT',LONLI1,K8BID)
      CALL JELIRA(LIGR2W//'.LIEL','LONT',LONLI2,K8BID)
C
      CALL JECREC(LIGOUT//'.LIEL',BASE//' V I','NU','CONTIG',
     +            'VARIABLE',NBOBJ1+NBOBJ2)
      CALL JEECRA(LIGOUT//'.LIEL','LONT',LONLI1+LONLI2,K8BID)
      DO 50 I = 1, NBOBJ1
          CALL JELIRA(JEXNUM(LIGR1W//'.LIEL',I),'LONMAX',LOBCOL,K8BID)
          CALL JECROC(JEXNUM(LIGOUT//'.LIEL',I))
          CALL JEECRA(JEXNUM(LIGOUT//'.LIEL',I),'LONMAX',LOBCOL,K8BID)
          CALL JEVEUO(JEXNUM(LIGR1W//'.LIEL',I),'L',IDLIG1)
          CALL JEVEUO(JEXNUM(LIGOUT//'.LIEL',I),'E',IDLIGR)
C
          DO 60 J = 1, LOBCOL
            ZI(IDLIGR+J-1) = ZI(IDLIG1+J-1)
  60      CONTINUE
C
  50  CONTINUE
C
      DO 70 I = 1, NBOBJ2
          CALL JELIRA(JEXNUM(LIGR2W//'.LIEL',I),'LONMAX',LOBCOL,K8BID)
          CALL JECROC(JEXNUM(LIGOUT//'.LIEL',I+NBOBJ1))
          CALL JEECRA(JEXNUM(LIGOUT//'.LIEL',I+NBOBJ1),'LONMAX',
     +                LOBCOL,K8BID)
          CALL JEVEUO(JEXNUM(LIGR2W//'.LIEL',I),'L',IDLIG2)
          CALL JEVEUO(JEXNUM(LIGOUT//'.LIEL',I+NBOBJ1),'E',IDLIGR)
C
          DO 80 J = 1, LOBCOL
            ZI(IDLIGR+J-1) = ZI(IDLIG2+J-1)
  80      CONTINUE
C
  70  CONTINUE
C
C --- CONCATENATION DES .NEMA :
C     -----------------------
      NBOBJ1 = 0
      NBOBJ2 = 0
      LONLI1 = 0
      LONLI2 = 0
C
      CALL JEEXIN(LIGR1W//'.NEMA',IRET3)
      CALL JEEXIN(LIGR2W//'.NEMA',IRET4)
C
      IF (IRET3.NE.0) THEN
        CALL JELIRA(LIGR1W//'.NEMA','NUTIOC',NBOBJ1,K8BID)
        CALL JELIRA(LIGR1W//'.NEMA','LONT',LONLI1,K8BID)
        CALL JEVEUO(LIGR1W//'.NEMA','L',IDLIG1)
      ENDIF
      IF (IRET4.NE.0) THEN
        CALL JELIRA(LIGR2W//'.NEMA','NUTIOC',NBOBJ2,K8BID)
        CALL JELIRA(LIGR2W//'.NEMA','LONT',LONLI2,K8BID)
        CALL JEVEUO(LIGR2W//'.NEMA','L',IDLIG2)
      ENDIF
C
      IF (IRET3+IRET4.NE.0) THEN
        CALL JECREC(LIGOUT//'.NEMA',BASE//' V I','NU','CONTIG',
     +              'VARIABLE',NBOBJ1+NBOBJ2)
        CALL JEECRA(LIGOUT//'.NEMA','LONT',LONLI1+LONLI2,K8BID)
      DO 90 I = 1, NBOBJ1
          CALL JECROC(JEXNUM(LIGOUT//'.NEMA',I))
          CALL JELIRA(JEXNUM(LIGR1W//'.NEMA',I),'LONMAX',LOBCOL,K8BID)
          CALL JEECRA(JEXNUM(LIGOUT//'.NEMA',I),'LONMAX',LOBCOL,K8BID)
  90  CONTINUE
      DO 100 I = 1, NBOBJ2
          CALL JECROC(JEXNUM(LIGOUT//'.NEMA',I+NBOBJ1))
          CALL JELIRA(JEXNUM(LIGR2W//'.NEMA',I),'LONMAX',LOBCOL,K8BID)
          CALL JEECRA(JEXNUM(LIGOUT//'.NEMA',I+NBOBJ1),
     +                'LONMAX',LOBCOL,K8BID)
 100  CONTINUE
        CALL JEVEUO(LIGOUT//'.NEMA','E',IDLIGR)
      ENDIF
C
      IF (IRET3.NE.0) THEN
        DO 110 I = 1, LONLI1
          ZI(IDLIGR+I-1) = ZI(IDLIG1+I-1)
 110    CONTINUE
      ENDIF
      IF (IRET4.NE.0) THEN
        DO 120 I = 1, LONLI2
          ZI(IDLIGR+LONLI1+I-1) = ZI(IDLIG2+I-1)
 120    CONTINUE
      ENDIF
C
C --- ADAPTATION DE LA TAILLE DES GRELS :
C     ---------------------------------
      CALL ADALIG(LIGOUT)
C
C --- CREATION ET AFFECTATION DE L'OBJET LIGOUT.REPE QUI EST TEL QUE
C ---  LIGOUT.REPE(2*(I-1)+1) = NUMERO DU GREL CONTENANT LA MAILLE I
C ---  LIGOUT.REPE(2*(I-1)+2) = NUMERO LOCAL DE LA MAILLE DANS LE GREL:
C      ---------------------------------------------------------------
      CALL CORMGI(BASE,LIGOUT)
C
C --- CREATION DE L'OBJET LIGOUT.PRNM QUI EST LE NOM (K24) DU
C --- VECTEUR DES ENTIERS CODES DECRIVANT LA NATURE DES DDLS DES
C --- NOEUDS PHYSIQUES
C --- ET CREATION DE L'OBJET LIGOUT.PRNS QUI EST LE NOM (K24) DU
C --- VECTEUR DES DDLS PORTES PAR LES NOEUDS TARDIFS DU LIGOUT :
C     --------------------------------------------------------
      CALL CREPRN(LIGOUT,' ',BASE,LIGOUT//'.PRNM',LIGOUT//'.PRNS')
C
      CALL DETRSD(LIGREL,LIGR1W)
      CALL DETRSD(LIGREL,LIGR2W)
C
      CALL JEDEMA()
C
      END
