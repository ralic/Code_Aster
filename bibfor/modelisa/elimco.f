      SUBROUTINE ELIMCO(CHAR  ,NOMA  ,NZOCO ,NSUCO ,NMACO ,
     &                  NNOCO ,NNOQUA)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 08/02/2008   AUTEUR MACOCCO K.MACOCCO 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT     NONE
      CHARACTER*8  CHAR
      CHARACTER*8  NOMA
      INTEGER      NZOCO
      INTEGER      NSUCO
      INTEGER      NMACO
      INTEGER      NNOCO
      INTEGER      NNOQUA
C      
C ----------------------------------------------------------------------
C
C ROUTINE CONTACT (METHODES MAILLEES - LECTURE DONNEES)
C
C ELIMINATION AU SEIN DE CHAQUE SURFACE DE CONTACT POTENTIELLE DES
C NOEUDS ET MAILLES REDONDANTS. MODIFICATION DES POINTEURS ASSOCIES.
C      
C ----------------------------------------------------------------------
C
C 
C IN  CHAR   : NOM UTILISATEUR DU CONCEPT DE CHARGE
C IN  NOMA   : NOM DU MAILLAGE
C IN  NZOCO  : NOMBRE DE ZONES DE CONTACT
C IN  NSUCO  : NOMBRE TOTAL DE SURFACES DE CONTACT
C IN  NMACO  : NOMBRE TOTAL DE MAILLES DES SURFACES
C I/O NNOCO  : NOMBRE TOTAL DE NOEUDS DES SURFACES
C I/O NNOQUA : NOMBRE TOTAL DE NOEUDS DES SURFACES
C
C -------------- DEBUT DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C ---------------- FIN DECLARATIONS NORMALISEES JEVEUX -----------------
C
      CHARACTER*24 PZONE,PSURMA,PSURNO,PNOQUA
      INTEGER      JZONE,JSUMA,JSUNO,JNOQUA
      CHARACTER*24 CONTMA,CONTNO,CONOQU
      INTEGER      JMACO,JNOCO,JNOQU
      INTEGER      JINDMA,JINDNO,JELIMA,JELINO,JELIQU,JINDQU
      INTEGER      JDECMA,JDECNO,JDECQU
      INTEGER      NNOCO0,NNOQU0,NMACO0
      INTEGER      ISUCO,I,II,K
      INTEGER      NBMA,NBNO,NBNOQU
      INTEGER      ELIMMA,ELIMNO,ELIMQU
      INTEGER      JMA,JNO,JQU
      INTEGER      IMA1,IMA2,INO1,INO2,INQ1,INQ2
      CHARACTER*8  K8BID
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
C
C --- CREATION DES VECTEURS DE TRAVAIL TEMPORAIRES
C
      CALL WKVECT('&&ELIMCO.INDIMA','V V I',NMACO ,JINDMA)
      CALL WKVECT('&&ELIMCO.INDINO','V V I',NNOCO ,JINDNO)
      CALL WKVECT('&&ELIMCO.ELIMMA','V V I',NSUCO+1,JELIMA)
      CALL WKVECT('&&ELIMCO.ELIMNO','V V I',NSUCO+1,JELINO)    
      IF (NNOQUA.NE.0) THEN
        CALL WKVECT('&&ELIMCO.ELIMQU','V V I',NSUCO+1,JELIQU)
        CALL WKVECT('&&ELIMCO.INDIQU','V V I',NNOQUA ,JINDQU)
      END IF
C 
C --- ACCES AUX STRUCTURES DE DONNEES DE CONTACT
C 
      PZONE  = CHAR(1:8)//'.CONTACT.PZONECO'
      PSURMA = CHAR(1:8)//'.CONTACT.PSUMACO'
      PSURNO = CHAR(1:8)//'.CONTACT.PSUNOCO'
      PNOQUA = CHAR(1:8)//'.CONTACT.PNOEUQU'
      CONTMA = CHAR(1:8)//'.CONTACT.MAILCO'
      CONTNO = CHAR(1:8)//'.CONTACT.NOEUCO'
      CONOQU = CHAR(1:8)//'.CONTACT.NOEUQU'
C
      CALL JEVEUO(PZONE ,'L',JZONE)
      CALL JEVEUO(PSURMA,'E',JSUMA)
      CALL JEVEUO(PSURNO,'E',JSUNO)
      CALL JEVEUO(PNOQUA,'E',JNOQUA)
      CALL JEVEUO(CONTMA,'E',JMACO)
      CALL JEVEUO(CONTNO,'E',JNOCO)
      IF (NNOQUA.NE.0) THEN
        CALL JEVEUO(CONOQU,'E',JNOQU)
      END IF
C
C --- INITIALISATIONS
C 
      ELIMMA = 0
      ELIMNO = 0
      ELIMQU = 0
      NMACO0 = NMACO
      NNOCO0 = NNOCO
      NNOQU0 = NNOQUA      
C
C --- REPERAGE DES MAILLES ET NOEUDS REDONDANTS POUR CHAQUE SURFACE
C 
      DO 110 ISUCO = 1,NSUCO
C
C --- TRAITEMENT DES MAILLES REDONDANTES
C
        ZI(JELIMA+ISUCO) = ZI(JELIMA+ISUCO-1)
        JDECMA = ZI(JSUMA+ISUCO-1)
        NBMA   = ZI(JSUMA+ISUCO) - ZI(JSUMA+ISUCO-1)
        DO 20 I = 1,NBMA
          IMA1 = ZI(JMACO+JDECMA+I-1)
          DO 10 II = 1,I - 1
            IMA2 = ZI(JMACO+JDECMA+II-1)
            IF (IMA1.EQ.IMA2) THEN
              ZI(JINDMA+JDECMA+I-1) = 1
              ZI(JELIMA+ISUCO)      = ZI(JELIMA+ISUCO) + 1
              ELIMMA                = ELIMMA + 1
              GO TO 20
            END IF
   10     CONTINUE
   20   CONTINUE
C
C --- TRAITEMENT DES NOEUDS REDONDANTS
C
        ZI(JELINO+ISUCO) = ZI(JELINO+ISUCO-1)
        JDECNO = ZI(JSUNO+ISUCO-1)
        NBNO   = ZI(JSUNO+ISUCO) - ZI(JSUNO+ISUCO-1)
        DO 40 I = 1,NBNO
          INO1 = ZI(JNOCO+JDECNO+I-1)
          DO 30 II = 1,I - 1
            INO2 = ZI(JNOCO+JDECNO+II-1)
            IF (INO1.EQ.INO2) THEN
              ZI(JINDNO+JDECNO+I-1) = 1
              ZI(JELINO+ISUCO)      = ZI(JELINO+ISUCO) + 1
              ELIMNO                = ELIMNO + 1
              GO TO 40
            END IF
   30     CONTINUE
   40   CONTINUE
C
C --- TRAITEMENT DES NOEUDS QUADRATIQUES REDONDANTS
C     
        IF (NNOQUA.NE.0) THEN
          ZI(JELIQU+ISUCO) = ZI(JELIQU+ISUCO-1)
          JDECQU = ZI(JNOQUA+ISUCO-1)
          NBNOQU = ZI(JNOQUA+ISUCO) - ZI(JNOQUA+ISUCO-1)
          DO 60 I = 1,NBNOQU
            INQ1 = ZI(JNOQU+3*JDECQU+3* (I-1))
            DO 50 II = 1,I - 1
              INQ2 = ZI(JNOQU+3*JDECQU+3* (II-1))
              IF (INQ1.EQ.INQ2) THEN
                ZI(JINDQU+JDECQU+I-1) = 1
                ZI(JELIQU+ISUCO)      = ZI(JELIQU+ISUCO) + 1
                ELIMQU                = ELIMQU + 1
                GO TO 60
              END IF
   50       CONTINUE
   60     CONTINUE
        END IF
  110 CONTINUE
C
C --- RECOPIE DES MAILLES ET NOEUDS NON ELIMINES DANS TABLEAU DE TRAVAIL
C 
      NMACO  = NMACO0 - ELIMMA
      NNOCO  = NNOCO0 - ELIMNO
      NNOQUA = NNOQU0 - ELIMQU
      CALL WKVECT('&&ELIMCO.TRAVMA','V V I',NMACO,JMA)
      CALL WKVECT('&&ELIMCO.TRAVNO','V V I',NNOCO,JNO)
      IF (NNOQUA.NE.0) THEN
        CALL WKVECT('&&ELIMCO.TRAVQU','V V I',3*NNOQUA,JQU)
      ENDIF        
C
C --- TRAITEMENT DES MAILLES
C
      K = 0
      DO 120 I = 1,NMACO0
        IF (ZI(JINDMA+I-1).EQ.0) THEN
          K = K + 1
          ZI(JMA+K-1) = ZI(JMACO+I-1)
        ENDIF
  120 CONTINUE
      CALL ASSERT (K.EQ.NMACO)
C
C --- TRAITEMENT DES NOEUDS
C
      K = 0
      DO 130 I = 1,NNOCO0
        IF (ZI(JINDNO+I-1).EQ.0) THEN
          K = K + 1
          ZI(JNO+K-1) = ZI(JNOCO+I-1)
        ENDIF
  130 CONTINUE
      CALL ASSERT (K.EQ.NNOCO)
C
C --- TRAITEMENT DES NOEUDS QUADRATIQUES
C
      IF (NNOQUA.NE.0) THEN
        K = 0
        DO 150 I = 1,NNOQU0
          IF (ZI(JINDQU+I-1).EQ.0) THEN
            K = K + 1
            DO 140 II = 1,3
              ZI(JQU+3* (K-1)+II-1) = ZI(JNOQU+3* (I-1)+II-1)
  140       CONTINUE
          ENDIF
  150   CONTINUE
        CALL ASSERT (K.EQ.NNOQUA)
      END IF
C 
C --- MODIFICATION DES POINTEURS PSURMA,PSURNO,PNOQUA
C 
      DO 160 ISUCO = 1,NSUCO
        ZI(JSUMA+ISUCO)  = ZI(JSUMA+ISUCO)  - ZI(JELIMA+ISUCO)
        ZI(JSUNO+ISUCO)  = ZI(JSUNO+ISUCO)  - ZI(JELINO+ISUCO)
        IF (NNOQUA.NE.0) THEN
          ZI(JNOQUA+ISUCO) = ZI(JNOQUA+ISUCO) - ZI(JELIQU+ISUCO)
        ENDIF  
  160 CONTINUE
C
C --- TRANSFERT DES VECTEURS DE TRAVAIL DANS CONTMA,CONTNO,CONOQU
C
      DO 170 I = 1,NMACO
        ZI(JMACO+I-1) = ZI(JMA+I-1)
  170 CONTINUE
C
      DO 190 I = 1,NNOCO
        ZI(JNOCO+I-1) = ZI(JNO+I-1)
  190 CONTINUE
C
      DO 220 I = 1,NNOQUA
        DO 210 K = 1,3
          ZI(JNOQU+3* (I-1)+K-1) = ZI(JQU+3* (I-1)+K-1)
  210   CONTINUE
  220 CONTINUE
C
C --- MAZ ET MODIFICATION DE L'ATTRIBUT LONUTI
C
      DO 180 I = NMACO + 1,NMACO0
        ZI(JMACO+I-1) = 0
  180 CONTINUE
      CALL JEECRA(CONTMA,'LONUTI',NMACO,K8BID)  
C      
      DO 200 I = NNOCO + 1,NNOCO0
        ZI(JNOCO+I-1) = 0
  200 CONTINUE
      CALL JEECRA(CONTNO,'LONUTI',NNOCO,K8BID)
C
      DO 240 I = NNOQUA + 1,NNOQU0
        DO 230 K = 1,3
          ZI(JNOQU+3* (I-1)+K-1) = 0
  230   CONTINUE
  240 CONTINUE
      IF (NNOQUA.EQ.0) THEN
        CALL JEDETR(CONOQU)
      ELSE
        CALL JEECRA(CONOQU,'LONUTI',3*NNOQUA,K8BID)
      END IF  
C
C --- DESTRUCTION DES VECTEURS DE TRAVAIL TEMPORAIRES
C
      CALL JEDETR('&&ELIMCO.INDIMA')
      CALL JEDETR('&&ELIMCO.INDINO')
      CALL JEDETR('&&ELIMCO.INDIQU')
      CALL JEDETR('&&ELIMCO.ELIMMA')
      CALL JEDETR('&&ELIMCO.ELIMNO')
      CALL JEDETR('&&ELIMCO.ELIMQU')
      CALL JEDETR('&&ELIMCO.TRAVMA')
      CALL JEDETR('&&ELIMCO.TRAVNO')
      CALL JEDETR('&&ELIMCO.TRAVQU')
C
      CALL JEDEMA()
      END
