      SUBROUTINE ARLFA1(DIME  ,ISMED ,
     &                  NOMARL,GRAPPA,TAMAFA,TAMAMA,
     &                  MAIL  ,LOMCUM,NOM1  ,NOM2  ,
     &                  TABCOL,
     &                  TYPMAI,NMA   ,LONCUM,
     &                  PANMA1,PANMA2,
     &                  BOSOM1,BOSOM2,BODIM1,BODIM2,
     &                  BOMNM1,BOMNM2,
     &                  NMAIN1,NMAIN2,NMASS ,NNOIN1,NNOIN2,
     &                  TYPEMA,NUMERO,NMAMA ,NFAM  ,ICPL   )
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 20/07/2009   AUTEUR MEUNIER S.MEUNIER 
C ======================================================================
C COPYRIGHT (C) 1991 - 2009  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE MEUNIER S.MEUNIER
C
      IMPLICIT NONE
C TOLE CRP_21
      INTEGER       DIME     ,ICPL
      INTEGER       NMA      ,LOMCUM(*),LONCUM(*),GRAPPA(*)
      INTEGER       TAMAMA(*),TAMAFA(*)
      INTEGER       BODIM1(*),BODIM2(*),NMAMA(*)
      INTEGER       NUMERO(*),NFAM
      LOGICAL       ISMED,TABCOL(NMA)
      CHARACTER*8   MAIL,NOMARL,TYPEMA(*)
      CHARACTER*10  NOM1,NOM2
      CHARACTER*16  TYPMAI
      REAL*8        PANMA1(DIME+2,*),PANMA2(DIME+2,*)
      REAL*8        BOSOM1(DIME,*)  ,BOSOM2(DIME,*)
      REAL*8        BOMNM1(2,DIME,*),BOMNM2(2,DIME,*)
      INTEGER       NMAIN1,NMAIN2,NMASS
      INTEGER       NNOIN1,NNOIN2
C
C ----------------------------------------------------------------------
C
C ROUTINE ARLEQUIN
C
C REGROUPEMENT DES INTEGRALES A CALCULER POUR LA JONCTION
C
C ----------------------------------------------------------------------
C
C
C IN  DIME   : DIMENSION DE L'ESPACE
C IN  ISMED  : VAUT .TRUE. SI ZONE DE COLLAGE EST LA ZONE MEDIATRICE
C IN  NOMARL : NOM DE LA SD PRINCIPALE ARLEQUIN
C IN  GRAPPA : GRAPHE D'APPARIEMENT
C IN  TAMAFA : LISTE DES COUPLES: NUMERO DE LA FAMILLE ASSOCIEE
C IN  TAMAMA : LISTE DES COUPLES DE MAILLES A INTEGRER
C                 (MA1.1, MA1.2, MA2.1, MA2.2, MA3.1, MA3.2, ... )
C                    SI MA*.1 > 0, INTEGRATION SUR MA*.1
C                    SI MA*.1 < 0, INTEGRATION SUR MA*.2
C                    SI MA*.2 > 0, INTEGRATION PAR INCLUSION
C                    SI MA*.2 < 0, INTEGRATION PAR SOUS-MAILLES
C                       MA*.1 INDEX DANS .GROUPEMA ASSOCIE AUX MULTIPLIC
C                       MA*.2 INDEX DANS L'AUTRE .GROUPEMA
C IN  MAIL   : NOM DU MAILLAGE
C IN  LOMCUM : LONGUEUR CUMULEE DE LA CONNEXITE DU MAILLAGE
C IN  NOM1   : NOM DE LA SD DE STOCKAGE MAILLES GROUP_MA_1
C IN  NOM2   : NOM DE LA SD DE STOCKAGE MAILLES GROUP_MA_2
C IN  TYPMAI : SD CONTENANT NOM DES TYPES ELEMENTS (&&CATA.NOMTM)
C IN  NMA    : NOMBRE DE MAILLES DANS LE GRAPHE D'APPARIEMENT
C IN  LONCUM : LONGUEUR CUMULEE DU GRAPHE D'APPARIEMENT
C IN  PANMA1 : SD BOITE.PAN ASSOCIEE A LA MAILLE MA1 (CF BOITE)
C IN  PANMA2 : SD BOITE.PAN ASSOCIEE A LA MAILLE MA2 (CF BOITE)
C IN  BOSOM1 : SD BOITE.SOMMET ASSOCIEE A MA1 (CF BOITE)
C IN  BOSOM2 : SD BOITE.SOMMET ASSOCIEE A MA2 (CF BOITE)
C IN  BODIM1 : SD BOITE.DIME ASSOCIEE A MA1 (CF BOITE)
C IN  BODIM2 : SD BOITE.DIME ASSOCIEE A MA2 (CF BOITE)
C IN  BOMNM1 : SD BOITE.MINMAX ASSOCIEE A MA1 (CF BOITE)
C IN  BOMNM2 : SD BOITE.MINMAX ASSOCIEE A MA2 (CF BOITE)
C OUT NMAIN1 : NBRE. MA. INTEG. SUR MAILLE 1
C OUT NMAIN2 : NBRE. MA. INTEG. SUR MAILLE 2
C OUT NMASS  : NBRE. MA .INTEG. PAR SOUS-MAILLES
C OUT NNOIN1 : NBRE. ND. INTEG. SUR MAILLE 1
C OUT NNOIN2 : NBRE. ND. INTEG. SUR MAILLE 2
C I/O TYPEMA : TYPE DE LA MAILLE SUPPORT (TMSUIN) POUR
C              CHAQUE FAMILLE (TMSUIN FAM.1, TMSUIN FAM.2, ...)
C I/O NUMERO : NUMERO DE LA FORMULE D'INTEGRATION (NFI)
C              (NFI FAM.1, NFI FAM.2, ...)
C I/O NMAMA  : NOMBRE DE COUPLES DE MAILLE (NCM) ASSOCIE
C              A CHAQ FAMILLE (NCM FAM.1, NCM FAM.2, ...)
C I/O NFAM   : NOMBRE DE FAMILLES DE QUADRATURE
C OUT ICPL   : COUPLE DE MAILLES COURANT A TRAITER

C
C
C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ---------------------
C
      INTEGER            ZI
      COMMON  / IVARJE / ZI(1)
      REAL*8             ZR
      COMMON  / RVARJE / ZR(1)
      COMPLEX*16         ZC
      COMMON  / CVARJE / ZC(1)
      LOGICAL            ZL
      COMMON  / LVARJE / ZL(1)
      CHARACTER*8        ZK8
      CHARACTER*16                ZK16
      CHARACTER*24                          ZK24
      CHARACTER*32                                    ZK32
      CHARACTER*80                                              ZK80
      COMMON  / KVARJE / ZK8(1) , ZK16(1) , ZK24(1) , ZK32(1) , ZK80(1)
C
C --- FIN DECLARATIONS NORMALISEES JEVEUX ------------------------------
C
      CHARACTER*8   TMSUIN,TMSREF,TM1,TM2
      INTEGER       NBNO1,NBNO2
      INTEGER       IMA1,IMA2,JAUX
      INTEGER       JDEBUT,JFIN
      REAL*8        H1,H2
      REAL*8        ARLGER,PREC
      LOGICAL       MINCLU
      LOGICAL       LINC21,LINC12,LSSMAI,LINCL1,LINCL2
      INTEGER       NUMMA1,NUMMA2
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
C
C --- TYPE DE MAILLE SUPPORT SI INTERSECTION DES DEUX MAILLES
C
      IF (DIME.EQ.2) THEN
        TMSREF = 'TRIA3'
      ELSEIF (DIME.EQ.3) THEN
        TMSREF = 'TETRA4'
      ELSE
        CALL ASSERT(.FALSE.)
      ENDIF
C
C --- PRECISION POUR DETECTER INCLUSION
C
      PREC = ARLGER(NOMARL,'PRECBO')
C
C --- INITIALISATIONS
C
      NMAIN1 = 0
      NMAIN2 = 0
      NMASS  = 0
      NNOIN1 = 0
      NNOIN2 = 0
C
      ICPL = 1
C
      DO 10 , IMA1 = 1, NMA
C
        JDEBUT = LONCUM(IMA1)
        JFIN   = LONCUM(IMA1+1)
C
        IF (.NOT.TABCOL(IMA1)) GOTO 10
C
C 1. ==> INFOS SUR LA MAILLE M1
C
        CALL ARLGRI(MAIL  ,TYPMAI,NOM1  ,DIME  ,IMA1 ,
     &              NUMMA1,TM1   ,H1)
        NBNO1 = LOMCUM(NUMMA1+1) - LOMCUM(NUMMA1)
C
C 2. ==> PARCOURS DES MAILLES POSSIBLES
C
        DO 20 , JAUX = JDEBUT, JFIN-1
C
C 2.1. ==> NUMERO ORDRE DANS LA LISTE DES GROUPES DE MAILLE
C
          IMA2 = GRAPPA(JAUX)
C
C 2.2. ==> INFOS SUR LA MAILLE M2
C
          CALL ARLGRI(MAIL  ,TYPMAI, NOM2  ,DIME  ,IMA2 ,
     &                NUMMA2,TM2   ,H2)
          NBNO2 = LOMCUM(NUMMA2+1) - LOMCUM(NUMMA2)
C
          LINCL1 = .FALSE.
          LINCL2 = .FALSE.
          LSSMAI = .FALSE.
C
C 2.3. ==> M2 INCLUSE DANS M1 ?
C
          LINC21 = MINCLU ( DIME  ,PREC  ,
     &                      IMA2  ,BODIM2,BOMNM2,BOSOM2,
     &                      IMA1  ,BODIM1,PANMA1)
          IF (LINC21) THEN
C OUI !
C ON VA INTEGRER IMA1 SUR IMA2 (IMA2 EST LA MAILLE SUPPORT)
            IF (ISMED) THEN
              NMAIN2 = NMAIN2 + 1
              NNOIN2 = NNOIN2 + NBNO2
              TAMAMA(2*ICPL-1) = -IMA1
              TAMAMA(2*ICPL  ) = IMA2
            ELSE
              NMAIN1 = NMAIN1 + 1
              NNOIN1 = NNOIN1 + NBNO1
              TAMAMA(2*ICPL-1) = IMA2
              TAMAMA(2*ICPL  ) = IMA1
            ENDIF
            LINCL2 = .TRUE.
            GOTO 26
          ENDIF
C
C 2.4. ==> M1 INCLUSE DANS M2 ?
C
          LINC12 = MINCLU ( DIME  ,PREC  ,
     &                      IMA1  ,BODIM1,BOMNM1,BOSOM1,
     &                      IMA2  ,BODIM2,PANMA2 )
C
          IF (LINC12) THEN
C ON VA INTEGRER IMA2 SUR IMA1 (IMA1 EST LA MAILLE SUPPORT)
            IF (ISMED) THEN
              NMAIN1 = NMAIN1 + 1
              NNOIN1 = NNOIN1 + NBNO1
              TAMAMA(2*ICPL-1) = IMA1
              TAMAMA(2*ICPL  ) = IMA2
            ELSE
              NMAIN2 = NMAIN2 + 1
              NNOIN2 = NNOIN2 + NBNO2
              TAMAMA(2*ICPL-1) = -IMA2
              TAMAMA(2*ICPL  ) = IMA1
            ENDIF
            LINCL1 = .TRUE.
            GOTO 26
          ENDIF
C
C 2.5. ==> INTERSECTION DE M1 ET M2 :
C --- INTEGRATION SPECIALE SUR LA MAILLE LA PLUS PETITE
C
          IF (ISMED) THEN
C
            IF (H1.LE.H2) THEN
C ON VA INTEGRER IMA2 SUR IMA1 (LA MAILLE SUPPORT EST INTERMEDIAIRE)
              TAMAMA(2*ICPL-1) = IMA1
            ELSE
C ON VA INTEGRER IMA1 SUR IMA2 (LA MAILLE SUPPORT EST INTERMEDIAIRE)
              TAMAMA(2*ICPL-1) = -IMA1
            ENDIF
            TAMAMA(2*ICPL) = -IMA2
C
          ELSE
C
            IF (H2.LE.H1) THEN
C ON VA INTEGRER IMA1 SUR IMA2 (LA MAILLE SUPPORT EST INTERMEDIAIRE)
              TAMAMA(2*ICPL-1) = IMA2
            ELSE
C ON VA INTEGRER IMA2 SUR IMA1 (LA MAILLE SUPPORT EST INTERMEDIAIRE)
              TAMAMA(2*ICPL-1) = -IMA2
            ENDIF
            TAMAMA(2*ICPL) = -IMA1
C
          ENDIF
C
          NMASS  = NMASS + 1
          LSSMAI = .TRUE.
C
C 2.6. ==> TYPE DE MAILLE SUPPORT D'INTEGRATION
C
 26       CONTINUE
C
          IF (LSSMAI) THEN
            TMSUIN = TMSREF
          ELSEIF (LINCL1) THEN
            TMSUIN = TM1
          ELSEIF (LINCL2) THEN
            TMSUIN = TM2
          ELSE
            CALL ASSERT(.FALSE.)
          ENDIF
C
C 2.7. ==> SELECTION DE LA FAMILLE DE QUADRATURE
C
          CALL ARLDEG(TMSUIN,TM1   ,TM2   ,TYPEMA,NUMERO,
     &                NMAMA ,NFAM  ,TAMAFA(ICPL))
C
C 2.8. ==> NOUVELLE PAIRE D'APPARIEMENT
C
          ICPL = ICPL + 1
C
 20     CONTINUE
C
 10   CONTINUE
C
      CALL JEDEMA()

      END
