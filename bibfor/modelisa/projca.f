      SUBROUTINE PROJCA(TABLCA,LIRELA,NMABET,NBMABE,MAILLA,
     &                  NBNOBE,NUNOBE,ICABL,NBNOCA,XNOCA,YNOCA,ZNOCA)
      IMPLICIT NONE
C-----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 18/09/2012   AUTEUR LADIER A.LADIER 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C-----------------------------------------------------------------------
C  DESCRIPTION : PROJECTION DES NOEUDS D'UN CABLE SUR LE MAILLAGE BETON
C  -----------   ET DETERMINATION DES RELATIONS CINEMATIQUES ENTRE LES
C                DDLS DES NOEUDS DU CABLE ET LES DDLS DES NOEUDS VOISINS
C                DE LA STRUCTURE BETON
C                APPELANT : OP0180 , OPERATEUR DEFI_CABLE_BP
C
C                EN SORTIE ON AJOUTE DES LIGNES DANS LA TABLE RESULTAT
C                LES CASES RENSEIGNEES CORRESPONDENT AUX PARAMETRES
C                <MAILLE_BETON_VOISINE>, <NOEUD_BETON_VOISIN>,
C                <INDICE_PROJECTION> ET <EXCENTRICITE>
C                LA SD DE TYPE LISTE_DE_RELATIONS EST MISE A JOUR
C
C  IN     : TABLCA : CHARACTER*19
C                    NOM DE LA TABLE DECRIVANT LES CABLES
C  IN     : LIRELA : CHARACTER*19 , SCALAIRE
C                    NOM DE LA SD DE TYPE LISTE_DE_RELATIONS
C  IN     : NMABET : CHARACTER*24
C                    OBJET CONTENANT LES MAILLES BETON
C  IN     : NBMABE : INTEGER, SCALAIRE
C                    NOMBRE DE MAILLES BETON
C  IN     : MAILLA : CHARACTER*8 , SCALAIRE
C                    NOM DU CONCEPT MAILLAGE ASSOCIE A L'ETUDE
C  IN     : NBNOBE : INTEGER , SCALAIRE
C                    NOMBRE DE NOEUDS APPARTENANT A LA STRUCTURE BETON
C  IN     : NUNOBE : CHARACTER*19 , SCALAIRE
C                    NOM D'UN VECTEUR D'ENTIERS POUR STOCKAGE DES
C                    NUMEROS DES NOEUDS APPARTENANT A LA STRUCTURE BETON
C  IN     : ICABL  : INTEGER , SCALAIRE
C                    NUMERO DU CABLE
C  IN     : NBNOCA : INTEGER , VECTEUR DE DIMENSION NBCABL
C                    CONTIENT LES NOMBRES DE NOEUDS DE CHAQUE CABLE
C  IN     : XNOCA  : CHARACTER*19 , SCALAIRE
C                    NOM D'UN VECTEUR DE REELS POUR STOCKAGE DES
C                    ABSCISSES X DES NOEUDS APPARTENANT AUX CABLES
C  IN     : YNOCA  : CHARACTER*19 , SCALAIRE
C                    NOM D'UN VECTEUR DE REELS POUR STOCKAGE DES
C                    ORDONNEES Y DES NOEUDS APPARTENANT AUX CABLES
C  IN     : ZNOCA  : CHARACTER*19 , SCALAIRE
C                    NOM D'UN VECTEUR DE REELS POUR STOCKAGE DES
C                    COTES Z DES NOEUDS APPARTENANT AUX CABLES
C
C-------------------   DECLARATION DES VARIABLES   ---------------------
C
C
C ARGUMENTS
C ---------
      INCLUDE 'jeveux.h'
      CHARACTER*8   MAILLA
      CHARACTER*19  LIRELA, NUNOBE, XNOCA, YNOCA, ZNOCA, TABLCA
      INTEGER       NBNOBE, ICABL, NBMABE, NBNOCA(*)
      CHARACTER*24  NMABET
C
C VARIABLES LOCALES
C -----------------
      INTEGER       IDECA, INOBE, INOCA, IPARA, IPROJ, ITRIA, JCOOR,
     &              JCXMA, JNOCA, JNUNOB, JTBLP, JTBNP, JXCA, JXYZMA,
     &              JYCA, JZCA, NBCNX, NBLIGN, NBNO, NBPARA,
     &              NNOMAX, NOE, NOEBE, NUMAIL
      REAL*8        D2, D2MIN, DX, DY, DZ, EXCENT, NORMAL(3), X3DCA(3),
     &              XBAR(3)
      COMPLEX*16    CBID
      CHARACTER*8   NNOECA, VOISIN(2)
      CHARACTER*19  LICNX, LNUMA
      CHARACTER*24  COORNO, NOMAMA, NONOCA, NONOMA
      LOGICAL       ENCORE
C
      CHARACTER*24  PARAM(4), PARCR
      DATA          PARAM /'MAILLE_BETON_VOISINE    ',
     &                     'NOEUD_BETON_VOISIN      ',
     &                     'INDICE_PROJECTION       ',
     &                     'EXCENTRICITE            '/
      DATA          PARCR /'NOEUD_CABLE             '/
C
C-------------------   DEBUT DU CODE EXECUTABLE    ---------------------
C
      CALL JEMARQ()
C
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
C 1   ACCES AUX DONNEES
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
C
C 1.1 OBJETS DU MAILLAGE
C ---
      COORNO = MAILLA//'.COORDO    .VALE'
      CALL JEVEUO(COORNO,'L',JCOOR)
      NOMAMA = MAILLA//'.NOMMAI'
      NONOMA = MAILLA//'.NOMNOE'
C
C 1.2 DONNEES RELATIVES AU CABLE
C ---
C.... NOMBRE DE NOEUDS
C
      NBNO = NBNOCA(ICABL)
C
C.... NOMS DES NOEUDS
C
      CALL JEVEUO(TABLCA//'.TBNP','L',JTBNP)
      NBPARA = ZI(JTBNP)
      NBLIGN = ZI(JTBNP+1)
      IDECA = NBLIGN - NBNO
      CALL JEVEUO(TABLCA//'.TBLP','L',JTBLP)
      DO 10 IPARA = 1, NBPARA
         IF ( ZK24(JTBLP+4*(IPARA-1)).EQ.PARCR ) THEN
            NONOCA = ZK24(JTBLP+4*(IPARA-1)+2)
            CALL JEVEUO(NONOCA,'L',JNOCA)
            GO TO 11
         ENDIF
  10  CONTINUE
  11  CONTINUE
C
C.... COORDONNEES DES NOEUDS
C
      CALL JEVEUO(XNOCA,'L',JXCA)
      CALL JEVEUO(YNOCA,'L',JYCA)
      CALL JEVEUO(ZNOCA,'L',JZCA)
C
C 1.3 NUMEROS DES NOEUDS DE LA STRUCTURE BETON
C ---
      CALL JEVEUO(NUNOBE,'L',JNUNOB)
C
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
C 2   PROJECTION DES NOEUDS DU CABLE SUR LA STRUCTURE BETON
C%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
C
C 2.1 CREATION D'OBJETS DE TRAVAIL
C ---
C.... LES MAILLES APPARTENANT A LA STRUCTURE BETON SONT DES MAILLES
C.... TRIA3, TRIA6, QUAD4, QUAD8 OU QUAD9 : LA VERIFICATION A ETE
C.... EFFECTUEE EN AMONT PAR LA ROUTINE TOMABE
C.... LE NOMBRE DE NOEUDS MAXIMAL SUR UNE MAILLE VAUT DONC 9
C
      NNOMAX = 9
      CALL WKVECT('&&PROJCA.XYZ_NOEMAI','V V R',3*NNOMAX,JXYZMA)
      CALL WKVECT('&&PROJCA.CNX_MAILLE','V V I',NNOMAX  ,JCXMA )
      WRITE(LNUMA,'(A19)') '&&PROJCA.NUMA_NOEBE'
      CALL JECREO(LNUMA,'V V I')
      CALL JEECRA(LNUMA,'LONMAX',NBMABE,' ')
      WRITE(LICNX,'(A19)') '&&PROJCA.ICNX_NOEBE'
      CALL JECREO(LICNX,'V V I')
      CALL JEECRA(LICNX,'LONMAX',NBMABE,' ')
C
C 2.2 BOUCLE SUR LE NOMBRE DE NOEUDS DU CABLE
C ---
      DO 100 INOCA = 1, NBNO
C
         NNOECA = ZK8(JNOCA+IDECA+INOCA-1)
         X3DCA(1) = ZR(JXCA+IDECA+INOCA-1)
         X3DCA(2) = ZR(JYCA+IDECA+INOCA-1)
         X3DCA(3) = ZR(JZCA+IDECA+INOCA-1)
C
         ENCORE = .TRUE.
C
C 2.2.1  DETERMINATION DU NOEUD DE LA STRUCTURE BETON LE PLUS PROCHE
C .....  DU NOEUD CABLE COURANT
C
         NOEBE = ZI(JNUNOB)
         DX = X3DCA(1) - ZR(JCOOR+3*(NOEBE-1)  )
         DY = X3DCA(2) - ZR(JCOOR+3*(NOEBE-1)+1)
         DZ = X3DCA(3) - ZR(JCOOR+3*(NOEBE-1)+2)
         D2MIN = DX * DX + DY * DY + DZ * DZ
         DO 110 INOBE = 2, NBNOBE
            NOE = ZI(JNUNOB+INOBE-1)
            DX = X3DCA(1) - ZR(JCOOR+3*(NOE-1)  )
            DY = X3DCA(2) - ZR(JCOOR+3*(NOE-1)+1)
            DZ = X3DCA(3) - ZR(JCOOR+3*(NOE-1)+2)
            D2 = DX * DX + DY * DY + DZ * DZ
            IF ( D2.LT.D2MIN ) THEN
               D2MIN = D2
               NOEBE = NOE
            ENDIF
 110     CONTINUE
C
C 2.2.2  TENTATIVE DE PROJECTION DU NOEUD CABLE SUR LES MAILLES
C .....  AUXQUELLES APPARTIENT LE NOEUD BETON LE PLUS PROCHE
C
         CALL PROJKM(NMABET,NBMABE,MAILLA,X3DCA(1),NOEBE,LNUMA,LICNX,
     &               NUMAIL,NBCNX,ZI(JCXMA),ZR(JXYZMA),NORMAL(1),
     &               ITRIA,XBAR(1),IPROJ,EXCENT)
         IF ( IPROJ.GE.0 ) ENCORE = .FALSE.

C SI LA GEOMETRIE DU BETON N'EST PAS PLANE (PAR EXEMPLE UN CYLINDRE),
C LA PROJECTION SUR LES MAILLES PEUT ECHOUER

C 2.2.3  EN CAS D'ECHEC DE LA PROJECTION SUR LES MAILLES, TENTATIVE DE
C .....  PROJECTION DU NOEUD CABLE SUR LES BORDS DES MAILLES AUXQUELLES
C        APPARTIENT LE NOEUD BETON LE PLUS PROCHE

         IF ( ENCORE ) THEN
            CALL PROJKB(MAILLA,X3DCA(1),LNUMA,LICNX,
     &                  NUMAIL,NBCNX,ZI(JCXMA),ZR(JXYZMA),NORMAL(1),
     &                  ITRIA,XBAR(1),IPROJ,EXCENT)
            IF ( IPROJ.GT.0 ) ENCORE = .FALSE.
         ENDIF

C EN CAS D'UNE DOUBLE COURBURE (PAR EXEMPLE UNE COUPOLE), LA PROJECTION
C SUR LES BORDS DES MAILLES PEUT EGALEMENT ECHOUER.

C 2.2.4  EN CAS D'ECHEC DES DEUX TENTATIVES PRECEDENTES, LE NOEUD CABLE
C .....  SE PROJETTE SUR LE NOEUD BETON LE PLUS PROCHE

         IF ( ENCORE ) THEN
C ON PEUT VERIFIER LE PASSAGE PAR PROJKN AVEC:
C            PRINT*,'projca.f: On utilise PROJKN.'
            CALL PROJKN(MAILLA,X3DCA(1),LNUMA,LICNX,
     &                  NUMAIL,NBCNX,ZI(JCXMA),ZR(JXYZMA),NORMAL(1),
     &                  ITRIA,IPROJ,EXCENT)
         ENDIF
C
C 2.2.5  DETERMINATION DES RELATIONS CINEMATIQUES
C .....
         CALL RECI2D(LIRELA,MAILLA,NNOECA,
     &               NOEBE,NBCNX,ZI(JCXMA),NORMAL(1),
     &               ITRIA,XBAR(1),IPROJ,EXCENT)
C
C 2.2.6  MISE A JOUR DE LA SD TABLE
C .....
         CALL JENUNO(JEXNUM(NOMAMA,NUMAIL),VOISIN(1))
         CALL JENUNO(JEXNUM(NONOMA,NOEBE) ,VOISIN(2))
         CALL TBAJLI(TABLCA,4,PARAM,
     &               IPROJ,EXCENT,CBID,VOISIN(1),IDECA+INOCA)
C
 100  CONTINUE
C
C --- MENAGE
      CALL JEDETR('&&PROJCA.XYZ_NOEMAI')
      CALL JEDETR('&&PROJCA.CNX_MAILLE')
      CALL JEDETR('&&PROJCA.NUMA_NOEBE')
      CALL JEDETR('&&PROJCA.ICNX_NOEBE')
C
      CALL JEDEMA()
C
C --- FIN DE PROJCA.
      END
