      SUBROUTINE ARLFC1(MAIL  ,DIME  ,NOM1  ,CINE1 ,NOM2  ,
     &                  CINE2 ,NOMC  ,NOMARL )
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 12/05/2009   AUTEUR MEUNIER S.MEUNIER 
C ======================================================================
C COPYRIGHT (C) 1991 - 2004  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE MEUNIER S.MEUNIER
C
      IMPLICIT NONE
      CHARACTER*10  NOM1,NOM2,NOMC
      CHARACTER*8   MAIL,CINE1,CINE2,NOMARL
      INTEGER       DIME
C
C ----------------------------------------------------------------------
C
C ROUTINE ARLEQUIN
C
C ALLOCATION ET ECRITURE DES POINTEURS DES MATRICES DE COUPLAGE ARLEQUIN
C LORSQUE LE GROUPE DE MAILLE DE COLLAGE EST CELUI DES MULTIPLICATEURS
C
C ----------------------------------------------------------------------
C
C
C IN  MAIL   : NOM UTILISATEUR DU MAILLAGE
C IN  DIME   : DIMENSION DE L'ESPACE GLOBAL
C IN  NOM1   : NOM DE LA SD POUR LES MAILLES GROUPE 1
C IN  NOM2   : NOM DE LA SD POUR LES MAILLES GROUPE 2
C IN  CINE1  : CINEMATIQUE DU PREMIER GROUPE DE MAILLE
C IN  CINE2  : CINEMATIQUE DU DEUXIEME GROUPE DE MAILLE
C IN  NOMARL : NOM DE LA SD PRINCIPALE ARLEQUIN
C I/O NOMC   : NOM DE LA SD POUR LE COLLAGE
C
C
C SD DE SORTIE
C NOMC.INO        : LISTE TRIEE DES NOEUDS LIGNE DE LA MATRICE MORSE
C                   (NO1, NO2, NO3, ... ) AVEC NO1 < NO2 < NO3 < ...
C NOM*.MORSE.DIME : DIMENSION DE LA MATRICE NODALE (* = 1, 2)
C NOM*.MORSE.INO  : LISTES TRIEES DES NOEUDS COLONNES MATRICE MORSE
C                   (XC V I NUMERO VARIABLE)
C                 [LIGNE 1] (NO1.1, NO1.2, ...) AVEC NO1.1 < NO1.2 < ...
C                 [LIGNE 2] (NO2.1, NO2.2, ...) AVEC NO2.1 < NO2.2 < ...
C                    ...
C
C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ---------------------
C
      CHARACTER*32       JEXNUM ,JEXATR
      INTEGER            ZI
      COMMON  / IVARJE / ZI(1)
      REAL*8             ZR
      COMMON  / RVARJE / ZR(1)
      COMPLEX*16         ZC
      COMMON  / CVARJE / ZC(1)
      LOGICAL            ZL
      COMMON  / LVARJE / ZL(1)
      CHARACTER*8        ZK8
      CHARACTER*16                ZK16
      CHARACTER*24                          ZK24
      CHARACTER*32                                    ZK32
      CHARACTER*80                                              ZK80
      COMMON  / KVARJE / ZK8(1) , ZK16(1) , ZK24(1) , ZK32(1) , ZK80(1)
C
C --- FIN DECLARATIONS NORMALISEES JEVEUX ------------------------------
C
      CHARACTER*16  NOMMO1,NOMMO2
      CHARACTER*24  NOMAPP
      CHARACTER*8   K8BID
      INTEGER       DIM1,DIM2,NNO,NN1,NN2,NNC,NM2,N1,N2,M1,M2
      INTEGER       JCONX,JCUMU,JNGRM1
      INTEGER       JCNCI,JCNCUM,JNAPP,JAPCUM,JNGRM2
      INTEGER       JFILN1,JCOMT1,JCOMT2,JFILMA,JFILN2
      INTEGER       JCINO,JMINO1,JMINO2
      INTEGER       I,J,K,P0,P1,P2,P3,P4,P5,JCOLM,IBID
      LOGICAL       IR
C
      CHARACTER*6  NOMPRO
      PARAMETER   (NOMPRO='ARLFC1')
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
C
C --- INITIALISATIONS
C
      CALL JEVEUO(NOMC(1:10)//'.MAILLE','E',JCOLM)
C
C --- NOMS OBJETS MORSE
C
      NOMMO1 = NOM1(1:10)//'.MORSE'
      NOMMO2 = NOM2(1:10)//'.MORSE'
C
C --- DIMENSION DES MATRICES NODALES ARLEQUIN
C
      CALL ARLMDI(DIME  ,CINE1 ,CINE2 ,DIM1  ,DIM2  )
C
C --- LECTURE DONNEES MAILLAGE
C
      CALL JEVEUO(MAIL(1:8)//'.CONNEX','L',JCONX)
      CALL JEVEUO(JEXATR(MAIL(1:8)//'.CONNEX','LONCUM'),'L',JCUMU)
C
C --- LECTURE DONNEES GROUPE DE MAILLES
C
      CALL JEVEUO(NOM1(1:10)//'.GROUPEMA','L',JNGRM1)
      CALL JEVEUO(NOM2(1:10)//'.GROUPEMA','L',JNGRM2)
      CALL JELIRA(NOM2(1:10)//'.GROUPEMA','LONMAX',NM2,K8BID)
C
C --- LECTURE CONNECTIVITES INVERSES
C
      CALL JEVEUO(NOM1(1:10)//'.CNCINV','L',JCNCI)
      CALL JELIRA(NOM1(1:10)//'.CNCINV','NMAXOC',NNO,K8BID)
      CALL JEVEUO(JEXATR(NOM1(1:10)//'.CNCINV','LONCUM'),'L',JCNCUM)
C
C --- LECTURE GRAPHE APPARIEMENT
C
      NOMAPP = NOMARL(1:8)//'.GRAPH'
      CALL JEVEUO(NOMAPP,'L',JNAPP)
      CALL JEVEUO(JEXATR(NOMAPP,'LONCUM'),'L',JAPCUM)
C
C --- ALLOCATION OBJETS TEMPORAIRES
C
      CALL WKVECT('&&'//NOMPRO//'.COMPTEUR1','V V I',NNO,JCOMT1)
      CALL WKVECT('&&'//NOMPRO//'.COMPTEUR2','V V I',NNO,JCOMT2)
      CALL WKVECT('&&'//NOMPRO//'.FILTRE.N1','V V L',NNO,JFILN1)
      CALL WKVECT('&&'//NOMPRO//'.FILTRE.N2','V V L',NNO,JFILN2)
      CALL WKVECT('&&'//NOMPRO//'.FILTRE.MA','V V L',NM2,JFILMA)

      DO 10 I = 1, NNO
        ZL(JFILN2-1+I) = .FALSE.
 10   CONTINUE

      NN1 = 0
      NN2 = 0
      NNC = 0
C
C --- DIMENSIONS DES MATRICES
C
      P1 = ZI(JCNCUM)
      IR = .TRUE.

      DO 20 N1 = 1, NNO

        P0 = P1
        P1 = ZI(JCNCUM+N1)
        IF (ZI(JCNCI-1+P0).EQ.0) GOTO 20

        IF (IR) THEN

          IR = .FALSE.

          DO 30 M2 = 1, NM2
            ZL(JFILMA-1+M2) = .FALSE.
 30       CONTINUE

          DO 40 N2 = 1, NNO
            ZL(JFILN1-1+N2) = .FALSE.
 40       CONTINUE

        ENDIF

        DO 50 I = P0, P1-1

          M1 = ZI(JCNCI-1+I)
          IF (.NOT.ZL(JCOLM+M1-1)) GOTO 50

          J = ZI(JNGRM1-1+M1)
          P2 = ZI(JCUMU-1+J)
          P3 = ZI(JCUMU+J)

          DO 60 J = P2, P3-1

            N2 = ZI(JCONX-1+J)
            IF (ZL(JFILN1-1+N2)) GOTO 60
            ZL(JFILN1-1+N2) = .TRUE.
            ZL(JFILN2-1+N2) = .TRUE.
            ZI(JCOMT1-1+N1) = ZI(JCOMT1-1+N1) + 1
            NN1 = NN1 + 1

 60       CONTINUE

          P2 = ZI(JAPCUM-1+M1)
          P3 = ZI(JAPCUM+M1)

          DO 70 J = P2, P3-1

            M2 = ZI(JNAPP-1+J)
            IF (ZL(JFILMA-1+M2)) GOTO 70
            ZL(JFILMA-1+M2) = .TRUE.
            K = ZI(JNGRM2-1+M2)
            P4 = ZI(JCUMU-1+K)
            P5 = ZI(JCUMU+K)

            DO 80 K = P4, P5-1

              N2 = ZI(JCONX-1+K)
              IF (ZL(JFILN1-1+N2)) GOTO 80
              ZL(JFILN1-1+N2) = .TRUE.

              IF (ZL(JFILN2-1+N2)) THEN
                CALL U2MESS('F','ARLEQUIN_17')
              ENDIF

              ZI(JCOMT2-1+N1) = ZI(JCOMT2-1+N1) + 1
              NN2 = NN2 + 1
              IR = .TRUE.

 80         CONTINUE

 70       CONTINUE

 50     CONTINUE

        IF (IR) NNC = NNC + 1

 20   CONTINUE
C
C --- ALLOCATION OBJET MORSES
C
      CALL ARLCMO(NOMMO1,'V',NN1,NNC,DIM1)
      CALL ARLCMO(NOMMO2,'V',NN2,NNC,DIM2)
      CALL JEVEUO(NOMMO1(1:16)//'.INO','E',JMINO1)
      CALL JEVEUO(NOMMO2(1:16)//'.INO','E',JMINO2)
C
C --- ALLOCATION OBJET INO
C
      CALL WKVECT(NOMC(1:10)//'.INO','V V I',NNC,JCINO)
C
      NNC = 0
      DO 90 N1 = 1, NNO
        IF (ZI(JCOMT2-1+N1).EQ.0) GOTO 90
        ZI(JCINO+NNC) = N1
        NNC = NNC + 1
        CALL JECROC(JEXNUM(NOMMO1(1:16)//'.INO',NNC))
        CALL JEECRA(JEXNUM(NOMMO1(1:16)//'.INO',NNC),'LONMAX',
     &              ZI(JCOMT1-1+N1),' ')
        CALL JECROC(JEXNUM(NOMMO2(1:16)//'.INO',NNC))
        CALL JEECRA(JEXNUM(NOMMO2(1:16)//'.INO',NNC),'LONMAX',
     &              ZI(JCOMT2-1+N1),' ')
 90   CONTINUE
C
C --- ECRITURE DU PROFIL DES MATRICES
C
      P1 = ZI(JCNCUM)

      DO 100 N1 = 1, NNO

        P0 = P1
        P1 = ZI(JCNCUM+N1)
        IF (ZI(JCOMT2-1+N1).EQ.0) GOTO 100

        DO 110 M2 = 1, NM2
          ZL(JFILMA-1+M2) = .FALSE.
 110    CONTINUE

        DO 120 N2 = 1, NNO
          ZL(JFILN1-1+N2) = .FALSE.
 120    CONTINUE

        NN1 = 0
        NN2 = 0

        DO 130 I = P0, P1-1

          M1 = ZI(JCNCI-1+I)
          IF (.NOT.ZL(JCOLM+M1-1)) GOTO 130
          J = ZI(JNGRM1-1+M1)
          P2 = ZI(JCUMU-1+J)
          P3 = ZI(JCUMU+J)

          DO 140 J = P2, P3-1

            N2 = ZI(JCONX-1+J)
            IF (ZL(JFILN1-1+N2)) GOTO 140
            ZL(JFILN1-1+N2) = .TRUE.
            ZI(JMINO1+NN1) = N2
            NN1 = NN1 + 1

 140      CONTINUE

          P2 = ZI(JAPCUM-1+M1)
          P3 = ZI(JAPCUM+M1)

          DO 150 J = P2, P3-1

            M2 = ZI(JNAPP-1+J)
            IF (ZL(JFILMA-1+M2)) GOTO 150
            ZL(JFILMA-1+M2) = .TRUE.
            K = ZI(JNGRM2-1+M2)
            P4 = ZI(JCUMU-1+K)
            P5 = ZI(JCUMU+K)

            DO 160 K = P4, P5-1

              N2 = ZI(JCONX-1+K)
              IF (ZL(JFILN1-1+N2)) GOTO 160
              ZL(JFILN1-1+N2) = .TRUE.
              ZI(JMINO2+NN2) = N2
              NN2 = NN2 + 1

 160        CONTINUE

 150      CONTINUE

 130    CONTINUE

        CALL TRI(ZI(JMINO1),IBID,0,NN1)
        CALL TRI(ZI(JMINO2),IBID,0,NN2)
        JMINO1 = JMINO1 + NN1
        JMINO2 = JMINO2 + NN2

 100  CONTINUE
C
C --- DESALLOCATIONS
C
      CALL JEDETR('&&'//NOMPRO//'.FILTRE.MA')
      CALL JEDETR('&&'//NOMPRO//'.COMPTEUR2')
      CALL JEDETR('&&'//NOMPRO//'.COMPTEUR1')
      CALL JEDETR('&&'//NOMPRO//'.FILTRE.N1')
      CALL JEDETR('&&'//NOMPRO//'.FILTRE.N2')

      CALL JEDEMA()

      END
