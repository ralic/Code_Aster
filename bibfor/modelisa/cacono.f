      SUBROUTINE CACONO(NOMA,NDIM,LLIST1,LLIST2,NO1,NO2,NORM1,NORM2,
     &                  INOMA)
      IMPLICIT NONE
      INCLUDE 'jeveux.h'

      CHARACTER*32 JEXNUM
      INTEGER NDIM,NO1,NO2
      CHARACTER*8 NOMA
      CHARACTER*24 LLIST1,LLIST2
      REAL*8 NORM1(*),NORM2(*)
C ----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 09/11/2012   AUTEUR DELMAS J.DELMAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C     BUT : CALCUL DES NORMALES AUX NOEUDS NO1 ET NO2
C     APPARTENANT AUX LISTES DE MAILLES LLIST1 ET LLIST2
C     CUMUL DE CES NORMALES
C     CALCUL DU JEU : SOMME_SUR_I(X1(I)*N1(I)+X2(I)*N2(I))
C
C IN  NOMA    K8  : NOM DU MAILLAGE
C IN  NDIM    I   : DIMENSION DU MODELE (2 SI COORD_2D OU 3 SI COORD_3D)
C IN  LLIST1  K8  : NOM DE LA LISTE DE MAILLE ASSOCIEE AU NOEUD NO1
C IN  LLIST2  K8  : NOM DE LA LISTE DE MAILLE ASSOCIEE AU NOEUD NO2
C IN  NO1     I   : NOEUD NUMERO 1 (NUMERO ABSOLU)
C IN  NO2     I   : NOEUD NUMERO 2 (NUMERO ABSOLU)
C
C OUT NORM1   R8  : NORMALE AU NOEUD 1
C OUT NORM2   R8  : NORMALE AU NOEUD 2
C OUT INOMA   I   :  = 0    SI LE NOEUD 1 N'APPARTIENT PAS A LLIST1
C                        ET SI LE NOEUD 2 N'APPARTIENT PAS A LLIST2
C                    = 1    SINON
C                    =-1    SI LE NOEUD 1 APPARTIENT A UNE MAILLE POI1
C                    =-2    SI LE NOEUD 2 APPARTIENT A UNE MAILLE POI1
C ROUTINES APPELEES :
C     CANORM      PACOOR      PANBNO
C
C
C
      REAL*8 VECNOR(3),COOR(27)
      INTEGER NBMA,NUMMA,IPOI1,INOMA,I,ILIST,IPOI2,IMA,ITYP,IMAD,INO
      INTEGER NBNO,NBNOTT(3),INORM,J
C DEBUT ----------------------------------------------------------------
C
C     INORM = 0 POUR VECTEUR NORMAL DE NORME "SURFACE"
C     INORM = 1 POUR VECTEUR NORMAL UNITAIRE
C-----------------------------------------------------------------------
C-----------------------------------------------------------------------
      CALL JEMARQ()
      INORM = 1
      DO 1 I = 1,3
        NORM1(I) = 0.0D0
        NORM2(I) = 0.0D0
    1 CONTINUE
      CALL JEVEUO(LLIST1,'L',ILIST)
      NBMA = ZI(ILIST)
      INOMA = 0
      IPOI1 = 0
      IPOI2 = 0
C
      DO 100 IMA = 1,NBMA
        ITYP = ZI(ILIST+2* (IMA-1)+2)
        CALL PANBNO(ITYP,NBNOTT)
        NBNO = NBNOTT(1) + NBNOTT(2) + NBNOTT(3)
        NUMMA = ZI(ILIST+2* (IMA-1)+1)
        CALL JEVEUO(JEXNUM(NOMA//'.CONNEX',NUMMA),'L',IMAD)
        DO 110 INO = 1,NBNO
          IF (ZI(IMAD-1+INO).EQ.NO1) THEN
C           CAS D'UNE MAILLE POI1
            IF (NBNO.EQ.1) THEN
               IPOI1 = 1
               GO TO 100
            ENDIF
            INOMA = 1
            CALL PACOOR(NOMA,NUMMA,NBNO,COOR)
            CALL CANORM(COOR,VECNOR,NDIM,ITYP,INORM)
            DO 120 J = 1,3
              NORM1(J) = NORM1(J) + VECNOR(J)
  120       CONTINUE
            GO TO 100

          END IF

  110   CONTINUE
  100 CONTINUE
C
      CALL JEVEUO(LLIST2,'L',ILIST)
      NBMA = ZI(ILIST)
      DO 200 IMA = 1,NBMA
        ITYP = ZI(ILIST+2* (IMA-1)+2)
        CALL PANBNO(ITYP,NBNOTT)
        NBNO = NBNOTT(1) + NBNOTT(2) + NBNOTT(3)
        NUMMA = ZI(ILIST+2* (IMA-1)+1)
        CALL JEVEUO(JEXNUM(NOMA//'.CONNEX',NUMMA),'L',IMAD)
        DO 210 INO = 1,NBNO
          IF (ZI(IMAD-1+INO).EQ.NO2) THEN
C           CAS D'UNE MAILLE POI1
            IF (NBNO.EQ.1) THEN
               IPOI2 = 1
               GO TO 200
            ENDIF
            INOMA = 1
            CALL PACOOR(NOMA,NUMMA,NBNO,COOR)
            CALL CANORM(COOR,VECNOR,NDIM,ITYP,INORM)
            DO 220 J = 1,3
              NORM2(J) = NORM2(J) + VECNOR(J)
  220       CONTINUE
            GO TO 200

          END IF

  210   CONTINUE
  200 CONTINUE
C
C     ON CHERCHE LE CAS OU NO2 APPARTIENT SEULEMENT A UNE MAILLE POI1
C     ON VERIFIE DANS CE CAS QUE NO1 N'APPARTIENT PAS A UNE MAILLE POI1
C
      IF (IPOI1.EQ.1) THEN
         INOMA = -1
      ELSE IF (IPOI2.EQ.1) THEN
         INOMA = -2
      ENDIF
      IF((IPOI1.EQ.1).AND.(IPOI2.EQ.1)) THEN
         CALL U2MESS('F','MODELISA2_43')
      ENDIF
C
C FIN ------------------------------------------------------------------
      CALL JEDEMA()
      END
