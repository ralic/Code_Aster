      SUBROUTINE SURFCL(CHAR,NOMA,IFM)
C      
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 08/07/2008   AUTEUR DESOZA T.DESOZA 
C ======================================================================
C COPYRIGHT (C) 1991 - 2007  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C 
      IMPLICIT NONE
      CHARACTER*8 CHAR
      CHARACTER*8 NOMA
      INTEGER     IFM
C      
C ----------------------------------------------------------------------
C
C ROUTINE CONTACT (METHODES MAILLEES - AFFICHAGE DONNEES)
C
C AFFICHAGE LES INFOS CONTENUES DANS LA SD CONTACT POUR LES FORMULATIONS
C MAILLEES (DISCRETE ET CONTINUE MAIS PAS XFEM)
C      
C ----------------------------------------------------------------------
C
C
C IN  CHAR   : NOM UTILISATEUR DU CONCEPT DE CHARGE
C IN  NOMA   : NOM DU MAILLAGE
C IN  IFM    : UNITE D'IMPRESSION
C
C -------------- DEBUT DECLARATIONS NORMALISEES JEVEUX -----------------
C
      CHARACTER*32 JEXNUM
      INTEGER ZI
      COMMON /IVARJE/ ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C ---------------- FIN DECLARATIONS NORMALISEES JEVEUX -----------------
C
      INTEGER      CFMMVD,ZMETH,ZDIRN,ZTOLE
      INTEGER      NDIM,NZOCO,NSUCO,NMACO
      INTEGER      NNOCO,NMANO,NNOMA,NMAMA,NESMAX
      INTEGER      NBSURF,NBMAIL,NBNOEU,NBSANS,NBELQU
      INTEGER      IZONE,ISURF,ISUCO,IMA,INO,K,IRET
      INTEGER      NBMA,NBNO
      INTEGER      JDECMA,JDECNO,JDEC,JNOMMA,JNOMNO,LSANSN
      INTEGER      NUMMA ,NUMNO,NUMNEX
      REAL*8       JSUP
      CHARACTER*8  CHAIN1,CHAIN2,CHAIN3,K8BID,NOMNOE
      CHARACTER*24 NOEUMA,MAILMA
C
      CHARACTER*24 METHCO,TOLECO
      INTEGER      JMETH ,JTOLE
C
      CHARACTER*24 PZONE,PSURMA,PSURNO,CONTMA,CONTNO,CONOQU,NDIMCO
      INTEGER      JZONE,JSUMA ,JSUNO ,JMACO ,JNOCO ,JNOQU ,JDIM
      CHARACTER*24 MANOCO,PMANO ,NOMACO,PNOMA ,MAMACO,PMAMA ,NOZOCO
      INTEGER      JMANO ,JPOMA ,JNOMA ,JPONO ,JMAMA ,JPOIN ,JZOCO
      CHARACTER*24 PSANS ,SANSNO      
      INTEGER      JPSANS,JSANS 
C
      CHARACTER*24 DIRAPP,DIRNOR
      INTEGER      JDIRAP,JDIRNO
      CHARACTER*24 JEUSUP,JEUFO1,JEUFO2,JEUFO3,JEUPOU,JEUCOQ
      INTEGER      JJSUP,JJFO1,JJFO2,JJFO3,JJPOU,JJCOQ       
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ    
C
C --- ACCES SD DU MAILLAGE
C
      MAILMA = NOMA(1:8)//'.NOMMAI'
      NOEUMA = NOMA(1:8)//'.NOMNOE'    
C   
C --- COMMUNS TOUTES FORMULATIONS 
C 
      METHCO = CHAR(1:8)//'.CONTACT.METHCO'              
      CALL JEVEUO(METHCO,'L',JMETH) 
C      
      ZMETH  = CFMMVD('ZMETH')                 
C   
C --- COMMUNS AVEC FORM. MAILLEES (DISCRET ET CONTINUE MAIS PAS XFEM)
C  
      NDIMCO = CHAR(1:8)//'.CONTACT.NDIMCO'
      TOLECO = CHAR(1:8)//'.CONTACT.TOLECO'
      PZONE  = CHAR(1:8)//'.CONTACT.PZONECO'
      NOZOCO = CHAR(1:8)//'.CONTACT.NOZOCO'      
      PSURMA = CHAR(1:8)//'.CONTACT.PSUMACO'
      PSURNO = CHAR(1:8)//'.CONTACT.PSUNOCO'
      CONTMA = CHAR(1:8)//'.CONTACT.MAILCO'
      CONTNO = CHAR(1:8)//'.CONTACT.NOEUCO'
      CONOQU = CHAR(1:8)//'.CONTACT.NOEUQU' 
      MANOCO = CHAR(1:8)//'.CONTACT.MANOCO'
      PMANO  = CHAR(1:8)//'.CONTACT.PMANOCO'
      NOMACO = CHAR(1:8)//'.CONTACT.NOMACO'
      PNOMA  = CHAR(1:8)//'.CONTACT.PNOMACO'
      MAMACO = CHAR(1:8)//'.CONTACT.MAMACO'
      PMAMA  = CHAR(1:8)//'.CONTACT.PMAMACO'
      SANSNO = CHAR(1:8)//'.CONTACT.SSNOCO'
      PSANS  = CHAR(1:8)//'.CONTACT.PSSNOCO'  
      JEUFO1 = CHAR(1:8)//'.CONTACT.JFO1CO'
      JEUFO2 = CHAR(1:8)//'.CONTACT.JFO2CO' 
      JEUFO3 = CHAR(1:8)//'.CONTACT.JFO3CO'       
      JEUPOU = CHAR(1:8)//'.CONTACT.JEUPOU'
      JEUCOQ = CHAR(1:8)//'.CONTACT.JEUCOQ' 
      JEUSUP = CHAR(1:8)//'.CONTACT.JSUPCO'       
      DIRAPP = CHAR(1:8)//'.CONTACT.DIRAPP'
      DIRNOR = CHAR(1:8)//'.CONTACT.DIRNOR'  
C
      CALL JEVEUO(NDIMCO,'L',JDIM)
      CALL JEVEUO(TOLECO,'L',JTOLE)
      CALL JEVEUO(PZONE, 'L',JZONE) 
      CALL JEVEUO(NOZOCO,'L',JZOCO)   
      CALL JEVEUO(PSURMA,'L',JSUMA)
      CALL JEVEUO(PSURNO,'L',JSUNO) 
      CALL JEVEUO(CONTMA,'L',JMACO)
      CALL JEVEUO(CONTNO,'L',JNOCO)       
      CALL JEVEUO(MANOCO,'L',JMANO)
      CALL JEVEUO(PMANO, 'L',JPOMA)
      CALL JEVEUO(NOMACO,'L',JNOMA)
      CALL JEVEUO(PNOMA, 'L',JPONO)
      CALL JEVEUO(MAMACO,'L',JMAMA)
      CALL JEVEUO(PMAMA, 'L',JPOIN)
      CALL JEVEUO(PSANS, 'L',JPSANS)
      CALL JEVEUO(SANSNO,'L',JSANS)   
      CALL JEVEUO(JEUFO1,'L',JJFO1)
      CALL JEVEUO(JEUFO2,'L',JJFO2)
      CALL JEVEUO(JEUFO3,'L',JJFO3)    
      CALL JEVEUO(JEUPOU,'L',JJPOU)
      CALL JEVEUO(JEUCOQ,'L',JJCOQ) 
      CALL JEVEUO(JEUSUP,'L',JJSUP) 
      CALL JEVEUO(DIRAPP,'L',JDIRAP)
      CALL JEVEUO(DIRNOR,'L',JDIRNO)        
C
      ZDIRN  = CFMMVD('ZDIRN') 
      ZTOLE  = CFMMVD('ZTOLE')        
C
C --- INITIALISATIONS
C      
      ISUCO  = 0        
      NDIM   = ZI(JDIM)
      NZOCO  = ZI(JMETH) 
      NSUCO  = ZI(JDIM+2)
      NMACO  = ZI(JDIM+3)
      NNOCO  = ZI(JDIM+4)
      NMANO  = ZI(JDIM+5)
      NNOMA  = ZI(JDIM+6)
      NMAMA  = ZI(JDIM+7)
      NESMAX = ZI(JDIM+8)
C
C --- CREATION VECTEURS TEMPORAIRES
C      
      CALL WKVECT('&&SURFCO.TRAVMA','V V K8',NMACO,JNOMMA)
      CALL JELIRA(SANSNO,'LONUTI',LSANSN,K8BID) 
      LSANSN = MAX(LSANSN,NNOCO)
      CALL WKVECT('&&SURFCO.TRAVNO','V V K8',LSANSN,JNOMNO)
C
C --- QUELQUES VERIFS
C             
      IF (NZOCO.NE.ZI(JDIM+1))                CALL ASSERT(.FALSE.)
      IF (NSUCO.NE.ZI(JZONE+NZOCO))           CALL ASSERT(.FALSE.)
      IF (NMACO.NE.ZI(JSUMA+ZI(JZONE+NZOCO))) CALL ASSERT(.FALSE.)
      IF (NNOCO.NE.ZI(JSUNO+ZI(JZONE+NZOCO))) CALL ASSERT(.FALSE.) 
C 
C --- IMPRESSIONS POUR L'UTILISATEUR
C 
      WRITE (IFM,*)      
      WRITE (IFM,*) '<CONTACT> INFOS GENERALES SUR LES FORMULATIONS'//
     &              ' MAILLEES'      
      WRITE (IFM,*) 
C
C --- PARAMETRES GENERAUX D'APPARIEMENT
C
      WRITE (IFM,*) '<CONTACT> ... PARAMETRES GENERAUX APPARIEMENT'
      DO 9 IZONE = 1,NZOCO 
        WRITE (IFM,*) '<CONTACT> ...... ZONE : ',IZONE         
        WRITE (IFM,1070) 'PROJECTION      ',ZI(JMETH+ZMETH*(IZONE-1)+4)
        WRITE (IFM,1070) 'NORMALE         ',ZI(JMETH+ZMETH*(IZONE-1)+8)
        WRITE (IFM,1070) 'LISSAGE         ',ZI(JMETH+ZMETH*(IZONE-1)+10)
        WRITE (IFM,1070) 'TYPE_APPA       ',ZI(JMETH+ZMETH*(IZONE-1)+9)
        IF (ZI(JMETH+ZMETH*(IZONE-1)+9).EQ.1) THEN
          WRITE (IFM,1072) 'DIRE_APPA       ',ZR(JDIRAP+3*(IZONE-1)),
     &          ZR(JDIRAP+3*(IZONE-1)+1),ZR(JDIRAP+3*(IZONE-1)+2)
        ENDIF
        WRITE (IFM,1070) 'VECT_MAIT       ',ZI(JMETH+ZMETH*(IZONE-1)+11)
        IF (ZI(JMETH+ZMETH*(IZONE-1)+11).EQ.1) THEN
          WRITE (IFM,1072) 'MAIT_FIXE       ',
     &          ZR(JDIRNO+ZDIRN*(IZONE-1)),
     &          ZR(JDIRNO+ZDIRN*(IZONE-1)+1),
     &          ZR(JDIRNO+ZDIRN*(IZONE-1)+2)
        ENDIF        
        IF (ZI(JMETH+ZMETH*(IZONE-1)+11).EQ.2) THEN
          WRITE (IFM,1072) 'MAIT_VECT_Y     ',
     &          ZR(JDIRNO+ZDIRN*(IZONE-1)),
     &          ZR(JDIRNO+ZDIRN*(IZONE-1)+1),
     &          ZR(JDIRNO+ZDIRN*(IZONE-1)+2)
        ENDIF          
        WRITE (IFM,1070) 'VECT_ESCL       ',ZI(JMETH+ZMETH*(IZONE-1)+12)
        IF (ZI(JMETH+ZMETH*(IZONE-1)+12).EQ.1) THEN
          WRITE (IFM,1072) 'ESCL_FIXE       ',
     &          ZR(JDIRNO+ZDIRN*(IZONE-1)+3),
     &          ZR(JDIRNO+ZDIRN*(IZONE-1)+4),
     &          ZR(JDIRNO+ZDIRN*(IZONE-1)+5)
        ENDIF        
        IF (ZI(JMETH+ZMETH*(IZONE-1)+12).EQ.2) THEN
          WRITE (IFM,1072) 'ESCL_VECT_Y     ',
     &          ZR(JDIRNO+ZDIRN*(IZONE-1)+3),
     &          ZR(JDIRNO+ZDIRN*(IZONE-1)+4),
     &          ZR(JDIRNO+ZDIRN*(IZONE-1)+5)
        ENDIF  
        WRITE (IFM,1071) 'TOLE_PROJ_EXT   ',ZR(JTOLE+ZTOLE*(IZONE-1))
  9   CONTINUE 
C  
 1070 FORMAT (' <CONTACT> ...... PARAM. : ',A16,' - VAL. : ',I5)
 1071 FORMAT (' <CONTACT> ...... PARAM. : ',A16,' - VAL. : ',E12.5)
 1072 FORMAT (' <CONTACT> ...... PARMA. : ',A16,' - VAL. : ',E12.5,
     &        E12.5,E12.5)     
C 
C --- IMPRESSIONS POUR LES JEUX SUPPS
C 
      WRITE (IFM,*) '<CONTACT> ... JEU SUPP.' 
      DO 21 IZONE = 1,NZOCO
        CHAIN1 = ZK8(JJFO1+IZONE-1)
        CHAIN2 = ZK8(JJFO2+IZONE-1)
        CHAIN3 = ZK8(JJFO3+IZONE-1)
        JSUP   = ZR(JJSUP+IZONE-1)      
        WRITE (IFM,*) '<CONTACT> ...... ZONE : ',IZONE          
        WRITE (IFM,1030) JSUP
        IF (CHAIN1.NE.' ') THEN
          WRITE (IFM,1031) CHAIN1
        ENDIF
        IF (CHAIN2.NE.' ') THEN
          WRITE (IFM,1032) CHAIN2
        ENDIF 
        IF (CHAIN3.NE.' ') THEN
          WRITE (IFM,1033) CHAIN3
        ENDIF               
 21   CONTINUE      
C          
 1030 FORMAT (' <CONTACT> ...... TYPE REEL              : ',E12.5) 
 1031 FORMAT (' <CONTACT> ...... TYPE FONC. SUR ESCLAVE : ',A8) 
 1032 FORMAT (' <CONTACT> ...... TYPE FONC. SUR MAITRE  : ',A8)
 1033 FORMAT (' <CONTACT> ...... TYPE FONC. INSTANT     : ',A8)     
C
C --- IMPRESSIONS POUR LES POUTRES
C 
      IF ( (ZR(JJPOU-1+NNOCO+1).GT.0.5D0)) THEN
        WRITE (IFM,*) '<CONTACT> ... JEU SUPP. PAR DIST_POUT'
        DO 60 INO = 1 , NNOCO
          IF ( ZR(JJPOU-1+INO).EQ.0.D0 ) GOTO 60
          CALL JENUNO(JEXNUM(NOEUMA,ZI(JNOCO-1+INO)),NOMNOE)
          WRITE (IFM,1014) NOMNOE, ZR(JJPOU-1+INO)
 60     CONTINUE
      END IF
C 
C --- IMPRESSIONS POUR LES COQUES
C 
      IF ( (ZR(JJCOQ-1+NNOCO+1).GT.0.5D0)) THEN
        WRITE (IFM,*) '<CONTACT> ... JEU SUPP. PAR DIST_COQUE'
        DO 70 INO = 1 , NNOCO
          IF ( ZR(JJCOQ-1+INO).EQ.0.D0 ) GOTO 70
          CALL JENUNO(JEXNUM(NOEUMA,ZI(JNOCO-1+INO)),NOMNOE)
          WRITE (IFM,1016) NOMNOE, ZR(JJCOQ-1+INO)
 70     CONTINUE
      END IF
C      
 1014 FORMAT (' <CONTACT> ...... NOEUD: ',A8,' RAYON_POUTRE  : ',E12.5)
 1016 FORMAT (' <CONTACT> ...... NOEUD: ',A8,' DEMI-EPAISSEUR: ',E12.5)
       
C 
C --- IMPRESSIONS GLOBALES (TOUTES ZONES)
C 
      WRITE (IFM,*)      
      WRITE (IFM,*) '<CONTACT> INFOS SPECIFIQUES SUR LES FORMULATIONS'//
     &              ' MAILLEES'
      WRITE (IFM,*) 
C       
      WRITE (IFM,*) '<CONTACT> DIMENSION DE L''ESPACE   : ',
     &               NDIM
      WRITE (IFM,*) '<CONTACT> NBRE ZONES DE CONTACT   : ', 
     &               NZOCO
      WRITE (IFM,*) '<CONTACT> NBRE SURF. DE CONTACT   : ', 
     &               NSUCO
      WRITE (IFM,*) '<CONTACT> NBRE MAIL. DE CONTACT   : ',
     &               NMACO
      WRITE (IFM,*) '<CONTACT> NBRE NOEU. DE CONTACT   : ',
     &               NNOCO
      WRITE (IFM,*) '<CONTACT> NBRE NOEUD ESCL. TOTAL  : ', 
     &               NESMAX
      WRITE (IFM,*) '<CONTACT> NBRE NOEU. ESCL./ZONE   : ',
     &      (ZI(JDIM+8+K), K = 1,ZI(JDIM+1))
C         
      WRITE (IFM,*)      
      WRITE (IFM,*) '<CONTACT> INFOS SUR LES SURFACES MAILLEES '
      WRITE (IFM,*)

      DO 10 IZONE = 1,NZOCO

        WRITE (IFM,*) '<CONTACT> ZONE : ',IZONE
C
        NBSURF = ZI(JZONE+IZONE) - ZI(JZONE+IZONE-1)
        IF (NBSURF.GT.2) CALL ASSERT(.FALSE.)
        WRITE (IFM,*) '<CONTACT> ... NOMBRE DE SURFACES         : ', 
     &                  NBSURF
C     
        NBMAIL = ZI(JSUMA+ZI(JZONE+IZONE))-ZI(JSUMA+ZI(JZONE+IZONE-1))
        WRITE (IFM,*) '<CONTACT> ... NOMBRE DE MAILLES          : ',
     &                  NBMAIL
C     
        NBNOEU = ZI(JSUNO+ZI(JZONE+IZONE))-ZI(JSUNO+ZI(JZONE+IZONE-1))
        WRITE (IFM,*) '<CONTACT> ... NOMBRE DE NOEUDS           : ',
     &                  NBNOEU

        DO 20 ISURF = 1,NBSURF
          WRITE (IFM,*) '<CONTACT> ...... SURFACE : ',ISURF         
          ISUCO  = ISUCO + 1
          NBMA   = ZI(JSUMA+ISUCO) - ZI(JSUMA+ISUCO-1)
          NBNO   = ZI(JSUNO+ISUCO) - ZI(JSUNO+ISUCO-1)
          JDECMA = ZI(JSUMA+ISUCO-1)
          JDECNO = ZI(JSUNO+ISUCO-1)
C                     
          IF (NBMA .LE. 1) THEN 
            CHAIN1 = ' MAILLE '
          ELSE
            CHAIN1 = ' MAILLES'
          ENDIF  
          IF (NBNO .LE. 1) THEN
            CHAIN2 = ' NOEUD  '
          ELSE
            CHAIN2 = ' NOEUDS '
          ENDIF 
C           
          WRITE (IFM,1035) NBMA, CHAIN1,' ET ',NBNO,CHAIN2
C          
          DO 30 IMA = 1,NBMA
            NUMMA = ZI(JMACO+JDECMA+IMA-1)
            CALL JENUNO(JEXNUM(MAILMA,NUMMA),ZK8(JNOMMA+IMA-1))
 30       CONTINUE
          WRITE (IFM,1040) '     LISTE DES MAILLES : '
          WRITE (IFM,1050) (ZK8(JNOMMA+IMA-1), IMA = 1,NBMA)
C          
          DO 40 INO = 1,NBNO
            NUMNO = ZI(JNOCO+JDECNO+INO-1)
            CALL JENUNO(JEXNUM(NOEUMA,NUMNO),ZK8(JNOMNO+INO-1))
 40       CONTINUE
          WRITE (IFM,1040) '     LISTE DES NOEUDS  : '
          WRITE (IFM,1050) (ZK8(JNOMNO+INO-1), INO = 1,NBNO)          
 20     CONTINUE
C
C --- NOEUDS MILIEUX DES MAILLES QUADRATIQUES
C         
        CALL JEEXIN(CONOQU,IRET)
        IF (IRET.NE.0) THEN
          CALL JEVEUO(CONOQU,'L',JNOQU)
          CALL JELIRA(CONOQU,'LONUTI',NBELQU,K8BID)
          NBELQU = NBELQU/3
          DO 45 INO = 1,NBELQU
            NUMNO = ZI(JNOQU+3*(INO-1)-1+1)
            CALL JENUNO(JEXNUM(NOEUMA,NUMNO),ZK8(JNOMNO+INO-1))   
 45       CONTINUE
          WRITE (IFM,1040) '     LISTE DES MILIEUX : '
          WRITE (IFM,1050) (ZK8(JNOMNO+INO-1), INO = 1,NBELQU) 
        ENDIF  
C 
C --- NOEUDS EXCLUS
C    
        NBSANS = ZI(JPSANS+IZONE) - ZI(JPSANS+IZONE-1)
        IF (NBSANS.EQ.0) THEN
          WRITE (IFM,*) '<CONTACT> ... PAS DE NOEUDS A EXCLURE '
        ELSE
          WRITE (IFM,*) '<CONTACT> ... NOMBRE DE NOEUDS A EXCLURE : ',
     &                    NBSANS
C        
          JDEC = ZI(JPSANS+IZONE-1)
          DO 50 INO = 1,NBSANS
            NUMNEX = ZI(JSANS+JDEC+INO-1)
            CALL JENUNO(JEXNUM(NOEUMA,NUMNEX),ZK8(JNOMNO+INO-1))
  50      CONTINUE   
C  
          WRITE (IFM,1040) '     LISTE DES NOEUDS  : '
          WRITE (IFM,1050) (ZK8(JNOMNO+INO-1), INO = 1,NBSANS)          
        ENDIF  
 10   CONTINUE
C
 1035 FORMAT (' <CONTACT> ...... ',I5,A8,A4,I5,A8)
 1040 FORMAT (' <CONTACT> ...... ',A25)       
 1050 FORMAT ((' <CONTACT> ...... ',17X,4(A8,1X)))       
C 
C --- IMPRESSIONS GLOBALES (TOUTES ZONES)
C 
      WRITE (IFM,*)      
      WRITE (IFM,*) '<CONTACT> INFOS SPECIFIQUES SUR LES FORMULATIONS'//
     &              ' MAILLEES - NIVEAU DEVELOPPEUR'
      WRITE (IFM,*) 
C
      WRITE (IFM,1080) 'PZONE  : '
      WRITE (IFM,1060) (ZI(JZONE+K), K = 0,NZOCO)

      WRITE (IFM,1080) 'PSURMA : '
      WRITE (IFM,1060) (ZI(JSUMA+K), K = 0,NSUCO)

      WRITE (IFM,1080) 'PSURNO : '
      WRITE (IFM,1060) (ZI(JSUNO+K), K = 0,NSUCO)

      WRITE (IFM,1080) 'CONTMA : '
      WRITE (IFM,1060) (ZI(JMACO+K-1), K = 1,NMACO)

      WRITE (IFM,1080) 'CONTNO : '
      WRITE (IFM,1060) (ZI(JNOCO+K-1), K = 1,NNOCO)
C  
 1060 FORMAT ((' <CONTACT> ',9X,6(I7,1X)))    
 1080 FORMAT (' <CONTACT> ',A9)
C 
      WRITE (IFM,*)      
      WRITE (IFM,*) '<CONTACT> INFOS SPECIFIQUES SUR LES FORMULATIONS'//
     &              ' MAILLEES - CONNECTIVITES INVERSES'
      WRITE (IFM,*) 
C  
      WRITE (IFM,*) '<CONTACT> NBRE CONNEC. INV        : ',
     &               NMANO
      WRITE (IFM,*) '<CONTACT> NNOMA                   : ', 
     &               NNOMA
      WRITE (IFM,*) '<CONTACT> NMAMA                   : ',
     &               NMAMA    
C
      WRITE (IFM,2080) 'MANOCO : '
      WRITE (IFM,2060) (ZI(JMANO+K-1), K = 1,NMANO)
      WRITE (IFM,2080) 'PMANO  : '
      WRITE (IFM,2060) (ZI(JPOMA+K), K = 0,NNOCO)
      WRITE (IFM,2080) 'NOMACO : '
      WRITE (IFM,2060) (ZI(JNOMA+K-1), K = 1,NNOMA)
      WRITE (IFM,2080) 'PNOMA  : '
      WRITE (IFM,2060) (ZI(JPONO+K), K = 0,NMACO)
      WRITE (IFM,2080) 'MAMACO : '
      WRITE (IFM,2060) (ZI(JMAMA+K-1), K = 1,NMAMA)
      WRITE (IFM,2080) 'PMAMA  : '
      WRITE (IFM,2060) (ZI(JPOIN+K), K = 0,NMACO)
      WRITE (IFM,2080) 'NOZOCO : '
      WRITE (IFM,2060) (ZI(JZOCO+K-1), K = 1,NNOCO)
      WRITE (IFM,*)
C
 2060 FORMAT ((' <CONTACT> ',9X,6(I7,1X)))
 2080 FORMAT (' <CONTACT> ',A9)
C 
C --- MENAGE
C
      CALL JEDETR('&&SURFCO.TRAVMA')
      CALL JEDETR('&&SURFCO.TRAVNO')
 999  CONTINUE
      CALL JEDEMA
      END
