      SUBROUTINE W18IMP(LIGREL,NOMA,NOMO)
      IMPLICIT NONE
      CHARACTER*19 LIGREL
      CHARACTER*8 NOMA,NOMO
C RESPONSABLE PELLET J.PELLET
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 08/02/2010   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2010  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C     ------------------------------------------------------------------
C     AFFE_MODELE IMPRESSION SUR 'MESSAGE' DES ELEMENTS FINIS AFFECTES
C     ------------------------------------------------------------------
C     ----- DEBUT COMMUNS NORMALISES  JEVEUX  --------------------------
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
      CHARACTER*32 JEXNOM,JEXNUM
C     -----  FIN  COMMUNS NORMALISES  JEVEUX  --------------------------

      INTEGER IFM,NUMVEC,NBGREL,I,NMGREL,NUMAIL,JDLI,NUTYPM
      INTEGER NTYPOI,JDTM,NUTYPE,IBID,JC,J,NUMNOE,IMPVER
      INTEGER JDNW,K,NIV,IEXI
      CHARACTER*8 K8B
      CHARACTER*8 TYPEMA,NOMAIL,TABMAI(8)
      CHARACTER*16 TYPELE,TYPEMO
      CHARACTER*24 NOMMAI,NOMNOE
      CHARACTER*32 PHEMOD

      NOMMAI=NOMA//'.NOMMAI'
      NOMNOE=NOMA//'.NOMNOE'

      CALL INFNIV(IFM,NIV)
      IMPVER=0
      IF (NIV.EQ.2)IMPVER=1

      WRITE (IFM,9000)
      NUMVEC=1
      TYPEMO=' '

      CALL JEEXIN(LIGREL//'.LIEL',IEXI)
      IF (IEXI.EQ.0) GOTO 9999


      CALL JELIRA(LIGREL//'.LIEL','NMAXOC',NBGREL,K8B)
      CALL JEVEUO(LIGREL//'.LIEL','L',JDLI)
      CALL JEVEUO(NOMA//'.TYPMAIL','L',JDTM)
      CALL JEEXIN(NOMO//'.MODELE    .NEMA',IEXI)
      JDNW=0
      IF (IEXI.GT.0) CALL JEVEUO(NOMO//'.MODELE    .NEMA','L',JDNW)
      CALL JENONU(JEXNOM('&CATA.TM.NOMTM','POI1'),NTYPOI)

      DO 20 I=1,NBGREL
        CALL JELIRA(JEXNUM(LIGREL//'.LIEL',I),'LONMAX',NMGREL,K8B)
        NUMAIL=ZI(JDLI+NUMVEC-1)
        IF (NUMAIL.LT.0) THEN
          NUTYPM=NTYPOI
        ELSE
          NUTYPM=ZI(JDTM+NUMAIL-1)
        ENDIF
        NUTYPE=ZI(JDLI+NUMVEC+NMGREL-2)
        CALL JENUNO(JEXNUM('&CATA.TM.NOMTM',NUTYPM),TYPEMA)
        CALL JENUNO(JEXNUM('&CATA.TE.NOMTE',NUTYPE),TYPELE)
        IF (TYPELE.EQ.'MECA_HEXS8') CALL U2MESS('A','ELEMENTS4_74')

        CALL DISMOI('F','PHEN_MODE',TYPELE,'TYPE_ELEM',IBID,PHEMOD,IBID)
        TYPEMO=PHEMOD(17:32)
        IF (PHEMOD(1:10).EQ.'#PLUSIEURS')TYPEMO=' '
        WRITE (IFM,9010)TYPEMO,TYPELE,TYPEMA,NMGREL-1
        IF (IMPVER.EQ.1) THEN
          IF (NUMAIL.LT.0) THEN
            WRITE (IFM,9020)
          ELSE
            WRITE (IFM,9030)
          ENDIF
          JC=0
          DO 10 J=JDLI+NUMVEC-1,JDLI+NUMVEC+NMGREL-3
            JC=JC+1
            NUMAIL=ZI(J)
            IF (NUMAIL.LT.0) THEN
              CALL ASSERT(JDNW.NE.0)
              NUMNOE=ZI(JDNW+(-NUMAIL*2-1)-1)
              CALL JENUNO(JEXNUM(NOMNOE,NUMNOE),NOMAIL)
            ELSE
              CALL JENUNO(JEXNUM(NOMMAI,NUMAIL),NOMAIL)
            ENDIF
            IF (JC.EQ.8 .OR. J.EQ.JDLI+NUMVEC+NMGREL-3) THEN
              TABMAI(JC)=NOMAIL
              WRITE (IFM,9040)(TABMAI(K),K=1,JC)
              JC=0
            ELSE
              TABMAI(JC)=NOMAIL
            ENDIF
   10     CONTINUE
        ENDIF
        NUMVEC=NUMVEC+NMGREL
   20 CONTINUE


 9999 CONTINUE


 9000 FORMAT (/,'    MODELISATION      ELEMENT FINI      TYPE MAILLE',
     &       '          NOMBRE')
 9010 FORMAT (4X,A16,2X,A16,2X,A8,7X,I12)
 9020 FORMAT (6X,'LISTE DES NOEUDS AFFECTES:')
 9030 FORMAT (6X,'LISTE DES MAILLES AFFECTEES:')
 9040 FORMAT (4X,8(2X,A8))
      END
