      SUBROUTINE FENEXC(NOMA,NOMNOA,LONG,NBN,NUNO,DIAX,
     &                  NBNFEN,NOEFEN,DISFEN)
      IMPLICIT REAL*8 (A-H,O-Z)
C-----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF MODELISA  DATE 03/11/97   AUTEUR KXBADNG N.GAY 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C-----------------------------------------------------------------------
C     EXTRACTION DE LA FENETRE EXCITEE SUR LA STRUCTURE
C     APPELANT : SPECFF
C-----------------------------------------------------------------------
C IN  : NOMA   : CHARACTER*8 , NOM DU CONCEPT MAILLAGE
C IN  : NOMNOA : CHARACTER*8 , NOM DU NOEUD D'APPLICATION (CENTRE DE
C                LA FENETRE EXCITEE)
C IN  : LONG   : REAL*8 , LONGUEUR EXCITEE
C IN  : NBN    : INTEGER , NOMBRE DE NOEUDS DU MAILLAGE
C IN  : NUNO   : INTEGER , VECTEUR DE DIM = NBRE DE NOEUDS DU MAILLAGE
C                LISTE DES NUMEROS DES NOEUDS DU MAILLAGE, REORDONNEE
C                PAR VALEURS CROISSANTES DE LA COORDONNEE D'ESPACE LE
C                LONG DE L'AXE DIRECTEUR DE LA STRUCTURE
C IN  : DIAX   : REAL*8 , VECTEUR DE DIM = NBRE DE NOEUDS DU MAILLAGE
C                LISTE CROISSANTE DES VALEURS DE LA COORDONNEE D'ESPACE
C                LE LONG DE L'AXE DIRECTEUR DE LA STRUCTURE
C OUT : NBNFEN : INTEGER , NOMBRE DE NOEUDS DU MAILLAGE APPARTENANT
C                A LA FENETRE EXCITEE
C OUT : NOEFEN : INTEGER , VECTEUR DE DIM = NBRE DE NOEUDS DU MAILLAGE
C                LISTE DES NUMEROS DES NOEUDS DU MAILLAGE APPARTENANT
C                A LA FENETRE EXCITEE, ORDONNEE PAR VALEURS CROISSANTES
C                DE LA COORDONNEE D'ESPACE LE LONG DE L'AXE DIRECTEUR
C                DE LA STRUCTURE
C OUT : DISFEN : REAL*8 , VECTEUR DE DIM = NBRE DE NOEUDS DU MAILLAGE
C                DISCRETISATION SPATIALE CORRESPONDANT A LA FENETRE
C                EXCITEE, ORDONNEE PAR VALEURS CROISSANTES ET TRANSLATEE
C                POUR ETRE SUPERPOSEE A LA DISCRETISATION DES FONCTIONS
C                DE FORME ASSOCIEES A L'EXCITATION
C
C     ----- DEBUT COMMUNS NORMALISES  JEVEUX  --------------------------
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C     -----  FIN  COMMUNS NORMALISES  JEVEUX  --------------------------
C
      CHARACTER*8   NOMA, NOMNOA
      INTEGER       NBN, NUNO(*), NBNFEN, NOEFEN(*)
      REAL*8        LONG, DIAX(*), DISFEN(*)
C
      LOGICAL       LEXSEG
      CHARACTER*1   K1BID
      CHARACTER*24  CONNEX, TYPMAI, NNOEMA, NOMMAI
      CHARACTER*32  JEXNOM
C
C-----------------------------------------------------------------------
      CALL JEMARQ()
      TOL = 100.D0 * R8PREM()
C
C --- 1.EXTRACTION DU NUMERO DU NOEUD D'APPLICATION DE L'EXCITATION
C
      NNOEMA = NOMA//'.NOMNOE'
      CALL JENONU(JEXNOM(NNOEMA,NOMNOA),NUNOAP)
      IF (NUNOAP.EQ.0) THEN
        CALL UTMESS('F','FENEXC','LE NOEUD D APPLICATION DE L '//
     &       'EXCITATION N EST PAS UN NOEUD DU MAILLAGE.')
      ENDIF
C
C --- 2.DETERMINATION DU RANG DU NOEUD D'APPLICATION DANS LA LISTE
C ---   ORDONNEE DES NOEUDS DE LA STRUCTURE
C
      INDAP = 1
      XAP = 0.D0
      DO 10 INO = 1,NBN
        IF (NUNO(INO).EQ.NUNOAP) THEN
          INDAP = INO
          XAP = DIAX(INO)
          GO TO 11
        ENDIF
  10  CONTINUE
  11  CONTINUE
C
C --- 3.TESTS DE RECOUVREMENT PAR RAPPORT AU DOMAINE DE DEFINITION
C ---   DU MAILLAGE
C
      IF (INDAP.EQ.1 .OR. INDAP.EQ.NBN) THEN
        CALL UTMESS('F','FENEXC','LE NOEUD D APPLICATION DE L '//
     &       'EXCITATION NE DOIT PAS ETRE SITUE AU BORD DU DOMAINE '//
     &       'DE DEFINITION DU MAILLAGE.')
      ENDIF
C
      X1 = XAP - LONG/2.D0
      X2 = XAP + LONG/2.D0
      DIFX1 = DIAX(1) - X1
      DIFX2 = X2 - DIAX(NBN)
      IF (DIFX1.GT.TOL .OR. DIFX2.GT.TOL) THEN
        CALL UTMESS('F','FENEXC','LA FENETRE EXCITEE DEBORDE DU '//
     &       'DOMAINE DE DEFINITION DU MAILLAGE.')
      ENDIF
C
C.....DEDUCTION DE XDECAL UTILE PLUS LOIN POUR TRANSLATION DE
C.....LA DISCRETISATION
C
      XDECAL = 0.D0
      REFX1 = DBLE(ABS(X1))
      IF (REFX1.GT.TOL) XDECAL = X1
C
C --- 4.CREATION D'OBJETS DE TRAVAIL - ACCES AUX OBJETS UTILES
C
      LP = INDAP
      CALL WKVECT('&&FENEXC.TEMP.NNOP','V V I',LP,INUNOP)
      CALL WKVECT('&&FENEXC.TEMP.DISP','V V R',LP,IDISCP)
C
      LS = NBN - INDAP + 1
      CALL WKVECT('&&FENEXC.TEMP.NNOS','V V I',LS,INUNOS)
      CALL WKVECT('&&FENEXC.TEMP.DISS','V V R',LS,IDISCS)
C
      CONNEX = NOMA//'.CONNEX'
      TYPMAI = NOMA//'.TYPMAIL'
      NOMMAI = NOMA//'.NOMMAI'
      CALL JELIRA(NOMMAI,'NOMUTI',NBRMA,K1BID)
C
C --- 5.DETERMINATION DE L'ENSEMBLE DES NOEUDS APPARTENANT A LA
C ---   DEMI-FENETRE EXCITEE EN AMONT DU NOEUD CENTRAL D'APPLICATION
C
      ZI(INUNOP+LP-1) = NUNO(INDAP)
      ZR(IDISCP+LP-1) = DIAX(INDAP)
      INO = 0
      NBNOP = 1
C
C.....REPETER
C
  20  CONTINUE
      INO = INO + 1
      IF ( INO.GT.LP-1 ) GO TO 21
      NUNOG = NUNO(INDAP-INO)
      NUNOD = ZI(INUNOP+LP-NBNOP)
      IF ( NBNOP.EQ.1 ) THEN
         IF ( LEXSEG(CONNEX,TYPMAI,NBRMA,NUNOG,NUNOD) ) THEN
            ISENSP = 1
            NBNOP = NBNOP + 1
            ZI(INUNOP+LP-NBNOP) = NUNOG
            X = DIAX(INDAP-INO)
            ZR(IDISCP+LP-NBNOP) = X
            IF ( X-X1.LT.TOL ) GO TO 21
         ELSE IF ( LEXSEG(CONNEX,TYPMAI,NBRMA,NUNOD,NUNOG) ) THEN
            ISENSP = -1
            NBNOP = NBNOP + 1
            ZI(INUNOP+LP-NBNOP) = NUNOG
            X = DIAX(INDAP-INO)
            ZR(IDISCP+LP-NBNOP) = X
            IF ( X-X1.LT.TOL ) GO TO 21
         ENDIF
      ELSE IF ( ISENSP.EQ.1 ) THEN
         IF ( LEXSEG(CONNEX,TYPMAI,NBRMA,NUNOG,NUNOD) ) THEN
            NBNOP = NBNOP + 1
            ZI(INUNOP+LP-NBNOP) = NUNOG
            X = DIAX(INDAP-INO)
            ZR(IDISCP+LP-NBNOP) = X
            IF ( X-X1.LT.TOL ) GO TO 21
         ENDIF
      ELSE
         IF ( LEXSEG(CONNEX,TYPMAI,NBRMA,NUNOD,NUNOG) ) THEN
            NBNOP = NBNOP + 1
            ZI(INUNOP+LP-NBNOP) = NUNOG
            X = DIAX(INDAP-INO)
            ZR(IDISCP+LP-NBNOP) = X
            IF ( X-X1.LT.TOL ) GO TO 21
         ENDIF
      ENDIF
      GO TO 20
C
C.....SORTIE EN ERREUR FATALE LE CAS ECHEANT
C
  21  CONTINUE
      IF ( NBNOP.EQ.1 ) THEN
         CALL UTMESS('F','FENEXC','LA DEMI-FENETRE EXCITEE EN AMONT '//
     &              'DU NOEUD CENTRAL D APPLICATION N EST PAS DEFINIE.')
      ENDIF
C
      DIFX1 = ZR(IDISCP+LP-NBNOP) - X1
      IF ( DIFX1.GT.TOL ) THEN
         CALL UTMESS('F','FENEXC','LA DEMI-FENETRE EXCITEE EN AMONT '//
     &               'DU NOEUD CENTRAL D APPLICATION DEBORDE DU '//
     &               'DOMAINE DE DEFINITION DU MAILLAGE.')
      ENDIF
C
C --- 6.DETERMINATION DE L'ENSEMBLE DES NOEUDS APPARTENANT A LA
C ---   DEMI-FENETRE EXCITEE EN AVAL DU NOEUD CENTRAL D'APPLICATION
C
      ZI(INUNOS) = NUNO(INDAP)
      ZR(IDISCS) = DIAX(INDAP)
      INO = 0
      NBNOS = 1
C
C.....REPETER
C
  30  CONTINUE
      INO = INO + 1
      IF ( INO.GT.LS-1 ) GO TO 31
      NUNOG = ZI(INUNOS+NBNOS-1)
      NUNOD = NUNO(INDAP+INO)
      IF ( NBNOS.EQ.1 ) THEN
         IF ( LEXSEG(CONNEX,TYPMAI,NBRMA,NUNOG,NUNOD) ) THEN
            ISENSS = 1
            IF ( ISENSS.NE.ISENSP ) THEN
               CALL UTMESS('F','FENEXC','LES DEMI-FENETRES EXCITEES '//
     &                     'EN AVAL ET EN AMONT DU NOEUD CENTRAL '//
     &                     'D APPLICATION NE SONT PAS RACCORDEES.')
            ENDIF
            NBNOS = NBNOS + 1
            ZI(INUNOS+NBNOS-1) = NUNOD
            X = DIAX(INDAP+INO)
            ZR(IDISCS+NBNOS-1) = X
            IF ( X2-X.LT.TOL ) GO TO 31
         ELSE IF ( LEXSEG(CONNEX,TYPMAI,NBRMA,NUNOD,NUNOG) ) THEN
            ISENSS = -1
            IF ( ISENSS.NE.ISENSP ) THEN
               CALL UTMESS('F','FENEXC','LES DEMI-FENETRES EXCITEES '//
     &                     'EN AVAL ET EN AMONT DU NOEUD CENTRAL '//
     &                     'D APPLICATION NE SONT PAS RACCORDEES.')
            ENDIF
            NBNOS = NBNOS + 1
            ZI(INUNOS+NBNOS-1) = NUNOD
            X = DIAX(INDAP+INO)
            ZR(IDISCS+NBNOS-1) = X
            IF ( X2-X.LT.TOL ) GO TO 31
         ENDIF
      ELSE IF ( ISENSS.EQ.1 ) THEN
         IF ( LEXSEG(CONNEX,TYPMAI,NBRMA,NUNOG,NUNOD) ) THEN
            NBNOS = NBNOS + 1
            ZI(INUNOS+NBNOS-1) = NUNOD
            X = DIAX(INDAP+INO)
            ZR(IDISCS+NBNOS-1) = X
            IF ( X2-X.LT.TOL ) GO TO 31
         ENDIF
      ELSE
         IF ( LEXSEG(CONNEX,TYPMAI,NBRMA,NUNOD,NUNOG) ) THEN
            NBNOS = NBNOS + 1
            ZI(INUNOS+NBNOS-1) = NUNOD
            X = DIAX(INDAP+INO)
            ZR(IDISCS+NBNOS-1) = X
            IF ( X2-X.LT.TOL ) GO TO 31
         ENDIF
      ENDIF
      GO TO 30
C
C.....SORTIE EN ERREUR FATALE LE CAS ECHEANT
C
  31  CONTINUE
      IF ( NBNOS.EQ.1 ) THEN
         CALL UTMESS('F','FENEXC','LA DEMI-FENETRE EXCITEE EN AVAL '//
     &              'DU NOEUD CENTRAL D APPLICATION N EST PAS DEFINIE.')
      ENDIF
C
      DIFX2 = X2 - ZR(IDISCS+NBNOS-1)
      IF ( DIFX2.GT.TOL ) THEN
         CALL UTMESS('F','FENEXC','LA DEMI-FENETRE EXCITEE EN AVAL '//
     &               'DU NOEUD CENTRAL D APPLICATION DEBORDE DU '//
     &               'DOMAINE DE DEFINITION DU MAILLAGE.')
      ENDIF
C
C --- 7.AFFECTATION DES RESULTATS
C
C.....NOMBRE DE NOEUDS APPARTENANT A LA FENETRE EXCITEE
C
      NBNFEN = NBNOP + NBNOS - 1
C
C.....LISTE DES NUMEROS DES NOEUDS APPARTENANT A LA FENETRE EXCITEE
C.....DISCRETISATION SPATIALE CORRESPONDANTE (TRANSLATEE)
C
      DO 40 INO = 1, NBNOP
         NOEFEN(INO) = ZI(INUNOP+LP-NBNOP+INO-1)
         DISFEN(INO) = ZR(IDISCP+LP-NBNOP+INO-1) - XDECAL
  40  CONTINUE
      DO 50 INO = 2, NBNOS
         NOEFEN(NBNOP+INO-1) = ZI(INUNOS+INO-1)
         DISFEN(NBNOP+INO-1) = ZR(IDISCS+INO-1) - XDECAL
  50  CONTINUE
C
      CALL JEDETC('V','&&FENEXC',1)
      CALL JEDEMA()
      END
