      SUBROUTINE XPOMAX(MO,MALINI,MAILX,NBNOC,NBMAC,PREFNO,NOGRFI,
     &                  MAXFEM,CNS1,CNS2,CES1,CES2,CESVI1,CESVI2,
     &                  LISTGR,DIRGRM,NIVGRM,RESUCO)

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF PREPOST  DATE 21/12/2010   AUTEUR MASSIN P.MASSIN 
C ======================================================================
C COPYRIGHT (C) 1991 - 2010  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE GENIAUT S.GENIAUT
C TOLE CRP_20
      IMPLICIT NONE

      INTEGER       NBNOC,NBMAC
      CHARACTER*2   PREFNO(4)
      CHARACTER*8   MO,MALINI,MAXFEM,NOGRFI,RESUCO
      CHARACTER*19  CNS1,CNS2,CES1,CES2,CESVI1,CESVI2
      CHARACTER*24  MAILX,LISTGR,DIRGRM,NIVGRM
C
C      TRAITEMENT DES MAILLES DE MAILX
C       - POUR POST_MAIL_XFEM : CREATION DES MAILLES CORRESPONDANTS
C                               AUX SOUS-ELEMENTS ET CREATION DES NOEUDS
C                               CORRESPONDANTS AUX SOMMETS DES SOUS
C                               ELEMENTS
C       - POUR POST_CHAM_XFEM : CALCUL DES DEPLACEMENTS AUX NOUVEAUX
C                               NOEUDS DE MAXFEM

C
C   IN
C       MO     : MODELE FISSURE
C       MALINI : MAILLAGE SAIN
C       MAILX  : LISTE DES NUMEROS DES MAILLES SOUS-DECOUPEES
C       NBNOC  : NOMBRE DE NOEUDS CLASSIQUES DU MAILLAGE FISSURE
C       NBMAC  : NOMBRE DE MAILLES CLASSIQUES DU MAILLAGE FISSURE
C       PREFNO : PREFERENCES POUR LE NOMAGE DES NOUVELLES ENTITES
C       NOGRFI : NOM DU GROUPE DE NOEUDS SUR LA FISSURE A CREER
C       MAXFEM : MAILLAGE FISSURE (SI POST_CHAMP_XFEM)
C       CNS1   : CHAMP_NO_S DU DEPLACEMENT EN ENTREE
C       CES1   : CHAMP_ELEM_S DE CONTRAINTES EN ENTREE
C       LISTGR : LISTE DES GROUPES CONTENANT CHAQUE MAILLE
C       DIRGRM : VECTEUR D'INDIRECTION ENTRE LES GROUP_MA
C       NIVGRM : VECTEUR DE REMPLISSAGE DES GROUP_MA DE MAXFEM
C       RESUCO : NOM DU CONCEPT RESULTAT DONT ON EXTRAIT LES CHAMPS
C   OUT
C       MAXFEM : MAILLAGE FISSURE (SI POST_MAIL_XFEM)
C       CNS2   : CHAMP_NO_S DU DEPLACEMENT EN SORTIE
C       CES2   : CHAMP_ELEM_S DE CONTRAINTES EN SORTIE
C       NIVGRM : VECTEUR DE REMPLISSAGE DES GROUP_MA DE MAXFEM



C -------------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ----------------
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
      CHARACTER*32 JEXNOM,JEXNUM,JEXATR
C---------------- FIN COMMUNS NORMALISES  JEVEUX  ----------------------


      INTEGER       I,IER,JMAX,NBMAX,ICH,IMA,NSE,ISE,IN
      INTEGER       JCESD(11),JCESV(11),JCESL(11),IAD,JCONX1,JCONX2
      INTEGER       J,INO,N,JDIRNO,JLSN,INN,NNN,NBNOMA,NFISS,IFISS
      INTEGER       IACOO1,IACOO2,NDIM,IAD2,INNTOT,NDIME,JTMDIM,INM
      INTEGER       JTYPM1,JTYPM2,INMTOT,ITYPSE(6),IAD1,IADC,IADV
      INTEGER       JCNSE,IAD4,IAD3,JMAIL,ITYPEL,NBELR,JHEA
      INTEGER       IGEOM,NFH,IFH,NFE,DDLC,CMP(50),JCNSD1,JLST,JFISNO
      INTEGER       NBCMP,JCNSV1,JCNSV2,IAD5,NBNOFI,INOFI,NBNOLA,INOLA
      INTEGER       JCNSL2,JCESV1,JCESD1,JCESL1,JCESV2,JCESD2,JCESL2
      INTEGER       JCVIV1,JCVID1,JCVIL1,JCVIV2,JCVID2,JCVIL2,NINTER
      INTEGER       JNIVGR,IAGMA,NGRM,JDIRGR,IAGNO,IAGNOL,IAD10,IAD11
      CHARACTER*8   K8B,ENTITE,TYPESE(6),ELREFP,LIREFE(10),NOGNO
      CHARACTER*8   TYPMA,NOMA
      CHARACTER*16  TYSD,K16B,NOMCMD,NOTYPE
      CHARACTER*19  CHS(11),CNSLN,CNSLT
      CHARACTER*24  DIRNO,GEOM,LINOFI,GRPNOE,LSN,LST,FISNO,HEA
      LOGICAL       OPMAIL,ISMALI
      INTEGER       IAD9,IRESE,NNOSE,TABSE(6),NCOMP
      INTEGER       IVIEX,IRET,JTYPMA,JCONQ1,JCONQ2,JCNSK1

      DATA  TYPESE /'SEG2','TRIA3','TETRA4','SEG3','TRIA6','TETRA4'/
      DATA  TABSE  /   2  ,   3   ,   4    ,   3  ,   6   ,   4    /

      CALL JEMARQ()

      CALL JEEXIN(MAILX,IER)
      IF (IER.EQ.0) GO TO 999

C     ------------------------------------------------------------------
C                   RECUPERATION DES OBJETS JEVEUX
C     ------------------------------------------------------------------

      CALL DISMOI('F','DIM_GEOM',MALINI,'MAILLAGE',NDIM,K8B,IER)

C     NOM DE LA COMMANDE (POST_MAIL_XFEM OU POST_CHAM_XFEM)
      CALL GETRES(K8B,K16B,NOMCMD)
      IF (NOMCMD.EQ.'POST_MAIL_XFEM') THEN
        OPMAIL = .TRUE.
      ELSEIF (NOMCMD.EQ.'POST_CHAM_XFEM') THEN 
        OPMAIL = .FALSE.
      ENDIF

      CALL JEVEUO(MAILX,'L',JMAX)
      CALL JELIRA(MAILX,'LONMAX',NBMAX,K8B)

      CHS(1)  = '&&XPOMAX.PINTTO'
      CHS(2)  = '&&XPOMAX.CNSETO'
      CHS(3)  = '&&XPOMAX.LONCHA'
      CHS(4)  = '&&XPOMAX.HEAV'
      CHS(6)  = '&&XPOMAX.CNSLN'
      CHS(7)  = '&&XPOMAX.CNSLT'
      CHS(8)  = '&&XPOMAX.CNSFI'
      CHS(9)  = '&&XPOMAX.PMILTO'
      CHS(10) = '&&XPOMAX.PLONCH'
      CHS(11) = '&&XPOMAX.PAIN'


      CALL CELCES(MO//'.TOPOSE.PIN','V', CHS(1))
      CALL CELCES(MO//'.TOPOSE.CNS','V', CHS(2))
      CALL CELCES(MO//'.TOPOSE.LON','V', CHS(3))
      CALL CELCES(MO//'.TOPOSE.HEA','V', CHS(4))
      CALL CELCES(MO//'.LNNO','V', CHS(6))
      CALL CELCES(MO//'.LTNO','V', CHS(7))

      CALL JEEXIN(MO//'.FISSNO    .CELD',IER)
      IF (IER.NE.0) THEN
        CALL CELCES(MO//'.FISSNO','V', CHS(8))
        CALL JEVEUO(CHS(8)//'.CESD','L',JCESD(8))
        CALL JEVEUO(CHS(8)//'.CESV','E',JCESV(8))
        CALL JEVEUO(CHS(8)//'.CESL','L',JCESL(8))
      ENDIF

      CALL JEEXIN(MO//'.TOPOSE.PMI.CELD',IER)
      IF (IER.NE.0) THEN
        CALL CELCES(MO//'.TOPOSE.PMI','V', CHS(9))
        CALL JEVEUO(CHS(9)//'.CESD','L',JCESD(9))
        CALL JEVEUO(CHS(9)//'.CESV','E',JCESV(9))
        CALL JEVEUO(CHS(9)//'.CESL','L',JCESL(9))
      ENDIF

C      CALL JEEXIN(MO//'.TOPOFAC.LO.CELD',IER)
C      IF (IER.NE.0) THEN
        CALL CELCES(MO//'.TOPOFAC.LO','V', CHS(10))
        CALL JEVEUO(CHS(10)//'.CESD','L',JCESD(10))
        CALL JEVEUO(CHS(10)//'.CESV','E',JCESV(10))
        CALL JEVEUO(CHS(10)//'.CESL','L',JCESL(10))
C      ENDIF

C      CALL JEEXIN(MO//'.TOPOFAC.AI.CELD',IER)
C      IF (IER.NE.0) THEN
        CALL CELCES(MO//'.TOPOFAC.AI','V', CHS(11))
        CALL JEVEUO(CHS(11)//'.CESD','L',JCESD(11))
        CALL JEVEUO(CHS(11)//'.CESV','E',JCESV(11))
        CALL JEVEUO(CHS(11)//'.CESL','L',JCESL(11))
C      ENDIF

      DO 10 ICH=1,4
        CALL JEVEUO(CHS(ICH)//'.CESD','L',JCESD(ICH))
        CALL JEVEUO(CHS(ICH)//'.CESV','E',JCESV(ICH))
        CALL JEVEUO(CHS(ICH)//'.CESL','L',JCESL(ICH))
 10   CONTINUE
      DO 11 ICH=6,7
        CALL JEVEUO(CHS(ICH)//'.CESD','L',JCESD(ICH))
        CALL JEVEUO(CHS(ICH)//'.CESV','E',JCESV(ICH))
        CALL JEVEUO(CHS(ICH)//'.CESL','L',JCESL(ICH))
 11   CONTINUE

      CALL JEVEUO(MALINI//'.CONNEX','L',JCONX1)
      CALL JEVEUO(JEXATR(MALINI//'.CONNEX','LONCUM'),'L',JCONX2)

      CALL JEVEUO(MALINI//'.COORDO    .VALE','L',IACOO1)
      CALL JEVEUO(MAXFEM//'.COORDO    .VALE','E',IACOO2)

      CALL JEVEUO('&CATA.TM.TMDIM','L',JTMDIM)
      CALL JEVEUO(MALINI//'.TYPMAIL','L',JTYPM1)
      CALL JEVEUO(MAXFEM//'.TYPMAIL','E',JTYPM2)
      CALL JEVEUO(MO//'.MAILLE','L',JMAIL)

      IF (.NOT.OPMAIL) THEN
        CALL JEVEUO(CNS1//'.CNSK','L',JCNSK1)
        CALL JEVEUO(CNS1//'.CNSD','L',JCNSD1)
        CALL JEVEUO(CNS1//'.CNSV','L',JCNSV1)
        CALL JEVEUO(CNS2//'.CNSV','E',JCNSV2)
        CALL JEVEUO(CNS2//'.CNSL','E',JCNSL2)

C  -----SI ON N'A PAS UNE MODE_MECA

        CALL GETTCO(RESUCO,TYSD)
        IF (TYSD(1:9).NE.'MODE_MECA') THEN
          CALL JEVEUO(CES1//'.CESV','L',JCESV1)
          CALL JEVEUO(CES1//'.CESD','L',JCESD1)
          CALL JEVEUO(CES1//'.CESL','L',JCESL1)
          CALL JEVEUO(CES2//'.CESV','E',JCESV2)
          CALL JEVEUO(CES2//'.CESD','L',JCESD2)
          CALL JEVEUO(CES2//'.CESL','E',JCESL2)
        
          CALL JEEXIN(CESVI1//'.CESV',IRET)
          IF(IRET .NE. 0) THEN      
            CALL JEVEUO(CESVI1//'.CESV','L',JCVIV1)
            CALL JEVEUO(CESVI1//'.CESD','L',JCVID1)
            CALL JEVEUO(CESVI1//'.CESL','L',JCVIL1)
          ELSE
            JCVIV1 = 0  
            JCVID1 = 0  
            JCVIL1 = 0  
          ENDIF
          IVIEX = IRET

          CALL JEEXIN(CESVI2//'.CESV',IRET)
          IF(IRET .NE. 0) THEN      
            CALL JEVEUO(CESVI2//'.CESV','E',JCVIV2)
            CALL JEVEUO(CESVI2//'.CESD','L',JCVID2)
            CALL JEVEUO(CESVI2//'.CESL','E',JCVIL2)
          ELSE
            JCVIV2 = 0  
            JCVID2 = 0  
            JCVIL2 = 0  
          ENDIF
          IVIEX = IVIEX*IRET

        ENDIF
      ENDIF

C     RECUP DES NUMEROS DES TYPE DE MAILLES DES SOUS-ELEMENTS
      CALL JENONU(JEXNOM('&CATA.TM.NOMTM',TYPESE(1)),ITYPSE(1))
      CALL JENONU(JEXNOM('&CATA.TM.NOMTM',TYPESE(2)),ITYPSE(2))
      CALL JENONU(JEXNOM('&CATA.TM.NOMTM',TYPESE(3)),ITYPSE(3))
      CALL JENONU(JEXNOM('&CATA.TM.NOMTM',TYPESE(4)),ITYPSE(4))
      CALL JENONU(JEXNOM('&CATA.TM.NOMTM',TYPESE(5)),ITYPSE(5))
      CALL JENONU(JEXNOM('&CATA.TM.NOMTM',TYPESE(6)),ITYPSE(6))

C     COMPTEURS DU NOMBRE DE NOUVEAUX NOEUDS ET MAILLES TOTAL
      INNTOT = 0
      INMTOT = 0

C     COMPTEUR ET LISTE DE NOEUDS PORTANT DES DDLS DE CONTACT
      LINOFI='&&XPOMAX.LINOFI'
C      LINOLA='&&XPOMAX.LINOLA'
      INOFI=-1
      IF (OPMAIL) THEN
        CALL DISMOI('F','NB_NO_MAILLA',MAXFEM,'MAILLAGE',
     &           NBNOFI,K8B,IRET)
        CALL WKVECT(LINOFI,'V V I',NBNOFI,INOFI)
C       CALL WKVECT(LINOLA,'V V I',NBNOFI,INOLA)
      ENDIF
      NBNOFI=0
C      NBNOLA=0

C     RECUPERATION DU VECTEUR DE REMPLISSAGE DES GROUP_MA
      IF (OPMAIL) THEN
        CALL JEEXIN(NIVGRM,IRET)
        IF (IRET.NE.0) CALL JEVEUO(NIVGRM,'E',JNIVGR)
      ENDIF

C     RECUPERATION DU VECTEUR D'INDIRECTION ENTRE LES GROUP_MA
      IF (OPMAIL) CALL JEVEUO(DIRGRM,'L',JDIRGR)

C     RECUPERATION DES INFOS DU MAILLAGE SAIN
      IF (.NOT.OPMAIL) THEN
        NOMA=ZK8(JCNSK1-1+1)
        CALL JEVEUO(NOMA//'.TYPMAIL        ','L',JTYPMA)
      ENDIF

C     ------------------------------------------------------------------
C                   BOUCLE SUR LES MAILLES DE MAILX
C     ------------------------------------------------------------------

      DO 100 I = 1,NBMAX

        IMA = ZI(JMAX-1+I)

C       COMPTEUR DES NOUVEAUX NOEUDS ET MAILLES AJOUTES (LOCAL)
        INN = 0
        INM = 0

C       RECUPERATION DU NOMBRE DE NOEUDS (2 METHODES)
        CALL JELIRA(JEXNUM(MALINI//'.CONNEX',IMA),'LONMAX',N,K8B)
        NBNOMA=ZI(JCONX2+IMA) - ZI(JCONX2+IMA-1)
        CALL ASSERT(N.EQ.NBNOMA)

C       RECUPERATION DU NOMBRE TOTAL DE SOUS ELEMENTS
C       CORRESPOD AU NOMBRE DE MAILLES À CREER
        CALL CESEXI('C',JCESD(3),JCESL(3),IMA,1,1,1,IAD3)
        CALL ASSERT(IAD3.GT.0)
        NSE=ZI(JCESV(3)-1+IAD3)

C       RECUPERATION DU NOMBRE DE POINTS D'INTERSECTION
C        NPI=ZI(JCESV(3)-1+IAD3+NIT+1)

C       RECUPERATION DU NOMBRE DE POINTS MILIEUX
C        NMI=ZI(JCESV(3)-1+IAD3+NIT+3)

C       RECUPERATION DU NOMBRE DE NOUVEAUX NOEUDS A CREER 
        NNN=ZI(JCESV(3)-1+IAD3+2)
        IF (NNN.EQ.0) GOTO 100
C        CALL ASSERT(NNN.NE.0)

C       NOMBRE DE FISSURES "VUES" DANS LA MAILLE
        NFISS = ZI(JCESD(6)-1+5+4*(IMA-1)+2)
C       NOMBRE DE COMPOSANTE DE LA TOPOSE.HEA
        NCOMP = ZI(JCESD(4)-1+5+4*(IMA-1)+3)

C       CREATION DU VECTEUR D'INDIRECTION DES NOEUDS (LOCAL A IMA)
        DIRNO='&&XPOMAX.DIRNO'
        CALL WKVECT(DIRNO,'V V I',NNN*(2+NFISS),JDIRNO)
C        CALL WKVECT(DIRNO,'V V I',(N+NPI+NMI)*3,JDIRNO)

C       DIMENSION TOPOLOGIQUE DE LA MAILLE
        NDIME= ZI(JTMDIM-1+ZI(JTYPM1-1+IMA))

C       1ER ELEMENT DE REFERENCE ASSOCIE A LA MAILLE
        ITYPEL = ZI(JMAIL-1+IMA)
        CALL JENUNO(JEXNUM('&CATA.TE.NOMTE',ITYPEL),NOTYPE)
        CALL ELREF2(NOTYPE,10,LIREFE,NBELR)
        ELREFP= LIREFE(1)


C       CREATION DE VECTEUR DES COORDONNÉES DE LA MAILLE IMA
C       AVEC DES VALEURS CONTIGUES
        GEOM = '&&XPOAJD.GEOM'
        CALL WKVECT(GEOM,'V V R',NDIM*N,IGEOM)
        DO 20 IN=1,N
          INO=ZI(JCONX1-1+ZI(JCONX2+IMA-1)+IN-1)
          DO 21 J=1,NDIM
            ZR(IGEOM-1+NDIM*(IN-1)+J)=ZR(IACOO1-1+3*(INO-1)+J)
 21       CONTINUE
 20     CONTINUE

        IF (.NOT.OPMAIL) THEN
C         TYPE DE LA MAILLE PARENT
          CALL JENUNO(JEXNUM('&CATA.TM.NOMTM',ZI(JTYPMA-1+IMA)),TYPMA)
C         CONNECTIVITES DU MAILLAGE QUADRATIQUE
C         POUR RECUPERER LES LAGRANGES
          CALL JEVEUO(NOMA//'.CONNEX','L',JCONQ1)
          CALL JEVEUO(JEXATR(NOMA//'.CONNEX','LONCUM'),'L',JCONQ2)
        ENDIF

C       COMPOSANTES DU CHAMP DE DEPLACEMENT 1 POUR LA MAILLE IMA
C       ET RECUPERATION DES CONTRAINTES 1
        IF (.NOT.OPMAIL) THEN 
          NBCMP = ZI(JCNSD1-1+2)
          CALL XPOCMP(ELREFP,CNS1,IMA,N,JCONX1,JCONX2,NDIM,NFH,NFE,
     &             DDLC,NBCMP,CMP)

          IF (TYSD(1:9).NE.'MODE_MECA') THEN
            CALL CESEXI('C',JCESD1,JCESL1,IMA,1,1,1,IADC)
            IF(IVIEX .NE. 0) CALL CESEXI('C',JCVID1,JCVIL1,IMA,
     &                                   1,1,1,IADV)
          ENDIF
        ENDIF

C       RECUPERATION DES COORDONNEES DES POINTS D'INTERSECTION
        CALL CESEXI('C',JCESD(1),JCESL(1),IMA,1,1,1,IAD1)
        CALL ASSERT(IAD1.GT.0)

C       RECUPERATION DES COORDONNEES DES POINTS MILIEUX
        IF (.NOT.ISMALI(ELREFP) .AND. NDIM.LE.2) THEN
          CALL CESEXI('C',JCESD(9),JCESL(9),IMA,1,1,1,IAD9)
          CALL ASSERT(IAD9.GT.0)
        ENDIF         

C       RECUPERATION DU NOMBRE DE POINTS D'INTERSECTION
C       SUR LES MAILLES PORTEUSES DE DDL DE CONTACT

        CALL CESEXI('C',JCESD(10),JCESL(10),IMA,1,1,1,IAD10)
C        IF (IAD10.GT.0) THEN
          NINTER=ZI(JCESV(10)-1+IAD10)
          CALL CESEXI('C',JCESD(11),JCESL(11),IMA,1,1,1,IAD11)
C        ELSE
C          NINTER =0
C        ENDIF

C       RECUPERATION DE LA CONNECTIVITE DES SOUS-ELEMENTS
        CALL CESEXI('C',JCESD(2),JCESL(2),IMA,1,1,1,IAD2)
        CALL ASSERT(IAD2.GT.0)

C       RECUPERATION DE LA FONCTION HEAVISIDE
        CALL CESEXI('C',JCESD(4),JCESL(4),IMA,1,1,1,IAD4)
        CALL ASSERT(IAD4.GT.0)

C       RECUPERATION DES INFOS CONCERNANT LES GROUP_MA CONTENANT IMA
        IF (OPMAIL) THEN
          CALL JEVEUO(JEXNUM(LISTGR,IMA),'L',IAGMA)
          CALL JELIRA(JEXNUM(LISTGR,IMA),'LONMAX',NGRM,K8B)
        ENDIF

C       RECUPERATION DES LEVELS SET AUX NOEUDS
        LST = '&&XPOAJD.LST'
        LSN = '&&XPOAJD.LSN'
        HEA = '&&XPOAJD.HEA'
        CALL WKVECT(LST,'V V R',N*NFISS,JLST)
        CALL WKVECT(LSN,'V V R',N*NFISS,JLSN)
        CALL WKVECT(HEA,'V V I',NFISS,JHEA)
        DO 30 IFISS=1,NFISS
          DO 40 J =1,N
            CALL CESEXI('C',JCESD(6),JCESL(6),IMA,J,IFISS,1,IAD)
            CALL ASSERT(IAD.GT.0)
            ZR(JLSN-1+(J-1)*NFISS+IFISS) = ZR(JCESV(6)-1+IAD)
            CALL CESEXI('C',JCESD(7),JCESL(7),IMA,J,IFISS,1,IAD)
            CALL ASSERT(IAD.GT.0)
            ZR(JLST-1+(J-1)*NFISS+IFISS) = ZR(JCESV(7)-1+IAD)
  40      CONTINUE
  30    CONTINUE

C       RECUPERATION DE LA CONNECTIVITÉ DES FISSURES
        IF (.NOT.OPMAIL.AND.NFH.GT.0) THEN
C         CORRECTION DE NFH SI ON SE TROMPE DANS XPOCMP
          IF (NFH.GT.NFISS) NFH = NFISS
          FISNO = '&&XPOAJD.FISNO'
          CALL WKVECT(FISNO,'V V I',N*NFH,JFISNO)
          DO 60 J=1,N
            IF (NFISS.EQ.1) THEN
              ZI(JFISNO-1+J) = 1
            ELSEIF (NFISS.GT.1) THEN
              NFH = ZI(JCESD(8)-1+5+4*(IMA-1)+2)
              DO 50 IFH =1,NFH
                CALL CESEXI('C',JCESD(8),JCESL(8),IMA,J,IFH,1,IAD)
                CALL ASSERT(IAD.GT.0)
                ZI(JFISNO-1+(J-1)*NFH+IFH) = ZI(JCESV(8)-1+IAD)
  50          CONTINUE
            ENDIF
  60      CONTINUE
        ENDIF

C ----- ON AJOUTE LES NOUVELLES MAILLES ET LES NOUVEAUX NOEUDS

        IF (.NOT.ISMALI(ELREFP) .AND. NDIM.LE.2) THEN
          IRESE = 3
        ELSE
          IRESE = 0
        ENDIF
        NNOSE = TABSE(NDIME+IRESE)

C         BOUCLE D'INTEGRATION SUR LES NSE SOUS-ELEMENTS
        DO 140 ISE=1,NSE
          DO 150 IFISS = 1,NFISS
            ZI(JHEA-1+IFISS) = ZI(JCESV(4)-1+IAD4-1+NCOMP*(IFISS-1)+ISE)
 150      CONTINUE
          JCNSE = JCESV(2)-1+IAD2
          CALL XPOAJM(MAXFEM,JTYPM2,ITYPSE(NDIME+IRESE),JCNSE,ISE,N,
     &         NNOSE,PREFNO,JDIRNO,NSE,INM,INMTOT,NBMAC,ZI(JHEA),JNIVGR,
     &            IAGMA,NGRM,JDIRGR,OPMAIL,NFISS,NDIM,NDIME,JCONX1,
     &            JCONX2,JCONQ1,JCONQ2,IMA,IAD1+JCESV(1)-1,NNN,INN,
     &            INNTOT,NBNOC,NBNOFI,INOFI,NBNOLA,INOLA,IACOO1,
     &            IACOO2,IAD9+JCESV(9)-1,NINTER,JCESV(11)+IAD11-1,
     &            ELREFP,JLSN,JLST,TYPMA,IGEOM,JFISNO,
     &            CMP,NBCMP,NFH,NFE,DDLC,JCNSV1,JCNSV2,JCNSL2)
          IF(.NOT.OPMAIL .AND.(TYSD(1:9).NE.'MODE_MECA')) THEN
C           ON AJOUTE DES CONTRAINTES
            CALL XPOAJC(NSE,INM,INMTOT,NBMAC,ISE,
     &                  JCESD1,JCESD2,JCVID1,JCVID2,IMA,NDIM,NDIME,
     &                  IADC,IADV,JCESV1,JCESL2,JCESV2,JCVIV1,
     &                  JCVIL2,JCVIV2)
          ENDIF
 140    CONTINUE

        IF (OPMAIL) CALL ASSERT(INN.EQ.NNN)
        
        CALL JEDETR(GEOM)
        CALL JEDETR(DIRNO)
        CALL JEDETR(LSN)
        CALL JEDETR(LST)
        CALL JEDETR(HEA)
        IF (.NOT.OPMAIL.AND.NFH.GT.0) CALL JEDETR(FISNO)

 100  CONTINUE

C     CREATION DU GROUPE DES NOEUDS SITUES SUR LA FISSURE 
C     PORTANT DES DDLS DE CONTACT
      GRPNOE=MAXFEM//'.GROUPENO'
      IF (OPMAIL.AND.NBNOFI.GT.0) THEN
        NOGNO=NOGRFI
C       ON SAIT QUE LE .GROUPENO N'EXISTE PAS
        CALL JECREC(GRPNOE,'G V I','NO','DISPERSE','VARIABLE',1)
        CALL JECROC(JEXNOM(GRPNOE,NOGNO))
        CALL JEECRA(JEXNOM(GRPNOE,NOGNO),'LONMAX',
     &        MAX(1,NBNOFI),K8B)
        CALL JEECRA(JEXNOM(GRPNOE,NOGNO),'LONUTI',NBNOFI,K8B)
        CALL JEVEUO(JEXNOM(GRPNOE,NOGNO),'E',IAGNO)
        DO 210 J = 1,NBNOFI
          ZI(IAGNO-1+J) = ZI(INOFI-1+J)
 210    CONTINUE
      ENDIF
C      IF (OPMAIL.AND.NBNOLA.GT.0) THEN
C        NOGNO=NOGRLA
C        CALL JECROC(JEXNOM(GRPNOE,NOGNO))
C        CALL JEECRA(JEXNOM(GRPNOE,NOGNO),'LONMAX',NBNOLA,K8B)
C        CALL JEVEUO(JEXNOM(GRPNOE,NOGNO),'E',IAGNOL)
C        DO 211 J = 1,NBNOLA
C          ZI(IAGNOL-1+J) = ZI(INOLA-1+J)
C 211    CONTINUE
C      ENDIF
      DO 300 ICH=1,4
        CALL DETRSD('CHAM_ELEM_S',CHS(ICH))
 300  CONTINUE
      DO 310 ICH=6,11
        CALL DETRSD('CHAM_ELEM_S',CHS(ICH))
 310  CONTINUE

      IF (OPMAIL) CALL JEDETR(MAILX)
      IF (OPMAIL) CALL JEDETR(LINOFI)

 999  CONTINUE

      CALL JEDEMA()
      END
