      SUBROUTINE IRCMPE ( NOFIMD,
     >                    NCMPVE, NTLCMP,
     >                    NBVATO, NBMAEC, LIMAEC, ADSD, ADSL,
     >                    TYPMA, NMATYP,
     >                    NTPROA, NOPFTY, NVALTY, NBPGTY, NBSPTY )
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF PREPOST  DATE 11/03/2003   AUTEUR DURAND C.DURAND 
C ======================================================================
C COPYRIGHT (C) 1991 - 2002  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE GNICOLAS G.NICOLAS
C_______________________________________________________________________
C  ECRITURE D'UN CHAMP - FORMAT MED - PROFIL POUR LES ELEMENTS
C     -  -       -              -     -               -
C_______________________________________________________________________
C     ENTREES :
C       NOFIMD : NOM DU FICHIER MED
C       NCMPVE : NOMBRE DE COMPOSANTES VALIDES EN ECRITURE
C       NTLCMP : SD DES NUMEROS DES COMPOSANTES VALIDES
C       NBVATO : NOMBRE DE VALEURS TOTALES
C       NBMAEC : NOMBRE D'ENTITES A ECRIRE (O, SI TOUTES)
C       LIMAEC : LISTE DES ENTITES A ECRIRE SI EXTRAIT
C       ADSK, D, ... : ADRESSES DES TABLEAUX DES CHAMPS SIMPLIFIES
C       TYPMA  : SD TYPE ASTER POUR CHAQUE MAILLE
C     SORTIES :
C       NMATYP : NOMBRE DE MAILLES POUR CHAQUE TYPE DE SUPPORT
C       NTPROA : SD DU PROFIL. C'EST LA LISTE DES NUMEROS ASTER DES
C                ELEMENTS POUR LESQUELS LE CHAMP EST DEFINI
C       NOPFTY : NOM DES PROFILS AU SENS MED
C       NVALTY : NOMBRE DE VALEURS UTILES POUR CHAQUE TYPE DE SUPPORT
C       NBPGTY : NOMBRE DE POINTS DE GAUSS POUR CHAQUE TYPE DE SUPPORT
C       NBSPTY : NOMBRE DE SOUS-POINTS POUR CHAQUE TYPE DE SUPPORT
C
C  COMMENTAIRE : C'EST AVEC L'APPEL A CESEXI QU'IL FAUT FILTRER LES
C                COMPOSANTES. SEUL MOYEN FIABLE AVEC UN CHAMELEM
C                SIMPLIFIE.
C_______________________________________________________________________
C
      IMPLICIT NONE
C
C 0.1. ==> ARGUMENTS
C
      INTEGER NMATYP(*)
      INTEGER NVALTY(*),NBPGTY(*), NBSPTY(*)
      INTEGER NBVATO, NCMPVE
      INTEGER NBMAEC
      INTEGER LIMAEC(*)
      INTEGER TYPMA(*)
      INTEGER ADSD, ADSL
C
      CHARACTER*(*) NOFIMD
      CHARACTER*(*) NTLCMP, NTPROA
      CHARACTER*32 NOPFTY(0:*)
C
C 0.2. ==> COMMUNS
C
C --------------- COMMUNS NORMALISES  JEVEUX  --------------------------
      INTEGER      ZI
      LOGICAL      ZL
      COMMON /IVARJE/ZI(1)
      COMMON /LVARJE/ZL(1)
C     -----  FIN  COMMUNS NORMALISES  JEVEUX --------------------------
C
C 0.3. ==> VARIABLES LOCALES
C
      CHARACTER*6 NOMPRO
      PARAMETER ( NOMPRO = 'IRCMPE' )
C
      INTEGER NTYMAX
      PARAMETER (NTYMAX=28)
C
      CHARACTER*8 SAUX08
      CHARACTER*24 EXICMP, NTPROM, NTPROR
C
      INTEGER IFM, NIVINF
C
      INTEGER IAUX, JAUX, KAUX
      INTEGER IMA
      INTEGER NRCMP, NRPG, NRSP, NBPG, NBSP, NVAL, TYPMAS
      INTEGER ADNUCM, ADPROA, ADPROM, ADPROR
      INTEGER ADEXIC
      INTEGER NMATY0(NTYMAX), NVALT0(NTYMAX), ADTYP0(NTYMAX)
C
C====
C 1. PREALABLES
C====
C 1.1. ==> RECUPERATION DU NIVEAU D'IMPRESSION
C
      CALL INFNIV ( IFM, NIVINF )
C
C 1.2. ==> NOMS DES TABLEAUX
C               12   345678   9012345678901234
      NTPROM = '&&'//NOMPRO//'.PROFIL_MED     '
      NTPROR = '&&'//NOMPRO//'.PROFIL_RECIPROQ'
      EXICMP = '&&'//NOMPRO//'.EXICMP         '
C
C 1.3. ==> NOMBRE DE MAILLES DANS LE MAILLAGE
C
      DO 13 , IMA = 1 , NBVATO
        NMATYP(TYPMA(IMA)) = NMATYP(TYPMA(IMA)) + 1
   13 CONTINUE
C
C====
C 2. ON CREE UN PREMIER TABLEAU PAR ELEMENT :
C    VRAI DES QU'UNE DES COMPOSANTES DU CHAMP EST PRESENTE SUR
C    L'ELEMENT
C    FAUX SINON
C    REMARQUE : ON EXAMINE LES NCMPVE COMPOSANTES QUI SONT DEMANDEES,
C    MAIS IL FAUT BIEN TENIR COMPTE DE LA NUMEROTATION DE REFERENCE
C====
C
      CALL WKVECT ( EXICMP, 'V V L', NBVATO, ADEXIC )
C
      CALL JEVEUO ( NTLCMP, 'L', ADNUCM )
C
      DO 21 , IAUX = 0 , NBVATO-1
C
        NBPG = ZI(ADSD+5+4*IAUX)
        NBSP = ZI(ADSD+6+4*IAUX)
        DO 211 , NRCMP = 1 , NCMPVE
          KAUX = ZI(ADNUCM+NRCMP-1)
          DO 2111 , NRPG = 1 , NBPG
            DO 2112 , NRSP = 1 , NBSP
              CALL CESEXI ('C',ADSD,ADSL,IAUX+1,NRPG,NRSP,KAUX,JAUX)
              IF ( JAUX.GT.0 ) THEN
                ZL(ADEXIC+IAUX) = .TRUE.
                GOTO 21
              ENDIF
 2112       CONTINUE
 2111     CONTINUE
  211   CONTINUE
C
   21 CONTINUE
C
C====
C 3. NTPROA : LISTE DES ELEMENTS POUR LESQUELS ON AURA IMPRESSION
C    UN ELEMENT EST PRESENT SI ET SEULEMENT SI AU MOINS UNE COMPOSANTE
C    Y EST DEFINIE ET S'IL FAIT PARTIE DU FILTRAGE DEMANDE
C====
C
      CALL WKVECT ( NTPROA, 'V V I', NBVATO, ADPROA )
      NVAL = 0
C
C 3.1. ==> SANS FILTRAGE : C'EST LA LISTE DES ELEMENTS AVEC
C          UNE COMPOSANTE VALIDE
C
      IF ( NBMAEC.EQ.0 ) THEN
C
        DO 31 , IAUX = 1 , NBVATO
          IF ( ZL(ADEXIC+IAUX-1) ) THEN
            ZI(ADPROA+NVAL) = IAUX
            NVAL = NVAL + 1
          ENDIF
   31   CONTINUE
C
C 3.2. ==> AVEC FILTRAGE : C'EST LA LISTE DES ELEMENTS REQUIS ET AVEC
C          UNE COMPOSANTE VALIDE
C
      ELSE
C
        DO 33 , JAUX = 1 , NBMAEC
          IAUX = LIMAEC(JAUX)
          IF ( ZL(ADEXIC+IAUX-1) ) THEN
            ZI(ADPROA+NVAL) = IAUX
            NVAL = NVAL + 1
          ENDIF
   33   CONTINUE
C
      ENDIF
C
C 3.3. ==> SAUVEGARDE DU NOMBRE DE VALEURS UTILES
C
      CALL JEECRA ( NTPROA, 'LONUTI',  NVAL, SAUX08 )
CGN      PRINT *,'. NVAL = ', NVAL
CGN      PRINT *,'. NTPROA :'
CGN      PRINT 1789,(ZI(IAUX),IAUX=ADPROA,ADPROA+NBVATO-1)
CGN1789  FORMAT(10I6)
C
C 3.4. ==> DETERMINATION DU NOMBRE D'ENTITES PAR TYPE DE MAILLES ASTER
C    REMARQUE : LES TABLEAUX NVALTY, NBPGTY ET NBSPTY SONT INITIALISES
C    AVANT L'APPEL POUR UNIFIER LES TRAITEMENTS
C
      DO 34 , IAUX = 0 , NVAL-1
        IMA = ZI(ADPROA+IAUX)
        TYPMAS = TYPMA(IMA)
        NVALTY(TYPMAS) = NVALTY(TYPMAS) + 1
        IF ( NBPGTY(TYPMAS).EQ.-1 ) THEN
          NBPGTY(TYPMAS) = ZI(ADSD+1+4*IMA)
          NBSPTY(TYPMAS) = ZI(ADSD+2+4*IMA)
        ENDIF
   34 CONTINUE
CGN      PRINT *,'. NVALTY '
CGN      PRINT 1789,(NVALTY(IAUX),IAUX=1, NTYMAX)
C
C 3.5. ==> CONVERSION DU PROFIL EN NUMEROTATION MED
C    NTPROR : C'EST LA LISTE RECIPROQUE. POUR L'ELEMENT NUMERO IAUX EN
C    NUMEROTATION ASTER, ON A SA POSITION DANS LE TABLEAU DES VALEURS
C    S'IL FAIT PARTIE DE LA LISTE ET 0 SINON.
C    NTPROM : ON STOCKE LES VALEURS DES NUMEROS DES MAILLES AU SENS MED
C    PAR TYPE DE MAILLES.
C    LE TABLEAU EST ORGANISE EN SOUS-TABLEAU CORRESPONDANT A CHAQUE TYPE
C    DE MAILLES. ON REPERE CHAQUE DEBUT DE SOUS-TABLEAU AVEC ADTYP0.
C
      CALL WKVECT ( NTPROM, 'V V I', NVAL, ADPROM )
      CALL WKVECT ( NTPROR, 'V V I', NBVATO, ADPROR )
C
      DO 351 , IAUX = 1 , NVAL
        IMA = ZI(ADPROA+IAUX-1)
        ZI(ADPROR+IMA-1) = IAUX
  351 CONTINUE
CGN      PRINT *,'. NTPROR '
CGN      PRINT 1789,(ZI(IAUX),IAUX=ADPROR,ADPROR+NBVATO-1)
C
      ADTYP0(1) = ADPROM
      NMATY0(1) = 0
      NVALT0(1) = 0
      DO 352 , IAUX = 2 , NTYMAX
        ADTYP0(IAUX) = ADTYP0(IAUX-1) + NVALTY(IAUX-1)
        NMATY0(IAUX) = 0
        NVALT0(IAUX) = 0
  352 CONTINUE
CGN      PRINT *,'. ADTYP0 '
CGN      PRINT 1788,(IAUX,ADTYP0(IAUX),IAUX=1, NTYMAX)
CGN1788  FORMAT(5(I2,' : ',I10,3x))
C
      IAUX = 0
      DO 353 , IMA = 1 , NBVATO
        TYPMAS = TYPMA(IMA)
        NMATY0(TYPMAS) = NMATY0(TYPMAS) + 1
        IF ( ZI(ADPROR+IMA-1).NE.0 ) THEN
          ZI(ADTYP0(TYPMAS)+NVALT0(TYPMAS)) = NMATY0(TYPMAS)
          NVALT0(TYPMAS) = NVALT0(TYPMAS) + 1
        ENDIF
  353 CONTINUE
CGN      PRINT *,'. NMATY0 '
CGN      PRINT 1789,(NMATY0(IAUX),IAUX=1, NTYMAX)
CGN      PRINT *,'. NVALT0 '
CGN      PRINT 1789,(NVALT0(IAUX),IAUX=1, NTYMAX)
CGN      PRINT *,'. NTPROM '
CGN      PRINT 1789,(ZI(IAUX),IAUX=ADPROM,ADPROM+NVAL-1)
C
C====
C 4. STOCKAGE DU PROFIL DANS LE FICHIER MED
C====
C
      DO 41 , IAUX = 1 , NTYMAX
        IF ( NVALTY(IAUX).NE.0 ) THEN
          IF ( NVALTY(IAUX).NE.NMATYP(IAUX) ) THEN
            CALL IRCMPF ( NOFIMD,
     >                    NVALTY(IAUX), ZI(ADTYP0(IAUX)), IAUX,
     >                    NOPFTY )
          ENDIF
        ENDIF
   41 CONTINUE
C
C====
C 5. LA FIN
C====
C
      IF ( NIVINF.GT.1 ) THEN
        WRITE (IFM,5001) NOMPRO, ' : NOMBRE DE VALEURS A ECRIRE : '
        DO 51 , IAUX = 1 , NTYMAX
          WRITE (IFM,5002) IAUX, NVALTY(IAUX)
          IF ( NVALTY(IAUX).NE.0 ) THEN
            WRITE (IFM,5003) NBPGTY(IAUX), NBSPTY(IAUX)
          ENDIF
   51   CONTINUE
      ENDIF
 5001 FORMAT(A5,A)
 5002 FORMAT('TYPE ',I4,' : ',I8)
 5003 FORMAT(11X,'REPARTIES SUR ',I3,' POINT(S) DE GAUSS ET',
     >                            I3,' SOUS-POINT(S)')
C
      CALL JEDETC('V','&&'//NOMPRO,1)
C
      END
