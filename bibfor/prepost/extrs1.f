      SUBROUTINE EXTRS1(RESU0,NBRANG,NUORDR,NBPARA,NOMPAR,NBARCH,NUARCH,
     &                  NBEXCL,CHEXCL,NBNOSY)
      IMPLICIT   NONE
      INCLUDE 'jeveux.h'
      INTEGER NBRANG,NUORDR(*),NBARCH,NBPARA,NUARCH(*),NBEXCL,NBNOSY
      CHARACTER*16 NOMPAR(*),CHEXCL(*)
      CHARACTER*(*) RESU0
C     ------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF PREPOST  DATE 13/06/2012   AUTEUR COURTOIS M.COURTOIS 
C RESPONSABLE PELLET
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C     EXTR_RESU / ARCHIVAGE + REUSE
C ----------------------------------------------------------------------
C
C
C 0.3. ==> VARIABLES LOCALES
C
C
      INTEGER IRANG,I,J,K,JTACH,IADIN,IADOU,IRE1
      INTEGER KORDR,IUNDF,ISNNEM
      REAL*8 RUNDF,R8VIDE
      CHARACTER*3 TYPE
      CHARACTER*16 NOMSYM
      CHARACTER*16 NOPARA
      CHARACTER*19 NOMSDR
      CHARACTER*19 CHAMIN
C     ------------------------------------------------------------------

      CALL JEMARQ()
      RUNDF=R8VIDE()
      IUNDF=ISNNEM()

      NOMSDR=RESU0


C     1. -- ON COMPACTE LES CHAMPS ARCHIVES :
C     -------------------------------------------------------
      DO 50 I=1,NBNOSY
        CALL JENUNO(JEXNUM(NOMSDR//'.DESC',I),NOMSYM)
        CALL JEVEUO(JEXNUM(NOMSDR//'.TACH',I),'E',JTACH)
        DO 20 J=1,NBEXCL
          IF (CHEXCL(J).EQ.NOMSYM) THEN
            DO 10 K=1,NBRANG
              IF (ZK24(JTACH+K-1)(1:1).EQ.' ')GOTO 10
              CALL RSEXCH(NOMSDR,NOMSYM,NUORDR(K),CHAMIN,IRE1)
              CALL ASSERT(IRE1.EQ.0)
              CALL DETRSD('CHAMP_GD',CHAMIN)
              ZK24(JTACH+K-1)=' '
   10       CONTINUE
            GOTO 50

          ENDIF
   20   CONTINUE

        IRANG=0
        DO 30 J=1,NBRANG
          IF (ZK24(JTACH+J-1)(1:1).EQ.' ')GOTO 30
          CALL RSEXCH(NOMSDR,NOMSYM,NUORDR(J),CHAMIN,IRE1)
          CALL ASSERT(IRE1.EQ.0)
          IF (NUARCH(J).EQ.0) THEN
            CALL DETRSD('CHAMP_GD',CHAMIN)
          ELSE
            IRANG=IRANG+1
            ZK24(JTACH+IRANG-1)=CHAMIN
          ENDIF
   30   CONTINUE

        DO 40 K=IRANG+1,NBRANG
            ZK24(JTACH+K-1)=' '
   40   CONTINUE
   50 CONTINUE


C     2. -- ON COMPACTE LES PARAMETRES ARCHIVES :
C     -------------------------------------------
      IRANG=0
      DO 70 I=1,NBRANG
        IF (NUARCH(I).EQ.0)GOTO 70
        IRANG=IRANG+1
        DO 60 J=1,NBPARA
          NOPARA=NOMPAR(J)
          CALL RSADPA(NOMSDR,'L',1,NOPARA,NUORDR(I),1,IADIN,TYPE)
          CALL EXTRS3(NOMSDR,NOPARA,IRANG,'E',1,TYPE,IADOU)
          IF (TYPE(1:1).EQ.'I') THEN
            ZI(IADOU)=ZI(IADIN)
          ELSEIF (TYPE(1:1).EQ.'R') THEN
            ZR(IADOU)=ZR(IADIN)
          ELSEIF (TYPE(1:1).EQ.'C') THEN
            ZC(IADOU)=ZC(IADIN)
          ELSEIF (TYPE(1:3).EQ.'K80') THEN
            ZK80(IADOU)=ZK80(IADIN)
          ELSEIF (TYPE(1:3).EQ.'K32') THEN
            ZK32(IADOU)=ZK32(IADIN)
          ELSEIF (TYPE(1:3).EQ.'K24') THEN
            ZK24(IADOU)=ZK24(IADIN)
          ELSEIF (TYPE(1:3).EQ.'K16') THEN
            ZK16(IADOU)=ZK16(IADIN)
          ELSEIF (TYPE(1:2).EQ.'K8') THEN
            ZK8(IADOU)=ZK8(IADIN)
          ENDIF
   60   CONTINUE
   70 CONTINUE
      CALL ASSERT(IRANG.EQ.NBARCH)


C     3. -- ON COMPACTE LES NUME_ORDRE ARCHIVES :
C     -------------------------------------------
      CALL JEECRA(NOMSDR//'.ORDR','LONUTI',NBARCH,' ')
      CALL JEVEUO(NOMSDR//'.ORDR','E',KORDR)
      IRANG=0
      DO 80 I=1,NBRANG
        IF (NUARCH(I).EQ.0)GOTO 80
        IRANG=IRANG+1
        ZI(KORDR+IRANG-1)=NUORDR(I)
   80 CONTINUE
      CALL ASSERT(IRANG.EQ.NBARCH)


C     4. -- ON MET A "ZERO" LES IRANG INUTILISES :
C     -------------------------------------------------
      DO 100 IRANG=NBARCH+1,NBRANG
        ZI(KORDR+IRANG-1)=IUNDF
        DO 90 J=1,NBPARA
          NOPARA=NOMPAR(J)
          CALL EXTRS3(NOMSDR,NOPARA,IRANG,'E',1,TYPE,IADOU)
          IF (TYPE(1:1).EQ.'I') THEN
            ZI(IADOU)=IUNDF
          ELSEIF (TYPE(1:1).EQ.'R') THEN
            ZR(IADOU)=RUNDF
          ELSEIF (TYPE(1:1).EQ.'C') THEN
            ZC(IADOU)=DCMPLX(RUNDF,RUNDF)
          ELSEIF (TYPE(1:3).EQ.'K80') THEN
            ZK80(IADOU)=' '
          ELSEIF (TYPE(1:3).EQ.'K32') THEN
            ZK32(IADOU)=' '
          ELSEIF (TYPE(1:3).EQ.'K24') THEN
            ZK24(IADOU)=' '
          ELSEIF (TYPE(1:3).EQ.'K16') THEN
            ZK16(IADOU)=' '
          ELSEIF (TYPE(1:2).EQ.'K8') THEN
            ZK8(IADOU)=' '
          ENDIF
   90   CONTINUE
  100 CONTINUE


C     -- IL FAUT ENCORE DETRUIRE LES SCORIES INUTILES :
      CALL RSMENA(NOMSDR)

      CALL JEDEMA()
      END
