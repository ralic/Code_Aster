      SUBROUTINE DALP2D(NELEM,NNOEM,DEGRE,NSOMMX,ICNC,NELCOM,
     &                   NUMELI,NDIM,XY,ERREUR,ENERGI,AIRE,
     &                   ALPHA,NALPHA)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF PREPOST  DATE 11/07/2005   AUTEUR VABHHTS J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2005  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================

C*******************************************************************
C              BUT DE CETTE ROUTINE :                              *
C CALCULER LE DEGRE DE LA SINGULARITE PE EN CHAQUE NOEUD           *
C PUIS EN CHAQUE EF A PARTIR DE PE                                 *
C 1) COMMENT REPERE T-ON UN NOEUD SINGULIER ?                      *
C    L ERREUR LOCALE EST GRANDE EN CE NOEUD                        *
C    COMPAREE A L ERREUR DE REFERENCE SUR TOUTE LA STRUCTURE       *
C    DEROULEMENT DE LA ROUTINE :                                   *
C                                                                  *
C    CALCUL DE L ERREUR DE REFERENCE                               *
C    NOEU1 EST IL SINGULIER ?                                      *
C      CALCUL DE L ERREUR LOCALE SUR LES EFS CONNECTES A NOEU1     *
C      TEST1 : SI ERREUR (NOEU1) > ERREUR GLOBALE                  *
C        TEST2 : SI ERREUR (NOEU1-2EM COUCHE) < ERREUR (NOEU1)     *
C          TEST3 : SI ERREUR (NOEU1) > 6*ERREUR (NOEU1-3EM COUCHE) *
C          NOEU1 EST SINGULIER                                     *
C    RQ : CES DIFFERENTS TESTS PERMETTENT D ETRE SUR QUE           *
C    NOEU1 EST BIEN SINGULIER                                      *
C 2) COMMENT CALCULE T-ON PE=ALPHAN(NOEU1) ?                       *
C    SI NOEU1 REGULIER ALPHAN(NOEU1)=DEGRE D INTERPOLATION DE L EF *
C    SI NOEU1 SINGULIER CALCUL DE PE                               *
C 3) COMMENT CALCULE T-ON PE ?                                     *
C    CONSTRUCTION DE LA COURBE ENERGIE EN FONCTION DU RAYON        *
C    EN 2D L ENERGIE EST CALCULEE SUR DES CERCLES DE CENTRE NOEU1  *
C    ET POUR DIFFERENTS RAYONS                                     *
C    CALCUL DE PE PAR REFERENCE A L ENERGIE EN POINTE DE FISSURE   *
C*******************************************************************  

C IN  NELEM                  : NOMBRE D ELEMENTS FINIS
C IN  NNOEM                  : NOMBRE DE NOEUDS
C IN  DEGRE                  : DEGRE DES EF (1 POUR P1 2 POUR P2)
C IN  NSOMMX                 : NBRE DE NOEUDS SOMMETS MAX PAR EF
C IN  ICNC(NSOMMX+2,NELEM)   : EF => NOEUDS SOMMETS CONNECTES A EF
C     1ERE VALEUR = NBRE DE NOEUDS SOMMETS CONNECTES A L EF N°X
C     2EME VALEUR = 1 SI EF UTILE 0 SINON 
C     CONNECTIVITE  EF N°X=>N° DE NOEUDS SOMMETS CONNECTES A X
C     EN 2D EF UTILE = QUAD OU TRIA
C     EN 3D EF UTILE = TETRA OU HEXA
C IN  NELCOM                 : NBRE D EF SURFACIQUE MAX PAR NOEUD 
C IN  NUMELI(NELCOM+2,NNOEM) : NOEUD =>EF SURFACIQUE CONNECTES A NOEUD
C     1ERE VALEUR = NBRE D EFS UTILES CONNECTES AU NOEUD N°X
C     2EME VALEUR = 0 NOEUD MILIEU OU NON CONNECTE A UN EF UTILE
C                   1 NOEUD SOMMET A L INTERIEUR + LIE A UN EF UTILE
C                   2 NOEUD SOMMET BORD + LIE A UN EF UTILE 
C     CONNECTIVITE  NOEUD N°X=>N° DES EF UTILE CONNECTES A X 
C IN  NDIM                   : DIMENSION DU PROBLEME
C IN  XY(3,NNOEM)            : COORDONNEES DES NOEUDS
C IN  ERREUR(NELEM)          : ERREUR SUR CHAQUE EF 
C IN  ENERGI(NELEM)          : ENERGIE SUR CHAQUE EF
C IN  AIRE(NELEM)            : SURFACE DE CHAQUE EF
C OUT ALPHA(NELEM)           : DEGRE DE LA SINGULARITE PAR ELEMENT
C OUT NALPHA                 : NOMBRE DE CPE PAR ELEMENT DIFFERENTS 
C                              1 PAR DEFAUT SI PAS DE SINGULARITE

      IMPLICIT NONE 

C DECLARATION GLOBALE

      INTEGER NELEM,NNOEM,DEGRE,NSOMMX,NELCOM,NDIM
      INTEGER ICNC(NSOMMX+2,NELEM),NUMELI(NELCOM+2,NNOEM)
      INTEGER NALPHA
      REAL*8  XY(3,NNOEM),ERREUR(NELEM),ENERGI(NELEM),AIRE(NELEM)
      REAL*8  ALPHA(NELEM)

C DECLARATION LOCALE

      INTEGER    INNO,INEL,NUEF
      INTEGER    TBNOZO(1000),NBNOZO(3),TBELZO(1000),NBELZO(3)
      INTEGER    NBNOE
      REAL*8     FACTPM,FACTP
      PARAMETER (FACTPM = 2.0D+0,FACTP=3.0D+0)
      REAL*8     PRECMO,PRECRE,PREC1,PREC2,PREC3,AIRTOT
      REAL*8     DTYP,ALPHAN(NNOEM),ALPREF,PE
      
C 1 - DEGRE D INTERPOLATION

      IF (DEGRE.EQ.1) THEN
        DTYP = 1.D+0
      ELSE
        DTYP = 2.D+0
      ENDIF

C 2 - INITIALISATION DES ALPHA = DEGRE D INTERPOLATION

      DO 10 INNO = 1,NNOEM
        ALPHAN(INNO)= DTYP 
 10   CONTINUE
      DO 20 INEL = 1,NELEM 
       ALPHA(INEL) = DTYP
 20   CONTINUE

C 3 - CALCUL DES PRECISIONS MOYENNE ET DE REFERENCE

      PRECMO = 0.D+0
      AIRTOT  = 0.D+0
      DO 30 INEL = 1,NELEM
        PRECMO = PRECMO + ERREUR(INEL)**2
        AIRTOT  = AIRTOT  + AIRE(INEL)
 30   CONTINUE                                                      
      PRECMO = SQRT( PRECMO / AIRTOT )
      PRECRE = PRECMO * FACTPM
      
C 4 - BOUCLE SUR LES NOEUDS SOMMETS POUR DETECTER 
C     SI LE NOEUD CONSIDERE EST SINGULIER OU PAS
C     RQ : ICNC(2,INNO)=0 NOEUD MILIEU

      DO 40 INNO = 1,NNOEM
        IF (NUMELI(2,INNO).EQ.0) GOTO 40

C 4.1 - ERREUR LOCALE SUR LA COUCHE 1 (=EF CONNECTES A INNO)

        PREC1 = 0.D+0
        AIRTOT = 0.D+0

        DO 50 INEL = 1,NUMELI(1,INNO)
          NUEF = NUMELI(2+INEL,INNO)
          PREC1 = PREC1 + ERREUR(NUEF)**2
          AIRTOT = AIRTOT + AIRE(NUEF)
 50     CONTINUE 
        IF (PREC1.NE.0.D+0 .AND. AIRTOT.NE.0.D+0) THEN
          PREC1 = SQRT(PREC1/AIRTOT)
        ELSE
          PREC1 = 0.D+0
        ENDIF

C 4.2 - TEST1 : SI PREC1 > PRECREF ON PASSE AU TEST2 

        IF (PREC1.GT.PRECRE) THEN

C 4.2.1 - RECHERCHE DES NOEUDS ET EFS COMPOSANTS LES COUCHES 1,2 ET 3

          CALL DZONFG(NSOMMX,ICNC,NELCOM,NUMELI,INNO,
     &                TBELZO,NBELZO,TBNOZO,NBNOZO)

C 4.2.2 - ERREUR LOCALE SUR LA COUCHE 2
          
          PREC2 = 0.D+0
          AIRTOT = 0.D+0
          DO 60 INEL = NBELZO(1)+1,NBELZO(2)
            NUEF = TBELZO(INEL)
            PREC2 = PREC2 + ERREUR(NUEF)**2
            AIRTOT = AIRTOT + AIRE(NUEF)
 60       CONTINUE 
          IF (PREC2.NE.0.D+0 .AND. AIRTOT.NE.0.D+0) THEN
            PREC2 = SQRT(PREC2/AIRTOT)
          ELSE
            PREC2 = PREC1
          ENDIF

C 4.2.3 - TEST2 : SI PREC2<PREC1 ON PASSE AU TEST3

          IF (PREC2.LT.PREC1) THEN
            
C 4.2.3.1 - ERREUR LOCALE SUR LA COUCHE 3

            PREC3 = 0.D+0
            AIRTOT = 0.D+0
            DO 70 INEL = NBELZO(2)+1,NBELZO(3)
              NUEF = TBELZO(INEL)
              PREC3 = PREC3 + ERREUR(NUEF)**2
              AIRTOT = AIRTOT + AIRE(NUEF)
 70         CONTINUE 
            IF (PREC3.NE.0.D+0 .AND. AIRTOT.NE.0.D+0) THEN
              PREC3 = SQRT(PREC3/AIRTOT)
            ELSE
              PREC3 = PREC2
            ENDIF
            PREC3 = MIN(PREC3,PREC2)

C 4.2.3.2 - TEST3 : SI PREC1>6*PREC3 LE NOEUD INNO EST SINGULIER

            IF (PREC1/PREC3.GE.FACTP) THEN
              
C 4.2.3.2.1 - CALCUL DE PE
                            
              NBNOE=NBNOZO(1)+NBNOZO(2)+NBNOZO(3)
              CALL DFORT2(NSOMMX,ICNC,NDIM,INNO,
     &                   TBELZO,NBELZO(3),TBNOZO,NBNOZO,NBNOE,
     &                   XY,AIRE,ENERGI,PE)

              ALPREF=0.95D+0
              IF (PE.LT.ALPREF) THEN
                ALPHAN(INNO) = 0.5D+0
                IF (PE.GE.0.5D0) ALPHAN(INNO)=PE
              ELSE
                ALPHAN(INNO) = ALPHAN(INNO)*0.9999D+0
              ENDIF 
              
C 4.2.3.2 - FIN DU TEST3 PREC1>6*PREC3              
            ENDIF 
C 4.2.3 - FIN DU TEST2 PREC2<PREC1            
          ENDIF 
C 4.2 - FIN DU TEST1 PREC1>PRECREF
        ENDIF  
C 4 - FIN DE LA BOUCLE SUR LES NOEUDS
 40   CONTINUE                                                    

C 5 - ON REMPLIT LE TABLEAU ALPHA = DEGRE DE LA SINGULARITE PAR EF 
      
      NALPHA=1
      DO 100 INEL = 1,NELEM
        IF (ICNC(2,INEL).LT.1) GOTO 100
        DO 110 INNO=1,ICNC(1,INEL)
          ALPHA(INEL) = MIN(ALPHA(INEL),ALPHAN(ICNC(INNO+2,INEL)))
 110    CONTINUE
        IF (ALPHA(INEL).LT.DTYP) THEN
          NALPHA = NALPHA + 1
        ENDIF
 100  CONTINUE
      
      END
