      SUBROUTINE MDCHII ( IDFIMD, NOCHMD, TYPENT, TYPGEO,
     &                    PREFIX, NBTV,
     &                    CODRET )
C_____________________________________________________________________
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF PREPOST  DATE 18/09/2012   AUTEUR LADIER A.LADIER 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE SELLENET N.SELLENET
C ======================================================================
C     FORMAT MED - CHAMP - INFORMATIONS - FICHIER CONNU PAR IDENTIFIANT
C            --    --      -                                -
C     DONNE LE NOMBRE DE TABLEAUX DE VALEURS ET LEURS CARACTERISTIQUES
C     TEMPORELLES POUR UN CHAMP ET UN SUPPORT GEOMETRIQUE
C-----------------------------------------------------------------------
C      ENTREES:
C        IDFIMD : IDENTIFIANT DU FICHIER MED
C        NOCHMD : NOM MED DU CHAMP A LIRE
C        TYPENT : TYPE D'ENTITE AU SENS MED
C        TYPGEO : TYPE DE SUPPORT AU SENS MED
C      ENTREES/SORTIES:
C        PREFIX : BASE DU NOM DES STRUCTURES
C                 POUR LE TABLEAU NUMERO I
C                 PREFIX//'.NUME' : T(2I-1) = NUMERO DE PAS DE TEMPS
C                                   T(2I)   = NUMERO D'ORDRE
C                 PREFIX//'.INST' : T(I) = INSTANT S'IL EXISTE
C      SORTIES:
C        NBTV   : NOMBRE DE TABLEAUX DE VALEURS DU CHAMP
C        CODRET : CODE DE RETOUR (0 : PAS DE PB, NON NUL SI PB)
C_____________________________________________________________________
C
      IMPLICIT NONE
C
C 0.1. ==> ARGUMENTS
C
      INCLUDE 'jeveux.h'
      INTEGER IDFIMD
      INTEGER NBTV
      INTEGER TYPENT, TYPGEO
      INTEGER CODRET
C
      CHARACTER*19 PREFIX
      CHARACTER*(*) NOCHMD
C
C 0.2. ==> COMMUNS
C 0.3. ==> VARIABLES LOCALES
C
      CHARACTER*6 NOMPRO
      PARAMETER ( NOMPRO = 'MDCHII' )
C
      INTEGER EDNOPT
      INTEGER VALI(2)
      PARAMETER (EDNOPT=-1)
      INTEGER EDNONO
      PARAMETER (EDNONO=-1)
      INTEGER MFLOAT
      PARAMETER (MFLOAT=6)
      INTEGER ITERMA
      PARAMETER (ITERMA=1)
C
      INTEGER IFM, NIVINF, NBTV2, NCMP
      INTEGER IAUX, NBCHAM, LNOCHM, LXLGUT, NBCMFI
      INTEGER ADNCMP, ADUCMP, JAUX, EXISTC
      INTEGER FINUPT, FINUNO, NUMSAU
      INTEGER ADNUME, ADINST, NCHMED, JNPTNO, JPASDT
      INTEGER TYPECH, NBCMP, JCMP, JUNIT, NSEQCA, NPRO
      LOGICAL ILOCAL
C
      CHARACTER*8 SAUX08
      CHARACTER*64 SAUX64,NOMCHA,NOMPRF,NOMLOC,NOMAM2,NOMAMD
C
      REAL*8 FIINST
C
      CALL JEMARQ()
      CALL INFNIV ( IFM, NIVINF )
C
C====
C 1. LE CHAMP EST-IL PRESENT ?
C====
C
C 1.1. ==> NBCHAM : NOMBRE DE CHAMPS DANS LE FICHIER
C
      CALL MFNCHA ( IDFIMD, NBCHAM, CODRET )
      IF ( CODRET.NE.0 ) THEN
        SAUX08='MFNCHA  '
        CALL U2MESG('F','DVP_97',1,SAUX08,1,CODRET,0,0.D0)
      ENDIF
C
C 1.2. ==> RECHERCHE DU CHAMP VOULU
C
      LNOCHM = LXLGUT(NOCHMD)
      EXISTC = 0
      ILOCAL = .TRUE.
      NUMSAU = 0
C
      DO 12 , IAUX = 1 , NBCHAM
C
C 1.2.1. ==> NBCMFI : NOMBRE DE COMPOSANTES DANS LE FICHIER POUR
C                     LE CHAMP NUMERO IAUX
C
        CALL MFNCOM ( IDFIMD, IAUX, NBCMFI, CODRET )
        IF ( CODRET.NE.0 ) THEN
          SAUX08='MFNCOM  '
          CALL U2MESG('F','DVP_97',1,SAUX08,1,CODRET,0,0.D0)
        ENDIF
C
C 1.2.2. ==> POUR LE CHAMP NUMERO IAUX, ON RECUPERE :
C            SAUX64 : NOM DU CHAMP
C            ZK16(ADNCMP) : NOM DE SES NBCMFI COMPOSANTES
C            ZK16(ADUCMP) : UNITES DE SES NBCMFI COMPOSANTES
C
        CALL CODENT ( IAUX,'G',SAUX08 )
        CALL WKVECT ('&&'//NOMPRO//'N'//SAUX08,
     &               'V V K16',  NBCMFI, ADNCMP )
        CALL WKVECT ('&&'//NOMPRO//'U'//SAUX08,
     &               'V V K16', NBCMFI, ADUCMP )
C                 12345678901234567890123456789012
        SAUX64 = '                                        '//
     &'                        '
        CALL MFCHAI ( IDFIMD, IAUX, SAUX64, JAUX, ZK16(ADNCMP),
     &                ZK16(ADUCMP), NSEQCA, CODRET )
        IF ( CODRET.NE.0 .OR. JAUX.NE.MFLOAT ) THEN
          IF ( CODRET.NE.0 ) THEN
            SAUX08='MFCHAI  '
            CALL U2MESG('F','DVP_97',1,SAUX08,1,CODRET,0,0.D0)
          ENDIF
C         TYPE INCORRECT
          IF (JAUX.NE.MFLOAT) THEN
            VALI (1) = JAUX
            CALL U2MESG('A+','MED_84',0,' ',1,VALI,0,0.D0)
            CALL U2MESS('F','MED_75')
          ENDIF
        ENDIF
C
C 1.2.3. ==> COMPARAISON DU NOM DU CHAMP
C
        JAUX = LXLGUT(SAUX64)
C
        IF ( JAUX.EQ.LNOCHM ) THEN
          IF ( SAUX64(1:JAUX).EQ.NOCHMD(1:LNOCHM) ) THEN
            NUMSAU = IAUX
            EXISTC = 1
          ENDIF
        ENDIF
C
   12 CONTINUE
      CALL ASSERT(NUMSAU.NE.0)
C
      IF ( EXISTC.NE.1 ) THEN
        CALL MFNCHA( IDFIMD, NCHMED, CODRET )
        CALL U2MESK('F+', 'MED_57', 1, NOCHMD(1:LNOCHM))
        DO 50, IAUX = 1, NCHMED
          CALL MFNCOM( IDFIMD, IAUX, NBCMP, CODRET )
          CALL WKVECT( '&&MDCHII.NOMCMP_K16', 'V V K16', NBCMP, JCMP )
          CALL WKVECT( '&&MDCHII.UNITCMP','V V K16', NBCMP, JUNIT )
          CALL MFCHAI ( IDFIMD, IAUX, NOMCHA, TYPECH, ZK16(JCMP),
     &                  ZK16(JUNIT), NSEQCA, CODRET)
          CALL U2MESK('F+', 'MED2_2', 1, NOMCHA)
          CALL JEDETR('&&MDCHII.NOMCMP_K16')
          CALL JEDETR('&&MDCHII.UNITCMP')
   50   CONTINUE
        CALL U2MESS('F', 'VIDE_1')
      ENDIF
C
C====
C 2. NOMBRE DE TABLEAUX DE VALEURS ASSOCIES AU CHAMP
C====
C
      CALL MFNCO2(IDFIMD,NOCHMD,NCMP,CODRET)
      CALL WKVECT('&&MDCHII.CNAME','V V K16',NCMP,JCMP)
      CALL WKVECT('&&MDCHII.CUNIT','V V K16',NCMP,JUNIT)
C
      CALL MFNPDT ( IDFIMD, NOCHMD, NOMAM2, NBTV, ZK16(JUNIT),
     &              ZK16(JCMP), CODRET )
C
      IF ( CODRET.NE.0 ) THEN
        SAUX08='MFNPDT  '
        CALL U2MESG('F','DVP_97',1,SAUX08,1,CODRET,0,0.D0)
      ENDIF
C
      CALL JEDETR('&&MDCHII.CNAME')
      CALL JEDETR('&&MDCHII.CUNIT')
      IF ( NIVINF.GT.1 ) THEN
        WRITE (IFM,20001) NOCHMD, NBTV
      ENDIF
20001 FORMAT
     & ('LE CHAMP MED ',A,' CONTIENT ',I6,' TABLEAUX SUR LES NOEUDS :')
C
C====
C 3. ALLOCATION DES TABLEAUX
C====
C
      IF ( NBTV.GT.0 ) THEN
C
        CALL WKVECT ( PREFIX//'.NUME' ,'V V I',   2*NBTV, ADNUME )
        CALL WKVECT ( PREFIX//'.INST' ,'V V R',     NBTV, ADINST )
C
      ENDIF
C
C====
C 4. POUR CHAQUE TABLEAU :
C====
C
      CALL CODENT ( NUMSAU,'G',SAUX08 )
      CALL JEEXIN('&&MDCHI2.'//SAUX08,CODRET)
      IF ( CODRET.EQ.0 ) THEN
        CALL WKVECT('&&MDCHI2.'//SAUX08,'V V I',2*NBTV,JNPTNO)
        CALL WKVECT('&&MDCHI3.'//SAUX08,'V V R',NBTV,JPASDT)
        DO 60, IAUX = 1 , NBTV
C
C 4.1. ==> LECTURE
C    . NUMERO, UNITE ET VALEUR DU PAS DE TEMPS : FINUPT, FIINST
C    . NUMERO D'ORDRE : FINUNO
C
          CALL MFPDTI ( IDFIMD, NOCHMD, IAUX, FINUPT, FINUNO, FIINST,
     &                  CODRET )
C
          IF ( CODRET.NE.0 ) THEN
            SAUX08='MFPDTI  '
            CALL U2MESG('F','DVP_97',1,SAUX08,1,CODRET,0,0.D0)
          ENDIF
          ZI(JNPTNO+IAUX*2-2) = FINUPT
          ZI(JNPTNO+IAUX*2-1) = FINUNO
          ZR(JPASDT+IAUX-1) = FIINST
  60    CONTINUE
      ELSE
        CALL JEVEUO('&&MDCHI2.'//SAUX08,'L',JNPTNO)
        CALL JEVEUO('&&MDCHI3.'//SAUX08,'L',JPASDT)
      ENDIF
C
      NBTV2 = NBTV
      DO 40 , IAUX = 1 , NBTV
C
        FINUPT = ZI(JNPTNO+IAUX*2-2)
        FINUNO = ZI(JNPTNO+IAUX*2-1)
        FIINST = ZR(JPASDT+IAUX-1)
C
        IF ( .NOT. ILOCAL ) THEN
          CALL U2MESS('F','MED_60')
        ENDIF
C
C       ON REGARDE DANS LE FICHIER MED SI DES VALEURS SONT REFERENCEES
C       POUR TYPENT ET TYPGEO, SI CE N'EST PAS LE CAS, ON RETIRE
C       CET INSTANT DE LA LISTE
        CALL MFPRLO( IDFIMD, NOCHMD, FINUPT, FINUNO, TYPENT, TYPGEO,
     &               ITERMA, NOMAMD, NOMPRF, NOMLOC, NPRO, CODRET )
C
        IF ( NPRO.EQ.0 ) THEN
          NBTV2 = NBTV2 - 1
          GOTO 40
        ENDIF
C
        IF ( NIVINF.GT.1 ) THEN
          IF ( FINUPT.EQ.EDNOPT ) THEN
            WRITE (IFM,40012)
          ELSE
            WRITE (IFM,40022) FINUPT, FIINST
          ENDIF
          IF ( FINUNO.EQ.EDNONO ) THEN
            WRITE (IFM,40013)
          ELSE
            WRITE (IFM,40023) FINUNO
          ENDIF
        ENDIF
C
C 4.2. ==> ARCHIVAGE DANS LES TABLEAUX EXPLOITES PAR LE PROGRAMME
C          APPELANT
C
        ZI(ADNUME+2*IAUX-2)  = FINUPT
        ZI(ADNUME+2*IAUX-1)  = FINUNO
        IF ( FINUPT.NE.EDNOPT ) THEN
          ZR(ADINST+IAUX-1)  = FIINST
        ENDIF
C
   40 CONTINUE
C
C     SOIT ON A TROUVE POUR TOUS LES INSTANTS DES VALEURS DANS
C     LE FICHIER MED, SOIT ON EN A TROUVE AUCUNE
C     LE CAS INTERMEDIAIRE EST PROBLEMATIQUE ET NON TRAITE
      CALL ASSERT((NBTV.EQ.NBTV2).OR.(NBTV2.EQ.0))
      IF ( NBTV2.EQ.0 ) THEN
        CALL JEDETR(PREFIX//'.NUME')
        CALL JEDETR(PREFIX//'.INST')
        NBTV = 0
      ENDIF
C
40012 FORMAT(  2X,'. AUCUNE INDICATION DE PAS DE TEMPS')
40022 FORMAT(  2X,'. PAS DE TEMPS NUMERO ',I5,
     &       /,2X,'. INSTANT : ',G13.5)
40013 FORMAT(  2X,'. AUCUNE INDICATION DE NUMERO D''ORDRE',/)
40023 FORMAT(  2X,'. NUMERO D''ORDRE : ',I5,/)
C
C --- MENAGE
      DO 70 , IAUX = 1 , NBCHAM
        CALL CODENT ( IAUX,'G',SAUX08 )
        CALL JEDETR ('&&'//NOMPRO//'N'//SAUX08)
        CALL JEDETR ('&&'//NOMPRO//'U'//SAUX08)
   70 CONTINUE
C
      CALL JEDEMA()
      END
