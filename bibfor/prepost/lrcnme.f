      SUBROUTINE LRCNME ( CHANOM, NOCHMD, NOMAMD,
     &                    NOMAAS, NOMGD, TYPENT,
     &                    NBCMPV, NCMPVA, NCMPVM,
     &                    IINST, NUMPT, NUMORD, INST, CRIT, PREC,
     &                    NROFIC, CODRET )
C_____________________________________________________________________
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF PREPOST  DATE 13/06/2012   AUTEUR COURTOIS M.COURTOIS 
C RESPONSABLE SELLENET N.SELLENET
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C     LECTURE D'UN CHAMP AUX NOEUDS - FORMAT MED
C     -    -       -         -               --
C-----------------------------------------------------------------------
C     ENTREES:
C        CHANOM : NOM ASTER DU CHAMP A LIRE
C        NOCHMD : NOM MED DU CHAMP DANS LE FICHIER
C        NOMAMD : NOM MED DU MAILLAGE LIE AU CHAMP A LIRE
C                  SI ' ' : ON SUPPOSE QUE C'EST LE PREMIER MAILLAGE
C                           DU FICHIER
C        TYPENT : TYPE D'ENTITE DU CHAMP 
C                (MED_NOEUD=3,MED_MAILLE=0,MED_NOEUD_MAILLE=4)
C        NOMAAS : NOM ASTER DU MAILLAGE
C        NOMGD  : NOM DE LA GRANDEUR ASSOCIEE AU CHAMP
C        NBCMPV : NOMBRE DE COMPOSANTES VOULUES
C                 SI NUL, ON LIT LES COMPOSANTES A NOM IDENTIQUE
C        NCMPVA : LISTE DES COMPOSANTES VOULUES POUR ASTER
C        NCMPVM : LISTE DES COMPOSANTES VOULUES DANS MED
C        IINST  : 1 SI LA DEMANDE EST FAITE SUR UN INSTANT, 0 SINON
C        NUMPT  : NUMERO DE PAS DE TEMPS EVENTUEL
C        NUMORD : NUMERO D'ORDRE EVENTUEL DU CHAMP
C        INST   : INSTANT EVENTUEL
C        CRIT   : CRITERE SUR LA RECHERCHE DU BON INSTANT
C        PREC   : PRECISION SUR LA RECHERCHE DU BON INSTANT
C        NROFIC : NUMERO NROFIC LOGIQUE DU FICHIER MED
C     SORTIES:
C        CODRET : CODE DE RETOUR (0 : PAS DE PB, NON NUL SI PB)
C_____________________________________________________________________
C
      IMPLICIT NONE
C
C 0.1. ==> ARGUMENTS
C
      INCLUDE 'jeveux.h'
      CHARACTER*(*) CHANOM
      CHARACTER*(*) NCMPVA, NCMPVM
      CHARACTER*8 NOMAAS, NOMGD
      CHARACTER*8 CRIT
      CHARACTER*(*) NOCHMD, NOMAMD
C
      INTEGER NROFIC,TYPENT
      INTEGER NBCMPV
      INTEGER IINST, NUMPT, NUMORD
      INTEGER CODRET
C
      REAL*8 INST
      REAL*8 PREC
C
C 0.2. ==> COMMUNS
C
C 0.3. ==> VARIABLES LOCALES
C
      CHARACTER*6 NOMPRO
      PARAMETER ( NOMPRO = 'LRCNME' )
C
      INTEGER IAUX,IRET,JCMPVA,NBCMPA,NBCHAM,I,NBCMP,JCMP,JUNIT
      INTEGER IBID,NSEQCA,TYCHA,IDFIMD,J
      INTEGER JNOCMP, NCMPRF,UBID
      PARAMETER(UBID=1)
      INTEGER UNBID(UBID)
      INTEGER EDLECT
      PARAMETER (EDLECT=0)
C
      CHARACTER*1 SAUX01
      CHARACTER*8 K8B,SAUX08,PARBID
      CHARACTER*19 CHAMN
      CHARACTER*19 CHAMNS,LIGBID
      CHARACTER*24 OPTBID
      CHARACTER*64 NOMCHA
      CHARACTER*200 NOFIMD
      CHARACTER*255 KFIC
      LOGICAL TTT
C
C====
C 1. ALLOCATION D'UN CHAM_NO_S  (CHAMNS)
C====
C
      CALL JEMARQ ( )
C 1.1. ==> REPERAGE DES CARACTERISTIQUES DE CETTE GRANDEUR
C
      CALL JENONU ( JEXNOM ( '&CATA.GD.NOMGD', NOMGD ) , IAUX )
      IF ( IAUX.EQ.0 ) THEN
        CALL U2MESS('F','MED_65')
      ENDIF
      CALL JEVEUO ( JEXNOM ( '&CATA.GD.NOMCMP', NOMGD ) ,
     &              'L', JNOCMP )
      CALL JELIRA ( JEXNOM ( '&CATA.GD.NOMCMP', NOMGD ) ,
     &              'LONMAX', NCMPRF, SAUX01 )
C
C 1.2. ==> ALLOCATION DU CHAM_NO_S
C
C               1234567890123456789
      CHAMNS = '&&      .CNS.MED   '
      CHAMNS(3:8) = NOMPRO
C
      CALL JEEXIN ( NCMPVA, IRET )
      IF ( IRET.GT.0 ) THEN
        CALL JEVEUO ( NCMPVA, 'L', JCMPVA )
        CALL JELIRA ( NCMPVA, 'LONMAX', NBCMPA, K8B )
        IF (NBCMPA.LE.NCMPRF) THEN
          DO 20 I=1,NBCMPA
            TTT=.FALSE.
            DO 30 J=1,NCMPRF
              IF (ZK8(JCMPVA+I-1).EQ.ZK8(JNOCMP+J-1)) TTT=.TRUE.
 30         CONTINUE
            IF (.NOT.TTT) THEN
              CALL U2MESS('F','MED_66')
            ENDIF
 20       CONTINUE
        ELSE
          CALL U2MESS('F','MED_70')
        ENDIF
C
      ELSE
        CALL ULISOG(NROFIC, KFIC, SAUX01)
        IF ( KFIC(1:1).EQ.' ' ) THEN
          CALL CODENT ( NROFIC, 'G', SAUX08 )
          NOFIMD = 'fort.'//SAUX08
        ELSE
          NOFIMD = KFIC(1:200)
        ENDIF
        CALL MFOUVR ( IDFIMD, NOFIMD, EDLECT, IRET )
        CALL MFNCHA ( IDFIMD, NBCHAM, IRET )
        DO 780 I=1,NBCHAM
          CALL MFNCOM(IDFIMD,I,NBCMP,IRET)
          CALL WKVECT('&&LRCNME.NOMCMP_K16','V V K16',NBCMP,JCMP)
          CALL WKVECT('&&LRCNME.UNITCMP','V V K16',NBCMP,JUNIT)
          CALL MFCHAI(IDFIMD,I,NOMCHA,TYCHA,ZK16(JCMP),
     &                ZK16(JUNIT),NSEQCA,IRET)
          IF( NOMCHA.EQ.NOCHMD )THEN
            NCMPRF=NBCMP
            CALL WKVECT('&&LRCNME.NOMCMP_K8','V V K8',NBCMP,JNOCMP)
            DO 778 J=1,NBCMP
              ZK8(JNOCMP+J-1)=ZK16(JCMP+J-1)(1:8)
 778        CONTINUE
            CALL JEDETR('&&LRCNME.NOMCMP_K16')
            CALL JEDETR('&&LRCNME.UNITCMP')
            GOTO 780
          ENDIF
          CALL JEDETR('&&LRCNME.NOMCMP_K16')
          CALL JEDETR('&&LRCNME.UNITCMP')
 780    CONTINUE
        CALL MFFERM ( IDFIMD, IRET )
C
      ENDIF
C
C====
C 2. LECTURE
C====
C
      LIGBID=' '
      OPTBID=' '
      PARBID=' '
      CALL LRCAME ( NROFIC, NOCHMD, NOMAMD, NOMAAS, LIGBID,
     &              OPTBID, PARBID, 'NOEU', TYPENT,
     &              UNBID, UNBID,
     &              NBCMPV, NCMPVA, NCMPVM,
     &              IINST, NUMPT, NUMORD, INST, CRIT, PREC,
     &              NOMGD,  NCMPRF, JNOCMP,
     &              CHAMNS, CODRET )
C
C====
C 3. TRANSFORMATION DU CHAM_NO_S EN CHAM_NO :
C====
C
      CHAMN = CHANOM
C
      CALL CNSCNO ( CHAMNS,' ','NON', 'G', CHAMN,'F',IBID)
C
      CALL DETRSD ( 'CHAM_NO_S', CHAMNS )
C
C====
C 4. BILAN
C====
C
      IF ( CODRET.NE.0 ) THEN
         CALL U2MESK('A','MED_55',1,CHAMN)
      ENDIF
      CALL JEDETR('&&LRCNME.NOMCMP_K8')
      CALL JEDEMA ( )
C
      END
