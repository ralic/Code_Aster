      SUBROUTINE IRMMFA ( FID, NOMAMD,
     >                    NBNOEU, NBMAIL,
     >                    NOMAST, NBGRNO, NOMGNO, NBGRMA, NOMGMA,
     >                    PREFIX,
     >                    TYPGEO, NOMTYP, NMATYP,
     >                    INFMED )
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF PREPOST  DATE 22/07/2003   AUTEUR LAVERNE J.LAVERNE 
C ======================================================================
C COPYRIGHT (C) 1991 - 2002  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C RESPONSABLE GNICOLAS G.NICOLAS
C TOLE CRP_20
C-----------------------------------------------------------------------
C     ECRITURE DU MAILLAGE -  FORMAT MED - LES FAMILLES
C        -  -     -                  -         --
C-----------------------------------------------------------------------
C     L'ENSEMBLE DES FAMILLES EST L'INTERSECTION DE L'ENSEMBLE 
C     DES GROUPES : UN NOEUD/MAILLE APPARAIT AU PLUS DANS 1 FAMILLE
C     TABLE  NUMEROS DES FAMILLES POUR LES NOEUDS  <-> TABLE  DES COO
C     TABLES NUMEROS DES FAMILLES POUR MAILLE/TYPE <-> TABLES DES CNX
C     PAR CONVENTION, LES FAMILLES DE NOEUDS SONT NUMEROTEES >0 ET LES
C     FAMILLES DE MAILLES SONT NUMEROTEES <0. LA FAMILLE NULLE EST
C     DESTINEE AUX NOEUDS / ELEMENTS SANS FAMILLE.
C     ENTREE:
C       FID    : IDENTIFIANT DU FICHIER MED
C       NOMAMD : NOM DU MAILLAGE MED
C       NBNOEU : NOMBRE DE NOEUDS DU MAILLAGE
C       NBMAIL : NOMBRE DE MAILLES DU MAILLAGE
C       NOMAST : NOM DU MAILLAGE ASTER
C       NBGRNO : NOMBRE DE GROUPES DE NOEUDS
C       NBGRMA : NOMBRE DE GROUPES DE MAILLES
C       NOMGNO : VECTEUR NOMS DES GROUPES DE NOEUDS
C       NOMGMA : VECTEUR NOMS DES GROUPES DE MAILLES
C       PREFIX : PREFIXE POUR LES TABLEAUX DES RENUMEROTATIONS
C       TYPGEO : TYPE MED POUR CHAQUE MAILLE
C       NOMTYP : NOM DES TYPES POUR CHAQUE MAILLE
C       NMATYP : NOMBRE DE MAILLES PAR TYPE
C       INFMED : NIVEAU DES INFORMATIONS A IMPRIMER
C-----------------------------------------------------------------------
C
      IMPLICIT NONE
C
C 0.1. ==> ARGUMENTS
C
      INTEGER FID 
      INTEGER TYPGEO(*), NMATYP(*)
      INTEGER NBNOEU, NBMAIL, NBGRNO, NBGRMA
      INTEGER INFMED
C
      CHARACTER*6 PREFIX
      CHARACTER*8 NOMAST, NOMGNO(*),NOMGMA(*)
      CHARACTER*8 NOMTYP(*)
      CHARACTER*(*) NOMAMD
C
C 0.2. ==> COMMUNS
C
C     ----------- COMMUNS NORMALISES  JEVEUX  --------------------------
      COMMON /IVARJE/ZI(1)
      COMMON /RVARJE/ZR(1)
      COMMON /CVARJE/ZC(1)
      COMMON /LVARJE/ZL(1)
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
      INTEGER ZI
      REAL*8 ZR
      COMPLEX*16 ZC
      LOGICAL ZL
      CHARACTER*8  ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32, JEXNUM
      CHARACTER*80 ZK80
C     -----  FIN  COMMUNS NORMALISES  JEVEUX --------------------------
C
C 0.3. ==> VARIABLES LOCALES
C
      CHARACTER*6 NOMPRO
      PARAMETER ( NOMPRO = 'IRMMFA' )
C
      INTEGER NTYMAX
      PARAMETER (NTYMAX = 48)
      INTEGER EDECRI
      PARAMETER (EDECRI=1)
      INTEGER EDMAIL, EDNOEU
      PARAMETER (EDMAIL=0, EDNOEU=3)
      INTEGER TYGENO
      PARAMETER (TYGENO=0)
C
      INTEGER CODRET
      INTEGER NBEC
      INTEGER IAUX, JAUX, KAUX
      INTEGER IAUX0, IAUX1, IAUX2
      INTEGER NUMFAM, NFAM
      INTEGER ITYP
      INTEGER NBN, IGN, INO, INOFAM, NBGNOF, NATT
      INTEGER NBM, IGM, IMA, IMAFAM, NBGMAF
      INTEGER JENTXG
      INTEGER JNOFAM, JNOFAC, JGNOFA, JGRNO
      INTEGER JMAFAM, JMAFAC, JGMAFA, JGRMA, JMAFAT
      INTEGER IFM, NIVINF
      INTEGER TABAUX(1)
C
      CHARACTER*8 SAUX08
      CHARACTER*32  NOMFAM
      CHARACTER*80  SAUX80
      CHARACTER*200 K200
C      REAL*8 TPS1(4),TPS2(4),TPS3(4)
C
C====
C 1. PREALABLES
C====
C
      CALL JEMARQ()
C
      CALL INFNIV ( IFM, NIVINF )
C      CALL UTTCPU(1,'INIT',4,TPS1)
C      CALL UTTCPU(2,'INIT',4,TPS2)
C      CALL UTTCPU(3,'INIT',4,TPS3)
C      CALL UTTCPU(1,'DEBUT',4,TPS1)
C
C====
C 2. LES FAMILLES DE NOEUDS
C====
C
C      CALL UTTCPU (2,'DEBUT',4,TPS2)
C
C 2.1. ==> INITIALISATIONS
C
C     NFAM = NUMERO DE LA DERNIERE FAMILLE ENREGISTREE (DE 0 A N>0)
C     FAMILLE 0 = NOEUDS N'APPARTENANT A AUCUN GROUPE
      NFAM  = 0
C
C     VECTEUR NUMEROS DES FAMILLES DE NOEUDS = NB NOEUDS
C     PAR DEFAUT, JEVEUX MET TOUT A 0. CELA SIGNIFIE QUE LES NOEUDS
C     APPARTIENNENT A LA FAMILLE NULLE.
C
      CALL WKVECT('&&'//NOMPRO//'.NOFAM','V V I',NBNOEU,JNOFAM)
C
C 2.2. ==> S'IL EXISTE DES GROUPES DE NOEUDS, ON CONSTRUIT LES FAMILLES
C
      IF ( NBGRNO .NE. 0 ) THEN
C
C 2.2.1. ==> ALLOCATIONS DE TABLEAUX DE TRAVAIL
C
C     VECTEUR NUMEROS DES FAMILLES DE NOEUDS CREES = NBNOEU MAX
      CALL WKVECT('&&'//NOMPRO//'.NOFAMC','V V I',NBNOEU,JNOFAC)
C
C     VECTEUR NOMS DES GROUPES DE NOEUDS / FAMILLE = NB GRP MAX
      CALL WKVECT('&&'//NOMPRO//'.GRNOFA','V V K80',NBGRNO,JGNOFA)
C
C     ON UTILISE DES TABLEAUX DE BITS 
C     (30 PAR ENTIER INT*4 NBEC ENIERS)  
C     POUR ENREGISTRER LA PRESENCE D UN NOEUD DANS UN GROUPE 
      NBEC = (NBGRNO-1) / 30 + 1
C     COLOSSAL ALLOC DU TABLEAU CROISE DES NOEUDS X GROUPES (>VECTEUR)
      CALL WKVECT('&&'//NOMPRO//'.NOXG','V V I',NBNOEU*NBEC,JENTXG)
C
C 2.2.2. ==> ON BOUCLE SUR LES GROUPES
C          POUR CHAQUE GROUPE, ON BOUCLE SUR LES NOEUDS QU'IL CONTIENT.
C          A LA FIN, ON CONNAIT POUR CHAQUE NOEUD SES GROUPES
C          D'APPARTENANCE
C
      DO 221 , IGN = 1 , NBGRNO
C
        CALL JEVEUO(JEXNUM(NOMAST//'.GROUPENO',IGN),'L',JGRNO)
        CALL JELIRA(JEXNUM(NOMAST//'.GROUPENO',IGN),'LONMAX',NBN,SAUX08)
C
        DO 2211 , IAUX = 1 , NBN
C         DEBUT VECTEUR ENTIER CODE POUR NOEUD INO DANS JENTXG 
          INO = ZI(JGRNO-1+IAUX)
C         ENREGISTREMENT APPARTENANCE DU NOEUD AU GROUPE
          CALL SETGFA (ZI(JENTXG+(INO-1)*NBEC),IGN)
C         MISE A -1 DU NUM DE FAMILLE POUR CE NOEUD DANS JNOFAM
C         POUR INDIQUER QU'IL APPARTIENT AU MOINS A UN GROUPE
          ZI(JNOFAM-1+INO) = -1          
 2211   CONTINUE              
C
 221  CONTINUE 
C
C 2.2.3. ==> ON BOUCLE SUR LES NOEUDS APPARTENANT AU MOINS A UN GROUPE
C            ET ON LES RASSEMBLE PAR IDENTITE D'APPARTENANCE
C            LES FAMILLES SONT NUMEROTEES DANS L'ORDRE D'APPARITION
C            ATTENTION : CET ALGORITHME A ETE OPTIMISE LE 6/9/2002
C                        ETRE PRUDENT DANS LES AMELIORATIONS FUTURES ...
C                        CE QUI EST PENALISANT : QUELQUES DIZAINES DE
C                        MILLIERS DE NOEUDS ET QUELQUES CENTAINES DE
C                        GROUPES
C                        EXEMPLE : ON EST PASSE D'UNE VINGTAINE D'HEURES
C                        A 3 MINUTES AVEC UN GROS MAILLAGE :
C                        . 426 817 NOEUDS EN 57 GROUPES ET
C                        . 418 514 MAILLES EN 8 629 GROUPES.
C
      IAUX0 = JNOFAM-1
      IAUX1 = JNOFAC-1
      IAUX2 = JENTXG-1-NBEC
C
      DO 231 , INO = 1 , NBNOEU
C 
        IF ( ZI(IAUX0+INO).NE.0 ) THEN
C
C         BOUCLE 2311 : ON PARCOURT TOUTES LES FAMILLES DEJA VUES.
C         POUR CHACUNE D'ELLES, ON COMPARE LES GROUPES ASSOCIES ET LES
C         GROUPES DU NOEUD COURANT :
C         MEME COMPOSITION DE GROUPES <==> MEMES ENTIERS CODES
C         . SI C'EST LA MEME COMPOSITION DE GROUPES, LA FAMILLE EST LA
C           MEME. ON DONNE DONC LE NUMERO DE FAMILLE AU NOEUD COURANT.
C         . SI ON N'A TROUVE AUCUNE FAMILLE, C'EST QU'UNE NOUVELLE
C           FAMILLE VIENT D'APPARAITRE. ON STOCKE SES CARACTERISTIQUES.
C
          JAUX = IAUX2 + NBEC*INO
C
          DO 2311 , NUMFAM = 1 , NFAM
C
            INOFAM = ZI(IAUX1+NUMFAM)
C
            KAUX = IAUX2 + NBEC*INOFAM
C
            DO 2312 , IAUX = 1 , NBEC
              IF ( ZI(JAUX+IAUX) .NE. ZI(KAUX+IAUX) ) THEN
                GOTO 2311
              ENDIF
 2312       CONTINUE 
C
C           ON A TROUVE UNE FAMILLE AVEC LA MEME COMPOSITION :
C           . ON NOTE QUE LA FAMILLE EST LA MEME
C           . ON PASSE AU NOEUD SUIVANT
C
            ZI(IAUX0+INO) = ZI(IAUX0+INOFAM)  
            GOTO 231      
C
 2311     CONTINUE  
C
C         AUCUN NOEUD NE CORRESPONDAIT : ON CREE UNE NOUVELLE FAMILLE
          NFAM = NFAM + 1      
C         ON MEMORISE CE NUMERO DE FAMILLE POUR LE NOEUD COURANT
          ZI(IAUX0+INO) = NFAM
C         ON INDIQUE OU SE TROUVE LA PREMIERE REFERENCE A CETTE FAMILLE
C         DANS LE VECTEUR JNOFAM POUR EVITER DE PERDRE SON TEMPS APRES
          ZI(IAUX1+NFAM) = INO
C
        ENDIF
C
 231  CONTINUE              
C
C 2.2.4. ==> CREATION DES FAMILLES DE NOEUDS 
C          ON PARCOURT LES FAMILLES REPERTORIEES.
C          ON MEMORISE LES NOMS ET NUMEROS DES GROUPES QUI LA
C          CARACTERISENT. POUR CELA, ON SE BASE SUR LE PREMIER NOEUD
C          QUI EN FAIT PARTIE.
C      
      DO 241 , IAUX = 1 , NFAM
C
C 2.2.4.1. ==> DETERMINATION DE LA FAMILLE : NOM, NOMS ET NUMEROS DES
C              GROUPES ASSOCIES
C
        NUMFAM = IAUX
C       NUMERO DU PREMIER NOEUD FAISANT REFERENCE A CETTE FAMILLE 
        INO = ZI(JNOFAC-1+IAUX)       
C       NB ET NOMS+NUMS DES GROUPES DE LA FAMILLE (LIRE ENTIERS CODES)
        CALL NOMGFA ( NOMGNO, NBGRNO, ZI(JENTXG+(INO-1)*NBEC),
     >                ZK80(JGNOFA), NBGNOF )
C       NOM DE LA FAMILLE
        CALL MDNOFA ( 1, NUMFAM, ZK80(JGNOFA), NBGNOF, NOMFAM )
C
C 2.2.4.2. ==> ECRITURE DES CARACTERISTIQUES DE LA FAMILLE
C
        NATT = 0
        CALL EFFAMC ( FID, NOMAMD, NOMFAM, NUMFAM,
     >                IAUX, IAUX, K200, NATT,
     >                ZK80(JGNOFA), NBGNOF, CODRET )        
        IF ( CODRET.NE.0 ) THEN
          CALL CODENT ( CODRET,'G',SAUX08 )
          CALL UTMESS ('F',NOMPRO,'MED: ERREUR EFFAMC NUMERO '//SAUX08)
        ENDIF
C
C 2.2.4.3. ==> INFORMATION EVENTUELLE
C
        IF ( INFMED.GE.2 ) THEN
          JAUX = 0
          DO 2413 , INO = 1,NBNOEU
            IF ( ZI(JNOFAM-1+INO).EQ.NUMFAM ) THEN
              JAUX = JAUX + 1
            ENDIF
 2413     CONTINUE
          KAUX = 0
          CALL DESGFA ( 1, NUMFAM, NOMFAM,
     >                  NBGNOF, ZK80(JGNOFA), NATT, TABAUX,
     >                  JAUX, KAUX,
     >                  IFM, CODRET )
        ENDIF
C
 241  CONTINUE
C
      ENDIF
C     
C 2.3. ==> ECRITURE DE LA TABLE DES NUMEROS DE FAMILLES DES NOEUDS
C
      CALL EFFAME ( FID, NOMAMD, ZI(JNOFAM), NBNOEU,
     >              EDECRI, EDNOEU, TYGENO, CODRET ) 
      IF ( CODRET.NE.0 ) THEN
        CALL CODENT ( CODRET,'G',SAUX08 )
        CALL UTMESS ('F',NOMPRO,'MED: ERREUR EFFAME NUMERO '//SAUX08)
      ENDIF
C
C      CALL UTTCPU (2,'FIN',4,TPS2)
C
C 2.4. ==> ON FAIT UN PREMIER MENAGE POUR LIBERER LA PLACE PRISE
C          PAR TOUS LES TABLEAUX DE TRAVAIL, EN PARTICULIER NOXG
C
      CALL JEDETC('V','&&'//NOMPRO,1)
C
C====
C 3. LES FAMILLES DE MAILLES
C====
C
C      CALL UTTCPU(3,'DEBUT',4,TPS3)
C
C 3.1. ==> INITIALISATIONS
C
C     NFAM = NUMERO DE LA DERNIERE FAMILLE ENREGISTREE (DE 0 A N>0)
C     FAMILLE 0 = MAILLES N'APPARTENANT A AUCUN GROUPE
      NFAM  = 0
C
C     VECTEUR NUMEROS DES FAMILLES DES MAILLES = NB MAILLES
C     PAR DEFAUT, JEVEUX MET TOUT A 0. CELA SIGNIFIE QUE LES MAILLES
C     APPARTIENNENT A LA FAMILLE NULLE.
C
      CALL WKVECT('&&'//NOMPRO//'.MAFAM','V V I',NBMAIL,JMAFAM)
C
C 3.2. ==> S'IL EXISTE DES GROUPES DE MAILLES, ON CONSTRUIT LES FAMILLES
C
      IF ( NBGRMA .NE. 0 ) THEN
C
C 3.2.1. ==> ALLOCATIONS DE TABLEAUX DE TRAVAIL
C     ON FAIT COMME POUR LES NOEUDS PUIS ON ECRIT LES FAMILLES PAR TYPE
C     ON UTILISE DES TABLEAUX DE BITS : 30 PAR ENTIER INT*4 (NBEC INT)  
C     POUR ENREGISTRER LA PRESENCE D UNE MAILLE DANS UN GROUPE 
      NBEC = (NBGRMA-1) / 30 + 1
C
C     VECTEUR NUMEROS DES FAMILLES DES MAILLES CREES=NBMAIL MAX
      CALL WKVECT('&&'//NOMPRO//'.MAFAMC','V V I',NBMAIL,JMAFAC)
C
C     VECTEUR NOMS DES GROUPES DE MAILLES / FAMILLE= NB GRP MAX 
      CALL WKVECT('&&'//NOMPRO//'.GRMAFA','V V K80',NBGRMA,JGMAFA)
C
C     COLOSSAL ALLOC DU TABLEAU CROISE DES MAILLES X GROUPES (>VECTEUR)
      CALL WKVECT('&&'//NOMPRO//'.MAXG','V V I',NBMAIL*NBEC,JENTXG)
C
C 3.2.2. ==> ON BOUCLE SUR LES GROUPES
C          POUR CHAQUE GROUPE, ON BOUCLE SUR LES MAILLES QU'IL CONTIENT.
C          A LA FIN, ON CONNAIT POUR CHAQUE MAILLE SES GROUPES
C          D'APPARTENANCE
C
      DO 321 , IGM = 1 , NBGRMA
C
        CALL JEVEUO(JEXNUM(NOMAST//'.GROUPEMA',IGM),'L',JGRMA)
        CALL JELIRA(JEXNUM(NOMAST//'.GROUPEMA',IGM),'LONMAX',NBM,SAUX08)
C
        DO 3211 , IAUX = 1, NBM
C         DEBUT VECTEUR ENTIER CODE POUR MAILLE IMA DANS JENTXG 
          IMA = ZI(JGRMA-1+IAUX)
C         ENREGISTREMENT APPARTENANCE DE LA MAILLE AU GROUPE
          CALL SETGFA (ZI(JENTXG+(IMA-1)*NBEC),IGM)
C         MISE A 1 DU NUM DE FAMILLE POUR CETTE MAILLE DANS JMAFAM
C         POUR INDIQUER QU'ELLE APPARTIENT AU MOINS A UN GROUPE
          ZI(JMAFAM-1+IMA) = 1          
 3211   CONTINUE              
C
  321 CONTINUE 
C
C 3.2.3. ==> ON BOUCLE SUR LES MAILLES APPARTENANT AU MOINS A UN GROUPE
C            ET ON LES RASSEMBLE PAR IDENTITE D'APPARTENANCE
C            LES FAMILLES SONT NUMEROTEES DANS L'ORDRE D'APPARITION
C            ATTENTION : CET ALGORITHME A ETE OPTIMISE LE 6/9/2002
C                        ETRE PRUDENT DANS LES AMELIORATIONS FUTURES ...
C                        CE QUI EST PENALISANT : QUELQUES DIZAINES DE
C                        MILLIERS DE MAILLES ET QUELQUES CENTAINES DE
C                        GROUPES
C                        EXEMPLE : ON EST PASSE D'UNE VINGTAINE D'HEURES
C                        A 3 MINUTES AVEC UN GROS MAILLAGE :
C                        . 426 817 NOEUDS EN 57 GROUPES ET
C                        . 418 514 MAILLES EN 8 629 GROUPES.
C
      IAUX0 = JMAFAM-1
      IAUX1 = JMAFAC-1
      IAUX2 = JENTXG-1-NBEC
C
      DO 323 , IMA = 1 , NBMAIL
C
        IF ( ZI(IAUX0+IMA).NE.0 ) THEN
C
C         BOUCLE 2311 : ON PARCOURT TOUTES LES FAMILLES DEJA VUES.
C         POUR CHACUNE D'ELLES, ON COMPARE LES GROUPES ASSOCIES ET LES
C         GROUPES DE LA MAILLE COURANTE :
C         MEME COMPOSITION DE GROUPES <==> MEMES ENTIERS CODES
C         . SI C'EST LA MEME COMPOSITION DE GROUPES, LA FAMILLE EST LA
C           MEME. ON DONNE DONC LE NUMERO DE FAMILLE A LA MAILLE
C           COURANTE.
C         . SI ON N'A TROUVE AUCUNE FAMILLE, C'EST QU'UNE NOUVELLE
C           FAMILLE VIENT D'APPARAITRE. ON STOCKE SES CARACTERISTIQUES.
C
          JAUX = IAUX2 + NBEC*IMA
C
          DO 3231 , NUMFAM = 1 , NFAM
C
            IMAFAM = ZI(IAUX1+NUMFAM)
C
            KAUX = IAUX2 + NBEC*IMAFAM
C
            DO 3232 , IAUX = 1 , NBEC
              IF ( ZI(JAUX+IAUX) .NE. ZI(KAUX+IAUX) ) THEN
                GOTO 3231
              ENDIF
 3232       CONTINUE 
C
C           ON A TROUVE UNE FAMILLE AVEC LA MEME COMPOSITION :
C           . ON NOTE QUE LA FAMILLE EST LA MEME
C           . ON PASSE A LA MAILLE SUIVANTE
C
            ZI(IAUX0+IMA) = ZI(IAUX0+IMAFAM)  
            GOTO 323
C
 3231     CONTINUE              
C
C         AUCUNE MAILLE NE CORRESPONDAIT : ON CREE UNE NOUVELLE FAMILLE
          NFAM = NFAM + 1      
C         ON MEMORISE CE NUMERO DE FAMILLE POUR LA MAILLE COURANTE
C         ATTENTION : LA CONVENTION MED VEUT QUE LE NUMERO SOIT NEGATIF
          ZI(IAUX0+IMA)  = -NFAM 
C         ON INDIQUE OU SE TROUVE LA PREMIERE REFERENCE A CETTE FAMILLE
C         DANS LE VECTEUR JMAFAM POUR EVITER DE PERDRE SON TEMPS APRES
          ZI(IAUX1+NFAM) = IMA
C
        ENDIF
C
  323 CONTINUE
C
C 3.2.4. ==> CREATION DES FAMILLES DE MAILLES 
C          ON PARCOURT LES FAMILLES REPERTORIEES.
C          ON MEMORISE LES NOMS ET NUMEROS DES GROUPES QUI LA
C          CARACTERISENT. POUR CELA, ON SE BASE SUR LE PREMIER NOEUD
C          QUI EN FAIT PARTIE.
C     
      DO 324 , IAUX = 1,NFAM
C
C 3.2.4.1. ==> DETERMINATION DE LA FAMILLE : NOM, NOMS ET NUMEROS DES
C            GROUPES
C
        NUMFAM = -IAUX
C       NUMERO DE LA PREMIERE MAILLE FAISANT REFERENCE A CETTE FAMILLE 
        IMA = ZI(JMAFAC-1+IAUX)       
C       NB ET NOMS+NUMS DES GROUPES DE LA FAMILLE (LIRE ENTIERS CODES)
        CALL NOMGFA ( NOMGMA, NBGRMA, ZI(JENTXG+(IMA-1)*NBEC),
     >                ZK80(JGMAFA), NBGMAF )
C       NOM DE LA FAMILLE
        CALL MDNOFA ( 2, NUMFAM, ZK80(JGMAFA), NBGMAF, NOMFAM )
C
C 3.2.4.2. ==> ECRITURE DE LA FAMILLE
C
        NATT = 0
        CALL EFFAMC ( FID, NOMAMD, NOMFAM, NUMFAM,
     >                IAUX, IAUX, K200, NATT,
     >                ZK80(JGMAFA), NBGMAF, CODRET )        
        IF ( CODRET.NE.0 ) THEN
          CALL CODENT ( CODRET,'G',SAUX08 )
          CALL UTMESS ('F',NOMPRO,'MED: ERREUR EFFAMC NUMERO '//SAUX08)
        ENDIF
C
C 3.2.4.3. ==> INFORMATION EVENTUELLE
C
        IF ( INFMED.GE.2 ) THEN
          JAUX = 0
          KAUX = 0
          DO 3243 , IMA = 1,NBMAIL
            IF ( ZI(JMAFAM-1+IMA).EQ.NUMFAM ) THEN
              KAUX = KAUX + 1
            ENDIF
 3243     CONTINUE
          CALL DESGFA ( 2, NUMFAM, NOMFAM,
     >                  NBGMAF, ZK80(JGMAFA), NATT, TABAUX,
     >                  JAUX, KAUX,
     >                  IFM, CODRET )
        ENDIF
C
  324 CONTINUE          
C
      ENDIF
C     
C 3.3. ==> ECRITURE TABLE DES NUMEROS DE FAMILLES DES MAILLES
C          CELA SE FAIT PAR TYPE. ON REUTILISE LES VECTEURS CONTENANT
C          LES NUMEROS DE MAILLES/TYPE
C
      CALL WKVECT('&&'//NOMPRO//'.MAFAMT','V V I',NBMAIL,JMAFAT)
C
      DO 33 , ITYP = 1 , NTYMAX
C
        IF ( NMATYP(ITYP).NE.0 ) THEN
C
C 3.3.1. ==> RECUPERATION DU TABLEAU DES RENUMEROTATIONS
C
          CALL JEVEUO('&&'//PREFIX//'.NUM.'//NOMTYP(ITYP),'L',KAUX)
C
C 3.3.2. ==> CREATION VECTEUR NUMEROS DE FAMILLE POUR LES MAILLES / TYPE
C
          DO 331 , IAUX = 1 , NMATYP(ITYP)
            ZI(JMAFAT-1+IAUX) = ZI(JMAFAM-1+ZI(KAUX-1+IAUX))
  331     CONTINUE              
C
C 3.3.3. ==> ECRITURE
C
          CALL EFFAME ( FID, NOMAMD, ZI(JMAFAT), NMATYP(ITYP),
     >                  EDECRI, EDMAIL, TYPGEO(ITYP), CODRET)        
          IF ( CODRET.NE.0 ) THEN
           CALL CODENT ( CODRET,'G',SAUX08 )
           CALL UTMESS ('F',NOMPRO,'MED: ERREUR EFFAME NUMERO '//SAUX08)
          ENDIF
C
        ENDIF  
C
   33 CONTINUE              
C
C      CALL UTTCPU(3,'FIN',4,TPS3)
C
C====
C 4. ON CREE TOUJOURS UNE FAMILLE DE NUMERO 0 NE REFERENCANT RIEN,
C    POUR LES NOEUDS ET ELEMENTS N'APPARTENANT A AUCUN GROUPE
C    REMARQUE : IL FAUT LE FAIRE A LA FIN POUR AVOIR LES BONNES
C    IMRPESSIONS
C====
C
C 4.1. ==> ECRITURE
C
      NUMFAM = 0
      NATT = 0
C               12345678901234567890123456789012
      NOMFAM = 'FAMILLE_NULLE___________________'
      CALL EFFAMC ( FID, NOMAMD, NOMFAM, NUMFAM,
     >              IAUX, IAUX, K200, NATT,
     >              SAUX80, 0, CODRET )
      IF ( CODRET.NE.0 ) THEN
        CALL CODENT ( CODRET,'G',SAUX08 )
        CALL UTMESS ('F',NOMPRO,'MED: ERREUR EFFAMC NUMERO '//SAUX08)
      ENDIF
C
C 4.2. ==> INFORMATION EVENTUELLE
C
      IF ( INFMED.GE.2 ) THEN
C
        JAUX = 0
        KAUX = 0
        DO 421 , INO = 1,NBNOEU
          IF ( ZI(JNOFAM-1+INO).EQ.NUMFAM ) THEN
            JAUX = JAUX + 1
          ENDIF
  421   CONTINUE
        DO 422 , IMA = 1,NBMAIL
          IF ( ZI(JMAFAM-1+IMA).EQ.NUMFAM ) THEN
            KAUX = KAUX + 1
          ENDIF
  422   CONTINUE
        IAUX = 0
        CALL DESGFA ( 0, NUMFAM, NOMFAM,
     >                IAUX, SAUX80, NATT, TABAUX,
     >                JAUX, KAUX,
     >                IFM, CODRET )
C
      ENDIF
C
C====
C 5. LA FIN
C====
C
      CALL JEDETC('V','&&'//NOMPRO,1)
C
C      CALL UTTCPU(1,'FIN',4,TPS1)
C      WRITE (IFM,*) 'DUREE TOTALE DE ',NOMPRO,' :',TPS1(4)
C      WRITE (IFM,*) '. DONT DUREE DE FAMILLE DE NOEUDS  :',TPS2(4)
C      WRITE (IFM,*) '. DONT DUREE DE FAMILLE DE MAILLES :',TPS3(4)
C
      CALL JEDEMA()
C
      END
