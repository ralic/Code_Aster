      SUBROUTINE  XPOLSN(ELREFP,INO,N,JLSN,JLST,IMA,IAD,IGEOM,NFISS,
     &                   NDIME,NDIM,JCONX1,JCONX2,CO,LSN,LST)
      IMPLICIT NONE

      INCLUDE 'jeveux.h'
      INTEGER     N,JLSN,JLST,NDIM,NDIME,INO,NFISS
      INTEGER     IAD,IGEOM,IMA,JCONX1,JCONX2
      CHARACTER*8 ELREFP
      REAL*8      CO(3),LSN(NFISS),LST(NFISS)

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF PREPOST  DATE 19/11/2012   AUTEUR MARTIN A.MARTIN 
C TOLE CRS_1404
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C              RECUPERATION DES COORDONNEES RELLES ET
C                 CALCUL DES LEVEL SET DE L'ENTITE
C
C   IN
C     ELREFP : ÉLÉMENT DE RÉFÉRENCE PARENT
C     INO   : NUMERO DU POINT D'INTER OU DU NOEUD
C     N      : NOMBRE DE NOEUDS DE L'ÉLÉMENT PARENT
C     JLSN   : ADRESSE DU CHAM_NO_S DE LA LEVEL NORMALE
C     JLST   : ADRESSE DU CHAM_NO_S DE LA LEVEL TANGENTE
C     IMA    : NUMERO DE MAILLE COURANTE PARENT
C     IAD    : POINTEUR DES COORDONÉES DE INO
C     IGEOM  : COORDONNÉES DES NOEUDS DE L'ÉLÉMENT PARENT
C     NFISS  : NOMBRE DE FISSURES "VUES" PAR L'ÉLÉMENT PARENT
C     NDIME  : DIMENSION TOPOLOGIQUE DE LA MAILLE PARENT
C     NDIM   : DIMENSION DU MAILLAGE
C     JCONX1 : ADRESSE DE LA CONNECTIVITE DU MAILLAGE SAIN
C              (CONNECTIVITE QUADRATIQUE SI LAGRANGES DE CONTACT
C              AUX ARETES)
C     JCONX2 : LONGUEUR CUMULEE DE LA CONNECTIVITE DU MAILLAGE SAIN
C              (CONNECTIVITE QUADRATIQUE SI LAGRANGES DE CONTACT
C              AUX ARETES)
C   OUT
C     CO     : COORDONNEES DU NOEUD OU DU POINT
C     LSN    : LEVEL SET NORMALE DU NOEUD OU DU POINT
C     LST    : LEVEL SET TANGENTE DU NOEUD OU DU POINT
C
      REAL*8       R8PREM,FF(N)
      INTEGER      I,IFISS

      CALL JEMARQ()
      CALL VECINI(3,0.D0,CO)

C     CAS D'UN NOEUD
      IF (INO.LT.1000) THEN
        I=ZI(JCONX1-1+ZI(JCONX2+IMA-1)+INO-1)
        CO(1)=ZR(IAD-1+3*(I-1)+1)
        CO(2)=ZR(IAD-1+3*(I-1)+2)
        CO(3)=ZR(IAD-1+3*(I-1)+3)

        DO 23 IFISS=1,NFISS
          LSN(IFISS) = ZR(JLSN-1+NFISS*(INO-1)+IFISS)
          LST(IFISS) = ZR(JLST-1+NFISS*(INO-1)+IFISS)
 23     CONTINUE

C     CAS D'UN POINT D'INTERSECTION OU D'UN POINT MILIEU
      ELSEIF (INO.GT.1000) THEN
        DO 11 I=1,NDIM
          CO(I)=ZR(IAD-1+I)
 11     CONTINUE

C       FF : FONCTIONS DE FORMES
        CALL XPOFFO(NDIM,NDIME,ELREFP,N,IGEOM,CO,FF)

        DO 35 IFISS=1,NFISS
          LSN(IFISS) = 0.D0
          LST(IFISS) = 0.D0
          DO 30 I=1,N
            LSN(IFISS)=LSN(IFISS)+ZR(JLSN-1+NFISS*(I-1)+IFISS)*FF(I)
            LST(IFISS)=LST(IFISS)+ZR(JLST-1+NFISS*(I-1)+IFISS)*FF(I)
 30       CONTINUE
 35     CONTINUE

      ENDIF

      CALL JEDEMA()

      END
