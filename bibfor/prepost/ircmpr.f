      SUBROUTINE IRCMPR ( NOFIMD,
     &                    TYPECH,
     &                    NBIMPR, NCAIMI, NCAIMK,
     &                    NCMPRF, NCMPVE, NTLCMP,
     &                    NBVATO, NBENEC, LIENEC, ADSD, ADSL,
     &                    NOMAAS, MODELE, TYPGEO, NOMTYP,
     &                    NTPROA, CHANOM )
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF PREPOST  DATE 14/09/2010   AUTEUR REZETTE C.REZETTE 
C ======================================================================
C COPYRIGHT (C) 1991 - 2002  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE SELLENET N.SELLENET
C_______________________________________________________________________
C     ECRITURE D'UN CHAMP -  FORMAT MED - CREATION DU PROFIL
C        -  -       -               -                 --
C_______________________________________________________________________
C     ENTREES :
C       NOFIMD : NOM DU FICHIER MED
C       TYPECH : TYPE DU CHAMP ('NOEU', 'ELNO', 'ELGA')
C       NCMPRF : NOMBRE DE COMPOSANTES DU CHAMP DE REFERENCE
C       NCMPVE : NOMBRE DE COMPOSANTES VALIDES EN ECRITURE
C       NTLCMP : SD DES NUMEROS DES COMPOSANTES VALIDES
C       NBVATO : NOMBRE DE VALEURS TOTALES
C       NBENEC : NOMBRE D'ENTITES A ECRIRE (O, SI TOUTES)
C       LIENEC : LISTE DES ENTITES A ECRIRE SI EXTRAIT
C       ADSK, D, ... : ADRESSES DES TABLEAUX DES CHAMPS SIMPLIFIES
C       NOMAAS : SD MAILLAGE ASTER
C       MODELE : SD MODELE
C       TYPGEO : TYPE GEOMETRIQUE DE MAILLE ASSOCIEE AU TYPE ASTER
C       NOMTYP : NOM DES TYPES DE MAILLES ASTER
C     SORTIES :
C       NBIMPR : NOMBRE D'IMPRESSIONS A REALISER
C       NCAIMI : STRUCTURE ASSOCIEE AU TABLEAU CAIMPI
C         CAIMPI : ENTIERS POUR CHAQUE IMPRESSION
C                  CAIMPI(1,I) = TYPE D'EF / MAILLE ASTER (0, SI NOEUD)
C                  CAIMPI(2,I) = NOMBRE DE POINTS (GAUSS OU NOEUDS)
C                  CAIMPI(3,I) = NOMBRE DE SOUS-POINTS
C                  CAIMPI(4,I) = NOMBRE DE MAILLES A ECRIRE
C                  CAIMPI(5,I) = TYPE DE MAILLES ASTER (0, SI NOEUD)
C                  CAIMPI(6,I) = TYPE GEOMETRIQUE AU SENS MED
C                  CAIMPI(7,I) = NOMBRE TOTAL DE MAILLES IDENTIQUES
C       NCAIMK : STRUCTURE ASSOCIEE AU TABLEAU CAIMPK
C         CAIMPK : CARACTERES POUR CHAQUE IMPRESSION
C                  CAIMPK(1,I) = NOM DE LA LOCALISATION ASSOCIEE
C                  CAIMPK(2,I) = NOM DU PROFIL AU SENS MED
C       NTPROA : SD DU PROFIL ASTER. C'EST LA LISTE DES NUMEROS ASTER
C                DES NOEUDS OU DES ELEMENTS POUR LESQUELS LE CHAMP
C                EST DEFINI
C_______________________________________________________________________
C
      IMPLICIT NONE
C
C 0.1. ==> ARGUMENTS
C
      INTEGER NBVATO, NCMPRF, NCMPVE
      INTEGER NBENEC,ADTYP2
      INTEGER LIENEC(*)
      INTEGER ADSD, ADSL
      INTEGER NBIMPR
      INTEGER TYPGEO(*)
C
      CHARACTER*(*) NOFIMD
      CHARACTER*(*) NTLCMP, NTPROA
      CHARACTER*8 NOMAAS, MODELE, TYPECH
      CHARACTER*8 NOMTYP(*)
      CHARACTER*19 CHANOM
      CHARACTER*24 NCAIMI, NCAIMK
C
C 0.2. ==> COMMUNS
C --------------- COMMUNS NORMALISES  JEVEUX  --------------------------
      INTEGER         ZI
      COMMON /IVARJE/ ZI(1)
      REAL*8          ZR
      COMMON /RVARJE/ ZR(1)
      COMPLEX*16      ZC
      COMMON /CVARJE/ ZC(1)
      LOGICAL         ZL
      COMMON /LVARJE/ ZL(1)
      CHARACTER*8     ZK8
      CHARACTER*16           ZK16
      CHARACTER*24                   ZK24
      CHARACTER*32                           ZK32
      CHARACTER*80                                   ZK80
      COMMON /KVARJE/ ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
      CHARACTER*32       JEXATR,JEXNOM
C     -----  FIN  COMMUNS NORMALISES  JEVEUX --------------------------
C
C 0.3. ==> VARIABLES LOCALES
C
      CHARACTER*6 NOMPRO
      PARAMETER ( NOMPRO = 'IRCMPR' )
C
      INTEGER IFM, NIVINF,IRET,J,I,ITR6,ITR7,IQU8,IQU9,IHE20,IHE27
      INTEGER IAUX,IMA,NBNO,NBMA,ISE2,ISE4,IPE18,IPE15
      INTEGER NBMAIL,IADCNX,ILCNX,IADTYP
      INTEGER CODRET,JNOCE,JCO
      INTEGER ADTYPM, ADEFMA
      INTEGER ADCAII, ADCAIK
      INTEGER ADPROA, ADPROM, ADEXIC, ADPROR
      INTEGER ADNUCM
      INTEGER ADAUXI
C
      CHARACTER*8 SAUX08,MA,K8B
      CHARACTER*24 NTPROM, EXICMP, NTPROR
      CHARACTER*24 NTAUXI
C
C====
C 1. PREALABLES
C====
C
C 1.1. ==> RECUPERATION DU NIVEAU D'IMPRESSION
C          -----------------------------------
C
      CALL INFNIV ( IFM, NIVINF )
C
      IF ( NIVINF.GT.1 ) THEN
        WRITE (IFM,1001) 'DEBUT DE '//NOMPRO
        CALL UTFLSH (CODRET)
      ENDIF
 1001 FORMAT(/,4X,10('='),A,10('='),/)
C
C               12   345678   9012345678901234
      NTPROM = '&&'//NOMPRO//'.PROFIL_MED     '
      EXICMP = '&&'//NOMPRO//'.EXICMP         '
      NTAUXI = '&&'//NOMPRO//'.NUME_RECIPROQUE'
      NTPROR = '&&'//NOMPRO//'.PROFIL_RECIPROQ'
C
C 1.2. ==> ALLOCATIONS DES TABLEAUX DE RENUMEROTATIONS
C
      CALL WKVECT ( NTPROA, 'V V I', NBVATO, ADPROA )
      CALL WKVECT ( NTPROM, 'V V I', NBVATO, ADPROM )
      CALL WKVECT ( EXICMP, 'V V L', NBVATO, ADEXIC )
C
      CALL JEVEUO ( NTLCMP, 'L', ADNUCM )
C
C 1.3. ==> COMPLEMENTS
C
C 1.3.1. ==> COMPLEMENTS POUR UN CHAMP AUX NOEUDS
C
        CALL JENONU(JEXNOM('&CATA.TM.NOMTM','SEG2'),ISE2)
        CALL JENONU(JEXNOM('&CATA.TM.NOMTM','SEG4'),ISE4)
        CALL JENONU(JEXNOM('&CATA.TM.NOMTM','TRIA7'),ITR7)
        CALL JENONU(JEXNOM('&CATA.TM.NOMTM','TRIA6'),ITR6)
        CALL JENONU(JEXNOM('&CATA.TM.NOMTM','QUAD9'),IQU9)
        CALL JENONU(JEXNOM('&CATA.TM.NOMTM','QUAD8'),IQU8)
        CALL JENONU(JEXNOM('&CATA.TM.NOMTM','PENTA15'),IPE15)
        CALL JENONU(JEXNOM('&CATA.TM.NOMTM','PENTA18'),IPE18)
        CALL JENONU(JEXNOM('&CATA.TM.NOMTM','HEXA27'),IHE27)
        CALL JENONU(JEXNOM('&CATA.TM.NOMTM','HEXA20'),IHE20)

      IF ( TYPECH(1:4).EQ.'NOEU' ) THEN
C
        NBIMPR = 1
        IAUX = 7*NBIMPR
        CALL WKVECT ( NCAIMI, 'V V I'  , IAUX, ADCAII )
        IAUX = 2*NBIMPR
        CALL WKVECT ( NCAIMK, 'V V K32', IAUX, ADCAIK )
C
C       ON CREE UN TABLEAU QUI PERMET DE DETECTER L'EXISTENCE DE NOEUDS
C       CENTRE (APPARTENANT AUX MAILLES DE TYPE TRIA7,QUAD9,PENTA18 OU
C       HEXA27)
C
        CALL JEVEUO(NOMAAS//'.TYPMAIL','L',IADTYP )
        CALL JEVEUO(NOMAAS//'.CONNEX','L',IADCNX)
        CALL JEVEUO(JEXATR(NOMAAS//'.CONNEX','LONCUM'),'L',ILCNX)
        CALL DISMOI('F','NB_NO_MAILLA',NOMAAS,'MAILLAGE',NBNO,K8B,IRET)
        CALL DISMOI('F','NB_MA_MAILLA',NOMAAS,'MAILLAGE',NBMA,K8B,IRET)
        CALL WKVECT('&&IRCMPR.NOEU_CENTR','V V I',NBNO,JNOCE)
        DO 5 I=1,NBNO
          ZI(JNOCE+I-1)=0
 5      CONTINUE
C
        DO 10 I=1,NBMA
           IF(ZI(IADTYP+I-1).EQ.ISE4)THEN
             JCO=IADCNX+ZI(ILCNX+I-1)-1
             ZI(JNOCE+ZI(JCO+3-1)-1)=1
             ZI(JNOCE+ZI(JCO+4-1)-1)=1
           ENDIF
           IF(ZI(IADTYP+I-1).EQ.ITR7)THEN
             JCO=IADCNX+ZI(ILCNX+I-1)-1
             ZI(JNOCE+ZI(JCO+7-1)-1)=1
           ENDIF
           IF(ZI(IADTYP+I-1).EQ.IQU9)THEN
             JCO=IADCNX+ZI(ILCNX+I-1)-1
             ZI(JNOCE+ZI(JCO+9-1)-1)=1
           ENDIF
           IF(ZI(IADTYP+I-1).EQ.IHE27)THEN
             JCO=IADCNX+ZI(ILCNX+I-1)-1
             DO 15 J=1,7
               ZI(JNOCE+ZI(JCO+20+J-1)-1)=1
 15          CONTINUE
           ENDIF
           IF(ZI(IADTYP+I-1).EQ.IPE18)THEN
             JCO=IADCNX+ZI(ILCNX+I-1)-1
             DO 16 J=1,3
               ZI(JNOCE+ZI(JCO+15+J-1)-1)=1
 16          CONTINUE
           ENDIF
 10    CONTINUE
C
C 1.3.2. ==> COMPLEMENTS POUR DES CHAMPS AUX ELEMENTS
C
      ELSEIF ( TYPECH(1:2).EQ.'EL' ) THEN
C
        CALL JEVEUO ( NOMAAS//'.TYPMAIL', 'L', ADTYPM )
        CALL JELIRA ( NOMAAS//'.TYPMAIL', 'LONUTI', NBMAIL, SAUX08 )
        CALL WKVECT('&&IRCMPR.TYPMA','V V I',NBMAIL,ADTYP2)
        DO 20 IMA=1,NBMAIL
          IF(ZI(ADTYPM+IMA-1).EQ.ISE4)THEN
             ZI(ADTYP2+IMA-1)=ISE2
          ELSEIF(ZI(ADTYPM+IMA-1).EQ.ITR7)THEN
             ZI(ADTYP2+IMA-1)=ITR6
          ELSEIF(ZI(ADTYPM+IMA-1).EQ.IQU9)THEN
             ZI(ADTYP2+IMA-1)=IQU8
          ELSEIF(ZI(ADTYPM+IMA-1).EQ.IHE27)THEN
             ZI(ADTYP2+IMA-1)=IHE20
          ELSEIF(ZI(ADTYPM+IMA-1).EQ.IPE18)THEN
             ZI(ADTYP2+IMA-1)=IPE15
          ELSE
             ZI(ADTYP2+IMA-1)=ZI(ADTYPM+IMA-1)
          ENDIF
 20     CONTINUE
        IF ( TYPECH(1:4).EQ.'ELGA' ) THEN
          CALL JEVEUO ( MODELE//'.MAILLE', 'L', ADEFMA )
        ENDIF
        CALL WKVECT ( NTPROR, 'V V I', NBVATO, ADPROR )
        CALL WKVECT ( NTAUXI, 'V V I', NBVATO, ADAUXI )
C
C 1.3.3. ==> ERREUR
C
      ELSE
C
        CALL U2MESK('F','MED_46',1,TYPECH)
C
      ENDIF
C
C====
C 2. APPELS DES PROGRAMMES SPECIFIQUES
C====
C
      IF ( TYPECH(1:4).EQ.'NOEU' ) THEN
C
C 2.1. ==> LES NOEUDS
C
        CALL IRCMPN ( NOFIMD,
     &                NCMPRF, NCMPVE, ZI(ADNUCM), ZL(ADEXIC),
     &                NBVATO, NBENEC, LIENEC, ADSL,
     &                ZI(ADCAII), ZK32(ADCAIK),
     &                ZI(ADPROA),ZI(JNOCE) )
C
      ELSE
C
C 2.2. ==> LES ELEMENTS
C
        IF ( TYPECH(1:4).EQ.'ELGA' ) THEN
          IAUX = ADEFMA
        ELSE
          IAUX = ADTYP2
        ENDIF
C
        CALL IRCMPE ( NOFIMD,
     &                NCMPVE, ZI(ADNUCM), ZL(ADEXIC),
     &                NBVATO, NBENEC, LIENEC, ADSD, ADSL,
     &                NBIMPR, NCAIMI, NCAIMK,
     &                ZI(IAUX), ZI(ADTYP2), TYPGEO, NOMTYP, TYPECH,
     &                ZI(ADPROA), ZI(ADPROM), ZI(ADPROR), ZI(ADAUXI),
     &                CHANOM )
C
      ENDIF
C
C====
C 3. LA FIN
C====
C
      CALL JEDETC('V','&&'//NOMPRO,1)
C
      IF ( NIVINF.GT.1 ) THEN
        WRITE (IFM,1001) 'FIN DE '//NOMPRO
      ENDIF
C
      END
