      SUBROUTINE OP0039 ()
      IMPLICIT   NONE
C ----------------------------------------------------------------------
C MODIF PREPOST  DATE 16/10/2012   AUTEUR SELLENET N.SELLENET 
C RESPONSABLE SELLENET N.SELLENET
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C
C     BUT:
C       IMPRIMER DES RESULTATS ET DES MAILLAGE
C       PROCEDURE IMPR_RESU
C
      INCLUDE 'jeveux.h'
      INTEGER NOCC,IOCC,IOC2,NBREST,IFC,IFI,VERSIO,INFMAI,NIVE,IER
      INTEGER NUMEMO,NBMODL,NMAIL,NRESU,NCHAM,IBID,NRES,N11,IRET
      INTEGER JLAST,JMODL,IARG,NMO,NN,NMOD,NFORMA,NGIBI
C
      REAL*8 VERSI2,EPS
C
      CHARACTER*1  K1OCC
      CHARACTER*8  MODELE,NOMA,NOMA2,FORM,NOMARE,NOMSQ
      CHARACTER*8  K8B,RESU,NOMAB,RESURE(9),RESUR(9)
      CHARACTER*16 FICH,FORMR
      CHARACTER*24 NOMJV,VALK(6),CORRN,CORRM
C
      LOGICAL LRESU,LCASTS,LMOD,ULEXIS
      LOGICAL LMAIL,LREST,LGMSH
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
      CALL INFMAJ()

C     --- RECUPERATION DU NOMBRE DE MISES EN FACTEUR DU MOT-CLE RESU ---
      CALL GETFAC ( 'RESU', NOCC )

      DO 100 IOCC = 1,NOCC
       CALL GETVTX('RESU','NOEUD_CMP',IOCC,IARG,0,K8B,NMO)
       IF(NMO.NE.0) THEN
         NN = NMO / 2
         IF(2*NN.NE.NMO) THEN
           CALL U2MESS('F','PREPOST3_65')
         ENDIF
       ENDIF
  100 CONTINUE

C     -----------------
C     --- LE MODELE ---
C     -----------------
      LMOD   = .FALSE.
      MODELE = ' '
      CALL GETVID(' ','MODELE',1,IARG,1,MODELE,NMOD)
      IF ( NMOD .NE. 0 ) LMOD= .TRUE.

C     ----------------------------------------------------------------
C     TRAITEMENT DU MOT CLE "RESTREINT"
C     ----------------------------------------------------------------
C     -- REMARQUE : LE MOT CLE RESTREINT CREE DES SD_RESULTAT
C        TEMPORAIRES + 1 MAILLAGE TEMPORAIRE ET CE SONT CES
C        SD QUI DOIVENT ETRE IMPRIMEES.
C        C'EST POURQUOI, DANS LE RESTE DE L'OPERATEUR (QUI IMPRIME),
C        ON DOIT REMPLACER :
C          - GETVID/RESULTAT PAR RESURE(IOCC)
C          - GETVID/MAILLAGE PAR NOMARE
      LREST=.FALSE.
      CALL GETFAC('RESTREINT',NBREST)
      CALL ASSERT(NBREST.EQ.0.OR.NBREST.EQ.1)
      NOMARE=' '
      IF (NBREST.EQ.1) THEN
        LREST=.TRUE.
C       -- SI RESTREINT, IL FAUT VERIFIER QUE RESU/RESULTAT EST
C          TOUJOURS FOURNI :
        CALL ASSERT(NOCC.LE.9)
        DO 74,IOCC=1,NOCC
          CALL GETVID('RESU','RESULTAT',IOCC,IARG,
     &                1,RESUR(IOCC),NRESU)
          IF (NRESU.EQ.0) CALL U2MESS('F','CALCULEL4_5')
  74    CONTINUE

C       -- ON VERIFIE QUE TOUS LES RESUR ONT LE MEME MAILLAGE
        CALL DISMOI('F','NOM_MAILLA',RESUR(1),'RESULTAT',IBID,NOMA,IER)
        DO 76,IOCC=2,NOCC
          CALL DISMOI('F','NOM_MAILLA',RESUR(IOCC),'RESULTAT',IBID,
     &                NOMA2,IER)
          CALL ASSERT(NOMA2.NE.' ')
          IF (NOMA2.NE.NOMA) THEN
            VALK(1)=RESUR(1)
            VALK(2)=RESUR(IOCC)
            VALK(3)=NOMA
            VALK(4)=NOMA2
            CALL U2MESK('F','CALCULEL4_2',4,VALK)
          ENDIF
 76     CONTINUE

C       -- ON VA FABRIQUER :
C          NOMARE : 1 SD_MAILLAGE "RESTREINT"
C          RESURE : NOCC SD_RESULTAT "RESTREINT"
        NOMARE='&&OP0039'
        CORRN='&&OP0039.CORRN'
        CORRM='&&OP0039.CORRM'
        CALL RDTMAI(NOMA,NOMARE,'V',CORRN,CORRM,'V',0,0)

        DO 77, IOCC=1,NOCC
          CALL CODENT(IOCC,'D',K1OCC)
          RESURE(IOCC)='&RESUR'//K1OCC//'_'
          CALL RDTRES(RESUR(IOCC),RESURE(IOCC),NOMA,NOMARE,CORRN,
     &                CORRM,IOCC)
 77     CONTINUE
      ENDIF

C     ---------------------------------------------
C     --- FORMAT, FICHIER ET UNITE D'IMPRESSION ---
C     ---------------------------------------------
C
C     --- FORMAT ---
      CALL GETVTX ( ' ', 'FORMAT'  ,1,IARG,1, FORM , NFORMA )
      IF (LREST.AND.FORM.NE.'MED') CALL U2MESS('F','CALCULEL4_3')

C     --- VERIFICATION DE LA COHERENCE ENTRE LE MAILLAGE ---
C     --- PORTANT LE RESULTAT ET LE MAILLAGE DONNE PAR   ---
C     --- L'UTILISATEUR DANS IMPR_RESU(FORMAT='IDEAS')   ---

      IF (FORM(1:5) .EQ. 'IDEAS') THEN
        CALL GETVID('RESU','RESULTAT',1,IARG,1,RESU,NRES)
        CALL GETVID('RESU','MAILLAGE',1,IARG,1,NOMA,NMAIL)
        IF (NRES*NMAIL .GT.0) THEN
          CALL DISMOI('F','NOM_MAILLA',RESU,'RESULTAT',IBID,NOMSQ,IER)
          IF (NOMSQ .NE. NOMA) THEN
            VALK(1)=NOMA
            VALK(2)=NOMSQ
            VALK(3)=RESU
            CALL U2MESK('A','PREPOST3_74',3,VALK)
          ENDIF
        ENDIF
      ENDIF

C
C     --- VERSION D'ECRITURE  ----
      NIVE = 0
      VERSIO = 0
      LCASTS = .FALSE.
      IF ( FORM .EQ. 'CASTEM' ) THEN
        LCASTS = .TRUE.
        CALL GETVIS(' ','NIVE_GIBI',1,IARG,1,NIVE,NGIBI)
      ELSEIF ( FORM(1:5) .EQ. 'IDEAS' ) THEN
        VERSIO = 5
        CALL GETVIS(' ','VERSION',1,IARG,1,VERSIO,NGIBI)
      ELSEIF ( FORM(1:4) .EQ. 'GMSH' ) THEN
        VERSIO = 1
        VERSI2 = 1.0D0
        EPS    = 1.0D-6
        CALL GETVR8(' ','VERSION',1,IARG,1,VERSI2,NGIBI)
        IF (VERSI2.GT.1.0D0-EPS.AND.VERSI2.LT.1.0D0+EPS) THEN
          VERSIO = 1
        ELSEIF (VERSI2.GT.1.2D0-EPS.AND.VERSI2.LT.1.2D0+EPS) THEN
          VERSIO = 2
        ENDIF
      ENDIF
C
C     --- FICHIER ---
      IFI = 0
      FICH = 'F_'//FORM
      CALL GETVIS ( ' ', 'UNITE', 1,IARG,1, IFI , N11 )
      IFC = IFI
      IF ( .NOT. ULEXIS( IFI ) ) THEN
         CALL ULOPEN ( IFI, ' ', FICH, 'NEW', 'O' )
      ENDIF

C     -- FORMAT CASTEM : IMPRESSION DU MAILLAGE :
C     -------------------------------------------
      FORMR=' '
      IF ( FORM .EQ. 'CASTEM' ) THEN
         NUMEMO = 0
         NOMJV  = '&&OP0039.NOM_MODELE'
         INFMAI = 0
         DO 200 IOCC = 1,NOCC
            IF ( NUMEMO .EQ. 0 ) THEN
               IF ( LMOD ) THEN
                  NBMODL = 1
                  CALL WKVECT ( NOMJV, 'V V K24', 10, JMODL )
                  CALL JEECRA ( NOMJV , 'LONUTI' , NBMODL , ' ')
                  CALL JEVEUO ( NOMJV, 'E', JMODL )
                  ZK24(JMODL) = MODELE//'.MODELE'
               ENDIF
               DO 202 IOC2 = 1 , NOCC
                 CALL GETVID('RESU','RESULTAT',IOC2,
     &                       IARG,1,RESU,NRESU)
                 IF ( NRESU .NE. 0 ) CALL RSCRMO ( IOC2, RESU , NOMJV)
 202           CONTINUE
               NUMEMO = NUMEMO + 1
            ENDIF

C           ---  IMPRESSION DU MAILLAGE -----
            CALL GETVID('RESU','MAILLAGE',IOCC,IARG,1,NOMA,NMAIL)
            IF ( NMAIL .NE. 0 ) THEN
               IF ( LMOD  ) THEN
                  CALL DISMOI('C','NOM_MAILLA',MODELE,'MODELE',IBID,
     &                                                 NOMAB,IRET)
                  IF (NOMA.NE.NOMAB) CALL U2MESS('F','PREPOST3_66')
               ENDIF
               CALL IRMAIL ( FORM,IFI,VERSIO,NOMA,LMOD,MODELE,NIVE,
     &                       INFMAI, FORMR )
               NUMEMO = NUMEMO + 1
            ENDIF
 200     CONTINUE

         IF ( NUMEMO .LE. 1 ) CALL U2MESS('F','PREPOST3_67')
C
         CALL JEEXIN('&&OP0039.LAST',IRET)
         IF(IRET.EQ.0)  CALL WKVECT('&&OP0039.LAST','V V I',8,JLAST)
      ENDIF

C     -- VERIFICATIONS POUR GMSH :
      IF (FORM(1:4).EQ.'GMSH') THEN
         LMAIL=.FALSE.
         LRESU=.FALSE.
         DO 220 IOCC = 1 , NOCC
            CALL GETVID('RESU','MAILLAGE',IOCC,IARG,1,NOMA,NMAIL)
            CALL GETVID('RESU','RESULTAT',IOCC,IARG,1,RESU,NRESU)
            CALL GETVID('RESU','CHAM_GD',IOCC,IARG,1,RESU,NCHAM)
            IF ( NRESU.NE.0 .OR. NCHAM.NE.0 ) THEN
               LRESU=.TRUE.
               GOTO 220
            ENDIF
            IF ( NMAIL.NE.0 )  LMAIL=.TRUE.
 220     CONTINUE
         IF ( LMAIL.AND.LRESU ) THEN
            CALL U2MESS('A','PREPOST3_68')
            GOTO 9999
         ENDIF
      ENDIF
      LGMSH = .FALSE.
C
C     --- BOUCLE SUR LE NOMBRE DE MISES EN FACTEUR ---
C     -----------------------------------------------------------------
      DO 10 IOCC = 1,NOCC

        CALL IRMFAC(IOCC,FORM,IFI,NIVE,VERSIO,
     &              MODELE,NOMA,NOMARE,RESURE(IOCC),LGMSH)

 10   CONTINUE
      IF(LCASTS) THEN
        IBID=5
        WRITE(IFC,'(A,I4)')  ' ENREGISTREMENT DE TYPE',IBID
        IF (NIVE.EQ.10) THEN
          IBID = 1
          WRITE(IFC,'(A,I4)')  'LABEL AUTOMATIQUE :',IBID
        ENDIF
      ENDIF

C     -- IMPRESSION DES CARTES DE DONNEES DE CHAM_MATER,  ... :
      CALL W039CA (IFI,FORM)


9999  CONTINUE
      CALL JEDEMA()
      END
