      SUBROUTINE IRCAME ( IFI, NOCHMD, CHANOM, TYPECH, MODELE,
     &                    NBCMP, NOMCMP,
     &                    NUMPT, INSTAN, UNIINS, NUMORD,
     &                    ADSK, ADSD, ADSC, ADSV, ADSL,
     &                    NBENEC, LIENEC,
     &                    CODRET )
C_______________________________________________________________________
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF PREPOST  DATE 20/02/2007   AUTEUR LEBOUVIER F.LEBOUVIER 
C RESPONSABLE GNICOLAS G.NICOLAS
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C     ECRITURE D'UN CHAMP - FORMAT MED
C        -  -       - -            --
C-----------------------------------------------------------------------
C     ENTREES :
C       IFI    : UNITE LOGIQUE D'IMPRESSION DU CHAMP
C       NOCHMD : NOM MED DU CHAMP A ECRIRE
C       CHANOM : NOM ASTER DU CHAM A ECRIRE
C       TYPECH : TYPE DU CHAMP ('NOEU', 'ELNO', 'ELGA')
C       MODELE : MODELE ASSOCIE AU CHAMP
C       NBCMP  : NOMBRE DE COMPOSANTES A ECRIRE. S'IL EST NUL, ON
C                PREND TOUT
C       NOMCMP : NOMS DES COMPOSANTES A ECRIRE
C       NUMPT  : NUMERO DE PAS DE TEMPS
C       INSTAN : VALEUR DE L'INSTANT A ARCHIVER
C       UNIINS : UNITE DE L'INSTANT A ARCHIVER
C       NUMORD : NUMERO D'ORDRE DU CHAMP
C       ADSK, D, ... : ADRESSES DES TABLEAUX DES CHAMPS SIMPLIFIES
C       NBVATO : NOMBRE DE VALEURS TOTALES
C       NBENEC : NOMBRE D'ENTITES A ECRIRE (O, SI TOUTES)
C       LIENEC : LISTE DES ENTITES A ECRIRE SI EXTRAIT
C     SORTIES:
C       CODRET : CODE DE RETOUR (0 : PAS DE PB, NON NUL SI PB)
C_______________________________________________________________________
C
      IMPLICIT NONE
C
C 0.1. ==> ARGUMENTS
C
      CHARACTER*8 UNIINS, TYPECH, MODELE
      CHARACTER*19 CHANOM
      CHARACTER*32 NOCHMD
      CHARACTER*(*) NOMCMP(*)
C
      INTEGER NBCMP, NUMPT, NUMORD, IFI
      INTEGER ADSK, ADSD, ADSC, ADSV, ADSL
      INTEGER NBENEC
      INTEGER LIENEC(*)
      INTEGER TYPENT, TYGEOM
C
      REAL*8 INSTAN
C
      INTEGER CODRET
C
C 0.2. ==> COMMUNS
C
C --------------- COMMUNS NORMALISES  JEVEUX  --------------------------
      INTEGER         ZI
      COMMON /IVARJE/ ZI(1)
      REAL*8          ZR
      COMMON /RVARJE/ ZR(1)
      COMPLEX*16      ZC
      COMMON /CVARJE/ ZC(1)
      LOGICAL         ZL
      COMMON /LVARJE/ ZL(1)
      CHARACTER*8     ZK8
      CHARACTER*16           ZK16
      CHARACTER*24                   ZK24
      CHARACTER*32                           ZK32
      CHARACTER*80                                   ZK80
      COMMON /KVARJE/ ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C     -----  FIN  COMMUNS NORMALISES  JEVEUX --------------------------
C
C 0.3. ==> VARIABLES LOCALES
C
      CHARACTER*6 NOMPRO
      PARAMETER ( NOMPRO = 'IRCAME' )
C
      INTEGER NTYMAX
      INTEGER VALI
      PARAMETER (NTYMAX=48)
      INTEGER NNOMAX
      PARAMETER (NNOMAX=27)
      INTEGER EDNOEU
      PARAMETER (EDNOEU=3)
      INTEGER EDMAIL
      PARAMETER (EDMAIL=0)
      INTEGER TYPNOE
      PARAMETER (TYPNOE=0)
C
      CHARACTER*1 SAUX01
      CHARACTER*8 SAUX08, K8BID
      CHARACTER*8 NOMAAS
      CHARACTER*8 NOMTYP(NTYMAX)
      CHARACTER*24 NTLCMP, NTNCMP, NTUCMP, NTPROA, NMCMFI
      CHARACTER*24 VALK(2)
      CHARACTER*24 NCAIMI, NCAIMK
      CHARACTER*32 NOMAMD, SAUX32
      CHARACTER*200 NOFIMD
      CHARACTER*255 KFIC
C
      INTEGER NDIM, TYPGEO(NTYMAX)
      INTEGER NBTYP, NNOTYP(NTYMAX)
      INTEGER MODNUM(NTYMAX)
      INTEGER NUMNOA(NTYMAX,NNOMAX), NUANOM(NTYMAX,NNOMAX)
      INTEGER RENUMD(NTYMAX)
      INTEGER IFM, NIVINF
      INTEGER LNOMAM
      INTEGER NCMPVE, NVALEC
      INTEGER NBVATO, NCMPRF
      INTEGER NBIMPR
      INTEGER ADCAII, ADCAIK
C
      INTEGER IAUX, JAUX
      INTEGER NRIMPR
      INTEGER EXISTC, NBCMFI, NBVAL
C
      LOGICAL LGAUX
      LOGICAL EXISTM
C
      CALL JEMARQ ( )
C
C====
C 1. PREALABLES
C====
C
C 1.1. ==> RECUPERATION DU NIVEAU D'IMPRESSION
C
      CALL INFNIV ( IFM, NIVINF )
C
C 1.2. ==> NOMS DES TABLEAUX DE TRAVAIL
C               12   345678   9012345678901234
      NTLCMP = '&&'//NOMPRO//'.LISTE_N0MCMP   '
      NTNCMP = '&&'//NOMPRO//'.NOMCMP         '
      NTUCMP = '&&'//NOMPRO//'.UNITECMP       '
      NTPROA = '&&'//NOMPRO//'.PROFIL_ASTER   '
      NMCMFI = '&&'//NOMPRO//'.NOMCMP_FICHIER '
      NCAIMI = '&&'//NOMPRO//'.CARAC_NOMBRES__'
      NCAIMK = '&&'//NOMPRO//'.CARAC_CHAINES__'
C
C 1.3. ==> NOM DU FICHIER MED
C
      CALL ULISOG(IFI, KFIC, SAUX01)
      IF ( KFIC(1:1).EQ.' ' ) THEN
        CALL CODENT ( IFI, 'G', SAUX08 )
        NOFIMD = 'fort.'//SAUX08
      ELSE
        NOFIMD = KFIC(1:200)
      ENDIF
C
      IF ( NIVINF.GT.1 ) THEN
        WRITE (IFM,*) '<',NOMPRO,'> NOM DU FICHIER MED : ',NOFIMD
      ENDIF
C
C 1.4. ==> LES NOMBRES CARACTERISTIQUES
C
      NBVATO = ZI(ADSD)
      NCMPRF = ZI(ADSD+1)
C
C====
C 2. LE MAILLAGE
C====
C
C 2.1. ==> NOM ET DIMENSION DU MAILLAGE ASTER
C
      NOMAAS = ZK8(ADSK-1+1)
      CALL DISMOI ( 'F', 'DIM_GEOM', NOMAAS, 'MAILLAGE', NDIM,
     &              SAUX32, CODRET )
      IF ( CODRET.NE.0 ) THEN
        CALL U2MESS('F','PREPOST_71')
      ENDIF
C
C 2.2. ==> CREATION DU NOM DU MAILLAGE POUR MED
C
      CALL MDNOMA ( NOMAMD, LNOMAM, NOMAAS, CODRET )
      IF ( CODRET.NE.0 ) THEN
        CALL CODENT ( CODRET,'G',SAUX08 )
        CALL U2MESK('F','PREPOST_72',1,SAUX08)
      ENDIF
C
C 2.3. ==> CE MAILLAGE EST-IL DEJA PRESENT DANS LE FICHIER ?
C
      IAUX = 0
      CALL MDEXMA ( NOFIMD, NOMAMD, IAUX, EXISTM, JAUX, CODRET )
C
C 2.4. ==> SI LE MAILLAGE EST ABSENT, ON L'ECRIT
C
      IF ( .NOT.EXISTM ) THEN
        SAUX08 = 'MED     '
        LGAUX = .FALSE.
        K8BID = '        '
        CALL IRMAIL ( SAUX08, IFI, IAUX, NOMAAS, LGAUX, K8BID, JAUX,
     &                NIVINF )
      ENDIF
C
C====
C 3. PREPARATION DU CHAMP A ECRIRE
C====
C
C 3.1. ==> NUMEROS, NOMS ET UNITES DES COMPOSANTES A ECRIRE
C
      CALL UTLICM ( NBCMP, NOMCMP,
     &              ZK8(ADSK+1), NCMPRF, ZK8(ADSC),
     &              NCMPVE, NTLCMP, NTNCMP, NTUCMP )
C
C 3.2. ==> . RECUPERATION DES NB/NOMS/NBNO/NBITEM DES TYPES DE MAILLES
C            DANS CATALOGUE
C          . RECUPERATION DES TYPES GEOMETRIE CORRESPONDANT POUR MED
C          . VERIF COHERENCE AVEC LE CATALOGUE
C
      CALL LRMTYP ( NBTYP, NOMTYP,
     &              NNOTYP, TYPGEO, RENUMD,
     &              MODNUM, NUANOM, NUMNOA )
C
C 3.3. ==> DEFINITIONS DES IMPRESSIONS ET CREATION DES PROFILS EVENTUELS
C
      CALL IRCMPR ( NOFIMD,
     &              TYPECH,
     &              NBIMPR, NCAIMI, NCAIMK,
     &              NCMPRF, NCMPVE, NTLCMP,
     &              NBVATO, NBENEC, LIENEC, ADSD, ADSL,
     &              NOMAAS, MODELE, TYPGEO, NOMTYP,
     &              NTPROA )
C
      CALL JEVEUO ( NCAIMI, 'L', ADCAII )
      CALL JEVEUO ( NCAIMK, 'L', ADCAIK )
C
C 3.4. ==> CARACTERISATION DES SUPPORTS QUAND CE NE SONT PAS DES NOEUDS
C
      IF ( TYPECH(1:2).EQ.'EL' ) THEN
C
        CALL IRMPGA ( NOFIMD,
     &                CHANOM, TYPECH, NOMTYP,
     &                NBIMPR, ZI(ADCAII), ZK32(ADCAIK),
     &                MODNUM, NUANOM,
     &                CODRET )
C
      ENDIF
C
C====
C 4. REPERAGE DU CHAMP : EXISTE-T-IL DEJA ?
C    ON DOIT PARCOURIR TOUTES LES IMPRESSIONS POSSIBLES POUR CE CHAMP
C====
C
      EXISTC = 0
C
      DO 41 , NRIMPR = 1 , NBIMPR
C
        IF ( CODRET.EQ.0 ) THEN
C
        TYGEOM = ZI(ADCAII+7*NRIMPR-2)
        IF ( TYGEOM.EQ.TYPNOE ) THEN
          TYPENT = EDNOEU
        ELSE
          TYPENT = EDMAIL
        ENDIF
        NVALEC = ZI(ADCAII+7*NRIMPR-4)
C
        CALL JEDETC('V',NMCMFI,1)
C
        CALL MDEXCH ( NOFIMD,
     &                NOCHMD, NOMAMD, NUMPT, NUMORD, NCMPVE, NTNCMP,
     &                NVALEC, TYPENT, TYGEOM,
     &                JAUX, NBCMFI, NMCMFI, NBVAL, CODRET )
C
        EXISTC = MAX ( EXISTC, JAUX )
C
        ENDIF
C
   41 CONTINUE
C
C====
C 5. ECRITURE SI C'EST POSSIBLE
C====
C
      IF ( EXISTC.LE.2 ) THEN
C
        CALL IRCAM1 ( NOFIMD, NOCHMD,
     &                EXISTC, NCMPRF,
     &                NUMPT, INSTAN, UNIINS, NUMORD,
     &                ADSD, ADSV, ADSL,
     &                NCMPVE, NTLCMP, NTNCMP, NTUCMP, NTPROA,
     &                NBIMPR, ZI(ADCAII), ZK32(ADCAIK),
     &                NOMAMD, NOMTYP, MODNUM, NUANOM,
     &                CODRET )
C
      ELSE
C
        VALK (1) = NOFIMD
        VALK (2) = NOCHMD
        VALI = EXISTC
        CALL U2MESG('A', 'PREPOST5_24',2,VALK,1,VALI,0,0.D0)
        CALL U2MESS('F','PREPOST_73')
C
      ENDIF
C
C====
C 6. LA FIN
C====
C
      WRITE (IFM,*) ' '
C
      CALL JEDETC('V','&&'//NOMPRO,1)
C
      CALL JEDEMA ( )
C
      END
