      SUBROUTINE LRCAME ( NROFIC, NOCHMD, NOMAMD, NOMAAS,
     >                    NBVATO, TYPECH,
     >                    NUMPT, NUMORD, NBCMPV, NCMPVA, NCMPVM,
     >                    NOMGD, NCMPRF, JNOCMP, ADSL, ADSV, ADSD,
     >                    CODRET )
C_____________________________________________________________________
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF PREPOST  DATE 14/04/2003   AUTEUR DURAND C.DURAND 
C RESPONSABLE GNICOLAS G.NICOLAS
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C     LECTURE D'UN CHAMP - FORMAT MED
C     -    -       - -            --
C-----------------------------------------------------------------------
C      ENTREES:
C        NROFIC : UNITE LOGIQUE DU FICHIER MED
C        NOCHMD : NOM MED DU CHAMP A LIRE
C        NOMAMD : NOM MED DU MAILLAGE LIE AU CHAMP A LIRE
C                  SI ' ' : ON SUPPOSE QUE C'EST LE PREMIER MAILLAGE
C                          DU FICHIER 
C        NOMAAS : NOM ASTER DU MAILLAGE
C        NBVATO : NOMBRE DE VALEURS TOTALES
C        TYPECH : TYPE DE CHAMP AUX ELEMENTS : ELEM/ELGA/ELNO/NOEU
C        NUMPT  : NUMERO DE PAS DE TEMPS
C        NUMORD : NUMERO D'ORDRE DU CHAMP
C        NBCMPV : NOMBRE DE COMPOSANTES VOULUES
C                 SI NUL, ON LIT LES COMPOSANTES A NOM IDENTIQUE
C        NCMPVA : LISTE DES COMPOSANTES VOULUES POUR ASTER
C        NCMPVM : LISTE DES COMPOSANTES VOULUES DANS MED
C        NOMGD  : NOM DE LA GRANDEUR ASSOCIEE AU CHAMP
C        NCMPRF : NOMBRE DE COMPOSANTES DE REFERENCE DU CHAMP SIMPLE
C        JNOCMP : ADRESSE DU NOM DES COMP. DE REF. DU CHAMP SIMPLE
C        ADSL, ADSV : ADRESSES DES TABLEAUX DES CHAMPS SIMPLIFIES
C      SORTIES:
C         CODRET : CODE DE RETOUR (0 : PAS DE PB, NON NUL SI PB)
C_____________________________________________________________________
C
      IMPLICIT NONE
C
C 0.1. ==> ARGUMENTS
C
      INTEGER NROFIC
      INTEGER ADSL, ADSV, ADSD
      INTEGER NBVATO, NCMPRF, JNOCMP
      INTEGER NUMPT, NUMORD, NBCMPV
      INTEGER CODRET
C
      CHARACTER*4 TYPECH
      CHARACTER*8 NOMGD, NOMAAS
      CHARACTER*32 NOCHMD, NOMAMD
      CHARACTER*(*) NCMPVA, NCMPVM
C
C 0.2. ==> COMMUNS
C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ---------------------
      CHARACTER*32       JEXNUM , JEXNOM , JEXR8 , JEXATR
      INTEGER            ZI
      COMMON  / IVARJE / ZI(1)
      REAL*8             ZR
      COMMON  / RVARJE / ZR(1)
      COMPLEX*16         ZC
      COMMON  / CVARJE / ZC(1)
      LOGICAL            ZL
      COMMON  / LVARJE / ZL(1)
      CHARACTER*8        ZK8
      CHARACTER*16                ZK16
      CHARACTER*24                          ZK24
      CHARACTER*32                                    ZK32
      CHARACTER*80                                              ZK80
      COMMON  / KVARJE / ZK8(1) , ZK16(1) , ZK24(1) , ZK32(1) , ZK80(1)
C --------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ---------------------
C
C 0.3. ==> VARIABLES LOCALES
C
      CHARACTER*6 NOMPRO
      PARAMETER ( NOMPRO = 'LRCAME' )
C
      INTEGER EDLECT
      PARAMETER (EDLECT=0)
      CHARACTER*32 EDNOPF
      PARAMETER ( EDNOPF='                                ' )
C
      INTEGER NTYMAX
      PARAMETER (NTYMAX=28)
      INTEGER EDNOEU
      PARAMETER (EDNOEU=3)
      INTEGER EDMAIL
      PARAMETER (EDMAIL=0)
      INTEGER TYPNOE
      PARAMETER (TYPNOE=0)
      INTEGER IAUX, LETYPE
      INTEGER IDFIMD
      INTEGER NBCMFI, NBVAL
      INTEGER NCMPUT
      INTEGER EXISTC
      INTEGER NDIM
      INTEGER LGPROA
      INTEGER IFM, NIVINF
      INTEGER TYPENT, TYGEOM, NBTYP
      INTEGER NNOTYP(NTYMAX),NITTYP(NTYMAX)
      INTEGER TYPGEO(NTYMAX),LYGEOM(NTYMAX),LYPENT(NTYMAX)
      INTEGER RENUMD(NTYMAX),RENULY(NTYMAX),NLYVAL(NTYMAX)
      INTEGER I,J,NBTYLU, NBVALS,IGREL,ITYPEL,IALIEL,NEL,NBGREL
      INTEGER JTYPMA,JLCONX,IMA,NBNOMA,NMATYP,IBID,NBMA
C
      CHARACTER*8 SAUX08
      CHARACTER*8 NOMTYP(NTYMAX),NOMMAI
      CHARACTER*24 NUMCMP, NTNCMP, NTUCMP, NTVALE, NMCMFI(NTYMAX)
      CHARACTER*24 NTPROA, NMCMFL
      CHARACTER*32 NOMPRF
      CHARACTER*200 NOFIMD
      CHARACTER*2 K2BID
      CHARACTER*1 K1BID
C
      LOGICAL EXISTM, EXISTT
C
C====
C 1. PREALABLES
C====
C
C 1.1. ==> RECUPERATION DU NIVEAU D'IMPRESSION
C
      CALL INFNIV ( IFM, NIVINF )
C
      IF ( NIVINF.GT.1 ) THEN
        WRITE (IFM,1001) 'DEBUT DE '//NOMPRO
      ENDIF
 1001 FORMAT(/,10('='),A,10('='),/)
C
C 1.2. ==> NOMS DES TABLEAUX DE TRAVAIL
C               12   345678   9012345678901234
      NUMCMP = '&&'//NOMPRO//'.NUMERO_CMP     '
      NTNCMP = '&&'//NOMPRO//'.N0MCMP         '
      NTUCMP = '&&'//NOMPRO//'.UNITECMP       '
      NTVALE = '&&'//NOMPRO//'.VALEUR         '
      NTPROA = '&&'//NOMPRO//'.PROFIL_ASTER   '
C
C 1.3. ==> NOM DU FICHIER MED
C
      CALL CODENT ( NROFIC, 'G', SAUX08 )
      NOFIMD = 'fort.'//SAUX08
      IF ( NIVINF.GT.1 ) THEN
        WRITE (IFM,*) NOMPRO, ' : NOM DU FICHIER MED : ', NOFIMD
      ENDIF
C
C 1.4. ==> VERIFICATION DE LA BONNE VERSION DU FICHIER MED
C
      CALL EFFOCO ( NOFIMD, CODRET )
      IF ( CODRET.NE.0 ) THEN
        CALL UTDEBM ( 'A', NOMPRO, 'FICHIER ' )
        CALL UTIMPK ( 'S', 'MED : ', 1, NOFIMD )
        CALL UTIMPK ( 'L', 'CHAMP : ', 1, NOCHMD )
        CALL UTIMPI ( 'L', 'ERREUR EFFOCO NUMERO ', 1, CODRET )
        CALL UTFINM ()
        CALL UTMESS ( 'F', NOMPRO, 'SOIT LE FICHIER N''EXISTE PAS,'//
     >             ' SOIT C''EST UNE MAUVAISE VERSION DE MED.' )
      ENDIF
C
C 1.5. ==> VERIFICATION DE L'EXISTENCE DU MAILLAGE CONCERNE
C
C 1.5.1. ==> C'EST LE PREMIER MAILLAGE DU FICHIER
C            ON RECUPERE SON NOM ET SA DIMENSION.
C
      IF ( NOMAMD.EQ.' ' ) THEN
C
        CALL MDEXPM ( NOFIMD, NOMAMD, EXISTM, NDIM, CODRET )
        IF ( .NOT.EXISTM ) THEN
          CALL UTMESS ( 'F', NOMPRO, 'PAS DE MAILLAGE DANS '//NOFIMD )
        ENDIF
C
C 1.5.2. ==> C'EST UN MAILLAGE DESIGNE PAR UN NOM
C            ON RECUPERE SA DIMENSION.
C
      ELSE
C
        CALL MDEXMA ( NOFIMD, NOMAMD, EXISTM, NDIM, CODRET )
        IF ( .NOT.EXISTM ) THEN
          CALL UTMESS ( 'F', NOMPRO,
     >   'LE MAILLAGE '//NOMAMD//' EST INCONNU DANS '//NOFIMD )
        ENDIF
C
      ENDIF
C
C 1.8. ==> VERIFICATIONS DES COMPOSANTES ASTER DEMANDEES
C          EN SORTIE, ON A :
C       NCMPUT : NOMBRE DE COMPOSANTES VALIDES.
C       NUMCMP : TABLEAU DES NUMEROS DES COMPOSANTES VALIDES
C       NTNCMP : TABLEAU DES NOMS DES COMPOSANTES VALIDES (K8)
C       NTUCMP : TABLEAU DES UNITES DES COMPOSANTES VALIDES (K16)
C
      IF ( NBCMPV.NE.0 ) THEN
        CALL JEVEUO ( NCMPVA, 'L', IAUX )
      ELSE
        IAUX = 1
      ENDIF
C
      CALL UTLICM ( NBCMPV, ZK8(IAUX),
     >              NOMGD, NCMPRF, ZK8(JNOCMP),
     >              NCMPUT, NUMCMP, NTNCMP, NTUCMP )
C
C 1.6. ==> . RECUPERATION DES NB/NOMS/NBNO/NBITEM DES TYPES DE MAILLES
C            DANS CATALOGUE
C          . RECUPERATION DES TYPES GEOMETRIE CORRESPONDANT POUR MED
C          . VERIF COHERENCE AVEC LE CATALOGUE
C
      CALL LRMTYP ( NDIM, NBTYP, NOMTYP,
     >              NNOTYP, NITTYP, TYPGEO, RENUMD )
C
C 1.7. ==> LE CHAMP EXISTE-T-IL DANS LE FICHIER ?
C          AU BON NUMERO D'ORDRE ?
C          CONTIENT-IL A MINIMA LES COMPOSANTES VOULUES ?
C          LE NOMBRE DE VALEURS EST-IL CORRECT ?
C          SI OUI A TOUTES SES QUESTIONS, EXISTC VAUT 3.
C          ON RECUPERE ALORS :
C          . LE NOMBRE DE COMPOSANTES QU'IL Y A.
C          . LE NOM DE CES COMPOSANTES.
C
      NBTYLU = 0
      NBCMFI = 0
      EXISTT = .FALSE.
C
      DO 31 , LETYPE = 0 , NBTYP
C
        IF ( CODRET.EQ.0 ) THEN
C
        IF ( LETYPE.EQ.0 ) THEN
          IAUX = LETYPE
        ELSE
          IAUX = RENUMD(LETYPE)
        ENDIF
C
        IF ( IAUX.EQ.0 ) THEN
          TYPENT = EDNOEU
          TYGEOM = TYPNOE
        ELSE
          TYPENT = EDMAIL
          TYGEOM = TYPGEO(IAUX)
        ENDIF
C
        CALL CODENT(LETYPE,'G',K2BID)
        NMCMFL = '&&'//NOMPRO//'.N0MCMP_FICHIE'//K2BID

        CALL MDEXCH ( NOFIMD,
     >                NOCHMD, NUMPT, NUMORD, NBCMPV, NCMPVM,
     >                NBVATO, TYPENT, TYGEOM,
     >                EXISTC, NBCMFI, NMCMFL, NBVAL, CODRET )
        IF ( EXISTC.GE.3 ) THEN 
           EXISTT = .TRUE.
           NBTYLU = NBTYLU + 1
           NMCMFI(NBTYLU) = NMCMFL
           IF ( TYPECH.NE.'NOEU' ) THEN 
              LYPENT(NBTYLU) = TYPENT
              LYGEOM(NBTYLU) = TYGEOM
              NLYVAL(NBTYLU) = NBVAL
           ENDIF
        ENDIF
C
        ENDIF
   31 CONTINUE
C
      IF ( .NOT.EXISTT ) THEN
        CALL UTDEBM ( 'A', NOMPRO, 'FICHIER ' )
        CALL UTIMPK ( 'S', 'MED : ', 1, NOFIMD )
        CALL UTIMPK ( 'L', 'CHAMP : ', 1, NOCHMD )
        CALL UTIMPI ( 'L', 'NUMERO D ORDRE : ', 1, NUMORD )
        CALL UTIMPI ( 'L', 'NUMERO DE PAS DE TEMPS : ', 1, NUMPT )
        CALL UTFINM ()
        IF ( EXISTC.EQ.0 ) THEN 
         CALL UTMESS ( 'A', NOMPRO, 'CHAMP INCONNU.' )
        ELSEIF ( EXISTC.EQ.1 ) THEN 
         CALL UTMESS ( 'A', NOMPRO, 'IL MANQUE DES COMPOSANTES.' )
        ELSEIF ( EXISTC.EQ.2 ) THEN 
         CALL UTMESS ( 'A', NOMPRO, 'AUCUNE VALEUR A CE NRO D ORDRE.' )
        ELSEIF ( EXISTC.EQ.4 ) THEN 
         CALL UTMESS ( 'A', NOMPRO, 'MAUVAIS NOMBRE DE VALEURS.' )
        ENDIF
        CALL UTMESS ( 'F', NOMPRO, 'LECTURE IMPOSSIBLE.' )
      ENDIF
C
C====
C 2. OUVERTURE DU FICHIER EN LECTURE
C====
C
      CALL CODENT (NROFIC, 'G', SAUX08 )
      NOFIMD = 'fort.'//SAUX08
C
      CALL EFOUVR ( IDFIMD, NOFIMD, EDLECT, CODRET)
      IF ( CODRET.NE.0 ) THEN
        CALL UTDEBM ( 'A', NOMPRO, 'FICHIER ' )
        CALL UTIMPK ( 'S', 'MED : ', 1, NOFIMD )
        CALL UTIMPK ( 'L', 'CHAMP : ', 1, NOCHMD )
        CALL UTIMPI ( 'L', 'ERREUR EFOUVR NUMERO ', 1, CODRET )
        CALL UTFINM ()
        CALL UTMESS ( 'F', NOMPRO, 'PROBLEME A L OUVERTURE DU FICHIER' )
      ENDIF
C
C=====================================================================
C 3. TRAITEMENT DES CHAMPS AUX NOEUDS                             ====
C=====================================================================
C
      IF (TYPECH(1:4).EQ.'NOEU') THEN
C====
C 3.1 LECTURE DES VALEURS
C====
C
      TYPENT = EDNOEU
      TYGEOM = TYPNOE
      CALL LRCMLE ( IDFIMD, NOCHMD, NOMAMD,
     >              NBCMFI, NBVATO, NUMPT, NUMORD,
     >              TYPENT, TYGEOM,
     >              NTVALE, NOMPRF,
     >              CODRET )
C
C====
C 3.2 LECTURE DU PROFIL
C====
C
      IF ( NOMPRF.EQ.EDNOPF ) THEN
        LGPROA = 0
      ELSE
        CALL LRCMPR ( IDFIMD, NOMPRF,NTPROA, LGPROA,CODRET )
      ENDIF
C
C====
C 3.3 TRANFERT DES VALEURS
C====
C
      CALL LRCMVA ( NTVALE, NBVATO, NTPROA, LGPROA,
     >              NCMPRF, ZK8(JNOCMP),
     >              NBCMFI, NMCMFI(1), NBCMPV, NCMPVM, NUMCMP,
     >              ADSL, ADSV,
     >              CODRET )
C
      ELSE
C
C=====================================================================
C 4. TRAITEMENT DES CHAMPS AUX ELEMENTS                           ====
C=====================================================================
C
C  ON BOUCLE (71) SUR LES TYPES DE MAILLE LUS DANS LE CHAMP MED. LE
C  NOMBRE DE MAILLES RELATIF A CE TYPE EST NMATYP RENVOYE PAR EFNEMA.
C  LES VALEURS NUMERIQUES SONT SAUVEES DANS LE TABLEAU D ADRESSE ADSV
C  CE TABLEAU A ETE DIMENSIONNE PAR CESCRE A :
C  NB DE TYPE DE MAIL * NB DE VALEURS PAR MAILLE * NB DE COMPOSANTES 
C  * NB DE MAILLES DU TYPE
C  LE NB DE VALEURS PAR MAILLE :
C       - POUR UN ELNO : NB DE NOEUDS (NBNOMA DONNE PAR CONNECTIVITE)
C       - POUR UN ELEM : 1
C       - POUR UN ELGA : 27 EN DUR (NB MAX DE PTS DE GAUSS)
C
      CALL JEVEUO(JEXATR(NOMAAS(1:8)//'.CONNEX','LONCUM'),'L',JLCONX)
      CALL JEVEUO('&CATA.TE.TYPEMA','L',JTYPMA)
      DO 71 , LETYPE = 1 , NBTYLU
C
C  RECUPERATION DE NMATYP : NOMBRE TOTAL DE MAILLES DANS LE TYPE LYGEOM
C
         CALL EFNEMA ( IDFIMD, NOMAMD, 1, 0, LYGEOM(LETYPE), 0,
     >                 NMATYP, CODRET )
         IF     (TYPECH(1:4).EQ.'ELNO') THEN
            DO 72 I=1,NBTYP
               IF (LYGEOM(LETYPE).EQ.TYPGEO(I)) NBNOMA=NNOTYP(I)
  72        CONTINUE
         ELSEIF (TYPECH(1:4).EQ.'ELEM') THEN
            NBNOMA =  1
         ELSEIF (TYPECH(1:4).EQ.'ELGA') THEN
            NBNOMA =  27
         ENDIF
C
C====
C 4.1 LECTURE DES VALEURS
C====
C
         CALL JEDETC ('V',NTVALE,1)
         NBVALS  =  NLYVAL(LETYPE)*NBCMFI
         CALL LRCMLE ( IDFIMD, NOCHMD, NOMAMD,
     >                 NBCMFI, NBVALS, NUMPT, NUMORD,
     >                 LYPENT(LETYPE), LYGEOM(LETYPE),
     >                 NTVALE, NOMPRF,
     >                 CODRET )
C
C====
C 4.2 LECTURE DU PROFIL
C====
C
         IF ( NOMPRF.EQ.EDNOPF ) THEN
             LGPROA = 0
         ELSE
             CALL JEDETC ('V',NTPROA,1)
             CALL LRCMPR ( IDFIMD, NOMPRF,
     >                     NTPROA, LGPROA,
     >                     CODRET )
         ENDIF
C
C====
C 4.3 TRANFERT DES VALEURS
C====
C
         CALL LRCMVE ( IDFIMD, NOMAMD, LYGEOM(LETYPE),
     >                 NTVALE, NMATYP, NBNOMA, NTPROA, LGPROA,
     >                 NCMPRF, ZK8(JNOCMP),
     >                 NBCMFI, NMCMFI(LETYPE), NBCMPV, NCMPVM, NUMCMP,
     >                 ADSL, ADSV, ADSD, 
     >                 CODRET )
C
   71 CONTINUE
      ENDIF
C
C====
C 5. FIN
C====
C
C 5.1. ==> FERMETURE FICHIER
C
      CALL EFFERM ( IDFIMD, CODRET )
      IF ( CODRET.NE.0 ) THEN
        CALL UTDEBM ( 'A', NOMPRO, 'FICHIER ' )
        CALL UTIMPK ( 'S', 'MED : ', 1, NOFIMD )
        CALL UTIMPK ( 'L', 'CHAMP : ', 1, NOCHMD )
        CALL UTIMPI ( 'L', 'ERREUR EFFERM NUMERO ', 1, CODRET )
        CALL UTFINM ()
        CALL UTMESS ( 'F', NOMPRO, 'PROBLEME A LA FERMETURE DU FICHIER')
      ENDIF
C
C 5.2. ==> MENAGE
C
      CALL JEDETC ('V','&&'//NOMPRO,1)
C
      IF ( NIVINF.GT.1 ) THEN
        WRITE (IFM,1001) 'FIN DE '//NOMPRO
      ENDIF
C
      END
