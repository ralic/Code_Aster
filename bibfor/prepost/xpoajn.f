      SUBROUTINE XPOAJN(MAXFEM,INO,LSN,JDIRNO,PREFNO,NFISS,HE,
     &                  NNN,INN,INNTOT,NBNOC,NBNOFI,INOFI,
     &                  CO,IACOO2)

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF PREPOST  DATE 09/11/2012   AUTEUR DELMAS J.DELMAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE GENIAUT S.GENIAUT

      IMPLICIT NONE

      INCLUDE 'jeveux.h'

      CHARACTER*32 JEXNOM
      CHARACTER*2   PREFNO(4)
      CHARACTER*8   MAXFEM
      INTEGER       JDIRNO,NNN,INN,INNTOT,NBNOC,INO
      INTEGER      NBNOFI,INOFI,IACOO2,NFISS,HE(NFISS)
      REAL*8        LSN(NFISS),CO(3)
C
C            ON AJOUTE UN NOUVEAU NOEUD AU NOUVEAU MAILLAGE X-FEM
C
C   IN
C     INO   : NUMÉRO DU NOEUD OU DU POINT D'INTERSECTION
C     LSN    : LEVEL SETS NORMALES EN INO
C     JDIRNO : ADRESSE DU TABLEAU DIRNO LOCAL
C     PREFNO : PREFERENCES POUR LE NOMAGE DES NOUVELLES ENTITES
C     NFISS  : NOMBRE DE FISSURES "VUES" PAR L'ÉLÉMENT PARENT
C     HE     : VALEURS DE(S) FONCTION(S) HEAVISIDE SUR LE SOUS ÉLÉMENT
C     NNN    : NOMBRE DE NOUVEAU NOEUDS A CREER SUR LA MAILLE PARENT
C     INN    : COMPTEUR LOCAL DU NOMBRE DE NOUVEAUX NOEUDS CREES
C     INNTOT : COMPTEUR TOTAL DU NOMBRE DE NOUVEAUX NOEUDS CREES
C     NBNOC  : NOMBRE DE NOEUDS CLASSIQUES DU MAILLAGE FISSURE
C     NBNOFI : NOMBRE DE NOEUDS SITUES SUR LA FISSURE
C     INOFI  : LISTE DES NOEUDS SITUES SUR LA FISSURE
C     NBNOLA : NOMBRE DE NOEUDS SITUES SUR LA FISSURE AVEC DES LAGS
C     INOLA  : LISTE DES NOEUDS SITUES SUR LA FISSURE AVEC DES LAGS
C     CO     : COORDONNEES  INITIALES DE INO
C     IACOO2 : ADRESSE DES COORDONNES DES NOEUDS DU MAILLAGE FISSURE
C     DDLC :  NOMBRE DE DDLS DE CONTACT DE L'ÉLÉMENT PARENT
C   OUT
C     MAXFEM : NOM DU MAILLAGE FISSURE
C     INN    : COMPTEUR LOCAL DU NOMBRE DE NOUVEAU NOEUDS CREES
C     INNTOT : COMPTEUR TOTAL DU NOMBRE DE NOUVEAU NOEUDS CREES
C     NBNOFI : NOMBRE DE NOEUDS SITUES SUR LA FISSURE
C     INOFI  : LISTE DES NOEUDS SITUES SUR LA FISSURE
C     NBNOLA : NOMBRE DE NOEUDS SITUES SUR LA FISSURE AVEC DES LAGS
C     INOLA  : LISTE DES NOEUDS SITUES SUR LA FISSURE AVEC DES LAGS
C     IACOO2 :  ADRESSE DES COORDONNES DES NOEUDS DU MAILLAGE FISSURE

C
      REAL*8        CRILSN,MINLSN,R8MAEM
      INTEGER      J,IFISS,FISS
      CHARACTER*2   NM
      CHARACTER*6   CHN
      CHARACTER*8   VALK(2)
      PARAMETER     (CRILSN = 1.D-4)
      LOGICAL     LPINT
      DATA          VALK /'NOEUDS','XPOAJN'/
C
C     ------------------------------------------------------------------

      CALL JEMARQ()

C --- LPINT EST VRAI SI LE NOEUD DU MAILLAGE X-FEM EST SUR LA FISSURE.
C --- ON ATTACHERA DANS CE CAS CE NOEUDS AU GROUPE NFISSU
      IF (INO.LT.1000) THEN
        LPINT = .FALSE.
        DO 10 IFISS = 1,NFISS
          IF (LSN(IFISS).EQ.0.D0) LPINT = .TRUE.
 10     CONTINUE
      ELSEIF (INO.GT.1000.AND.INO.LT.2000) THEN
        LPINT = .TRUE.
      ELSEIF (INO.GT.2000) THEN
        LPINT = .FALSE.
        DO 20 IFISS = 1,NFISS
          IF (ABS(LSN(IFISS)).LT.CRILSN) LPINT = .TRUE.
 20     CONTINUE
      ENDIF

      IF (LPINT) THEN
        MINLSN = R8MAEM()
        DO 50 IFISS = 1, NFISS
C     ON DETECTE LA FISSURE CORESPONDANTE AU POINT D'INTERSECTION
C     ATTENTION, IL PEUT Y AVOIR PLUSIEURS CANDIDAT AU NIV DE L'INTER
          IF (ABS(LSN(IFISS)).LT.MINLSN.AND.HE(IFISS).NE.0) THEN
            MINLSN = ABS(LSN(IFISS))
            FISS = IFISS
          ENDIF
 50     CONTINUE
        IF (HE(FISS).EQ.-1) THEN
          NM=PREFNO(2)
        ELSE
          NM=PREFNO(3)
        ENDIF
      ELSE
        NM=PREFNO(1)
      ENDIF

C     COMPTEUR DES NOMS DES NOEUDS
      IF (INNTOT.GE.999999) CALL U2MESK('F','XFEM_8',1,VALK)
      INN = INN + 1
      INNTOT= INNTOT +1
      CALL ASSERT(INN.LE.NNN)

      ZI(JDIRNO-1+(2+NFISS)*(INN-1)+1) = INO
      ZI(JDIRNO-1+(2+NFISS)*(INN-1)+2) = NBNOC + INNTOT
      DO 30 IFISS = 1,NFISS
        ZI(JDIRNO-1+(2+NFISS)*(INN-1)+2+IFISS) = HE(IFISS)
 30   CONTINUE
      CALL CODENT(INNTOT,'G',CHN)

      CALL JECROC(JEXNOM(MAXFEM//'.NOMNOE',NM//CHN))
      DO 40 J=1,3
        ZR(IACOO2-1+3*(NBNOC+INNTOT-1)+J)=CO(J)
 40   CONTINUE
C       LISTE DES NOEUDS SUR LA FISSURE
      IF (LPINT) THEN
        NBNOFI=NBNOFI+1
        ZI(INOFI-1+NBNOFI)=NBNOC+INNTOT
C        IF (HE(FISS).EQ.-1.AND.DDLC.GT.0) THEN
C        ATTENTION, IL FAUDRA RÉCUPÉRER DDLC AVEC XPOCMP DS XPOMAX
C       LISTE DES NOEUDS PORTANT DDLS DE CONTACT (COTÉ ESCLAVE)
C          NBNOLA=NBNOLA+1
C          ZI(INOLA-1+NBNOLA)=NBNOC+INNTOT
C        ENDIF
      ENDIF

      CALL JEDEMA()
      END
