      SUBROUTINE DALP3D(NELEM,NNOEM,DEGRE,NSOMMX,ICNC,NELCOM,
     &                   NUMELI,NDIM,XY,ERREUR,ENERGI,VOLUME,
     &                   ALPHA,NALPHA)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF PREPOST  DATE 11/07/2005   AUTEUR VABHHTS J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2005  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================

C*******************************************************************
C              BUT DE CETTE ROUTINE :                              *
C CALCULER LE DEGRE DE LA SINGULARITE PE EN CHAQUE NOEUD           *
C PUIS EN CHAQUE EF A PARTIR DE PE                                 *
C 1) COMMENT REPERE T-ON UN NOEUD SINGULIER ?                      *
C    L ERREUR LOCALE EST GRANDE EN CE NOEUD                        *
C    COMPAREE A L ERREUR DE REFERENCE SUR TOUTE LA STRUCTURE       *
C    DEROULEMENT DE LA ROUTINE :                                   *
C                                                                  *
C    CALCUL DE L ERREUR DE REFERENCE                               *
C    NOEU1 EST IL SINGULIER ?                                      *
C      CALCUL DE L ERREUR LOCALE SUR LES EFS CONNECTES A NOEU1     *
C      TEST1 : SI ERREUR (NOEU1) > ERREUR GLOBALE                  *
C        NOEU2 = NOEUDS CONNECTES A NOEU1                          *
C        CALCUL DE L ERREUR LOCALE SUR LES EFS CONNECTES A NOEU2   *
C        TEST2 : SI ERREUR (NOEU2) > ERREUR GLOBALE                *
C        LE NOEU1 EST SINGULIER                                    *
C    REMARQUE : CONTRAIREMENT AU 2D NOEU1 EST SINGULIER UNIQUEMENT *
C               SI L UN DE SES VOISINS L EST AUSSI                 *
C               => ON OUBLIE LES NOEUDS SINGULIERS ISOLES          *
C 2) COMMENT CALCULE T-ON PE=ALPHAN(NOEU1) ?                       *
C    SI NOEU1 REGULIER ALPHAN(NOEU1)=DEGRE D INTERPOLATION DE L EF *
C    SI NOEU1 SINGULIER CALCUL DE PE       *
C      ALPHAN(NOEU1)=MIN(ALPHAN(NOEU1),PE) ET                      *   
C      ALPHAN(NOEU2)=MIN(ALPHAN(NOEU2),PE)                         *
C 3) COMMENT CALCULE T-ON PE ?                                     *
C    CONSTRUCTION DE LA COURBE ENERGIE EN FONCTION DU RAYON        *
C    EN 3D L ENERGIE EST CALCULEE SUR DES CYLINDRES D EXTREMITE    *
C    NOEU1 ET NOEU2 ET POUR DIFFERENTS RAYONS                      *
C    CALCUL DE PE PAR REFERENCE A L ENERGIE EN POINTE DE FISSURE   *
C 4) REMARQUE : EN 3D ON REGARDE UNIQUEMENT LES NOEUDS SOMMET BORD *
C*******************************************************************

C IN  NELEM                  : NOMBRE D ELEMENTS FINIS
C IN  NNOEM                  : NOMBRE DE NOEUDS
C IN  DEGRE                  : DEGRE DES EF (1 POUR P1 2 POUR P2)
C IN  NSOMMX                 : NBRE DE NOEUDS SOMMETS MAX PAR EF
C IN  ICNC(NSOMMX+2,NELEM)   : EF => NOEUDS SOMMETS CONNECTES A EF
C     1ERE VALEUR = NBRE DE NOEUDS SOMMETS CONNECTES A L EF N°X
C     2EME VALEUR = 1 SI EF UTILE 0 SINON 
C     CONNECTIVITE  EF N°X=>N° DE NOEUDS SOMMETS CONNECTES A X
C     EN 2D EF UTILE = QUAD OU TRIA
C     EN 3D EF UTILE = TETRA OU HEXA
C IN  NELCOM                 : NBRE D EF SURFACIQUE MAX PAR NOEUD 
C IN  NUMELI(NELCOM+2,NNOEM) : NOEUD =>EF SURFACIQUE CONNECTES A NOEUD
C     1ERE VALEUR = NBRE D EFS UTILES CONNECTES AU NOEUD N°X
C     2EME VALEUR = 0 NOEUD MILIEU OU NON CONNECTE A UN EF UTILE
C                   1 NOEUD SOMMET A L INTERIEUR + LIE A UN EF UTILE
C                   2 NOEUD SOMMET BORD + LIE A UN EF UTILE 
C     CONNECTIVITE  NOEUD N°X=>N° DES EF UTILE CONNECTES A X 
C IN  NDIM                   : DIMENSION DU PROBLEME
C IN  XY(3,NNOEM)            : COORDONNEES DES NOEUDS
C IN  ERREUR(NELEM)          : ERREUR SUR CHAQUE EF 
C IN  ENERGI(NELEM)          : ENERGIE SUR CHAQUE EF
C IN  VOLUME(NELEM)          : VOLUME DE CHAQUE EF
C OUT ALPHA(NELEM)           : DEGRE DE LA SINGULARITE PAR ELEMENT
C OUT NALPHA                 : NOMBRE DE CPE PAR ELEMENT DIFFERENTS 
C                              1 PAR DEFAUT SI PAS DE SINGULARITE

      IMPLICIT NONE 
 
C DECLARATION GLOBALE

      INTEGER NELEM,NNOEM,DEGRE,NSOMMX,NELCOM,NDIM
      INTEGER ICNC(NSOMMX+2,NELEM),NUMELI(NELCOM+2,NNOEM)
      INTEGER NALPHA
      REAL*8  XY(3,NNOEM),ERREUR(NELEM),ENERGI(NELEM),VOLUME(NELEM)
      REAL*8  ALPHA(NELEM)

C DECLARATION LOCALE

      INTEGER I,INNO,INEL,NUEF,NOEU1,NOEU2
      INTEGER TBNOZO(1000),NBNOZO(3),TBELZO(1000),NBELZO(3)
      INTEGER NBNOE
      REAL*8  PRECMO,PRECRE,PREC,VOL
      REAL*8  DTYP,ALPHAN(NNOEM),PE

C 1 - DEGRE D INTERPOLATION

      IF (DEGRE.EQ.1) THEN
        DTYP = 1.D+0
      ELSE
        DTYP = 2.D+0      
      ENDIF

C 2 - INITIALISATION DES ALPHA = DEGRE D INTERPOLATION

      DO 10 INNO = 1,NNOEM
        ALPHAN(INNO)= DTYP
 10   CONTINUE
      DO 20 INEL = 1,NELEM 
        ALPHA(INEL) = DTYP
 20   CONTINUE
 
C 3 - CALCUL DES PRECISIONS MOYENNE ET DE REFERENCE
      
      PRECMO = 0.D+0
      VOL  = 0.D+0
      DO 30 INEL = 1,NELEM
        PRECMO = PRECMO + ERREUR(INEL)**2
        VOL  = VOL  + VOLUME(INEL)
 30   CONTINUE                                                      
      PRECMO = SQRT( PRECMO / VOL )
      
      IF ( DTYP.EQ.1.0D0 ) THEN
        PRECRE = 3.D0 * PRECMO        
      ELSE IF (DTYP.EQ.2.0D0) THEN
        PRECRE = 2.D0 * PRECMO
      ENDIF  

C 4 - BOUCLE SUR LES NOEUDS SOMMET BORD POUR DETECTER 
C     SI LE NOEUD CONSIDERE EST SINGULIER OU PAS

      DO 40 INNO = 1,NNOEM
        
        IF (NUMELI(2,INNO).NE.2) GOTO 40
        
C 4.1 - ERREUR LOCALE SUR COUCHE 1 (=EF CONNECTES A INNO)

        PREC = 0.0D0
        VOL = 0.0D0

        DO 50 INEL = 1,NUMELI(1,INNO)
          NUEF=NUMELI(2+INEL,INNO)
          PREC = PREC + ERREUR(NUEF)**2
          VOL = VOL + VOLUME(NUEF)  
 50     CONTINUE
        IF (PREC.NE.0.D+0 .AND. VOL.NE.0.D+0) THEN
          PREC = SQRT(PREC/VOL)
        ELSE
          PREC = 0.D+0
        ENDIF

C 4.2 - TEST1 : SI PREC > PRECREF ON VA CHERCHER LES NOEUDS NOEU2
C       CONNECTES A INNO 

        IF (PREC.GE.PRECRE) THEN          

C 4.2.1 - RECHERCHE DES NOEUDS ET EFS COMPOSANTS LES COUCHES 1,2 ET 3

          CALL DZONFG(NSOMMX,ICNC,NELCOM,NUMELI,INNO,
     &                TBELZO,NBELZO,TBNOZO,NBNOZO)
          NOEU1=INNO

C 4.2.2 - TEST2 : POUR CHAQUE NOEU2 CONNECTE A NOEU1
C         ON DETECTE SI NOEU2 EST SINGULIER
C         SI OUI ON DETERMINE PE ET UNIQUEMENT SI NOEU2>NOEU1
C         POUR EVITER DE FAIRE DEUX FOIS LE CALCUL

          DO 60 I = 1,NBNOZO(1)
            NOEU2 = TBNOZO(I)
            IF (NUMELI(2,NOEU2).NE.2) GOTO 60
            IF (NOEU2.GT.NOEU1) THEN
              VOL = 0.0D0
              PREC = 0.0D0
              DO 70 INEL = 1,NUMELI(1,NOEU2)
                NUEF=NUMELI(2+INEL,NOEU2)
                PREC = PREC + ERREUR(NUEF)**2
                VOL = VOL + VOLUME(NUEF)  
 70           CONTINUE
              IF (PREC.NE.0.D+0 .AND. VOL.NE.0.D+0) THEN
                PREC = SQRT(PREC/VOL)
              ELSE
                PREC = 0.D+0
              ENDIF

              IF ( PREC.GE.PRECRE ) THEN

C 4.2.2.1 - CALCUL DE PE

                NBNOE=NBNOZO(1)+NBNOZO(2)+NBNOZO(3)
                CALL DFORT3(NSOMMX,ICNC,NDIM,NOEU1,NOEU2,
     &                     TBELZO,NBELZO(3),TBNOZO,NBNOZO,NBNOE,
     &                     XY,VOLUME,ENERGI,PE)

                IF (ALPHAN(NOEU1).GT.PE) ALPHAN(NOEU1)=PE
                IF (ALPHAN(NOEU2).GT.PE) ALPHAN(NOEU2)=PE
              ENDIF
            ENDIF
 60       CONTINUE
        ENDIF
 40   CONTINUE

C 5 - ON REMPLIT LE TABLEAU ALPHA = DEGRE DE LA SINGULARITE PAR EF
 
      NALPHA = 1
      DO 110 INEL = 1,NELEM
        IF (ICNC(2,INEL).LT.1) GOTO 110
        DO 120 INNO = 1,ICNC(1,INEL)
          ALPHA(INEL) = MIN(ALPHA(INEL),ALPHAN(ICNC(INNO+2,INEL)))
 120    CONTINUE
        IF (ALPHA(INEL).LT.DTYP) THEN
          NALPHA = NALPHA + 1
        ENDIF
 110  CONTINUE

      END
