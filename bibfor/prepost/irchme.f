      SUBROUTINE IRCHME ( IFI, CHANOM,
     >                    LRESU, NORESU, NOSIMP, NOPASE, NOMSYM,
     >                    TYPECH, NUMORD,
     >                    NBCMP,  NOMCMP, 
     >                    NBNOEC, LINOEC, NBMAEC, LIMAEC,
     >                    CODRET )
C_______________________________________________________________________
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF PREPOST  DATE 06/02/2006   AUTEUR D6BHHJP J.P.LEFEBVRE 
C RESPONSABLE GNICOLAS G.NICOLAS
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C        IMPRESSION DU CHAMP CHANOM NOEUD/ELEMENT ENTIER/REEL 
C        AU FORMAT MED
C     ENTREES:
C        IFI    : UNITE LOGIQUE D'IMPRESSION DU CHAMP
C        CHANOM : NOM ASTER DU CHAM A ECRIRE
C        LRESU  : .TRUE.  : INDIQUE IMPRESSION D'UN CONCEPT RESULTAT
C                 .FALSE. : IMPRESSION D'UN CHAMP GRANDEUR
C        NORESU : NOM DU RESULTAT D'OU PROVIENT LE CHAMP A IMPRIMER.
C        NOSIMP : NOM SIMPLE ASSOCIE AU CONCEPT NORESU SI SENSIBILITE
C        NOPASE : NOM DU PARAMETRE SENSIBLE
C        NOMSYM : NOM SYMBOLIQUE DU CHAMP
C        TYPECH : TYPE DU CHAMP
C        NUMORD : NUMERO D'ORDRE DU CHAMP DANS LE RESULTAT_COMPOSE.
C        NBCMP  : NOMBRE DE COMPOSANTES A ECRIRE
C        NOMCMP : NOMS DES COMPOSANTES A ECRIRE
C        NBNOEC : NOMBRE DE NOEUDS A ECRIRE (O, SI TOUS LES NOEUDS)
C        LINOEC : LISTE DES NOEUDS A ECRIRE SI EXTRAIT
C        NBMAEC : NOMBRE DE MAILLES A ECRIRE (0, SI TOUTES LES MAILLES)
C        LIMAEC : LISTE DES MAILLES A ECRIRE SI EXTRAIT
C     SORTIES:
C        CODRET : CODE DE RETOUR (0 : PAS DE PB, NON NUL SI PB)
C_______________________________________________________________________
C
C     ARBORESCENCE DE L'ECRITURE DES CHAMPS AU FORMAT MED :
C  IRCH19
C  IRCHME
C  MDNOCH RSADPA  IRCNME IRCEME
C                   .    .
C                    .  .
C                   IRCAME
C                    .  .
C                   .    .
C  MDNOMA MDEXMA IRMAIL UTLICM LRMTYP IRCMPR MDEXCH EFOUVR ...
C                   ... IRCMCC IRCMPG IRCMVA IRCMEC EFFERM
C
      IMPLICIT NONE
C
C 0.1. ==> ARGUMENTS
C
      CHARACTER*8 NORESU, NOSIMP, NOPASE, TYPECH
      CHARACTER*16 NOMSYM
      CHARACTER*19 CHANOM
      CHARACTER*(*)  NOMCMP(*)
C
      LOGICAL LRESU
C
      INTEGER NUMORD, NBCMP, IFI, IRET
      INTEGER NBNOEC, NBMAEC
      INTEGER LINOEC(*), LIMAEC(*)
C
      INTEGER CODRET
C
C 0.2. ==> COMMUNS
C --------------- COMMUNS NORMALISES  JEVEUX  --------------------------
      INTEGER      ZI
      REAL*8       ZR
      CHARACTER*8  ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /IVARJE/ZI(1)
      COMMON /RVARJE/ZR(1)
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C     -----  FIN  COMMUNS NORMALISES  JEVEUX --------------------------
C
C 0.3. ==> VARIABLES LOCALES
C
      CHARACTER*6 NOMPRO
      PARAMETER ( NOMPRO = 'IRCHME' )
C
      INTEGER EDNONO
      PARAMETER (EDNONO=-1)
      INTEGER EDNOPT
      PARAMETER (EDNOPT=-1)
C
      INTEGER IFM, NIVINF
      INTEGER NUMPT
      INTEGER LNOCHM
      INTEGER IAUX
C
      CHARACTER*8 SAUX08
      CHARACTER*8 UNIINS, MODELE
      CHARACTER*32 NOCHMD
C
      REAL*8 INSTAN
C
C====
C 1. PREPARATIFS
C====
C
      CALL INFNIV ( IFM, NIVINF )
C
10000 FORMAT(/,81('='),/,81('='),/)
10001 FORMAT(81('-'),/)
      IF ( NIVINF.GT.1 ) THEN
        CALL UTFLSH (CODRET)
        WRITE (IFM,10000)
        CALL UTMESS ('I',NOMPRO,'DEBUT DE L''ECRITURE MED DE '//CHANOM)
      ENDIF
C
C 1.1. ==> NOM DU CHAMP DANS LE FICHIER MED
C
      IF ( NOPASE.EQ.'        ' ) THEN
        SAUX08 = NORESU
      ELSE
        SAUX08 = NOSIMP
      ENDIF
C
      CALL MDNOCH ( NOCHMD, LNOCHM,
     >              LRESU, SAUX08, NOMSYM, NOPASE, CODRET )
C
      IF ( CODRET.EQ.0 ) THEN
        WRITE (IFM,11000) SAUX08
        WRITE (IFM,11001) NOMSYM
        IF ( NOPASE.NE.'        ' ) THEN
          WRITE (IFM,11002) NOPASE
        ENDIF
        IF ( NIVINF.GT.1 ) THEN
          WRITE (IFM,11003) TYPECH
        ENDIF
        WRITE (IFM,11004) NOCHMD
      ELSE
         CALL UTMESS
     > ( 'A' , NOMPRO, 'IMPOSSIBLE DE DETERMINER UN NOM DE CHAMP MED.' )
         CALL UTMESS ( 'A' , NOMPRO, 'PAS D ECRITURE POUR '//CHANOM )
         CALL UTMESS ( 'A' , NOMPRO, 'ISSU DE '//NORESU )
      ENDIF
C
11000 FORMAT(1X,'RESULTAT           : ',A8)
11001 FORMAT(1X,'CHAMP              : ',A16)
11002 FORMAT(1X,'PARAMETRE SENSIBLE : ',A8)
11003 FORMAT(1X,'TYPE DE CHAMP      : ',A)
11004 FORMAT(3X,'==> NOM MED DU CHAMP : ',A32,/)
C
C 1.2. ==> INSTANT CORRESPONDANT AU NUMERO D'ORDRE
C          REMARQUE : COMME ON NE SAIT PAS FAIRE MIEUX, ON IDENTIFIE
C                     LE NUMERO DE PAS DE TEMPS ET LE NUMERO D'ORDRE.
C                     L'UNITE EST INCONNUE.
C          REMARQUE : CELA N'A DU SENS QUE POUR UN CHAMP ISSU D'UN
C                     RESULTAT. POUR UNE GRANDEUR, IL N'Y A NI NUMERO
C                     D'ORDRE, NI PAS DE TEMPS.
C
      IF ( CODRET.EQ.0 ) THEN
C
      IF ( LRESU ) THEN
         CALL JEEXIN ( NORESU(1:8)//'           .INST', IRET )
         IF ( IRET.NE.0 ) THEN
           CALL RSADPA ( NORESU, 'L', 1, 'INST', NUMORD, 0, IAUX, 
     &                   SAUX08 )
           INSTAN = ZR(IAUX)
         ENDIF
         CALL JEEXIN ( NORESU(1:8)//'           .FREQ', IRET )
         IF ( IRET.NE.0 ) THEN
           CALL RSADPA ( NORESU, 'L', 1, 'FREQ', NUMORD, 0, IAUX, 
     &                   SAUX08 )
           INSTAN = ZR(IAUX)
         ENDIF
        NUMPT = NUMORD
C                 12345678
        UNIINS = 'INCONNUE'
C
      ELSE
C
        NUMORD = EDNONO
        NUMPT = EDNOPT
C
      ENDIF

      ENDIF
C
C 1.3. ==> NOM DU MODELE ASSOCIE, DANS LE CAS D'UNE STRUCTURE RESULTAT
C
      IF ( CODRET.EQ.0 ) THEN
C
      IF ( LRESU ) THEN
C
        CALL RSADPA ( NORESU, 'L', 1, 'MODELE', NUMORD, 0, IAUX,
     &                SAUX08 )
        MODELE = ZK8(IAUX)
C
      ELSE
C
        MODELE = '        '
C                 12345678
C
      ENDIF
C
      IF ( NIVINF.GT.1 ) THEN
        WRITE (IFM,13001) MODELE
13001 FORMAT(2X,'MODELE ASSOCIE AU CHAMP : ',A)
      ENDIF
      ENDIF
C
C====
C 2. ECRITURE DANS LE FICHIER MED
C====
C
      IF ( CODRET.EQ.0 ) THEN
C
      IF ( TYPECH(1:4).EQ.'NOEU' ) THEN
        CALL IRCNME ( IFI, NOCHMD, CHANOM, TYPECH, MODELE,
     >                NBCMP, NOMCMP,
     >                NUMPT, INSTAN, UNIINS, NUMORD,
     >                NBNOEC, LINOEC,
     >                CODRET )
      ELSE IF ( TYPECH(1:2).EQ.'EL' ) THEN
        CALL IRCEME ( IFI, NOCHMD, CHANOM, TYPECH, MODELE,
     >                NBCMP, NOMCMP,
     >                NUMPT, INSTAN, UNIINS, NUMORD,
     >                NBMAEC, LIMAEC,
     >                CODRET )
      ELSE
        CODRET = 1
        CALL UTMESS
     >   ( 'A' , NOMPRO, 'TYPE '//TYPECH(1:4)//' INCONNU POUR MED.' )
      ENDIF
C
      ENDIF
C
C====
C 3. BILAN
C====
C
      IF ( CODRET.NE.0 ) THEN
         CALL UTMESS
     > ( 'A' , NOMPRO, 'ECRITURE IMPOSSIBLE POUR '//CHANOM//
     >   ' AU FORMAT MED' )
      ENDIF
C
      IF ( NIVINF.GT.1 ) THEN
        CALL UTMESS ('I',NOMPRO,'FIN DE L''ECRITURE MED DE '//CHANOM)
        WRITE (IFM,10000)
        CALL UTFLSH (CODRET)
      ELSE
        WRITE (IFM,10001)
      ENDIF
C
      END
