      SUBROUTINE CM27MA(NBMATO, NBMA  , NBNO  , NBNOMI,
     &                  LIMA  , TYPEMA,CONNIZ, CONNOZ, NOFILS,
     &                  NBTYMA, NOMAST, REFTYP, NBREF, IMPMAI )
      IMPLICIT NONE
      INTEGER       NBMATO, NBMA, NBNO, LIMA(NBMA), NBTYMA, NBREF(*),
     &              NOFILS(6,*), TYPEMA(*), REFTYP(*), IMPMAI(*),
     &              NBNOMI
      CHARACTER*8   NOMAST(*)
      CHARACTER*(*) CONNIZ, CONNOZ
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF PREPOST  DATE 23/07/2007   AUTEUR SALMONA L.SALMONA 
C ======================================================================
C COPYRIGHT (C) 1991 - 2007  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================


C ----------------------------------------------------------------------
C           MISE A JOUR DES MAILLES (CREA_MAILLAGE HEXA20_27)
C ----------------------------------------------------------------------
C IN        NBMATO  NOMBRE TOTAL DE MAILLES DU MAILLAGE
C IN        NBMA    NOMBRE DE MAILLES DE LA LISTE DES MAILLES A TRAITER
C IN        NBNO    NOMBRE DE NOEUDS DU MAILLAGE INITIAL
C IN        NBNOMI  NOMBRE DE NOEUDS AJOUTE (HORMIS LES NOEUDS CENTRAUX)
C IN        LIMA    LISTE DES MAILLES A TRAITER
C VAR       TYPEMA  LISTE DES TYPES DES MAILLES
C IN        NDINIT  NUMERO INITIAL DES NOEUDS CREES
C IN        CONNIZ  CONNECTIONS INITIALES (COLLECTION JEVEUX)
C IN/JXOUT  CONNOZ  NOUVELLES CONNECTIONS (COLLECTION JEVEUX)
C IN        NOFILS  LISTE DES NOEUDS CREES PAR MAILLE A TRAITER
C ----------------------------------------------------------------------

C     ----- DEBUT COMMUNS NORMALISES  JEVEUX  --------------------------
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
      CHARACTER*32 JEXNUM,JEXNOM
C     -----  FIN  COMMUNS NORMALISES  JEVEUX  --------------------------

      INTEGER      M, MA, TYMAIN, TYMAOU, NBNOIN, NBNOOU, N, JMAMO,
     +             JPOSMA, JCONXI, JCONXO, IFM, NIV, NBHE20
      CHARACTER*8  KBID
      CHARACTER*24 CONNEI, CONNEO, MAMO, POSMAI
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
      CONNEI = CONNIZ
      CONNEO = CONNOZ
      CALL INFNIV ( IFM, NIV )

C --- LISTE DES MAILLES MODIFIEES 

      MAMO   = '&&CMLQMA.MAMO'
      POSMAI = '&&CMLQMA.POSMAI'
      CALL WKVECT(MAMO  ,'V V L',NBMATO, JMAMO)
      CALL WKVECT(POSMAI,'V V I',NBMATO, JPOSMA)
      DO 12 M = 1, NBMATO
         ZL(JMAMO-1+M) = .FALSE.
 12   CONTINUE

      DO 14 M = 1, NBMA
         MA = LIMA(M)
         TYMAIN = TYPEMA(MA)
         TYMAOU = REFTYP(TYMAIN)
         IF (TYMAIN.NE.TYMAOU) THEN
            ZL(JMAMO -1+MA) = .TRUE.
            ZI(JPOSMA-1+MA) = M
            IMPMAI(TYMAIN)  = IMPMAI(TYMAIN) + 1
         END IF
 14   CONTINUE

C --- CREATION DE LA CONNECTIVITE
      NBHE20=0
      DO 20 MA = 1, NBMATO

C ------ ANCIENNE CONNECTIVITE
         CALL JELIRA(JEXNUM(CONNEI,MA),'LONMAX',NBNOIN,KBID)
         CALL JEVEUO(JEXNUM(CONNEI,MA),'L',JCONXI)

C ------ NOUVEAU NOMBRE DE NOEUD POUR LA MAILLE COURANTE
         TYMAIN = TYPEMA(MA)
         IF (ZL(JMAMO-1 + MA)) THEN
            NBNOOU = NBREF(TYMAIN)
         ELSE
            NBNOOU = NBNOIN
         END IF

C ------ NOUVELLE CONNECTIVITE
         CALL JEECRA(JEXNUM(CONNEO,MA),'LONMAX',NBNOOU,KBID)
         CALL JEVEUO(JEXNUM(CONNEO,MA),'E',JCONXO)

C ------ RECOPIE DES NOEUDS INCHANGES
         DO 22 N = 1, NBNOIN
            ZI(JCONXO-1+N) = ZI(JCONXI-1+N)
 22      CONTINUE

C ------ INSERTION DES NOUVEAUX NOEUDS
         IF (ZL(JMAMO-1+MA)) THEN

C        MAILLES DE TYPE HEXA20
            IF (TYMAIN.EQ.25) THEN
              DO 24 N = NBNOIN+1, NBNOOU-1
                ZI(JCONXO-1+N) = NOFILS(N-NBNOIN,ZI(JPOSMA-1+MA)) + NBNO
 24           CONTINUE
              NBHE20=NBHE20+1
              ZI(JCONXO+NBNOOU-1) = NBNO+NBNOMI+NBHE20
C        MAILLES DE TYPE QUAD8
            ELSE IF (TYMAIN.EQ.14) THEN
              DO 25 N = NBNOIN+1, NBNOOU
                ZI(JCONXO-1+N) = NOFILS(N-NBNOIN,ZI(JPOSMA-1+MA)) + NBNO
 25           CONTINUE
            ENDIF
C ------ MODIFICATION DU TYPE
            TYMAOU = REFTYP(TYMAIN)
            TYPEMA(MA) = TYMAOU
         END IF

 20   CONTINUE

C --- IMPRESSION DU NOMBRE DE MAILLES HEXA20_27

      IF ( NIV .GE. 1 ) THEN
         WRITE(IFM,1000) 1
         DO 30 M = 1, NBTYMA
            IF ( IMPMAI(M) .NE. 0 ) THEN
               WRITE(IFM,1002) IMPMAI(M), NOMAST(M), NOMAST(REFTYP(M))
            END IF
 30      CONTINUE
      END IF

 1000 FORMAT('MOT CLE FACTEUR "HEXA20_27", OCCURRENCE ',I4)
 1002 FORMAT('   TRANSFORMATION DE ',I6,' MAILLES ',A8,' EN ',A8)

      CALL JEDETR ( MAMO )
      CALL JEDETR ( POSMAI )
      CALL JEDEMA()
      END
