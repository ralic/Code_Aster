      SUBROUTINE STAPU2(NBOBST,NBPT,NBPAIR,TEMPS,FCHO,VGLI,DLOC,COEF,
     &                  ANG,WK1,WK2,WK3,WK4,WK5,WK6,IDEBUT,NBLOC,NBVAL,
     &                  INOE,ISUPP,NBINST,TMPVU,PUSURN,VUSTUB,
     &                  VUSOB,PUS,PMOYE,POURPU,POUPRE)

C-----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF PREPOST  DATE 03/07/2012   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C TOLE CRP_21
C-----------------------------------------------------------------------
C     CALCUL DE LA PUISSANCE D'USURE AU SENS D'ARCHARD

C IN  : NBOBST : NB DE NOEUDS DE CHOC
C IN  : NBPT   : NB DE PAS DE TEMPS TEMPORELS ARCHIVES
C IN  : TEMPS  : INSTANTS DE CALCUL
C IN  : FCHO   : VECTEUR DES FORCES DE CHOC
C IN  : VGLI   : VECTEUR DES VITESSES DE GLISSEMENT
C IN  : NBLOC  : NB DE BLOCS POUR LE MOYENNAGE
C IN  : INOE   : NUMERO DE NOEUD TRAITE
C OUT : PUSURN : PUISSANCE D'USURE MOYENNEE
C-----------------------------------------------------------------------
      IMPLICIT NONE
      INTEGER OCCURE(100)
      REAL*8 TEMPS(*),FCHO(*),VGLI(*),WK1(*),WK2(*),WK3(*),WK4(*),
     &       WK5(*),WK6(*),DLOC(*),ANG(*),COEF(*),TMPVU(*),
     &       VUSTUB(NBPAIR,NBINST),VUSOB(NBPAIR,NBINST),PMOYE,POUPRE(*),
     &       POURPU(*),PUS(*)
      REAL*8 ZERO,PUSEE
C     REAL*8   TMPV(10)
C-----------------------------------------------------------------------
      INTEGER IBL ,IDEBUT ,IN ,INOE ,ISUPP ,J ,JDG 
      INTEGER K ,NBINST ,NBLOC ,NBOBST ,NBPAIR ,NBPT ,NBVAL 
      INTEGER NOCCUR ,NTOT 
      REAL*8 PUSURN ,R8RDDG ,RAD 
C-----------------------------------------------------------------------
      RAD = R8RDDG()
      ZERO = 0.D0
      PUSURN = 0.D0
      NTOT = 0
      PMOYE = ZERO
      CALL DCOPY(NBPT,DLOC(3* (INOE-1)+2),3*NBOBST,WK4,1)
      CALL DCOPY(NBPT,DLOC(3* (INOE-1)+3),3*NBOBST,WK5,1)
      DO 10 IN = 1,NBPT
        IF ((WK4(IN).NE.ZERO) .OR. (WK5(IN).NE.ZERO)) THEN
          WK6(IN) = RAD*ATAN2(WK5(IN),WK4(IN))
        ELSE
          WK6(IN) = ZERO
        END IF
   10 CONTINUE

      CALL DCOPY(NBPT,FCHO(3* (INOE-1)+1),3*NBOBST,WK1,1)
      CALL DCOPY(NBPT,VGLI(3* (INOE-1)+2),3*NBOBST,WK2,1)
      CALL DCOPY(NBPT,VGLI(3* (INOE-1)+3),3*NBOBST,WK3,1)
      DO 30 JDG = 1,NBPAIR
        PUS(JDG) = ZERO
        OCCURE(JDG) = 0
        DO 20 IBL = 1,NBLOC
          CALL PUSUR2(JDG,NBVAL,ANG,
     &                WK1((IBL-1)*NBVAL+IDEBUT),
     &                WK2((IBL-1)*NBVAL+IDEBUT),
     &                WK3((IBL-1)*NBVAL+IDEBUT),
     &                WK6((IBL-1)*NBVAL+IDEBUT),
     &                TEMPS((IBL-1)*NBVAL+IDEBUT),PUSEE,NOCCUR)
          PUS(JDG) = PUS(JDG) + PUSEE
          OCCURE(JDG) = OCCURE(JDG) + NOCCUR
   20   CONTINUE
        NTOT = NTOT + OCCURE(JDG)
   30 CONTINUE

      DO 40 JDG = 1,NBPAIR

        PUS(JDG) = PUS(JDG)/NBLOC
        PMOYE = PMOYE + PUS(JDG)*OCCURE(JDG)
   40 CONTINUE
      PMOYE = PMOYE/NTOT
      DO 70 JDG = 1,NBPAIR
        POUPRE(JDG) = OCCURE(JDG)*1.D0/NTOT
        POURPU(JDG) = (PUS(JDG)*OCCURE(JDG)*100.D0/ (NTOT*PMOYE))
        IF (ISUPP.EQ.1) THEN
          DO 50 K = 1,NBINST
            VUSTUB(JDG,K) = COEF(JDG)*TMPVU(K)*PUS(JDG)*POUPRE(JDG)
C         TMPV(K)=TMPVU(K)/365.25/24/3600
   50     CONTINUE
        ELSE IF (ISUPP.EQ.2) THEN
          DO 60 K = 1,NBINST
            VUSOB(JDG,K) = COEF(JDG)*TMPVU(K)*PUS(JDG)*POUPRE(JDG)
   60     CONTINUE
        END IF
   70 CONTINUE

      DO 80 J = 1,NBPAIR
        PUSURN = PUSURN + COEF(J)*OCCURE(J)*PUS(J)
   80 CONTINUE
      PUSURN = PUSURN/NTOT
      END
