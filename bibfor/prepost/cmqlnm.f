      SUBROUTINE CMQLNM(MAIN,NBTNO,NBMA,NUMMAQ,NUNOMI,NBNM)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF PREPOST  DATE 12/01/2009   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2005  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
      IMPLICIT NONE
      INTEGER NBMA,NBTNO,NUMMAQ(NBMA),NUNOMI(NBTNO)
      CHARACTER*8 MAIN
C ----------------------------------------------------------------------
C         TRANSFORMATION DES MAILLES QUADRATIQUES -> LINEAIRE
C-----------------------------------------------------------------------
C     -   ON RECUPERE LES NOEUDS MILIEUX
C     -   ON VERIFIE QUE LES NOEUDS MILIEUX COMMUNS A PLUSIEURS MAILLES
C         QUADRATIQUES APPARTIENNENT AUX MAILLES REFERENCEES.
C         SI DEUX MAILLES QUADRATIQUES SE PARTAGENT UN NOEUD MILIEU ET
C         QUE L'UTILISATEUR NE LINEARISE QU'UNE DES 2 MAILLES, ALORS
C         ON EMET UNE ALARME
C-----------------------------------------------------------------------
C IN        MAIN   K8  NOM DU MAILLAGE INITIAL
C IN        NBTNO   I  NOMBRE TOTAL DE NOEUDS
C IN        NBMA    I  NOMBRE DE MAILLES QUADRATIQUES TRAITEES
C IN        NUMMAQ  I  NUMEROS DES MAILLES QUADRATIQUES TRAITEES
C OUT       NUNOMI  K8 NUMEROS DES NOEUDS MILIEUX A RECUPERER
C OUT       NBNM    I  NOMBRE DE NOEUDS MILIEUX A RECUPERER
C-----------------------------------------------------------------------

C  ======      ====      =================   ========================
C  MAILLE      TYPE      NB NOEUDS MILIEUX   POSITION DU PREMIER NOEUD
C  ======      ====      =================   MILIEU DANS LA MAILLE
C                                            ========================

C  SEG3         4             1                    3
C  TRIA6        9             3                    4
C  QUAD8       14             4                    5
C  QUAD9       16             5                    5
C  TETRA10     19             6                    5
C  PENTA15     21             9                    7
C  PYRAM13     23             8                    6
C  HEXA20      25            12                    9
C  HEXA27      26            19                    9


C     ----- DEBUT COMMUNS NORMALISES  JEVEUX  --------------------------
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
      CHARACTER*32 JEXNUM,JEXNOM,JEXATR
C     -----  FIN  COMMUNS NORMALISES  JEVEUX  --------------------------
      INTEGER JNNMPM,JPPNM,NBTNMM,I,J,K,JNMPM,ICOUNT
      INTEGER NBNMTM(26),PPNM(26),NBTMA,NBNMA,JMACO,JCO
      INTEGER NBNPM,NBMPN,NBNM,JNM,JDIM,JTYP,IACNX1,ILCNX1
      INTEGER IACNX2,ILCNX2,II,JJ,NBM1
      CHARACTER*24 VALK(2)
      CHARACTER*8 NOM

C     NBNMTM: NOMBRE DE NOEUDS MILIEU PAR TYPE DE MAILLE
C     PPNM:   POSITION DU PREMIER NOEUD MILIEU PAR TYPE DE MAILLE

      DATA NBNMTM /3*0,1,4*0,3,4*0, 4,0, 5,2*0, 6,0, 9,0, 8,0,12,19/
      DATA PPNM   /3*0,3,4*0,4,4*0, 5,0, 5,2*0, 5,0, 7,0, 6,0, 9, 9/

      CALL JEMARQ()

      CALL ASSERT(NBMA.GE.0)
      IF (NBMA.EQ.0) THEN
C       -- IL N'Y A RIEN A FAIRE :
        CALL U2MESS('A','MODELISA3_8')
        NBNM=0
        GOTO 9999
      ENDIF

      CALL JEVEUO(MAIN//'.TYPMAIL','L',JTYP)
      CALL JEVEUO(MAIN//'.CONNEX','L',IACNX1)
      CALL JEVEUO(JEXATR(MAIN//'.CONNEX','LONCUM'),'L',ILCNX1)


C     0.1 CALCUL DE LA CONNECTIVITE INVERSE:
C     ---------------------------------------
      CALL CNCINV(MAIN,0,0,'V','&&CMQLNM.CONINV')
      CALL JEVEUO('&&CMQLNM.CONINV','L',IACNX2)
      CALL JEVEUO(JEXATR('&&CMQLNM.CONINV','LONCUM'),'L',ILCNX2)


C     1.  NOMBRE DE NOEUDS MILIEU PAR MAILLE: ZI(JNNMPM)
C         POSITION DU PREMIER NOEUD MILIEU PAR MAILLE: ZI(JPPNM)
C     -------------------------------------------------------------
      CALL WKVECT('&&CMQLNM.NBNOMIL','V V I',NBMA,JNNMPM)
      CALL WKVECT('&&CMQLNM.PREMNOMIL','V V I',NBMA,JPPNM)
C     NOMBRE TOTAL DE NOEUDS MILIEU PAR MAILLE : NBTNMM
      NBTNMM=0
      DO 10 I=1,NBMA
         ZI(JNNMPM+I-1)=NBNMTM(ZI(JTYP+NUMMAQ(I)-1))
         ZI(JPPNM+I-1)=PPNM(ZI(JTYP+NUMMAQ(I)-1))
         NBTNMM=NBTNMM+ZI(JNNMPM+I-1)
 10   CONTINUE


C     2.  ON RECUPERE LES NOEUDS MILIEUX PAR MAILLE:
C     -------------------------------------------------------------
      CALL WKVECT('&&CMQLNM.NOMILPARMA','V V I',NBTNMM,JNMPM)
      K=0
      DO 20 I=1,NBMA
         JCO=IACNX1+ ZI(ILCNX1-1+NUMMAQ(I))-1
         DO 30 J=1,ZI(JNNMPM+I-1)
            K=K+1
            ZI(JNMPM+K-1)=ZI(JCO+ZI(JPPNM+I-1)-1+J-1)
 30      CONTINUE
 20   CONTINUE


C     3. ON RECUPERE LES NOEUDS MILIEUX DES MAILLES REFERENCEES:
C     (ON OTE LES DOUBLONS DU TABLEAU PRECEDENT)
C     -------------------------------------------------------------
      ICOUNT=0
      DO 40 I=1,NBTNMM-1
         DO 50 J=I+1,NBTNMM
           IF(ZI(JNMPM+I-1).EQ.ZI(JNMPM+J-1).AND.ZI(JNMPM+I-1).NE.0)THEN
               ZI(JNMPM+J-1)=0
               ICOUNT=ICOUNT+1
            ENDIF
 50      CONTINUE
 40   CONTINUE
      NBNM=NBTNMM-ICOUNT
      CALL WKVECT('&&CMQLNM.NOMIL','V V I',NBNM,JNM)
      J=0
      DO 60 I=1,NBTNMM
         IF (ZI(JNMPM+I-1).NE.0)THEN
            J=J+1
            ZI(JNM+J-1)=ZI(JNMPM+I-1)
         ENDIF
 60   CONTINUE


C     4. ON FILTRE DAVANTAGE:
C        SI DEUX MAILLES QUADRATIQUES SE PARTAGENT UN NOEUD MILIEU ET
C        QUE L'UTILISATEUR NE LINEARISE QU'UNE DES 2 MAILLES, ALORS LE
C        NOEUD MILIEU EN COMMUN EST CONSERVE ET ON EMET UNE ALARME
C        POUR SIGNALER QUE LE MAILLAGE EST INCOMPATIBLE
C     -------------------------------------------------------------

C     NOMBRE DE MAILLES DU MAILLAGE
      CALL JEVEUO(MAIN//'.DIME','L',JDIM)
      NBTMA=ZI(JDIM+2)

      NBNMA=0
C     ON PARCOURT LES NOEUDS MILIEUX DES MAILLES REFERENCEES POUR
C     RECUPERER LES MAILLES DU MAILLAGE LES CONTENANT.
      CALL WKVECT('&&CMQLNM.MAIL_COMM','V V I',NBTMA,JMACO)
      DO 80 I=1,NBNM
         NBMPN=0
         II=ZI(JNM+I-1)

C        ON PARCOURT LES MAILLES (J) CONNECTEES A II :
         NBM1=ZI(ILCNX2+II)-ZI(ILCNX2-1+II)
         DO 90 JJ=1,NBM1
            J=ZI(IACNX2+ZI(ILCNX2-1+II)-1+JJ-1)
C           ON PARCOURT LES NOEUDS DE LA MAILLE J
            NBNPM=ZI(ILCNX1+J)-ZI(ILCNX1-1+J)
            JCO=IACNX1+ ZI(ILCNX1-1+J)-1
            DO 100 K=1,NBNPM
               IF(ZI(JNM+I-1).EQ.ZI(JCO+K-1))THEN
                  NBMPN=NBMPN+1
                  ZI(JMACO+NBMPN-1)=J
                  GOTO 90
               ENDIF
 100        CONTINUE
 90      CONTINUE

C        ON COMPARE LES MAILLES DU MAILLAGE AVEC LES MAILLES REFERENCEES
         ICOUNT=0
         DO 110 J=1,NBMPN
            DO 120 K=1,NBMA
               IF(ZI(JMACO+J-1).EQ.NUMMAQ(K))THEN
                  ICOUNT=ICOUNT+1
                  GOTO 110
               ENDIF
 120        CONTINUE
 110     CONTINUE

         IF(ICOUNT.NE.NBMPN)THEN
            CALL JENUNO(JEXNUM(MAIN//'.NOMNOE',ZI(JNM+I-1)),NOM)
            NBNMA=NBNMA+1
            VALK (1) = NOM

            CALL U2MESG('A', 'PREPOST5_12',2,VALK,0,0,0,0.D0)
            ZI(JNM+I-1)=0
         ENDIF

 80   CONTINUE
      CALL JEDETR('&&CMQLNM.MAIL_COMM')

C     NOEUDS MILIEUX A CONSIDERER
      K=0
      DO 130 I=1,NBNM
         IF(ZI(JNM+I-1).NE.0) THEN
           K=K+1
           NUNOMI(K)=ZI(JNM+I-1)
         ENDIF
 130  CONTINUE

C     NOMBRE DE NOEUDS MILIEUX
      NBNM=NBNM-NBNMA

      CALL JEDETR('&&CMQLNM.NBNOMIL')
      CALL JEDETR('&&CMQLNM.PREMNOMIL')
      CALL JEDETR('&&CMQLNM.NOMILPARMA')
      CALL JEDETR('&&CMQLNM.NOMIL')
      CALL JEDETR('&&CMQLNM.CONINV')

9999  CONTINUE
      CALL JEDEMA()

      END
