      SUBROUTINE IREMED(NOMCON,IFICHI,NOCHAM,NOVCMP,PARTIE,
     &                  LIORDR,LRESU, NBNOEC,LINOEC,NBMAEC,
     &                  LIMAEC,NOMCMP,LVARIE,CARAEL)
      IMPLICIT NONE
C
      INCLUDE 'jeveux.h'

      CHARACTER*8       CARAEL
      CHARACTER*(*)     NOMCON,NOVCMP,NOCHAM,LIORDR,NOMCMP,PARTIE
      INTEGER           IFICHI,NBNOEC,LINOEC(*),NBMAEC,LIMAEC(*)
      LOGICAL           LRESU,LVARIE
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF PREPOST  DATE 09/11/2012   AUTEUR DELMAS J.DELMAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE SELLENET N.SELLENET
C
C-----------------------------------------------------------------------
C     ECRITURE D'UN CONCEPT SUR FICHIER MED
C
C IN  NOMCON : K8  : NOM DU CONCEPT A IMPRIMER
C IN  IFICHI : IS  : UNITE LOGIQUE D'ECRITURE
C IN  NOCHAM : K*  : NOM DES CHAMPS A IMPRIMER ( EX 'DEPL', .... )
C IN  NOVCMP : K*  : NOM JEVEUX DE L'OBJET CONTENANT LES NOMS MED
C IN  PARTIE : K4  : IMPRESSION DE LA PARTIE COMPLEXE OU REELLE DU CHAMP
C IN  LIORDR : K*  : LISTE DES NUMEROS D'ORDRE A IMPRIMER
C IN  LRESU  : L   : INDIQUE SI NOMCON EST UN CHAMP OU UN RESULTAT
C IN  NBNOEC : I   : NOMBRE DE NOEUDS A IMPRIMER
C IN  LINOEC : I   : NUMEROS DES NOEUDS A IMPRIMER
C IN  NBMAEC : I   : NOMBRE DE MAILLES A IMPRIMER
C IN  LIMAEC : I   : NUMEROS DES MAILLES A IMPRIMER
C IN  NOMCMP : K*  : NOMS DES COMPOSANTES A IMPRIMER
C IN  CARAEL : K*  : NOM DU CARA_ELEM
C     ------------------------------------------------------------------
C TOLE CRS_1404
C
C     ------------------------------------------------------------------
      CHARACTER*1  K1BID
      CHARACTER*6  CHNUMO
      CHARACTER*8  TYPECH,NOMGD,SAUX08,NORESU,SDCARM,CAREL2,VALK2(2)
      CHARACTER*16 NOSY16
      CHARACTER*19 CHAM19,CESNSP,CESCOQ,CESFIB,CESORI,CESTUY
      CHARACTER*24 VALK(2),TYRES
      CHARACTER*64 NOMMED,NOCHMD
C
      INTEGER      NUMORD,ISY,IORDR,IRET,LXLGUT,IBID,CODRET,NBCHAM
      INTEGER      LNOCHM,I,CRESAV,NBCMDU,JNOSYM,IERD
      INTEGER      JNOCHA,JLIORD,NBORDR,NBRCMP,JNOCMP,JNUCMP
C
      LOGICAL      LFIRST
      PARAMETER   (CESNSP = '&&IREMED.CANBSP')
      PARAMETER   (CESCOQ = '&&IREMED.CARCOQUE')
      PARAMETER   (CESFIB = '&&IREMED.CAFIBR')
      PARAMETER   (CESORI = '&&IREMED.CARORIEN')
      PARAMETER   (CESTUY = '&&IREMED.CARGEOPO')
C     ------------------------------------------------------------------
      CALL JEMARQ()
C
      NORESU=NOMCON
      LFIRST=.TRUE.
C
      CALL JEVEUO(NOCHAM,'L',JNOCHA)
      CALL JELIRA(NOCHAM,'LONMAX',NBCHAM,K1BID)
C
      CALL JEVEUO(LIORDR,'L',JLIORD)
      CALL JELIRA(LIORDR,'LONMAX',NBORDR,K1BID)
C
      NBRCMP = 0
      JNOCMP = 0
      CALL JEEXIN(NOMCMP,IRET)
      IF ( IRET.NE.0 ) THEN
        CALL JEVEUO(NOMCMP,'L',JNOCMP)
        CALL JELIRA(NOMCMP,'LONMAX',NBRCMP,K1BID)
      ENDIF
C
      NBCMDU = 0
      JNOSYM = 0
      CALL JEEXIN(NOVCMP,IRET)
      IF (IRET.NE.0) THEN
        CALL JEVEUO(NOVCMP,'L',JNOSYM)
        IF (ZK80(JNOSYM).EQ.' ') THEN
          JNOSYM=0
        ELSE
          CALL JELIRA(NOVCMP,'LONMAX',NBCMDU,K1BID)
        ENDIF
      ENDIF
C
      SDCARM = ' '
      IF ( CARAEL.NE.' ' ) THEN
        IF ( LRESU ) THEN
          CALL DISMOI('C','CARA_ELEM',NOMCON,'RESULTAT',
     &                IBID,CAREL2,IERD)
          IF ( CAREL2.NE.CARAEL ) THEN
            VALK2(1) = CARAEL
            VALK2(2) = CAREL2
            CALL U2MESK('F','MED_64',1,VALK2)
          ENDIF
        ENDIF
        SDCARM = '&&IREMED'
        CALL CELCES(CARAEL//'.CANBSP','V',CESNSP)
        CALL EXISD('CARTE',CARAEL//'.CARORIEN',IRET)
        IF ( IRET.NE.0 ) THEN
          CALL CARCES(CARAEL//'.CARORIEN','ELEM',' ',
     &                'V',CESORI,'A',IRET)
        ENDIF
C
        CALL EXISD('CARTE',CARAEL//'.CARCOQUE',IRET)
        IF ( IRET.NE.0 ) THEN
          CALL CARCES(CARAEL//'.CARCOQUE','ELEM',' ',
     &                'V',CESCOQ,'A',IRET)
        ENDIF
C
        CALL EXISD('CARTE',CARAEL//'.CARGEOPO',IRET)
        IF ( IRET.NE.0 ) THEN
          CALL CARCES(CARAEL//'.CARGEOPO','ELEM',' ',
     &                'V',CESTUY,'A',IRET)
        ENDIF
C
        CALL EXISD('CHAM_ELEM',CARAEL//'.CAFIBR',IRET)
        IF ( IRET.NE.0 ) THEN
          CALL CELCES(CARAEL//'.CAFIBR','V',CESFIB)
        ENDIF
      ENDIF
C
C     --- BOUCLE SUR LE NOMBRE DE CHAMPS A IMPRIMER
C
      DO 20 ISY = 1,NBCHAM
        CRESAV=0
C
C       --- BOUCLE SUR LA LISTE DES NUMEROS D'ORDRE
C
        DO 21 IORDR = 1,NBORDR
C
C         --- SI VARIABLE DE TYPE RESULTAT = RESULTAT COMPOSE :
C             VERIFICATION CORRESPONDANCE ENTRE NUMERO D'ORDRE
C             UTILISATEUR ORDR(IORDR) ET NUMERO DE RANGEMENT IRET
C AU CAS OU ON NE PASSE PAS EN DESSOUS ON INITIALISE LORDR A FALSE
          IF ( LRESU ) THEN
            CALL RSUTRG(NOMCON,ZI(JLIORD+IORDR-1),IRET,IBID)
            IF(IRET.EQ.0) THEN
C             - MESSAGE NUMERO D'ORDRE NON LICITE
              IF ( LFIRST ) THEN
                CALL CODENT(ZI(JLIORD+IORDR-1),'G',CHNUMO)
                CALL U2MESK('A','PREPOST2_46',1,CHNUMO)
              ENDIF
              GOTO 22
            ENDIF
          ENDIF
C
          IF( LRESU ) THEN
C         * RESULTAT COMPOSE
C           - VERIFICHICATION EXISTENCE DANS LA SD RESULTAT NOMCON
C             DU CHAMP CHAM(ISY) POUR LE NO. D'ORDRE ORDR(IORDR)
C             ET RECUPERATION DANS CHAM19 DU NOM SE LE CHAM_GD EXISTE
            CALL RSEXCH(' ',NOMCON,ZK16(JNOCHA+ISY-1),
     &                  ZI(JLIORD+IORDR-1),CHAM19,IRET)
            IF(IRET.NE.0) GOTO 21
          ELSE
C         * CHAM_GD
            CHAM19 = NOMCON
          ENDIF
C
C         * IMPRESSION DU CHAMP (CHAM_NO OU CHAM_ELEM)
C             LE CHAMP EST UN CHAM_GD SIMPLE SI LRESU=.FALSE. OU
C             LE CHAMP EST LE CHAM_GD CHAM(ISY) DE NUMERO D'ORDRE
C             ORDR(IORDR) ISSU DE LA SD_RESULTAT NOMCON
          IF(NBCMDU.EQ.0)THEN
              NOMMED=' '
          ELSE
              NOMMED=ZK80(JNOSYM+ISY-1)
          ENDIF
          NOSY16=ZK16(JNOCHA+ISY-1)
          NUMORD=ZI(JLIORD+IORDR-1)
C
C       --- TYPE DU CHAMP A IMPRIMER (CHAM_NO OU CHAM_ELEM)
          CALL DISMOI('F','TYPE_CHAMP',CHAM19,'CHAMP',
     &                IBID,TYPECH,CODRET)
          CALL DISMOI('F','TYPE_RESU',NORESU,'RESULTAT',
     &                IBID,TYRES,CODRET)

          IF ((TYPECH(1:4).EQ.'NOEU') .OR. (TYPECH(1:2).EQ.'EL')) THEN
          ELSEIF ( TYPECH(1:4).EQ. 'CART' ) THEN
C            GOTO 9999
          ELSE
            VALK(1) = TYPECH
            VALK(2) = CHAM19
            IF( TYRES(1:9).EQ.'MODE_GENE'.OR.
     &          TYRES(1:9).EQ.'HARM_GENE') THEN
              CALL U2MESK('A+','PREPOST_87',2,VALK)
              CALL U2MESS('A' ,'PREPOST6_36')
            ELSE
              CALL U2MESK('A','PREPOST_87',2,VALK)
            ENDIF
          ENDIF
C
C         --- NOM DE LA GRANDEUR ASSOCIEE AU CHAMP CHAM19
          CALL DISMOI('F','NOM_GD',CHAM19,'CHAMP',IBID,NOMGD,CODRET)
          IF ( (TYPECH(1:4).EQ. 'CART'.AND.NOMGD(6:7).NE.'R').OR.
     &         NOMGD.EQ.'COMPOR' ) GOTO 9999
C
          IF ( NBRCMP .NE. 0 ) THEN
            IF ( (NOMGD.EQ.'VARI_R') .AND. (TYPECH(1:2).EQ.'EL') ) THEN
              CALL WKVECT('&&IREMED.NUMCMP','V V I',NBRCMP,JNUCMP)
C ----------- TRAITEMENT SUR LES "NOMCMP"
              CALL UTCMP3 ( NBRCMP, ZK8(JNOCMP), ZI(JNUCMP) )
              CALL JEDETR('&&IREMED.NUMCMP')
            ENDIF
          ENDIF

          SAUX08 = NORESU

          IF (NBCMDU.EQ.0) THEN
            CALL MDNOCH ( NOCHMD, LNOCHM,
     &                    LRESU, SAUX08, NOSY16, CODRET )
          ELSE
            DO 10 I=1,64
              NOCHMD(I:I) = ' '
 10         CONTINUE
            I = LXLGUT(NOMMED)
            NOCHMD(1:I) = NOMMED(1:I)
          ENDIF
C
C         -- ON LANCE L'IMPRESSION:
C         -------------------------
C
          IF ( .NOT.LRESU ) NORESU = ' '
          CALL IRCHME ( IFICHI, CHAM19, PARTIE, NOCHMD, NORESU,
     &                  NOSY16, TYPECH, NUMORD, NBRCMP, ZK8(JNOCMP),
     &                  NBNOEC, LINOEC, NBMAEC, LIMAEC, LVARIE,
     &                  SDCARM, CODRET )
C
 9999     CONTINUE
C
          IF ( CODRET.NE.0.AND.CODRET.NE.100 ) THEN
            VALK(1) = CHAM19
            VALK(2) = 'MED'
            CALL U2MESK('A','PREPOST_90', 2 ,VALK)
          ENDIF
          IF ( CODRET.EQ.100 ) CRESAV=100
C
   22     CONTINUE
C
   21   CONTINUE
        IF ( CRESAV.EQ.100 ) THEN
          VALK(1) = NOSY16
          CALL U2MESK('I','MED_30',1,VALK)
        ENDIF
        LFIRST=.FALSE.
C
   20 CONTINUE
C
      CALL DETRSD('CHAM_ELEM_S',CESNSP)
      CALL DETRSD('CHAM_ELEM_S',CESCOQ)
      CALL DETRSD('CHAM_ELEM_S',CESFIB)
      CALL DETRSD('CHAM_ELEM_S',CESORI)
      CALL DETRSD('CHAM_ELEM_S',CESTUY)
C
      CALL JEDEMA()
      END
