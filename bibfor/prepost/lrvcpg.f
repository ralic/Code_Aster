      SUBROUTINE LRVCPG(IDFIMD,NBPGM, NBPGA, NOMTM, TYGEOS,
     &                  ELREFA,ELREFM,FAPG,  NLOC,  PERMU,
     &                  NUTYMA,CODRET)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF PREPOST  DATE 18/12/2012   AUTEUR SELLENET N.SELLENET 
C RESPONSABLE SELLENET N.SELLENET
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C-----------------------------------------------------------------------
C     LECTURE FICHIER MED - VERIFICATION ET COMPARAISON DES PG ASTER/MED
C     -    -                -               -               --
C-----------------------------------------------------------------------
C
C     ROUTINE APPELEE PAR: LRMPGA
C
C     IN :
C       IDFIMD : IDENTIFIANT DU FICHIER MED
C       NBPGM  : NOMBRE DE PG MED
C       NBPGA  : NOMBRE DE PG ASTER
C       ELREFA : NOM DE L'ELEMENT DE REFERENCE ASTER
C       ELREFM : NOM DE L'ELEMENT DE REFERENCE MED
C       FAPG   : FAMILLE DE PG GLOBALE
C       NLOC   : NOMBRE DE LOCALISATIONS PRESENTES DANS LE FICHIER MED
C    IN/OUT:
C       PERMU  : TABLEAU (EVENTUEL) DES PERMUTATIONS DES PG
C                PERMU(I_PG_MED)=I_PG_ASTER
C    OUT :
C       NUTYMA : NUMERO DU TYPE DE MAILLE DE ELREFA
C       CODRET : CORRESPONDANCE DES PG ASTER/MED
C                 CODRET=0 -->  OK
C                 CODRET=1 -->  NECESSITE DES PERMUTATIONS
C                 CODRET=2 -->  NOOK (SOIT ABSENCE DE LOCALISATION, SOIT
C                                     AUCUNE CORRESPONDANCE POSSIBLE )
C
C-----------------------------------------------------------------------
C-----------------------------------------------------------------------

      IMPLICIT NONE

      INCLUDE 'jeveux.h'

      CHARACTER*32 JEXNOM
      INTEGER TYGEOS,NBPGM,NBPGA,NLOC,NUTYMA,IDFIMD
      INTEGER PERMU(NBPGM),CODRET
      CHARACTER*8 ELREFA,ELREFM,FAPG,NOMTM


C
C
      INTEGER VALI(2),JCOPGA,JWPGA,NDIM,NBFPG,ILOC,DIME
      INTEGER TYPGEO,IBID,IRET,NNOREF,NPGREF,JREFCO,JGSCOO,JWG,JCORRE
      INTEGER NCORRE,IGAU,IDIM,AD,IPGM,IPGA,ADA,IM,IFM,NIVINF
      CHARACTER*8 VALK(3)
      CHARACTER*64 LOCNAM,NOMASU
      INTEGER EDFUIN
      PARAMETER (EDFUIN=0)
      REAL*8 XPGM,YPGM,ZPGM,XPGA,YPGA,ZPGA,VALR(2)

      DATA VALK / 'X','Y','Z'/

      CALL JEMARQ()

      CALL INFNIV ( IFM, NIVINF)

C     DETERMINATION DES COORDONNES DES PG
C     DE L'ELEMENT DE REFERENCE ASTER
C     -------------------------------
      CALL WKVECT('&&LRVCPG_COORD_PG_ASTER','V V R',
     &             3*NBPGA,JCOPGA)
      CALL WKVECT('&&LRVCPG_POIDS_PG_ASTER','V V R',
     &             NBPGA,JWPGA)
      CALL ELRAGA(ELREFA,FAPG,DIME,NBFPG,ZR(JCOPGA),ZR(JWPGA))

C     NUMERO DU TYPE DE MAILLE DE L'ELEMENT DE REFERENCE : NUTYMA
      CALL JENONU(JEXNOM('&CATA.TM.NOMTM',NOMTM),NUTYMA)

C     VERIFICATION SUR LE NOMBRE DE POINTS DE GAUSS
C     ---------------------------------------------
C     ATTENTION: POUR LES CHAMPS A SOUS-POINTS,
C     LE NBRE DE PG D'UN ELEMENT DE REF MED
C     PREND EN COMPTE LE NOMBRE DE SOUS-POINTS
C     CECI PEUT ETRE LA CAUSE DE L'EMISSION
C     DU MESSAGE CI-DESSOUS
      IF(NBPGM.GT.NBPGA) THEN
         VALI(1)=NBPGM
         VALI(2)=NBPGA
         CALL U2MESI('A', 'MED_2', 2, VALI)
         CODRET = 4
         GOTO 9999
      ENDIF

C     DETERMINATION DU NOM DE LA LOCALISATION DES PG PRESENTE
C     DANS LE FICHIER MED ET CORRESPONDANT A L'ELEM DE REF ASTER.
C     ----------------------------------------------------------
C     -SI LA LOCALISATION EST ABSENTE : ON PREND EN COMPTE LA
C      LOCALISATION ASTER --> RISQUE DE RESULTATS FAUX
C     -SI LA LOCALISATION EST PRESENTE, ON COMPARE LES
C      COORDONNES DES PG ASTER/MED
      IF (NIVINF.GT.1) THEN
         WRITE(IFM,1001) NLOC
      ENDIF
C
      DO 130 ILOC=1,NLOC
         CALL MFGAUI(IDFIMD,ILOC,LOCNAM,TYPGEO,IBID,NDIM,NOMASU,IRET)
         IF(TYGEOS.EQ.TYPGEO)GOTO 140
 130  CONTINUE
C     SI ON EST ICI, CELA SIGNIFIE QU'AUCUNE LOCALISATION
C     N'A ETE IDENTIFIEE POUR L'ELEMENT DE REFERENCE EN COURS
      CALL U2MESK('A','MED_1',1,ELREFA)
      CODRET=2
      GOTO 9999
 140  CONTINUE

      IF (NIVINF.GT.1) THEN
         WRITE(IFM,1002) LOCNAM
      ENDIF
C
C     DETERMINATION DES COORDONNES DES PG
C     DE L'ELEMENT DE REFERENCE MED
C     -------------------------------
      NNOREF=(TYPGEO/100)*MOD(TYPGEO,100)
      NPGREF=(TYPGEO/100)*NBPGM
      CALL WKVECT('&&LRVCPG_COORD_NO_MED','V V R',NNOREF,JREFCO)
      CALL WKVECT('&&LRVCPG_COORD_PG_MED','V V R',NPGREF,JGSCOO)
      CALL WKVECT('&&LRVCPG_POIDS_PG_MED','V V R',NBPGM,JWG)
      CALL MFGAUL(IDFIMD,ZR(JREFCO),ZR(JGSCOO),ZR(JWG),EDFUIN,
     &            LOCNAM,IRET)
      CALL ASSERT(TYPGEO/100.EQ.DIME)

C     COMPARAISON DES COORD DES PG ENTRE ASTER ET MED
C     -----------------------------------------------
C     NOMBRE DE PG NON APPARENTES : NCORRE
C     TABLEAU DE TRAVAIL ZI(JCORRE) DIMENSIONNE AU NBRE DE PG QUI VAUT:
C        -LE NUMERO DU PG LOCAL SI LA CORRESPONDANCE N'A PAS EU LIEU
C        -0 SINON

      CALL WKVECT('&&LRVCPG_CORRESP_PG','V V I',NBPGM,JCORRE)
      NCORRE=0
      DO 100 IGAU=1,NBPGM
         ZI(JCORRE+IGAU-1)=0
         DO 110 IDIM=1,DIME
            AD=DIME*(IGAU-1)+IDIM
            IF (NIVINF.GT.1) THEN
               WRITE(IFM,1100) IGAU,IDIM,ZR(JGSCOO+AD-1),ZR(JCOPGA+AD-1)
            ENDIF
            IF(ABS(ZR(JGSCOO+AD-1)-ZR(JCOPGA+AD-1)).GT.1.D-3)THEN
               NCORRE=NCORRE+1
               ZI(JCORRE+IGAU-1)=IGAU
               GOTO 100
            ENDIF
 110     CONTINUE
 100  CONTINUE

C     SI LES PG ASTER/MED CORRESPONDENT : TOUT VA BIEN
      IF(NCORRE.EQ.0)THEN
         CODRET=0
         GOTO 9999
      ELSE
C        .. SINON, ON RECHERCHE UNE EVENTUELLE PERMUTATION:
C        PERMU = LE TABLEAU DE PERMUTATIONS DIMENSIONNE
C        AU NBRE DE PG: PERMU(NUM_PG_MED)=NUM_PG_ASTER
         DO 200 IPGM=1,NBPGM
            PERMU(IPGM)=0
            IF(ZI(JCORRE+IPGM-1).EQ.0)THEN
               PERMU(IPGM)=IPGM
            ELSE
               AD=DIME*(IPGM-1)
               XPGM=ZR(JGSCOO+AD+1-1)
               YPGM=0.D0
               ZPGM=0.D0
               IF(DIME.GE.2)YPGM=ZR(JGSCOO+AD+2-1)
               IF(DIME.GE.3)ZPGM=ZR(JGSCOO+AD+3-1)
               DO 210 IPGA=1,NBPGM
                  ADA=DIME*(IPGA-1)
                  XPGA=ZR(JCOPGA+ADA+1-1)
                  YPGA=0.D0
                  ZPGA=0.D0
                  IF(DIME.GE.2)YPGA=ZR(JCOPGA+ADA+2-1)
                  IF(DIME.GE.3)ZPGA=ZR(JCOPGA+ADA+3-1)
                  IF(ABS(XPGM-XPGA).LT.1.D-3 .AND.
     &               ABS(YPGM-YPGA).LT.1.D-3 .AND.
     &               ABS(ZPGM-ZPGA).LT.1.D-3)THEN
                     PERMU(IPGM)=IPGA
                     CODRET=1
                     GOTO 200
                  ENDIF
C                 SI ON EST ICI, CELA SIGNIFIE QUE L'UN DES PG MED
C                 N'A PAS PU ETRE IDENTIFIE A L'UN DES PG ASTER
C                 --> INCOMPATIBILITE DES PG, RISQUE DE RESULTATS FAUX
                  IF(IPGA.EQ.NBPGM)THEN
                     DO 190 IM=1,NBPGM
                        CALL U2MESI('A+','MED_4',1,IM)
                        DO 191 IDIM=1,DIME
                           VALR(1)=ZR(JGSCOO+DIME*(IM-1)+IDIM-1)
                           VALR(2)=ZR(JCOPGA+DIME*(IM-1)+IDIM-1)
                           CALL U2MESG('A+','MED_5',1,VALK(IDIM),
     &                                              0,IBID,2,VALR)
 191                    CONTINUE
 190                 CONTINUE
                     CALL U2MESS('A','MED_3')
                     CODRET=2
                     GOTO 9999
                  ENDIF
 210           CONTINUE
            ENDIF
 200     CONTINUE
      ENDIF

      IF(CODRET.EQ.1)THEN
C        AFFICHAGE DES COORD DES PG MED/ASTER POUR
C        METTRE EN EVIDENCE LES PERMUTATIONS
         DO 211 IM=1,NBPGM
            CALL U2MESI('A+','MED_4',1,IM)
            DO 212 IDIM=1,DIME
               VALR(1)=ZR(JGSCOO+DIME*(IM-1)+IDIM-1)
               VALR(2)=ZR(JCOPGA+DIME*(IM-1)+IDIM-1)
               CALL U2MESG('A+','MED_5',1,VALK(IDIM),
     &                                  0,IBID,2,VALR)
 212        CONTINUE
 211     CONTINUE
         CALL U2MESS('A','MED_6')
      ENDIF
C
9999  CONTINUE
C
      CALL JEDETR('&&LRVCPG_COORD_PG_ASTER')
      CALL JEDETR('&&LRVCPG_POIDS_PG_ASTER')
      CALL JEDETR('&&LRVCPG_COORD_NO_MED')
      CALL JEDETR('&&LRVCPG_COORD_PG_MED')
      CALL JEDETR('&&LRVCPG_POIDS_PG_MED')
      CALL JEDETR('&&LRVCPG_CORRESP_PG')

      CALL JEDEMA()
C
 1001 FORMAT('  NOMBRE DE LOCALISATIONS LUES :',I4)
 1002 FORMAT('  LOCALISATION MED UTILISEE    :', A32)
 1100 FORMAT('  PT GAUSS',I4,' DIM ',I1,' COORD MED',1PE12.5,
     &                                  ' COORD ASTER',1PE12.5)
      END
