      SUBROUTINE MDNOFA ( NUMFAM, NOGRF, NBGF, NBFAEX, NOFAEX, NOMFAM )
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF PREPOST  DATE 31/01/2006   AUTEUR GNICOLAS G.NICOLAS 
C RESPONSABLE GNICOLAS G.NICOLAS
C ======================================================================
C COPYRIGHT (C) 1991 - 2003  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C  ENTREES :
C     NUMFAM = NUMERO DE LA FAMILLE
C     NOGRF  = NOMS DES GROUPES D ENTITES DE LA FAMILLE
C     NBGF   = NOMBRE DE GROUPES DE LA FAMILLE
C     NBFAEX = NOMBRE DE FAMILLES EXISTANTES POUR CETTE SERIE
C     NOFAEX = NOMS DES FAMILLES DEJA CREEES
C  SORTIES :
C     NOMFAM = NOM DE LA FAMILLE
C     ------------------------------------------------------------------
C
      IMPLICIT NONE
C
C 0.1. ==> ARGUMENTS
C
      INTEGER NUMFAM
      INTEGER NBGF
      INTEGER NBFAEX
      CHARACTER*80 NOGRF(*)
      CHARACTER*32 NOMFAM
      CHARACTER*32 NOFAEX(*)
C
C 0.2. ==> COMMUNS
C
C 0.3. ==> VARIABLES LOCALES
C
      INTEGER IAUX, JAUX, KAUX
C
      CHARACTER*8 SAUX08
C
      LOGICAL ACORRI
C
      INTEGER LXLGUT
C
C====
C 1. ON INITIALISE AVEC DES '_'
C====
C
C               12345678901234567890123456789012
      NOMFAM = '________________________________'
C
C====
C 2. TANT QU'IL Y A MOINS DE 4 GROUPES DANS LA FAMILLE, LE NOM DE LA
C    FAMILLE EST LA CONCATENATION DE CES NOMS DE GROUPES.
C    ON REMPLACE TOUS LES EVENTUELS BLANCS PAR DES '_'
C    EXEMPLE :
C       GROUPE 1 : 'HAUT'
C       GROUPE 2 : 'FACE_X'
C       FAMILLE ==> 'HAUT____FACE_X__________________'
C                    12345678901234567890123456789012
C    IL FAUT APPORTER UNE CORRECTION SI LE NOM D'UN GROUPE SE
C    TERMINE AVEC UN '_'. ON DOIT AJOUTER UN DECALAGE SINON 2 FAMILLES
C    POURRAIENT PORTER LE MEME NOM.
C    EXEMPLE :
C       GROUPE 1 : 'HAUT'
C       GROUPE 2 : 'FACE_X__'
C       SI ON GARDE LE REGLE BRUTE :
C       FAMILLE ==> 'HAUT____FACE_X__________________'
C       CE QUI EST LE MEME NOM QUE CELUI DE L'EXEMPLE PRECEDENT.
C       ON MODIFIE LE DERNIER CARACTERE :
C       FAMILLE ==> 'HAUT____FACE_X_________________1'
C                    12345678901234567890123456789012
C    IDEM POUR :'_'
C       GROUPE 1 : 'HAUT__'
C       GROUPE 2 : 'FACE_X'
C
C    ON CONTROLE QUE LE NOM N'A PAS DEJA ETE ATTRIBUE
C====
C
      IF ( NBGF.LE.4 ) THEN
C
C 2.1. ==> NOM BRUT
C
        DO 21 , IAUX = 1 , NBGF
          JAUX = LXLGUT(NOGRF(IAUX)(1:8))
          NOMFAM(8*(IAUX-1)+1:8*(IAUX-1)+JAUX) = NOGRF(IAUX)(1:JAUX)
   21   CONTINUE
C
C 2.2. ==> DOIT-ON CORRIGER ?
C
        ACORRI = .FALSE.
        DO 22 , IAUX = 1 , NBGF
          JAUX = LXLGUT(NOGRF(IAUX)(1:8))
          IF ( NOGRF(IAUX)(JAUX:JAUX).EQ.'_' ) THEN
            ACORRI = .TRUE.
          ENDIF
   22   CONTINUE
C
C 2.3. ==> LA CORRECTION EVENTUELLE
C
        IF ( ACORRI ) THEN
C
          KAUX = 0
   23     CONTINUE
C
          KAUX = KAUX + 1
          CALL CODENT ( KAUX,'G',SAUX08 )
          JAUX = LXLGUT(SAUX08)
          NOMFAM(33-JAUX:32) = SAUX08(1:JAUX)
          DO 231 , IAUX = 1 , NBFAEX
            IF ( NOFAEX(IAUX).EQ.NOMFAM ) THEN
              GOTO 23
            ENDIF
  231     CONTINUE
C
        ENDIF
C
C====
C 3. SINON, C'EST UN NOM ARBITRAIRE, CONSTRUIT AVEC LE NUMERO DE
C    LA FAMILLE
C====
C
      ELSE
C
C                      12345678
        NOMFAM(1:8) = 'FAMILLE_'
C
        CALL CODENT ( NUMFAM,'G',SAUX08 )
C
        JAUX = LXLGUT(SAUX08)
        NOMFAM(9:8+JAUX) = SAUX08(1:JAUX)
C
      ENDIF
C
C====
C 4. MEMORISATION DU NOM DANS LE TABLEAU
C====
C
      NOFAEX(NBFAEX+1) = NOMFAM
C
      END
