      SUBROUTINE ASCELI ( MAILLA )
      IMPLICIT   NONE
      INCLUDE 'jeveux.h'
      CHARACTER*8         MAILLA
C-----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF PREPOST  DATE 13/06/2012   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C     OPERATEUR: "MODI_MAILLAGE" , MOTCLE FACTEUR "PLAQ_TUBE"
C     ELIMINE LES NOEUDS EN DOUBLE:
C             SURFACE BORD1 AVEC BORD2
C
C
C-----------------------------------------------------------------------
C-----------------------------------------------------------------------
C
      INTEGER  INUMA, NBPT, JPOIN, INO, INOV, NBMAT, IFM, IUNIFI, JPOIN2
      INTEGER   NBNO, JGRN1, JGRN2, JVI1, JVI2, IRET, IGR, K, JIND2
      INTEGER   IDIME, NBNOT, NBGRNO, JCOOR, I, JIND, NBNO2, JCOR, KK
      CHARACTER*8   K8B,  NOGRN1, NOGRN2, NOMGRN, TMP, NOMNO
      CHARACTER*24  GRPNOE, CONNEX, LISO1, LISO2
C     ------------------------------------------------------------------
C
      CALL JEMARQ ( )

      IFM   = IUNIFI('MESSAGE')

      GRPNOE = MAILLA//'.GROUPENO'
      CONNEX = MAILLA//'.CONNEX'

      NOGRN1 = 'BORD1   '
      NOGRN2 = 'BORD2   '
      CALL JEEXIN ( JEXNOM(GRPNOE,NOGRN1), IRET )
      IF ( IRET .EQ. 0 ) THEN
         CALL U2MESK('F','PREPOST_1',1,NOGRN1)
      ENDIF
      CALL JEEXIN ( JEXNOM(GRPNOE,NOGRN2), IRET )
      IF ( IRET .EQ. 0 ) THEN
         CALL U2MESK('F','PREPOST_1',1,NOGRN2)
      ENDIF
      CALL JELIRA ( JEXNOM(GRPNOE,NOGRN1), 'LONUTI', NBNO, K8B )
      CALL JEVEUO ( JEXNOM(GRPNOE,NOGRN1), 'L', JGRN1 )
      CALL JEVEUO ( JEXNOM(GRPNOE,NOGRN2), 'L', JGRN2 )
      LISO1 = '&&ASCELI.NOEUD_1'
      LISO2 = '&&ASCELI.NOEUD_2'
      CALL PACOA1 ( ZI(JGRN1), ZI(JGRN2), NBNO, MAILLA, LISO1, LISO2 )
      CALL JEVEUO ( LISO1, 'L', JVI1 )
      CALL JEVEUO ( LISO2, 'L', JVI2 )
      CALL JEVEUO (MAILLA//'.DIME','E',IDIME)
      NBNOT = ZI(IDIME)
      NBMAT = ZI(IDIME+2)

      WRITE(IFM,*) 'COUTURE - NOMBRE DE NOEUDS ELIMINES: ',NBNO
      WRITE(IFM,*) 'COUTURE - NOMBRE DE NOEUDS TOTAL   : ',NBNOT-NBNO

C     OBJET .GROUPENO : ON CHANGE LES NUMEROS DE NOEUDS DES GROUPENO
C     --------------------------------------------------------------
      CALL JELIRA ( GRPNOE, 'NUTIOC', NBGRNO, K8B )
      DO 70 IGR = 1,NBGRNO
         CALL JENUNO ( JEXNUM(GRPNOE,IGR),NOMGRN )
         CALL JELIRA ( JEXNOM(GRPNOE,NOMGRN), 'LONUTI', NBPT, K8B )
         CALL JEVEUO ( JEXNOM(GRPNOE,NOMGRN), 'E', JPOIN )
         DO 80 INO = 1 , NBPT
           DO 90 INOV = 1 , NBNO
              IF ( ZI(JVI2+INOV-1) .EQ. ZI(JPOIN+INO-1) ) THEN
                   ZI(JPOIN+INO-1) = ZI(JVI1+INOV-1)
                   GOTO 80
              END IF
 90        CONTINUE
 80      CONTINUE
 70   CONTINUE

C     -- ON SUPPRIME LES EVENTUELS DOUBLONS CREES DANS .GROUPENO :
      CALL GRPDBL(MAILLA,'GROUPENO')


C     OBJET .CONNEX : ON CHANGE LES CONNECTIVITES
C     --------------------------------------------------------
      DO 200 INUMA = 1, NBMAT
         CALL JELIRA ( JEXNUM(CONNEX,INUMA), 'LONMAX', NBPT, K8B )
         CALL JEVEUO ( JEXNUM(CONNEX,INUMA), 'E', JPOIN )
         DO 202 INO = 1 , NBPT
            DO 204 INOV = 1 , NBNO
               IF ( ZI(JVI2+INOV-1) .EQ. ZI(JPOIN+INO-1) ) THEN
                  ZI(JPOIN+INO-1) = ZI(JVI1+INOV-1)
                  GOTO 202
               ENDIF
 204        CONTINUE
 202     CONTINUE
 200  CONTINUE

C     OBJETS .NOMNOE, .COORDO.VALE : ON ACTUALISE CES OBJETS EN
C     SUPPRIMANT LES NOEUDS DOUBLES SUITE AU RECOLLEMENT DES BORDS
C     ------------------------------------------------------------
      TMP='TMP'
      NBNO2=NBNOT-NBNO
      ZI(IDIME)=NBNO2
C
      CALL JEDUP1(MAILLA//'.NOMNOE','V',TMP//'.NOMNOE')
      CALL JEDUP1(MAILLA//'.CONNEX','V',TMP//'.CONNEX')
      CALL JEDUP1(MAILLA//'.GROUPENO','V',TMP//'.GROUPENO')
      CALL COPICH('V',MAILLA//'.COORDO',TMP//'.COORDO')
C
      CALL JEDETR(MAILLA//'.NOMNOE')
      CALL JECREO(MAILLA//'.NOMNOE','G N K8')
      CALL JEECRA(MAILLA//'.NOMNOE','NOMMAX',NBNO2,K8B)
      CALL JEDETR(MAILLA//'.COORDO    .VALE')
      CALL JECREO(MAILLA//'.COORDO    .VALE','G V R')
      CALL JEECRA(MAILLA//'.COORDO    .VALE','LONMAX',3*NBNO2,' ')
      CALL JEVEUO(MAILLA//'.COORDO    .VALE','E',JCOOR)
      CALL JEVEUO(   TMP//'.COORDO    .VALE','L',JCOR)

C     TABLEAUX DE TRAVAIL:
C     - ZI(JIND) INDIQUE LES NOEUDS A SUPPRIMER PAR UNE VALEUR 1
C     - ZI(JIND2) PERMET DE REACTUALISER LES NUMEROS DES NOEUDS:
C       ZI(JIND2+I-1)=J SIGNIFIE QUE LE NOEUD I DE LA NOUVELLE
C       SD MAILLAGE PORTERA LE NUMERO I-J
      CALL WKVECT('&&ASCELI_IND_NOEU_DEL','V V I',NBNOT,JIND)
      CALL WKVECT('&&ASCELI_IND_NOEU_CONN','V V I',NBNOT,JIND2)
      DO 206 I=1,NBNOT
        ZI(JIND+I-1)=0
        ZI(JIND2+I-1)=0
 206  CONTINUE
      DO 207 I=1,NBNO
        ZI(JIND+ZI(JVI2+I-1)-1)=1
 207  CONTINUE
      K=0
      KK=0
      DO 205 I=1,NBNOT
        IF(ZI(JIND+I-1).EQ.0)THEN
          K=K+1
          CALL JENUNO(JEXNUM(TMP//'.NOMNOE',I),NOMNO)
          CALL JECROC(JEXNOM(MAILLA//'.NOMNOE',NOMNO))
          ZR(JCOOR+3*(K-1))  =ZR(JCOR+3*(I-1))
          ZR(JCOOR+3*(K-1)+1)=ZR(JCOR+3*(I-1)+1)
          ZR(JCOOR+3*(K-1)+2)=ZR(JCOR+3*(I-1)+2)
          ZI(JIND2+I-1)=KK
        ELSE
          KK=KK+1
        ENDIF
 205  CONTINUE
      CALL ASSERT(K.EQ.NBNO2)


C     ON ACTUALISE LES OBJETS .GROUPENO ET .CONNEX
C     CAR LES NOMS DES NOEUDS ONT CHANGE
C     ----------------------------------
      DO 210 INUMA = 1, NBMAT
         CALL JELIRA(JEXNUM(TMP//'.CONNEX',INUMA),'LONMAX',NBPT,K8B)
         CALL JEVEUO(JEXNUM(CONNEX,INUMA),'E',JPOIN )
         CALL JEVEUO(JEXNUM(TMP//'.CONNEX',INUMA),'L',JPOIN2 )
         DO 212 INO = 1 , NBPT
          ZI(JPOIN+INO-1)= ZI(JPOIN2+INO-1)-ZI(JIND2+ZI(JPOIN2+INO-1)-1)
 212     CONTINUE
 210  CONTINUE

      DO 270 IGR = 1,NBGRNO
         CALL JENUNO ( JEXNUM(GRPNOE,IGR),NOMGRN )
         CALL JELIRA ( JEXNOM(GRPNOE,NOMGRN), 'LONUTI', NBPT, K8B )
         CALL JEVEUO ( JEXNOM(GRPNOE,NOMGRN), 'E', JPOIN )
         CALL JEVEUO ( JEXNOM(TMP//'.GROUPENO',NOMGRN), 'L', JPOIN2 )
         DO 280 INO = 1 , NBPT
          ZI(JPOIN+INO-1)= ZI(JPOIN2+INO-1)-ZI(JIND2+ZI(JPOIN2+INO-1)-1)
 280      CONTINUE
 270   CONTINUE


      CALL DETRS2('CHAM_NO',TMP//'.COORDO')
      CALL JEDETR(TMP//'.NOMNOE')
      CALL JEDETR(TMP//'.CONNEX')
      CALL JEDETR(TMP//'.GROUPENO')
      CALL JEDETR('&&ASCELI_IND_NOEU_DEL')
      CALL JEDETR('&&ASCELI.NOEUD_1')
      CALL JEDETR('&&ASCELI.NOEUD_2')

      CALL JEDEMA ( )
      END
