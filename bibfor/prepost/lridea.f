      SUBROUTINE LRIDEA(RESU,TYPRES,LINOCH,NBNOCH,NOMCMD,LISTRZ,LISTIZ,
     &                  PRECIS,CRIT,EPSI,ACCES,MFICH,NOMA,LIGREZ,NBVARI)
      IMPLICIT  NONE
      CHARACTER*(*) TYPRES,LINOCH(1),NOMCMD
      CHARACTER*8   RESU
      CHARACTER*(*) LISTRZ,LISTIZ,CRIT,ACCES
      CHARACTER*(*) LIGREZ
      REAL*8 EPSI
      INTEGER PRECIS,NBNOCH,MFICH,NBVARI

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF PREPOST  DATE 06/04/2004   AUTEUR DURAND C.DURAND 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================

C TOLE CRS_512 CRP_20
C----------------------------------------------------------------------
C  LECTURE DES RESULTATS PRESENTS DANS LE FICHIER UNIVERSEL ET STOCKAGE
C  DANS LA SD RESULTAT

C IN  : RESU   : K8    : NOM DE LA SD_RESULTAT
C IN  : TYPRES : K16   : TYPE DE RESULTAT ('EVOL_ELAS','DYNA_TRANS')
C IN  : LINOCH : L_K16 : LISTE DES NOMS DE CHAMP ('DEPL',SIEF_ELNO')
C IN  : NBNOCH : I     : NOMBRE DE CHAMPS A LIRE
C IN  : NOMCMD : K16   : NOM DE LA COMMANDE
C IN  : LISTRZ : K19   : NOM DE L'OBJET CONTENANT LA LISTE DES INSTANTS
C                        OU DES FREQUENCES A LIRE
C IN  : LISTIZ : K19   : NOM DE L'OBJET CONTENANT LA LISTE DES
C                        NUMEROS D'ORDRE A LIRE
C IN  : PRECIS : I     : INDICATEUR DE VERIFICATION DE LA PRECISION
C IN  : CRIT   : K8    : PRECISION : CRITERE RELATIF OU ABSOLU
C IN  : EPSI   : R     : PRECISION DEMANDEE
C IN  : ACCES  : K10   : TYPE D'ACCES ('TOUT_ORDRE','NUME_ORDRE','INST'
C                                      'LIST_INST',...)
C IN  : MFICH  : I     : NUMERO UNITE LOGIQUE DU FICHIER UNIVERSEL
C IN  : NOMA   : K8    : NOM DU MAILLAGE
C IN  : LIGREZ : K19   : NOM DU LIGREL
C IN  : NBVARI : I     : NOMBRE DE VARIABLES INTERNES A LIRE POUR LE
C                        CHAMP DE VARIABLES INTERNES (VARI_R)
C     -----------------------------------------------------------------
C     ----- DEBUT COMMUNS NORMALISES  JEVEUX --------------------------
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR, ZERO
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24,NOOJB
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
      CHARACTER*32 JEXNUM,JEXNOM,JEXR8,JEXATR
C     -----  FIN  COMMUNS NORMALISES  JEVEUX --------------------------
      REAL*8 RBID,VAL(1000),IOUF,MASGEN
      INTEGER LFINOM,LFINUM,LFIPAR,LFILOC,LFINBC,LFICMP,LAMRE
      INTEGER JCNSD,JCNSV,JCNSL,JCESD,JCESV,JCESL,JREFE
      INTEGER NBREC,NUMDAT,NUMCH,JPERM,IAST,ISUP,JTYPM,ITYPE
      INTEGER INOIDE,INOAST,IELAST,IELIDE,KNOIDE,KNOAST
      INTEGER NBCMP,NBCMID,ICH,ICMP,NBCMP1,MAXNOD,LON1
      INTEGER IREC,ICOMPT,VALATT,IFIELD,IORD,IBID,ILU1,NBTITR
      INTEGER I,J,IEXP,NBNOE,NBFIEL,IER,NBNOEU,NBELEM,JTITR
      INTEGER IRET,IDECAL,ICOEF,ICMP1,ICMP2,INATUR,KK,NUMODE
      LOGICAL TROUVE,ASTOCK,CHAMOK,ZCMPLX,LMASU
      CHARACTER*4 TYCHAS,TYCHID,ACCE2,TYPDAT
      CHARACTER*6 KAR
      CHARACTER*8 NOMGD,LICMP(1000),K8BID,NOMNO,NOMMA,NOMA,MATRA,MATRB
      CHARACTER*14 NUDDL
      CHARACTER*16 NOMCH,NOIDEA,CONCEP,NOMC2
      CHARACTER*19 CHS,LISTR8,LISTIS,LIGREL,PRCHNO,PRCHN2,PRCHN3
      CHARACTER*80 REC(20),TITMAI

      PARAMETER (NBFIEL=40)
C ---------------------------------------------------------------------


      CALL JEMARQ()

      ZERO = 0.D0
      NOMC2=' '

      LISTR8 = LISTRZ
      LISTIS = LISTIZ
      LIGREL = LIGREZ
      ZCMPLX = .FALSE.

      ACCE2 = 'INST'
      CALL RSEXPA(RESU,0,'FREQ',IRET)
      IF (IRET.GT.0)  ACCE2 = 'FREQ'


      CALL DISMOI('F','NB_MA_MAILLA',NOMA,'MAILLAGE',NBELEM,K8BID,IER)
      CALL DISMOI('F','NB_NO_MAILLA',NOMA,'MAILLAGE',NBNOEU,K8BID,IER)


C- ON REGARDE SI LE MAILLAGE PROVIENT D'IDEAS :
      LMASU = .FALSE.
      CALL JEEXIN(NOMA//'           .TITR',IRET)
      IF(IRET.NE.0) THEN
        CALL JEVEUO(NOMA//'           .TITR','L',JTITR)
        CALL JELIRA(NOMA//'           .TITR','LONMAX',NBTITR,K8BID)
        IF(NBTITR.GE.1) THEN
          TITMAI=ZK80(JTITR-1+1)
          IF(TITMAI(10:31).EQ.'AUTEUR=INTERFACE_IDEAS') LMASU=.TRUE.
        ENDIF
      ELSE
        CALL UTMESS('A','LRIDEA','POUR LIRE_RESU / FORMAT=IDEAS'
     &  //' LE MAILLAGE DOIT NORMALEMENT AVOIR ETE LU AU FORMAT IDEAS.')
      ENDIF
      CALL JEVEUO(NOMA//'.TYPMAIL','L',JTYPM)


C- TABLEAU DE PERMUTATION POUR LES CONNECTIVITES DES MAILLES :
      CALL IRADHS()
      CALL JEVEUO('&&IRADHS.PERMUTA','L',JPERM)
      CALL JELIRA('&&IRADHS.PERMUTA','LONMAX',LON1,K8BID)
      MAXNOD=ZI(JPERM-1+LON1)



C- CREATION DE LA STRUCTURE DE DONNEES FORMAT_IDEAS ---

      NOIDEA = '&&LIRE_RESU_IDEA'
      CALL CRSDFI(LINOCH,NBNOCH,NOIDEA)

C- OUVERTURE EN LECTURE DES OBJETS COMPOSANTS LA SD FORMAT_IDEAS

      CALL JEVEUO(NOIDEA//'.FID_NOM','L',LFINOM)
      CALL JEVEUO(NOIDEA//'.FID_NUM','L',LFINUM)
      CALL JEVEUO(NOIDEA//'.FID_PAR','L',LFIPAR)
      CALL JEVEUO(NOIDEA//'.FID_LOC','L',LFILOC)
      CALL JEVEUO(NOIDEA//'.FID_CMP','L',LFICMP)
      CALL JEVEUO(NOIDEA//'.FID_NBC','L',LFINBC)


C CREATION DE L'OBJET .REFE DANS LES MODE_MECA
C S'IL N'Y PAS DE PROFIL DE STOCKAGE PREDEFINI IL FAUT EN CREER UN
C C'EST FAIT DANS CNSCNO EN LUI INDIQUANT UN NOM DE PROFIL MIS A BLANC
C SINON ON RECHERCHE LE PROFIL DE LA MATRICE DE RIGIDITE (MATRB)
      PRCHNO = ' '
      IF ((TYPRES.EQ.'MODE_MECA').OR.(TYPRES.EQ.'MODE_MECA_C')) THEN
        CALL GETVID(' ','MATR_A',IBID,1,1,MATRA,IREC)
        IF (IREC.NE.1) THEN
          MATRA = ' '
        ENDIF
        CALL GETVID(' ','MATR_B',IBID,1,1,MATRB,IREC)
        IF (IREC.EQ.1) THEN
          CALL DISMOI('F','NOM_NUME_DDL',MATRB,'MATR_ASSE',
     &                IBID,NUDDL,IREC)
          CALL DISMOI('F','PROF_CHNO',NUDDL,'NUME_DDL',
     &                IBID,PRCHNO,IREC)
        ELSE
          MATRB = ' '
        ENDIF
        CALL WKVECT(RESU//'           .REFE','G V K24',3,JREFE)
        ZK24(JREFE-1+1) = MATRB
        ZK24(JREFE-1+2) = ' '
        ZK24(JREFE-1+3) = MATRA
      ENDIF


      REWIND MFICH

C- LECTURE DU NUMERO DU DATASET

   10 CONTINUE
      READ (MFICH,'(A6)',END=170,ERR=160) KAR

C- ON NE LIT QUE LES DATASETS 55, 57 ET 2414

      IF (KAR.EQ.'    55') THEN
        NBREC = 8
        NUMDAT = 55
      ELSE IF (KAR.EQ.'    57') THEN
        NBREC = 8
        NUMDAT = 57
      ELSE IF (KAR.EQ.'  2414') THEN
        NBREC = 13
        NUMDAT = 2414
      ELSE
        GO TO 10
      END IF

C-LECTURE DE L'ENTETE DU DATASET

      DO 20 IREC = 1,NBREC
        READ (MFICH,'(A80)',END=160) REC(IREC)
   20 CONTINUE

C-TRAITEMENT DE L'ENTETE : ON RECHERCHE SI LE CONTENU EST
C CONFORME A CELUI PRESENT DANS LA SD FORMAT_IDEAS

      DO 30 ICH = 1,NBNOCH
        IF (ZI(LFINUM-1+ICH).EQ.NUMDAT) GO TO 40
   30 CONTINUE
      GO TO 10

   40 CONTINUE
      ICOMPT = 0
      CHAMOK = .FALSE.
      DO 70 ICH = 1,NBNOCH
        DO 60 IREC = 1,NBREC
          DO 50 IFIELD = 1,NBFIEL
            VALATT = ZI(LFIPAR-1+ (ICH-1)*800+ (IREC-1)*40+IFIELD)
            IF (VALATT.NE.9999) THEN
              CALL DECOD1(REC,IREC,IFIELD,VALATT,TROUVE)
              IF (.NOT.TROUVE) GO TO 70
            END IF
   50     CONTINUE
   60   CONTINUE
        CHAMOK = .TRUE.
        NOMCH = LINOCH(ICH)
        ICOMPT = ICOMPT + 1
        NUMCH = ICH
        IF (ICOMPT.NE.1) THEN
          CALL UTMESS('F','LRIDEA','ON A TROUVE PLUSIEURS CHAMPS '//
     &                'POUR LE MEME DATASET')
        END IF
   70 CONTINUE
      IF (.NOT.CHAMOK) GO TO 10

C- TRAITEMENT DU NUMERO D'ORDRE, DE L'INSTANT OU DE LA FREQUENCE

      IREC = ZI(LFILOC-1+ (NUMCH-1)*10+1)
      IFIELD = ZI(LFILOC-1+ (NUMCH-1)*10+2)
      CALL DECOD2(REC,IREC,IFIELD,0,IORD,RBID,TROUVE)
      IF (.NOT.TROUVE) THEN
        CALL UTMESS('F','LRIDEA','ON N''A PAS TROUVE LE NUMERO '//
     &              'D''ORDRE A L''ADRESSE INDIQUEE')
      END IF

      IF (ACCES.EQ.'INST' .OR. ACCES.EQ.'LIST_INST'
     &    .OR. ACCE2.EQ.'INST') THEN
        IREC = ZI(LFILOC-1+ (NUMCH-1)*10+3)
        IFIELD = ZI(LFILOC-1+ (NUMCH-1)*10+4)
        CALL DECOD2(REC,IREC,IFIELD,1,IBID,IOUF,TROUVE)
        IF (.NOT.TROUVE) THEN
          CALL UTMESS('F','LRIDEA','ON N''A PAS TROUVE L''INSTANT '//
     &                'A L''ADRESSE INDIQUEE')
        END IF
      END IF

      IF (ACCES.EQ.'FREQ' .OR. ACCES.EQ.'LIST_FREQ'
     &    .OR. ACCE2.EQ.'FREQ') THEN
        IREC = ZI(LFILOC-1+ (NUMCH-1)*10+5)
        IFIELD = ZI(LFILOC-1+ (NUMCH-1)*10+6)
        CALL DECOD2(REC,IREC,IFIELD,1,IBID,IOUF,TROUVE)
        IF (.NOT.TROUVE) THEN
          CALL UTMESS('F','LRIDEA','ON N''A PAS TROUVE LA FREQUENCE '//
     &                'A L''ADRESSE INDIQUEE')
        END IF
      END IF


C---  ON RECUPERE NUME_MODE ET MASS_GENE S'ILS SONT PRESENTS:
      NUMODE=0
      MASGEN=0.D0
C---  NUME_MODE :
      IREC = ZI(LFILOC-1+ (NUMCH-1)*10+7)
      IFIELD = ZI(LFILOC-1+ (NUMCH-1)*10+8)
      CALL DECOD2(REC,IREC,IFIELD,0,NUMODE,RBID,TROUVE)
C---  MASS_GENE :
      IREC = ZI(LFILOC-1+ (NUMCH-1)*10+9)
      IFIELD = ZI(LFILOC-1+ (NUMCH-1)*10+10)
      CALL DECOD2(REC,IREC,IFIELD,1,IBID,MASGEN,TROUVE)



C---  ON VERIFIE SI LE NUMERO D'ORDRE OU L'INSTANT OU LA FREQUENCE LU
C     CORRESPOND A CELUI OU CELLE RECHERCHEE.

      CALL NUMEOK(ACCES,IORD,IOUF,LISTR8,LISTIS,PRECIS,CRIT,EPSI,ASTOCK)

C- ON RECHERCHE LE TYPE DE CHAMP

      IF (NUMDAT.EQ.55) THEN
        TYCHID = 'NOEU'
      ELSE IF (NUMDAT.EQ.57) THEN
        TYCHID = 'ELNO'
      ELSE IF (NUMDAT.EQ.2414) THEN
        IREC = 3
        IFIELD = 1
        CALL DECOD2(REC,IREC,IFIELD,0,ILU1,RBID,TROUVE)
        IF (.NOT.TROUVE) THEN
          CALL UTMESS('F','LRIDEA','ON N''A PAS TROUVE DANS LE FICHIER'
     &                //' UNV LE TYPE DE CHAMP')
        END IF
        IF (ILU1.EQ.1) THEN
          TYCHID = 'NOEU'
        ELSE IF (ILU1.EQ.2) THEN
          TYCHID = 'ELGA'
        ELSE IF (ILU1.EQ.3) THEN
          TYCHID = 'ELNO'
        END IF
      END IF

C- RECHERCHE DU NOMBRE DE COMPOSANTES CONTENUES DANS LE DATASET

      IF (NUMDAT.EQ.55 .OR. NUMDAT.EQ.57) THEN
        IREC = 6
        IFIELD = 6
      ELSE IF (NUMDAT.EQ.2414) THEN
        IREC = 9
        IFIELD = 6
      END IF
      CALL DECOD2(REC,IREC,IFIELD,0,NBCMID,RBID,TROUVE)
      IF (.NOT.TROUVE) THEN
        CALL UTMESS('F','LRIDEA','ON N''PAS TROUVE DANS LE FICHIER'//
     &              ' UNV LE NOMBRE DE COMPOSANTES A LIRE')
      END IF

C- ON RECHERCHE DANS LE FICHIER UNV SI LA NATURE DU CHAMP
C  DE DEPLACEMENT
C  'DEPL_R' -> REEL     --> INATUR = 2,4 -> REEL
C  'DEPL_C' -> COMPLEXE --> INATUR = 5,6-> COMPLEXE

      IFIELD = 5
      CALL DECOD2(REC,IREC,IFIELD,0,INATUR,RBID,TROUVE)
      IF (.NOT.TROUVE) THEN
        CALL UTMESS('F','LRIDEA','ON N''PAS TROUVE DANS LE FICHIER'//
     &              ' UNV LA NATURE DU CHAMP (REEL OU COMPLEXE)')
      END IF
      IF (INATUR.EQ.5 .OR. INATUR.EQ.6) ZCMPLX = .TRUE.

C- ON RECHERCHE LE TYPE DE CHAMP DEMANDE PAR L'UTILISATEUR
C  ET LA GRANDEUR ASSOCIEE

      CALL RSUTC2(TYPRES,NOMCH,NOMGD,TYCHAS)

C- VERIFICATION DE LA COMPATIBILITE DU CHAMP DEMANDE
C  AVEC LE CHAMP IDEAS
      IF (TYCHID.NE.TYCHAS) THEN
        CALL UTMESS('F','LRIDEA',
     &  'LE TYPE DE CHAMP DEMANDE EST DIFFERENT DU TYPE DE CHAMP A LIRE'
     &              )
      END IF

C- VERIFICATION SI LE CHAMP IDEAS ET ASTER SONT DE MEME NATURE
C  REEL OU COMPLEXE

      IF (.NOT.ZCMPLX) THEN
        IF (TYPRES.EQ.'DYNA_HARM' .OR. TYPRES.EQ.'HARM_GENE'
     &   .OR.TYPRES.EQ.'MODE_MECA_C') THEN
          CALL UTMESS('F','LRIDEA','LE CHAMP DEMANDE N''EST PAS DE'//
     &                'MEME NATURE QUE LE CHAMP A LIRE (REEL/COMPLEXE)')
        END IF
      END IF

      IF (ASTOCK) THEN

C- CREATION DES CHAMPS SIMPLES NOEUDS ET ELEMENTS

        NBCMP = ZI(LFINBC-1+NUMCH)

        NBCMP1 = 0
        DO 80 ICMP = 1,NBCMP
          IF (ZK8(LFICMP-1+ (NUMCH-1)*1000+ICMP).NE.'XXX') THEN
            NBCMP1 = NBCMP1 + 1
            LICMP(NBCMP1) = ZK8(LFICMP-1+ (NUMCH-1)*1000+ICMP)
          END IF
   80   CONTINUE

        IF (TYCHID.EQ.'NOEU') THEN

          CHS = '&&LRIDEA.CHNS'
          CALL CNSCRE(NOMA,NOMGD,NBCMP1,LICMP,'V',CHS)
        ELSE

          CALL JEEXIN(LIGREL//'.NOMA',IRET)
          IF (IRET.EQ.0) THEN
            CALL UTMESS('F','LRIDEA',
     &             'LE MOT CLE MODELE EST OBLIGATOIRE POUR UN CHAM_ELEM'
     &                  )
          END IF
          CHS = '&&LRIDEA.CHES'

          IF (NOMCH(1:4).EQ.'VARI') NBCMP1 = NBVARI

          CALL CESCRE('V',CHS,TYCHAS,NOMA,NOMGD,NBCMP1,LICMP,IBID,-1,
     &                -NBCMP1)
        END IF

C --- LECTURE DU CHAMP NOEUDS

        IF (TYCHID.EQ.'NOEU') THEN
          CALL JEVEUO(CHS//'.CNSD','E',JCNSD)
          CALL JEVEUO(CHS//'.CNSV','E',JCNSV)
          CALL JEVEUO(CHS//'.CNSL','E',JCNSL)

   90     CONTINUE
          READ (MFICH,'(I10)',END=160) INOIDE
          IF (INOIDE.EQ.-1) GO TO 150
          IF (LMASU) THEN
            NOMNO='NOXXXXXX'
            CALL CODENT(INOIDE,'G',NOMNO(3:8))
            CALL JENONU(JEXNOM(NOMA//'.NOMNOE',NOMNO),INOAST)
            CALL ASSERT(INOAST.GT.0)
          ELSE
            INOAST=INOIDE
          ENDIF

          IF (INOAST.GT.NBNOEU) THEN
            CALL UTDEBM('F',NOMCMD,'NOEUD INCONNU DANS LE FICHIER'//
     &                  ' IDEAS')
            CALL UTIMPI('L','  NOEUD NUMERO : ',1,INOAST)
            CALL UTFINM()
          END IF

          IDECAL = (INOAST-1)*ZI(JCNSD-1+2)
          IF (ZCMPLX) THEN
            READ (MFICH,'(6E13.5)',END=160) (VAL(I),I=1,2*NBCMID)
            ICMP1 = 0
            DO 100 ICMP = 1,NBCMP
              ICMP2 = ICMP*2
              IF (ZK8(LFICMP-1+ (NUMCH-1)*1000+ICMP).NE.'XXX') THEN
                ICMP1 = ICMP1 + 1
                ZC(JCNSV-1+IDECAL+ICMP1) = DCMPLX(VAL(ICMP2-1),
     &                                     VAL(ICMP2))
                ZL(JCNSL-1+IDECAL+ICMP1) = .TRUE.
              END IF
  100       CONTINUE
          ELSE
            READ (MFICH,'(6E13.5)',END=160) (VAL(I),I=1,NBCMID)
            ICMP1 = 0
            DO 110 ICMP = 1,NBCMP
              IF (ZK8(LFICMP-1+ (NUMCH-1)*1000+ICMP).NE.'XXX') THEN
                ICMP1 = ICMP1 + 1
                ZR(JCNSV-1+IDECAL+ICMP1) = VAL(ICMP)
                ZL(JCNSL-1+IDECAL+ICMP1) = .TRUE.
              END IF
  110       CONTINUE
          END IF

          GO TO 90

C - LECTURE DU CHAMP ELEMENT

        ELSE IF (TYCHID.EQ.'ELNO') THEN
          CALL JEVEUO(CHS//'.CESD','L',JCESD)
          CALL JEVEUO(CHS//'.CESV','E',JCESV)
          CALL JEVEUO(CHS//'.CESL','E',JCESL)

  120     CONTINUE
          READ (MFICH,'(4I10)',END=160) IELIDE,IEXP,NBNOE,NBCMID
          IF (NOMCH(1:4).EQ.'VARI') NBCMP = NBVARI
          IF (IELIDE.EQ.-1) GO TO 150
          IF (LMASU) THEN
            NOMMA='MAXXXXXX'
            CALL CODENT(IELIDE,'G',NOMMA(3:8))
            CALL JENONU(JEXNOM(NOMA//'.NOMMAI',NOMMA),IELAST)
            CALL ASSERT(IELAST.GT.0)
          ELSE
            IELAST=IELIDE
          ENDIF

          IF (IELAST.GT.NBELEM) THEN
            CALL UTDEBM('F',NOMCMD,'ELEMENT INCONNU DANS LE'//
     &                  'FICHIER IDEAS')
            CALL UTIMPI('L',' ELEMENT NUMERO : ',1,IELAST)
            CALL UTFINM()
          END IF
          ITYPE=ZI(JTYPM-1+IELAST)

          DO 140 KNOIDE = 1,NBNOE

C           -- CALCUL DE KNOAST :
            DO 141 IAST = 1,NBNOE
               ISUP=ZI(JPERM-1+MAXNOD*(ITYPE-1)+IAST)
               IF (ISUP.EQ.KNOIDE) GOTO 142
 141        CONTINUE
            CALL UTMESS('F','LRIDEA','PB CORRESPONDANCE NOEUD IDEAS')
 142        CONTINUE
            KNOAST=IAST

            READ (MFICH,'(6E13.5)',END=160) (VAL(I),I=1,NBCMID)
            ICMP1 = 0
            DO 130 ICMP = 1,NBCMP
              IF (ZK8(LFICMP-1+ (NUMCH-1)*1000+ICMP).NE.'XXX') THEN
                ICMP1 = ICMP1 + 1
                CALL CESEXI('S',JCESD,JCESL,IELAST,KNOAST,1,ICMP1,KK)
                ZR(JCESV-1+ABS(KK)) = VAL(ICMP)
                ZL(JCESL-1+ABS(KK)) = .TRUE.
              END IF
  130       CONTINUE
  140     CONTINUE

          GO TO 120
        ELSE IF (TYCHID.EQ.'ELGA') THEN
          CALL UTMESS('F','LRIDEA','LE CHAMP DE TYPE ELGA N''EST PAS '//
     &                ' SUPPORTE')
        END IF

  150   CONTINUE

C- TRANSFERT DES RESULTATS DU CHAMP SIMPLE VERS LE CHAMP VRAI :
C  (SD RESULTATS)

C       -- ON CHERCHE A ECONOMISER LES PROF_CHNO :
        IF (PRCHNO.EQ.' ') THEN
          IF (NOMCH.EQ.NOMC2) THEN
            PRCHN3=PRCHN2
          ELSE
            NOOJB='12345678.00000.NUME.PRNO'
            CALL GNOMSD ( NOOJB,10,14)
            PRCHN3=NOOJB(1:19)
          END IF
          NOMC2=NOMCH
          PRCHN2=PRCHN3
        ELSE
          PRCHN3=PRCHNO
        END IF

        CALL STOCK(RESU,NOMCMD,CHS,NOMCH,LIGREL,TYCHAS,IORD,IOUF,
     &  NUMODE,MASGEN,PRCHN3)

C EN CAS DE MODE_MECA
C MISE A ZERO DES VALEURS D'AMORTISSEMENT MODAL
      IF ((TYPRES.EQ.'MODE_MECA').OR.(TYPRES.EQ.'MODE_MECA_C')) THEN
       CALL RSADPA(RESU,'E',1,'AMOR_REDUIT',IORD,0,LAMRE,K8BID)
       ZR(LAMRE) = ZERO
      ENDIF

        GO TO 10
      ELSE
        GO TO 10
      END IF

      GO TO 180
  160 CONTINUE
      CALL GETRES(RESU,CONCEP,NOMCMD)
      CALL UTMESS('F',NOMCMD,'ERREUR DANS LA LECTURE DU FICHIER IDEAS')

C- FIN DE FICHIER

  170 CONTINUE

  180 CONTINUE


      CALL JEDETR('&&IRADHS.PERMUTA')
      CALL JEDETR('&&IRADHS.CODEGRA')
      CALL JEDETR('&&IRADHS.CODEPHY')
      CALL JEDETR('&&IRADHS.CODEPHD')


      CALL JEDEMA()
      END
