      SUBROUTINE IRMASU(IFC,NDIM,NNO,COORDO,NBMA,CONNEX,POINT
     &     ,TYPMA,TYPEL,CODGRA,CODPHY,CODPHD,PERMUT,MAXNOD,LMOD,
     &            NOMA,NBGRN,NOGN,NBGRM,NOGM,
     &                  LMASU,NOMAI,NONOE,VERSIO)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF PREPOST  DATE 03/07/2012   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C TOLE CRP_21
      IMPLICIT NONE
C
C     BUT :   ECRITURE DU MAILLAGE SUR FICHIER UNIVERSEL SUPERTAB
C     ENTREE:
C       NOMA   : NOM DU MAILLAGE
C       IFC    : NUMERO D'UNITE LOGIQUE DU FICHIER UNIVERSEL
C       NDIM   : DIMENSION DU PROBLEME (2  OU 3)
C       NNO    : NOMBRE DE NOEUDS DU MAILLAGE
C       COORDO : VECTEUR DES COORDONNEES DES NOEUDS
C       NBMA   : NOMBRE DE MAILLES DU MAILLAGE
C       CONNEX : CONNECTIVITES
C       POINT  : POINTEUR DANS LES CONNECTIVITES
C       TYPMA  : TYPES DES MAILLES
C       TYPEL  : TYPES DES ELEMENTS
C       CODGRA : CODE GRAPHIQUE SUPERTAB ASSOCIE A CHAQUE TYPE DE MAILLE
C       CODPHY : CODE PHYSIQUE SUPERTAB ASSOCIE A CHAQUE TYPE D'ELEMENT
C       PERMUT : TABLEAU DES PERMUTATIONS DES NOEUDS DE CHAQUE MAILLE
C       MAXNOD : NOMBRE MAXI DE NOEUDS POUR UN TYPE_MAILLE
C       LMOD   : LOGIQUE INDIQUANT SI IMPRESSION MODELE OU MAILLAGE
C                 .TRUE. MODELE
C       VERSIO : NUMERO DE VERSION SUPERTAB 4 OU 5, 5 PAR DEFAUT
C                (EN VERSION 5 CREATION DU DATASET 775)
C       LMASU  : INDIQUE SI MAILLAGE ISSU IDEAS .TRUE.
C       NOMAI  : NOMS DES MAILLES
C       NONOE  : NOMS DES NOEUDS
C       TOUT CE QUI SUIT CONCERNE LES GROUPES:
C          NBGRN: NOMBRE DE GROUPES DE NOEUDS
C          NOGN : NOM DES GROUPES DE NOEUDS
C          NBGRM: NOMBRE DE GROUPES DE MAILLES
C          NOGM : NOM DES GROUPES DE MAILLES
      INCLUDE 'jeveux.h'
      CHARACTER*8  NOMA
C     ------------------------------------------------------------------
      CHARACTER*8 NOGN(*),NOGM(*),NOMAI(*),NONOE(*), NOMTM
      REAL*8 COORDO(*),R(3),BIDON,RCSF
      INTEGER CONNEX(*),TYPMA(*),POINT(*),TYPEL(*)
      INTEGER NODSUP(32),NODAST(32),PERMUT(MAXNOD,*),CODGRA(*),CODPHY(*)
      INTEGER ICODNO,ICODMA,VERSIO,CODPHD(*)
      INTEGER ITRI7, IQUA9, ISEG4, IHEX27, IPEN18
      LOGICAL LMASU,LPOUT,LMOD
      CHARACTER*8 KBID
C ---------------------------------------------------------------------
C-----------------------------------------------------------------------
      INTEGER I ,IAGRMA ,IAGRNO ,ICA ,ICC ,ICF ,ICOD 
      INTEGER ICOD1 ,ICOD2 ,ICOL ,ICOLD ,ICOLE ,ICOLL ,ICOLP 
      INTEGER ICOLT ,ICOU ,ICS ,ICSS ,ICST ,ICSV ,IER 
      INTEGER IFC ,IGM ,IGM2 ,IGN ,IMA ,IMAIL ,IMAS 
      INTEGER IMAT ,INBO ,INO ,IOFF ,IPHY ,IPOIN ,IRET 
      INTEGER ISUP ,ITSEG2 ,ITYPE ,J ,JM ,JMAGR ,JN 
      INTEGER JNOGR ,K ,L ,M ,MAXNOD ,NBGRM ,NBGRN 
      INTEGER NBM ,NBM2 ,NBMA ,NBN ,NDIM ,NNO ,NNOE 

C-----------------------------------------------------------------------
      CALL JEMARQ()
      LPOUT=.FALSE.
      ITRI7  = 0
      IQUA9  = 0
      ISEG4  = 0
      IHEX27 = 0
      IPEN18 = 0
C
C     RECHERCHE DE LA PRESENCE DE POUTRES
C
      DO 1 IMAIL=1,NBMA

        IF(LMOD) THEN
          ICOD = CODPHY(TYPEL(IMAIL))
        ELSE
          ICOD = CODPHD(TYPMA(IMAIL))
        ENDIF
        IF(ICOD.EQ.21.OR.ICOD.EQ.22.OR.ICOD.EQ.23.OR.ICOD.EQ.24) THEN
          LPOUT=.TRUE.
          GO TO 2
        ENDIF
 1    CONTINUE
 2    CONTINUE
C
C     ECRITURE D'UN DATASET 775 (PROPRIETES DES POUTRES) BIDON
C
      IF(VERSIO.EQ.5.AND.LPOUT) THEN
        WRITE (IFC,'(A)') '    -1'
        WRITE (IFC,'(A)') '   775   %PROPRIETES BIDON SECTION POUTRES'
        ICST=1
        ICSS=0
        ICSV=0
        WRITE(IFC,'(3I10)') ICST,ICSS,ICSV
        WRITE(IFC,'(A)') 'BEAM1'
        BIDON=0.0D0
        WRITE(IFC,'(6(1PE13.6))') BIDON,BIDON,BIDON,BIDON,BIDON,BIDON
        WRITE(IFC,'(4(1PE13.6))') BIDON,BIDON,BIDON,BIDON
        WRITE(IFC,'(6(1PE13.6))') BIDON,BIDON,BIDON,BIDON,BIDON,BIDON
        WRITE(IFC,'(6(1PE13.6))') BIDON,BIDON,BIDON,BIDON,BIDON,BIDON
        WRITE(IFC,'(6(1PE13.6))') BIDON,BIDON,BIDON,BIDON,BIDON,BIDON
        WRITE(IFC,'(6(1PE13.6))') BIDON,BIDON,BIDON,BIDON,BIDON,BIDON
        WRITE(IFC,'(6(1PE13.6))') BIDON,BIDON,BIDON,BIDON,BIDON,BIDON
        ICOLP=11
        ICOLL= 7
        ICOLE= 8
        ICOLD=14
        ICOL = 1
        ICOLT=10
        WRITE(IFC,'(6I10)') ICOLP,ICOLL,ICOLE,ICOLD,ICOL,ICOLT
        ICF=0
        ICA=45
        ICS=1
        ICC=11
        RCSF=1.0D0
        WRITE(IFC,'(4I10,1PE13.6)') ICF,ICA,ICS,ICC,RCSF
        WRITE (IFC,'(A)') '    -1'
      ENDIF
C
C     --- ECRITURE DES COORDONNEES DES NOEUDS
C
      WRITE (IFC,'(A)') '    -1'
      IF(VERSIO.EQ.5) THEN
         WRITE (IFC,'(A)') '  2411'
      ELSEIF(VERSIO.EQ.4) THEN
         WRITE (IFC,'(A)') '    15   %NOEUDS'
      ENDIF
C     I : NUMERO DES NOEUDS
C     J : SYSTEME DE COORDONNEES
C     K : ""           ""
C     L : COULEUR
      J = 1
      K = 1
      L = 11
      NDIM = 3
C     --- BOUCLE SUR TOUS LES NOEUDS DU MAILLAGE
      DO 5 I = 1,NNO
C       - ON ECRIT TOUJOURS 3 COORDONNEES (PARAMETRE NDIM ECRASE)
        DO 6 M = 1,NDIM
          R(M) = COORDO(3*(I-1)+M)
    6   CONTINUE
C       - RECUPERATION DU NUMERO DE NOEUDS (RECHERCHE SI MAILLAGE IDEAS)
        IF(LMASU) THEN
          CALL LXLIIS(NONOE(I)(2:8),INO,IER)
        ELSE
          INO = I
        ENDIF
        IF(VERSIO.EQ.5) THEN
          WRITE (IFC,FMT='(4I10)') INO,J,K,L
          WRITE (IFC,FMT='(3E25.17)') (R(M),M=1,NDIM)
        ELSEIF(VERSIO.EQ.4) THEN
          WRITE (IFC,FMT='(4I10,3E13.6)') INO,J,K,L, (R(M),M=1,NDIM)
        ENDIF
    5 CONTINUE
      WRITE (IFC,'(A)') '    -1'
C
C     --- ECRITURE DES CONNECTIVITES
C
      WRITE (IFC,'(A)') '    -1'
      IF(VERSIO.EQ.5) THEN
        WRITE (IFC,'(A)') '  2412'
      ELSEIF(VERSIO.EQ.4) THEN
        WRITE (IFC,'(A)') '    71   %ELEMENTS'
      ENDIF
      IMAT = 1
      ICOU = 6+IMAT
      DO 21 IMA = 1,NBMA
        IMAT = 1
        ITYPE = TYPMA(IMA)
C       - RECUPERATION DU NOMBRE DE NOEUDS DE L'ELEMENT
        IPOIN=POINT(IMA)
        NNOE=POINT(IMA+1)-IPOIN
CCC
        CALL JENUNO(JEXNUM('&CATA.TM.NOMTM',ITYPE),NOMTM)
        IF ( NOMTM .EQ. 'HEXA27' ) THEN
           IF (IHEX27.EQ.0) CALL U2MESS('I','PREPOST2_78')
           IHEX27 = 1
           NNOE = NNOE - 7
        ELSE IF ( NOMTM .EQ. 'TRIA7' ) THEN
           IF (ITRI7.EQ.0) CALL U2MESS('I','PREPOST2_79')
           ITRI7 = 1
           NNOE = NNOE - 1
        ELSE IF ( NOMTM .EQ. 'PENTA18' ) THEN
           IF (IPEN18.EQ.0) CALL U2MESS('I','PREPOST2_85')
           IPEN18 = 1
           NNOE = NNOE - 3
        ELSEIF ( NOMTM .EQ. 'QUAD9' ) THEN
           IF (IQUA9.EQ.0) CALL U2MESS('I','PREPOST2_80')
           IQUA9 = 1
           NNOE = NNOE - 1
        ELSEIF ( NOMTM .EQ. 'SEG4' ) THEN
           IF (ISEG4.EQ.0) CALL U2MESS('I','PREPOST2_81')
           ISEG4 = 1
           NNOE = NNOE - 2
CJMP
           CALL JENONU(JEXNOM('&CATA.TM.NOMTM','SEG2'),ITSEG2)
           ITYPE=ITSEG2
        ENDIF
CCC
C       - IMAT = 2 CORRESPOND A UN ELEMENT REDUIT A UN NOEUD
        IF(NNOE.EQ.1) IMAT = 2
C       - PERMUTATION DES NOEUDS SELON LE TYPE DE MAILLE
        DO 20 J = 1,NNOE
          NODAST(J) = CONNEX(IPOIN-1+J)
          ISUP=PERMUT(J,ITYPE)
          NODSUP(ISUP)=NODAST(J)
   20   CONTINUE
C       - CODE GRAPHIQUE DE LA MAILLE
        ICOD1=CODGRA(ITYPE)
C       - SI LMOD =.TRUE. ON IMPRIME LE MODELE SINON LE MAILLAGE
        IF(LMOD) THEN
          IF(TYPEL(IMA).EQ.0) THEN
            ICOD2=0
          ELSE
            ICOD2=CODPHY(TYPEL(IMA))
            IPHY =TYPEL(IMA)
          END IF
        ELSE
          ICOD2=CODPHD(TYPMA(IMA))
CJMPESSAI          ICOD2=CODPHD(ITYPE)
          IPHY =ITYPE
        ENDIF
C
C       - ELEMENTS NON DISPONIBLES DANS IDEAS
        IF (CODPHD(TYPMA(IMA)).EQ.6000) THEN
          CALL U2MESS('A','PREPOST2_82')
        ELSE IF (CODPHD(TYPMA(IMA)).EQ.6001) THEN
          CALL U2MESS('A','PREPOST2_83')
        ENDIF
        CALL ASSERT((ICOD2.LE.10000).AND.(ICOD2.GE.0))
        IF(ICOD1.NE.0.AND.ICOD2.NE.0) THEN
          IF(LMASU) THEN
            CALL LXLIIS(NOMAI(IMA)(2:8),IMAS,IER)
            DO 751 J=1,NNOE
              CALL LXLIIS(NONOE(NODSUP(J))(2:8),NODSUP(J),IER)
 751        CONTINUE
          ELSE
            IMAS = IMA
          ENDIF
          IF(VERSIO.EQ.5) THEN
            IPHY=1
            WRITE (IFC,FMT='(6I10)') IMAS,ICOD2,IPHY,IMAT,ICOU,NNOE
C           - ENREGISTREMENT SPECIAL POOUR LES POUTRES
            IF(ICOD2.EQ.21.OR.ICOD2.EQ.22.OR.ICOD2.EQ.23.
     &                     OR.ICOD2.EQ.24) THEN
              INBO = 0
              IOFF = 1
              WRITE(IFC,FMT='(3I10)') INBO,IOFF,IOFF
            ENDIF
          ELSEIF(VERSIO.EQ.4) THEN
            WRITE (IFC,FMT='(7I10)') IMAS,ICOD1,ICOD2,IPHY,IMAT,
     &                               ICOU,NNOE
          ENDIF
          WRITE (IFC,FMT='(8I10)') (NODSUP(J),J=1,NNOE)
        END IF
   21 CONTINUE
      WRITE (IFC,'(A)') '    -1'
C
C     --- ECRITURE DES GROUPES DE NOEUDS ET DE MAILLES
      WRITE (IFC,'(A)') '    -1'
      WRITE (IFC,'(A)') '  2477'
      ICODMA = 8
      ICODNO = 7
      CALL JEEXIN('&&IRMASU.NOGR',IRET)
      IF(IRET.NE.0) CALL JEDETR('&&IRMASU.NOGR')
C     --- TRAITEMENT DES GROUPES DE NOEUDS
      DO 752 IGN = 1,NBGRN
        CALL JEVEUO(JEXNUM(NOMA//'.GROUPENO',IGN),'L',IAGRNO)
        CALL JELIRA(JEXNUM(NOMA//'.GROUPENO',IGN),'LONUTI',NBN,KBID)
        CALL WKVECT('&&IRMASU.NOGR','V V I',NBN,JNOGR)
        WRITE (IFC,FMT='(8I10)') IGN,0,0,0,0,0,0,NBN
        WRITE (IFC,FMT='(A)') NOGN(IGN)
        DO 755 JN=1,NBN
          ZI(JNOGR-1+JN)=ZI(IAGRNO-1+JN)
          IF(LMASU) THEN
            CALL LXLIIS(NONOE(ZI(JNOGR-1+JN))(2:8),ZI(JNOGR-1+JN),IER)
          ENDIF
  755   CONTINUE
        WRITE (IFC,FMT='(8I10)') (ICODNO,ZI(JNOGR-1+JN),0,0,JN=1,NBN)
        CALL JEDETR('&&IRMASU.NOGR')
  752 CONTINUE
C     --- TRAITEMENT DES GROUPES DE MAILLES
      IGM2=0
      CALL JEEXIN('&&IRMASU.MAGR',IRET)
      IF(IRET.NE.0) CALL JEDETR('&&IRMASU.MAGR')
      DO 754 IGM = 1,NBGRM
C       - RECUPERATION DU NOMBRE DE MAILLES DU GROUPE
C         ET DU POINTEUR SURLE GROUPE DE MAILLES
        CALL JEVEUO(JEXNUM(NOMA//'.GROUPEMA',IGM),'L',IAGRMA)
        CALL JELIRA(JEXNUM(NOMA//'.GROUPEMA',IGM),'LONUTI',NBM,KBID)
        CALL WKVECT('&&IRMASU.MAGR','V V I',NBM,JMAGR)
        NBM2=0
        DO 756 JM=1,NBM
C         - MAILLE PRISE EN COMPTE SI TYPE D'ELEMENT DIFFERENT DE 0
          IF(LMOD)THEN
            IF(TYPEL(ZI(IAGRMA-1+JM)).EQ.0) GOTO 756
          ENDIF
          ITYPE=TYPMA(ZI(IAGRMA-1+JM))
          CALL JENUNO(JEXNUM('&CATA.TM.NOMTM',ITYPE),NOMTM)
          IF((NOMTM.EQ.'PYRAM5').OR.(NOMTM.EQ.'PYRAM13')) GOTO 756
          NBM2=NBM2+1
          ZI(JMAGR-1+NBM2)=ZI(IAGRMA-1+JM)
          IF(LMASU) THEN
C           - SI MAILLAGE IDEAS RECUPERATION DU NUMERO DE MAILLE
            CALL LXLIIS(NOMAI(ZI(JMAGR-1+NBM2))(2:8),
     &                                     ZI(JMAGR-1+NBM2),IER)
          ENDIF
  756   CONTINUE
        IF(NBM2.NE.0) THEN
          IGM2=IGM2+1
          WRITE (IFC,FMT='(8I10)') NBGRN+IGM2,0,0,0,0,0,0,NBM2
          WRITE (IFC,FMT='(A)') NOGM(IGM)
          WRITE (IFC,FMT='(8I10)') (ICODMA,ZI(JMAGR-1+JM),0,0,JM=1,NBM2)
        ENDIF
        CALL JEDETR('&&IRMASU.MAGR')
  754 CONTINUE
      WRITE (IFC,'(A)') '    -1'
      CALL JEDEMA()
      END
