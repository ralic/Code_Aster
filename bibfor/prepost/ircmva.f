      SUBROUTINE IRCMVA ( NTLCMP, NCMPVE, NCMPRF,
     >                    NVALEC, NBPG, NBSP,
     >                    ADSV, ADSD, ADSL,
     >                    TYPMAI, TYMAST,
     >                    VAL, NTPROA )
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF PREPOST  DATE 11/03/2003   AUTEUR DURAND C.DURAND 
C ======================================================================
C COPYRIGHT (C) 1991 - 2002  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C RESPONSABLE GNICOLAS G.NICOLAS
C_______________________________________________________________________
C     ECRITURE D'UN CHAMP -  FORMAT MED - CREATION DES VALEURS
C        -  -       -               -                  --
C_______________________________________________________________________
C     ENTREES :
C       NTLCMP : SD DES NUMEROS DES COMPOSANTES
C       NCMPVE : NOMBRE DE COMPOSANTES VALIDES EN ECRITURE
C       NVALEC : NOMBRE DE VALEURS A ECRIRE
C       NBPG   : NOMBRE DE POINTS DE GAUSS (1 POUR DES CHAMNO)
C       NBSP   : NOMBRE DE SOUS-POINTS (1 POUR DES CHAMNO)
C       ADSV,D,L : ADRESSES DES TABLEAUX DES CHAMPS SIMPLIFIES
C       TYMAST : TYPE ASTER DE MAILLE QUE L'ON VEUT (O POUR LES NOEUDS)
C       NTPROA : SD DU PROFIL ASTER. C'EST LA LISTE DES NUMEROS ASTER
C                DES NOEUDS OU DES ELEMENTS POUR LESQUELS LE CHAMP
C                EST DEFINI
C     SORTIES :
C       VAL    : VALEURS EN MODE ENTRELACE
C_______________________________________________________________________
C
      IMPLICIT NONE
C
C 0.1. ==> ARGUMENTS
C
      CHARACTER*(*) NTLCMP, NTPROA, TYPMAI
C
      INTEGER NCMPVE, NCMPRF, NVALEC, NBPG, NBSP
      INTEGER ADSV, ADSD, ADSL
      INTEGER TYMAST
C
      REAL*8 VAL(NCMPVE,NBSP,NBPG,NVALEC)
C
C 0.2. ==> COMMUNS
C
C --------------- COMMUNS NORMALISES  JEVEUX  --------------------------
      INTEGER      ZI
      REAL*8       ZR
      LOGICAL      ZL
      COMMON /IVARJE/ZI(1)
      COMMON /RVARJE/ZR(1)
      COMMON /LVARJE/ZL(1)
C     -----  FIN  COMMUNS NORMALISES  JEVEUX --------------------------
C
C 0.3. ==> VARIABLES LOCALES
C
      CHARACTER*6 NOMPRO
      PARAMETER ( NOMPRO = 'IRCMVA' )
C
      CHARACTER*8 SAUX08
C
      INTEGER ADNUCM, ADPROA, ADTMAS
C
      INTEGER IAUX, JAUX, KAUX
      INTEGER NVAL
      INTEGER ADSLXX, ADSVXX
      INTEGER INO, IMA, NRCMP, NRCMPR, NRPG, NRSP
      INTEGER IFM, NIVINF
C
C====
C 1. PREALABLES
C====
C
C 1.1. ==> RECUPERATION DU NIVEAU D'IMPRESSION
C
      CALL INFNIV ( IFM, NIVINF )
C
C 1.2. ==> CARACTERISTIQUES DES TABLEAUX UTILES
C
      CALL JEVEUO ( NTPROA, 'L', ADPROA )
      CALL JELIRA ( NTPROA, 'LONUTI', NVAL, SAUX08 )
      CALL JEVEUO ( NTLCMP, 'L', ADNUCM )
C
C 1.3. ==> INFORMATION
C
      IF ( NIVINF.GT.1 ) THEN
        WRITE (IFM,13001) NOMPRO, TYMAST,
     >                    NVALEC, NCMPVE, NBPG, NBSP
      ENDIF
13001 FORMAT(A6,' : ',/
     >   '. TYMAST =',I4,
     >   ', NVALEC =',I8,', NCMPVE =',I8,
     >   ', NBPG   =',I8,', NBSP   =',I8)
C
C====
C 2. CREATION DU CHAMP DE VALEURS AD-HOC
C    LE TABLEAU DE VALEURS EST UTILISE AINSI : 
C        TV(NCMPVE,NBSP,NBPG,NVALEC)
C    EN FORTRAN, CELA CORRESPOND AU STOCKAGE MEMOIRE SUIVANT :
C    TV(1,1,1,1), TV(2,1,1,1), ..., TV(NCMPVE,1,1,1),
C    TV(1,2,1,1), TV(2,2,1,1), ..., TV(NCMPVE,2,1,1), 
C            ...     ...     ...
C    TV(1,NBSP,NBPG,NVALEC), TV(2,NBSP,NBPG,NVALEC), ... ,
C                                      TV(NCMPVE,NBSP,NBPG,NVALEC)
C    C'EST CE QUE MED APPELLE LE MODE ENTRELACE
C    ATTENTION : LE CHAMP SIMPLIFIE EST DEJA PARTIELLEMENT FILTRE ...
C    ATTENTION ENCORE : LE CHAMP SIMPLIFIE N'A PAS LA MEME STRUCTURE
C    POUR LES NOEUDS ET LES ELEMENTS. IL FAUT RESPECTER CE TRAITEMENT
C    REMARQUE : SI UNE COMPOSANTE EST ABSENTE, ON AURA UNE VALEUR NULLE
C    REMARQUE : ATTENTION A BIEN REDIRIGER SUR LE NUMERO DE
C    COMPOSANTE DE REFERENCE
C====
C
C 2.1. ==> POUR LES NOEUDS : ON PREND TOUT CE QUI FRANCHIT LE FILTRE
C
      IF ( TYMAST.EQ.0 ) THEN
C
        DO 21 , NRCMP = 1 , NCMPVE
C
          ADSLXX = ADSL-1+ZI(ADNUCM+NRCMP-1)-NCMPRF
          ADSVXX = ADSV-1+ZI(ADNUCM+NRCMP-1)-NCMPRF
          JAUX = 0
          DO 211 , IAUX = 0 , NVAL-1
            INO = ZI(ADPROA+IAUX)
            JAUX = JAUX + 1
            IF ( ZL(ADSLXX+INO*NCMPRF) ) THEN
              VAL(NRCMP,1,1,JAUX) = ZR(ADSVXX+INO*NCMPRF)
            ENDIF
  211     CONTINUE
C
   21   CONTINUE
C
      ELSE
C
C 2.2. ==> POUR LES MAILLES : ON PREND TOUT CE QUI FRANCHIT LE FILTRE
C          ET QUI EST DU TYPE EN COURS
C          REMARQUE : ON NE REDECODE PAS LES NOMBRES DE POINTS DE GAUSS
C          NI DE SOUS-POINT CAR ILS SONT INVARIANTS POOUR UN TYPE DE
C          MAILLE DONNE
C
        CALL JEVEUO ( TYPMAI, 'L', ADTMAS )
        DO 22 , NRCMP = 1 , NCMPVE
C
          NRCMPR = ZI(ADNUCM+NRCMP-1)
          JAUX = 0
          DO 221 , IAUX = 0 , NVAL-1
            IMA = ZI(ADPROA+IAUX)
            IF ( ZI(ADTMAS+IMA-1).EQ.TYMAST ) THEN
              JAUX = JAUX + 1
              DO 2211 , NRPG = 1 , NBPG
                DO 2212 , NRSP = 1 , NBSP
                  CALL CESEXI ('C',ADSD,ADSL,IMA,NRPG,NRSP,NRCMPR,KAUX)
                  IF ( KAUX.GT.0 ) THEN
                    VAL(NRCMP,NRSP,NRPG,JAUX) = ZR(ADSV-1+KAUX)
                  ENDIF
 2212           CONTINUE
 2211         CONTINUE
            ENDIF
  221     CONTINUE
C
   22   CONTINUE
C
      ENDIF
C
      END
