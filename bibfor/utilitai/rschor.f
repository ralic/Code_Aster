      SUBROUTINE RSCHOR ( NORESZ, NOMSYZ, NBORDR, TABORD, CODRET )
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF UTILITAI  DATE 05/03/2002   AUTEUR GNICOLAS G.NICOLAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2002  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C     ------------------------------------------------------------------
C      RESULTAT - CHAMP - QUELS NUMEROS D'ORDRE ?
C      - -        --                      --
C     ------------------------------------------------------------------
C     ENTREES:
C        NORESZ : NOM DU RESULTAT A EXAMINER
C        NOMSYZ : NOM SYMBOLIQUE DU CHAMP
C     SORTIES:
C        NBORDR : NOMBRE DE NUMEROS D'ORDRE POUR (NORESZ,NOMSYZ)
C        TABORD : LISTE DES NUMEROS D'ORDRE ; L'OBJET EST ALLOUE ICI.
C        CODRET : CODE DE RETOUR (0 : PAS DE PB, NON NUL SI PB)
C     ------------------------------------------------------------------
C     REMARQUE : PROGRAMME INSPIRE DE RSUTNC
C_______________________________________________________________________
C
      IMPLICIT NONE
C
C 0.1. ==> ARGUMENTS
C
      INTEGER NBORDR
C
      CHARACTER*(*) NORESZ, NOMSYZ, TABORD
C
      INTEGER CODRET
C
C 0.2. ==> COMMUNS
C
C --------------- COMMUNS NORMALISES  JEVEUX  --------------------------
      INTEGER      ZI
      CHARACTER*8  ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /IVARJE/ZI(1)
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
      CHARACTER*32 JEXNUM,JEXNOM
C     -----  FIN  COMMUNS NORMALISES  JEVEUX --------------------------
C
C 0.3. ==> VARIABLES LOCALES
C
      CHARACTER*6 NOMPRO
      PARAMETER ( NOMPRO = 'RSCHOR' )
C
      INTEGER LXLGUT
C
      INTEGER IAUX
      INTEGER ADTABO, JORDR, JTACH
      INTEGER NBTONO
C
      CHARACTER*1 K1BID
      CHARACTER*16 NOMSYM
      CHARACTER*19 NORESU
      CHARACTER*24 CHEXTR
C
C====
C 1. PREALABLE
C====
C
      CODRET = 0
C
      IAUX = LXLGUT(NORESZ)
C              1234567890123456789
      NORESU = '                   '
      NORESU(1:IAUX) = NORESZ(1:IAUX)
C
      IAUX = LXLGUT(NOMSYZ)
C               1234567890123456
      NOMSYM = '                '
      NOMSYM(1:IAUX) = NOMSYZ(1:IAUX)
C
C====
C 2. EXPLORATION DE LA STRUCTURE DE RESULTATS
C====
C 2.1. ==> REPERAGES POUR LE COUPLE (NORESU,NOMSYM)
C NBTONO : NOMBRE TOTAL DE NUMEROS D'ORDRE DANS LA STRUCTURE NORESU
C
      CALL JELIRA ( NORESU//'.ORDR', 'LONUTI', NBTONO, K1BID )
      CALL JEVEUO ( NORESU//'.ORDR', 'L', JORDR )
      CALL JENONU ( JEXNOM(NORESU//'.DESC',NOMSYM), IAUX )
      CALL JEVEUO ( JEXNUM(NORESU//'.TACH',IAUX), 'L', JTACH )
C
C 2.2. ==> ALLOCATION DU TABLEAU QUI CONTIENDRA LES NUMEROS D'ORDRE
C          SPECIFIQUES DU CHAMP SYMBOLIQUE. ON ALLOUE AU NOMBRE TOTAL,
C          MAIS CE N'EST PAS GRAVE.
C
      CALL WKVECT ( TABORD, 'V V I' , NBTONO , ADTABO )
C
C 2.3. ==> ON PARCOURT TOUS LES NUMEROS D'ORDRE DE LA STRUCTURE RESULTAT
C          QUAND ON TROUVE UN CHAMP ENREGISTRE CORRESPONDANT AU CHAMP
C          VOULU, ON LE MEMORISE
C
      NBORDR = 0
      DO 20 , IAUX = 0 , NBTONO - 1
C
        CHEXTR = ZK24(JTACH+IAUX)
        IF ( CHEXTR.NE.' ' ) THEN
          ZI(ADTABO+NBORDR) = ZI(JORDR+IAUX)
          NBORDR = NBORDR + 1
        ENDIF
C
   20 CONTINUE
C
C====
C 3. BILAN
C====
C
      IF ( CODRET .GT. 0 ) THEN
        CALL UTMESS ( 'A', NOMPRO,
     >  'PROBLEME DANS LE DECODAGE DE ('//NORESU//','//NOMSYM//')' )
      ENDIF
C
      END
