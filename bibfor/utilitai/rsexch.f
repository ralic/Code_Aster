      SUBROUTINE RSEXCH(KSTOP,NOMSD,NOMSY,IORDR,CHEXTR,ICODE)
      IMPLICIT NONE
      INCLUDE 'jeveux.h'
      INTEGER IORDR,ICODE
      CHARACTER*(*) KSTOP,NOMSD,NOMSY,CHEXTR
C ----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF UTILITAI  DATE 24/07/2012   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE PELLET J.PELLET
C      RECUPERATION DU NOM DU CHAMP-GD  CORRESPONDANT A:
C          NOMSD(IORDR,NOMSY).
C ----------------------------------------------------------------------
C IN  : KSTOP  : ' ' / 'F' : VOIR ICODE  (CI-DESSOUS)
C IN  : NOMSD  : NOM DE LA STRUCTURE "RESULTAT"
C IN  : NOMSY  : NOM SYMBOLIQUE DU CHAMP A CHERCHER.
C IN  : IORDR  : NUMERO D'ORDRE DU CHAMP A CHERCHER.
C OUT : CHEXTR : NOM DU CHAMP EXTRAIT (SI POSSIBLE)
C                SINON : '???' (CODE=110,101,102)
C OUT : ICODE  : CODE RETOUR  (SI KSTOP=' ') :
C    0 : LE CHAMP CHEXTR EST "POSSIBLE" ET IL EXISTE (EXISD)
C  100 : LE CHAMP CHEXTR EST "POSSIBLE" MAIS IL N'EXISTE PAS
C  110 : IL FAUT AGRANDIR NOMSD AVANT DE STOCKER IORDR
C  101 : LE NOM SYMBOLIQUE NOMSY EST INTERDIT DANS LA SD
C  102 : LE NUMERO D'ORDRE N'EXISTE PAS DANS LS SD ET IL
C        NE PEUT PAS ETRE AJOUTE CAR IL N'EST PAS SUPERIEUR
C        AU PLUS GRAND.
C  SI KSTOP='F' :
C    SI ICODE=100 => ERREUR <F> DESTINEE A L'UTILISATEUR
C    SI ICODE>100 => ERREUR <F> DESTINEE AU PROGRAMMEUR
C ----------------------------------------------------------------------

      CHARACTER*16 NOMS2
      CHARACTER*19 NOMD2,CHEXT2,CHEXT3
      CHARACTER*1 K1BID
      CHARACTER*24 VALK(3)
      INTEGER IEXI,IRANG,ISYMB,JTACH,NBORDR,NBORMX,JORDR,K
      REAL*8 RBID
C ----------------------------------------------------------------------
      CALL JEMARQ()
      ICODE = -99
      NOMS2 = NOMSY
      NOMD2 = NOMSD
      CHEXTR = '???'
      CALL ASSERT(KSTOP.EQ.' '.OR.KSTOP.EQ.'F')


C     --- NOM SYMBOLIQUE PERMIS ?
      CALL JENONU(JEXNOM(NOMD2//'.DESC',NOMS2),ISYMB)
      IF (ISYMB.EQ.0) THEN
        ICODE=101
        GO TO 10
      ENDIF


C     --- RECUPERATION DU NUMERO DE RANGEMENT ---
      CALL RSUTRG(NOMD2,IORDR,IRANG)


C     -- LE NUMERO DE RANGEMENT EXISTE :
C     -----------------------------------------
      IF (IRANG.GT.0) THEN
        CALL JEVEUO(JEXNUM(NOMD2//'.TACH',ISYMB),'L',JTACH)
        CHEXT2 = ZK24(JTACH+IRANG-1)(1:19)
        IF (CHEXT2.EQ.' ') THEN
          CALL RSUTCH(NOMSD,NOMS2,IORDR,CHEXT2,.TRUE.)
        ELSE
          CALL RSUTCH(NOMSD,NOMS2,IORDR,CHEXT3,.TRUE.)
          CALL ASSERT(CHEXT2.EQ.CHEXT3)
        END IF


C     --- LE NUMERO DE RANGEMENT N'EXISTE PAS :
C     -----------------------------------------
      ELSE
        CALL JELIRA(NOMD2//'.ORDR','LONMAX',NBORMX,K1BID)
        CALL JELIRA(NOMD2//'.ORDR','LONUTI',NBORDR,K1BID)
        IF (NBORDR.GE.NBORMX) THEN
          ICODE = 110
          GOTO 10
        END IF

C       -- ON VERIFIE QUE LE NOUVEAU IORDR EST PLUS GRAND
C          QUE L'ANCIEN PLUS GRAND :
        IF (NBORDR.GE.1) THEN
          CALL JEVEUO(NOMD2//'.ORDR','L',JORDR)
          IF (IORDR.LE.ZI(JORDR-1+NBORDR)) THEN
             ICODE=102
             GOTO 10
          ENDIF
        ENDIF

        CALL RSUTCH(NOMSD,NOMS2,IORDR,CHEXT2,.TRUE.)
      END IF


C     --- LA SD CHEXTR EXISTE-T-ELLE ? :
C     -----------------------------------------
      CHEXTR = CHEXT2
      CALL ASSERT(CHEXTR.NE.' ')
      CALL EXISD('CHAMP_GD',CHEXTR,IEXI)
      IF (IEXI.GT.0) THEN
        ICODE = 0
      ELSE
        ICODE = 100
      ENDIF

   10 CONTINUE
      IF (KSTOP.EQ.'F'.AND.ICODE.NE.0) THEN
        VALK(1)=NOMSD
        VALK(2)=NOMSY
        IF (ICODE.EQ.100) THEN
          CALL U2MESG('F','CALCULEL_29',2,VALK,1,IORDR,0,RBID)
        ELSE
          CALL U2MESG('F','CALCULEL_29',2,VALK,1,IORDR,0,RBID)
        ENDIF
      ENDIF


      CALL JEDEMA()
      END
