      SUBROUTINE PEBPCT(MODELE,NBMA,LMA,CHAM,NOMCMP,DIM,
     &                  BFIX,BORNE,NORME,BORPCT,VOLTOT)
      IMPLICIT NONE
      INTEGER DIM,NBMA,BFIX
      REAL*8 BORPCT(DIM),BORNE(2),VOLTOT
      CHARACTER*8 MODELE,NOMCMP,NORME
      CHARACTER*19 CHAM
      CHARACTER*24 LMA
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF UTILITAI  DATE 22/08/2011   AUTEUR DURAND C.DURAND 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C
C     OPERATEUR :  POST_ELEM
C     TRAITEMENT DU MOT CLE-FACTEUR : "VOLUMOGRAMME"
C
C     BUT :  DETERMINE LES BORNES INF ET SUP DE CHAQUE INTERVALLE
C            AINSI QUE LA DISTRIBUTION VOLUMIQUE OU SURFACIQUE
C            DE LA COMPOSANTE POUR CHAQUE INTERVALLE
C
C    ARGUMENTS:
C
C     IN :
C          MODELE  =  NOM DU MODELE
C          NBMA    =  NOMBRE DE MAILLES A CONSIDERER
C          LMA     =  NOM JEVEUX DES NUMEROS DE MAILLES A CONSIDERER
C          CHAM    =  NOM DU CHAMP
C          NOMCMP  =  NOM DE LA COMPOSANTE
C          DIM     =  3 * NOMBRE D'INTERVALLES
C          BFIX    =  CALCUL DU MIN/MAX POUR DEFINIR LES BORNES
C       	     (0=OUI, 1=BORNES FOURNIES PAR L UTILISATEUR)
C          BORNE   =  BORNE MIN/MAX DE LA PLAGE 
C                     - SI FIXEE PAR L UTILISATEUR
C          NORME   =  VOLUME CALCULE RELATIF OU ABSOLU
C     IN/OUT :
C          BORPCT  =  TABLEAU RESULTAT
C       	      ON Y STOCKE RESPECTIVEMENT POUR CHAQUE
C       	      INTERVALLE: (1) : LA BORNE INF
C       	        	  (2) : LA BORNE SUP
C       	        	  (3) : LE POURCENTAGE VOLUMIQUE
C          VOLTOT  =  VOLUME TOTAL CONCERNE PAR LE FILTRE
C
C     ------------------------------------------------------------------
C     ----- DEBUT COMMUNS NORMALISES  JEVEUX  --------------------------
      INTEGER          ZI
      COMMON  /IVARJE/ ZI(1)
      REAL*8           ZR
      COMMON  /RVARJE/ ZR(1)
      COMPLEX*16       ZC
      COMMON  /CVARJE/ ZC(1)
      LOGICAL          ZL
      COMMON  /LVARJE/ ZL(1)
      CHARACTER*8      ZK8
      CHARACTER*16            ZK16
      CHARACTER*24                    ZK24
      CHARACTER*32                            ZK32
      CHARACTER*80                                    ZK80
      COMMON  /KVARJE/ ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C     -----  FIN  COMMUNS NORMALISES  JEVEUX  --------------------------

      INTEGER IBID,IRET,NBMAT,I,NBINTV,INDIK8,JCESC
      INTEGER JCESV,JCESL,JCESD,JCESK,JPOIV,JPOIL,JPOID,JPDSM,JVAL,JVOL
      INTEGER IMA,NBSP,NBPT,IAD,IPT,J,JNUMA,NBPTMX,K
      REAL*8  VOLPT,PAS,P0,VALMIN,VALMAX,R8MIEM,PDIV
      INTEGER NCMPM,NUCMP,NBVAL
      CHARACTER*4 TYCH,NON
      CHARACTER*8  NOMA,K8B
      CHARACTER*19 LIGREL,CESOUT,CESPOI,CHAMS
      CHARACTER*24 TABVAL,TABVOL
      LOGICAL FIRST
C     ------------------------------------------------------------------

       CALL JEMARQ()

      CALL DISMOI('F','NOM_LIGREL',MODELE,'MODELE',IBID,LIGREL,IRET)
      CALL DISMOI('F','NOM_MAILLA',MODELE,'MODELE',IBID,NOMA,IRET)
      CALL DISMOI('F','NB_MA_MAILLA',NOMA,'MAILLAGE',NBMAT,K8B,IRET)

C --- CALCULS DES VALEURS ET VOLUMES POUR CHAQUE POINT DE CHAQUE MAILLE
C     ----------------------------------------------------------------
      TABVAL='&&PEBPCT_VAL_CMP_MAIL'
      TABVOL='&&PEBPCT_VOLUME_MAIL'
      CESOUT='&&PEBPCT_CHAMS_POND'
      CESPOI='&&PEBPCT_POIDS'
      CHAMS='&&PEBPCT.CHAM_EL_S'

C     PASSAGE AU CHAMP SIMPLE
      CALL CELCES(CHAM,'V',CHAMS)
      CALL JEVEUO(CHAMS//'.CESV','L',JCESV)
      CALL JEVEUO(CHAMS//'.CESL','L',JCESL)
      CALL JEVEUO(CHAMS//'.CESD','L',JCESD)
      CALL JEVEUO(CHAMS//'.CESC','L',JCESC)
      CALL JEVEUO(CHAMS//'.CESK','L',JCESK)
      NBPTMX=ZI(JCESD+2)

C     DETERMINATION DES POIDS DES POINTS DE GAUSS
      NON='NON'
      CALL DISMOI('F','TYPE_CHAMP',CHAM,'CHAMP',IBID,TYCH,IRET)
      CALL CHPOND(TYCH,NON,CHAM,CESOUT,CESPOI,MODELE)
      CALL JEVEUO(CESPOI//'.CESV','L',JPOIV)
      CALL JEVEUO(CESPOI//'.CESL','L',JPOIL)
      CALL JEVEUO(CESPOI//'.CESD','L',JPOID)
      IF (TYCH.NE.'ELGA') CALL JEVEUO(CESPOI//'.PDSM','L',JPDSM)

C     CREATION DES TABLEAUX RECENSANT LES VALEURS DE LA COMPOSANTE
C     (VAL) ET LE VOLUME*POIDS (VOL) ASSOCIE
      CALL WKVECT(TABVAL,'V V R',NBMA*NBPTMX,JVAL)
      CALL WKVECT(TABVOL,'V V R',NBMA*NBPTMX,JVOL)

      CALL JELIRA(CHAMS//'.CESC','LONMAX',NCMPM,K8B)
      NUCMP=INDIK8(ZK8(JCESC),NOMCMP,1,NCMPM)
      CALL ASSERT(NUCMP.GE.0)

C     MAILLES A CONSIDERER
      CALL JEVEUO(LMA,'L',JNUMA)

      VOLTOT=0.D0
      FIRST=.TRUE.
      K=0

C     ON REMPLIT LES TABLEAUX VAL ET VOL
      DO 35 I=1,NBMA

         IMA=ZI(JNUMA+I-1)
         NBPT=ZI(JCESD-1+5+4*(IMA-1)+1)
         NBSP=ZI(JCESD-1+5+4*(IMA-1)+2)
         IF(NBSP.GT.1) CALL U2MESS('F','UTILITAI8_60')

         DO 40 IPT=1,NBPT
C
            CALL CESEXI('C',JCESD,JCESL,IMA,IPT,1,NUCMP,IAD)

            IF ((IAD.GT.0).AND.(BFIX.EQ.0)) THEN

               K=K+1
               ZR(JVAL+K-1)=ZR(JCESV-1+IAD)

               IF (TYCH.EQ.'ELGA') THEN
                    CALL CESEXI('C',JPOID,JPOIL,IMA,IPT,1,1,IAD)
                    CALL ASSERT(IAD.GT.0)
                    VOLPT=ZR(JPOIV-1+IAD)
               ELSEIF (TYCH.EQ.'ELEM') THEN
                    CALL ASSERT(NBPT.EQ.1)
                    VOLPT=ZR(JPDSM-1+IMA)
               ELSEIF (TYCH.EQ.'ELNO') THEN
                    CALL ASSERT(NBPT.GE.1)
                    VOLPT=ZR(JPDSM-1+IMA)/NBPT
               ENDIF

               ZR(JVOL+K-1)=VOLPT
               VOLTOT=VOLTOT+VOLPT

               IF(FIRST)THEN
                  VALMIN=ZR(JVAL+K-1)
                  VALMAX=ZR(JVAL+K-1)
                  FIRST=.FALSE.
               ELSE
                  IF(ZR(JVAL+K-1).LE.VALMIN)VALMIN=ZR(JVAL+K-1)
                  IF(ZR(JVAL+K-1).GE.VALMAX)VALMAX=ZR(JVAL+K-1)
               ENDIF

            ELSEIF ((IAD.GT.0).AND.(BFIX.EQ.1)) THEN

               IF ((ZR(JCESV-1+IAD).GE.BORNE(1)).AND.
     &             (ZR(JCESV-1+IAD).LE.BORNE(2))) THEN
                  K=K+1
                  ZR(JVAL+K-1)=ZR(JCESV-1+IAD)

                  IF (TYCH.EQ.'ELGA') THEN
                       CALL CESEXI('C',JPOID,JPOIL,IMA,IPT,1,1,IAD)
                       CALL ASSERT(IAD.GT.0)
                       VOLPT=ZR(JPOIV-1+IAD)
                  ELSEIF (TYCH.EQ.'ELEM') THEN
                       CALL ASSERT(NBPT.EQ.1)
                       VOLPT=ZR(JPDSM-1+IMA)
                  ELSEIF (TYCH.EQ.'ELNO') THEN
                       CALL ASSERT(NBPT.GE.1)
                       VOLPT=ZR(JPDSM-1+IMA)/NBPT
                  ENDIF

                  ZR(JVOL+K-1)=VOLPT
                  VOLTOT=VOLTOT+VOLPT

               ENDIF

            ENDIF

 40      CONTINUE

 35   CONTINUE

C     NOMBRE DE VALEURS STOCKEES
      NBVAL=K

C     NOMBRE D'INTERVALLES
      NBINTV=DIM/3


C --- DETERMINATION DES INTERVALLES

C     CAS BORNES FIXEES PAR L UTILISATEUR : BORNE(1)-BORNE(2)
C     ON REMPLACE LES EXTREMA CALCULES VALMIN-VALMAX
C     PAR LES BORNES FOURNIES BORNE(1)-BORNE(2)
      IF (BFIX.EQ.1) THEN
         VALMIN=BORNE(1)
         VALMAX=BORNE(2)
C     CAS OU AUCUNE VALEUR N'A ETE TROUVEE
      ELSE IF(ABS(VALMIN-VALMAX).LE.R8MIEM())THEN
         DO 45 I=1,NBINTV
            BORPCT(3*(I-1)+3)=100.D0/NBINTV
 45      CONTINUE
         GOTO 100
      ENDIF

      PAS=(VALMAX-VALMIN)/NBINTV
      P0=VALMIN
      DO 50 I=1,NBINTV
         BORPCT(3*(I-1)+1)=P0
         BORPCT(3*(I-1)+2)=P0+PAS
         P0=P0+PAS
 50   CONTINUE

C --- AJOUT DES VOLUMES DANS 'BORPCT' EN FONCTION DES VALEURS
C     DE LA COMPOSANTE
      DO 60 J=1,NBVAL
          IF( ZR(JVAL+J-1).LT.BORPCT(2))THEN
             BORPCT(3)=BORPCT(3)+ZR(JVOL+J-1)
          ENDIF
 60   CONTINUE
      DO 70 I=2,NBINTV-1
        DO 75 J=1,NBVAL
          IF( ZR(JVAL+J-1).LT.BORPCT(3*(I-1)+2) .AND.
     &        ZR(JVAL+J-1).GE.BORPCT(3*(I-1)+1))THEN
                BORPCT(3*(I-1)+3)=BORPCT(3*(I-1)+3)+ZR(JVOL+J-1)
          ENDIF
 75     CONTINUE
 70   CONTINUE
      DO 80 J=1,NBVAL
          IF( ZR(JVAL+J-1).GE.BORPCT(3*(NBINTV-1)+1))THEN
             BORPCT(3*(NBINTV-1)+3)=BORPCT(3*(NBINTV-1)+3)+ZR(JVOL+J-1)
          ENDIF
 80   CONTINUE

      IF (NORME(1:7).EQ.'RELATIF') THEN 
          PDIV=VOLTOT
      ELSE
          PDIV=1.D0
      ENDIF
C
      DO 90 I=1,NBINTV
        BORPCT(3*(I-1)+3)=100*BORPCT(3*(I-1)+3)/PDIV
 90   CONTINUE

 100  CONTINUE

      CALL DETRSD('CHAM_ELEM_S',CHAMS)
      CALL JEDETR(TABVAL)
      CALL JEDETR(TABVOL)

      CALL JEDEMA()

      END
