      SUBROUTINE PECAP2(CHGEOZ,IY,IZ,S,ALPHA,XG,YG,TEMP1Z,TEMP2Z,AY,AZ,
     &                  EY,EZ,PCTX,PCTY)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF UTILITAI  DATE 03/11/2008   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.

C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.

C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C.======================================================================
      IMPLICIT REAL*8 (A-H,O-Z)

C      PECAP2  -- DETERMINATION : .DU CENTRE DE TORSION/CISAILLEMENT
C                                 .DES COEFFICIENTS DE CISAILLEMENT

C          .LE DOMAINE SUR-LEQUEL ON TRAVAILLE REPRESENTE LA
C           SECTION DE LA POUTRE MAILLEE AVEC DES ELEMENTS 2D
C           ISOPARAMETRIQUES THERMIQUES (THERMIQUES CAR ON
C           DOIT RESOUDRE DES EQUATIONS DE LAPLACE).

C          .LES COEFFICIENTS DE CISAILLEMENT AY ET AZ SONT
C           DETERMINES EN FAISANT RESPECTIVEMENT LA RESOLUTION
C           DE L' EQUATION  1 :
C                G*LAPLACIEN(PSI_Z) = -Z*TZ/IY     DANS LA SECTION
C       AVEC     D(PSI_Z )/DN = 0     SUR LE CONTOUR DE LA SECTION
C       ET       PSI_Z = 0    EN UN NOEUD ARBITRAIRE DE LA SECTION

C           ET DE L' EQUATION 2 :
C                G*LAPLACIEN(PSI_Y) = -Y*TY/IZ     DANS LA SECTION
C       AVEC     D(PSI_Y )/DN = 0     SUR LE CONTOUR DE LA SECTION
C       ET       PSI_Y = 0    EN UN NOEUD ARBITRAIRE DE LA SECTION

C               AY = 2*S*U1_Y/TY**2
C               AZ = 2*S*U1_Z/TZ**2
C       AVEC U1_Y = 0.5*SOMME_S(G*(GRAD(PSI_Y)**2).DS)
C       AVEC U1_Z = 0.5*SOMME_S(G*(GRAD(PSI_Z)**2).DS)

C          X DESIGNE L'AXE DE LA POUTRE
C          Y ET Z DESIGNENT LES AXES PRINCIPAUX D'INERTIE DE LA SECTION
C          L'ORIGINE EST SITUEE AU CENTRE DE GRAVITE DE LA SECTION
C          N DESIGNE LE VECTEUR NORMAL A LA FRONTIERE

C         TY ET TZ DESIGNENT LES EFFORTS TRANCHANTS
C         ON PREND TY = 1 ET TZ = 1
C         G EST LE MODULE DE CISAILLEMENT
C         ON FAIT L'HYPOTHESE QUE LE MATERIAU EST ISOTROPE
C         DANS CE CAS G N'INTERVIENT PAS
C         IY ET IZ SONT LES MOMENTS D'INERTIE PRINCIPAUX PAR-RAPPORT
C         AUX AXES Y ET Z

C          .LES COORDONNEES DU CENTRE DE TORSION/CISAILLEMENT
C           SONT EGALES A :
C             EY =  MX0_Y/TZ
C             EZ = -MX0_Z/TY

C           AVEC MX0_Y = SOMME_S((SIGMA_XZ*Y - SIGMA_XY*Z).DS)
C           SACHANT QUE SIGMA_XY = G*D(PSI_Z)/DY
C                   ET  SIGMA_XZ = G*D(PSI_Z)/DZ

C           ET  MX0_Z = SOMME_S((SIGMA_XZ*Y - SIGMA_XY*Z).DS)
C           SACHANT QUE SIGMA_XY = G*D(PSI_Y)/DY
C                   ET  SIGMA_XZ = G*D(PSI_Y)/DZ

C     OPTION : 'CARA_CISA'

C   ARGUMENT        E/S  TYPE         ROLE
C    CHGEOZ         IN    K*      COORDONNEES DES CONNECTIVITES
C                                 DANS LE REPERE PRINCIPAL D'INERTIE
C    IY             IN    R       MOMENT D'INERTIE PRINCIPAL/Y
C    IZ             IN    R       MOMENT D'INERTIE PRINCIPAL/Z
C    S              IN    R       SURFACE DE LA SECTION
C    ALPHA          IN    R       ANGLE FAISANT PASSER DU REPERE
C                                 PRINCIPAL D'INERTIE AU REPERE GLOBAL
C    XG             IN    R       ABSCISSE DU CENTRE DE GRAVITE DE
C                                 LA SECTION DANS LE REPERE GLOBAL
C    YG             IN    R       ORDONNEE DU CENTRE DE GRAVITE DE
C                                 LA SECTION DANS LE REPERE GLOBAL
C    TEMP1Z         IN    K*      RESULTAT DE TYPE EVOL_THER
C                                 REFERENCANT LE CHAMP DE SCALAIRES
C                                 SOLUTION DE L'EQUATION 2
C    TEMP2Z         IN    K*      RESULTAT DE TYPE EVOL_THER
C                                 REFERENCANT LE CHAMP DE SCALAIRES
C                                 SOLUTION DE L'EQUATION 1
C    AY             OUT   R       INVERSE DU COEFFICIENT DE
C                                 CISAILLEMENT DEFINI CI-DESSUS
C    AZ             OUT   R       INVERSE DU COEFFICIENT DE
C                                 CISAILLEMENT DEFINI CI-DESSUS
C    EY             OUT   R       COORDONNEE SELON Y DU CENTRE DE
C                                 CISAILLEMENT/TORSION DANS LE REPERE
C                                 PRINCIPAL D'INERTIE
C    EZ             OUT   R       COORDONNEE SELON Z DU CENTRE DE
C                                 CISAILLEMENT/TORSION DANS LE REPERE
C                                 PRINCIPAL D'INERTIE
C    PCTX           OUT   R       ABSCISSE DU CENTRE DE
C                                 CISAILLEMENT/TORSION DANS LE REPERE
C                                 GLOBAL
C    PCTY           OUT   R       ORDONNEE DU CENTRE DE
C                                 CISAILLEMENT/TORSION DANS LE REPERE
C                                 GLOBAL

C.========================= DEBUT DES DECLARATIONS ====================
C -----  ARGUMENTS
      CHARACTER*(*) CHGEOZ,TEMP1Z,TEMP2Z
      REAL*8 IY,IZ
C -----  VARIABLES LOCALES
      CHARACTER*8 LPAIN(3),LPAOUT(1)
      CHARACTER*8 TEMPE1,TEMPE2
      CHARACTER*8 CRIT,MODELE,K8BID
      CHARACTER*19 PRCHNO
      CHARACTER*14 TYPRE1,TYPRE2
      CHARACTER*19 KNUM1,KNUM2,LIGRTH
      CHARACTER*24 LCHIN(3),LCHOUT(1),CHGEOM
      CHARACTER*24 CHTEM1,CHTEM2
      REAL*8 WORK(9),P(2,2)
      COMPLEX*16 CBID
C.========================= DEBUT DU CODE EXECUTABLE ==================

C ---- INITIALISATIONS
C      ---------------
      ZERO = 0.0D0
      PREC = 1.0D-3
      CHGEOM = CHGEOZ
      TEMPE1 = TEMP1Z
      TEMPE2 = TEMP2Z
      KNUM1 = '&&PECAP2.NUME_ORD_1'
      KNUM2 = '&&PECAP2.NUME_ORD_2'
      CRIT = 'RELATIF'

      DO 10 I = 1,9
        WORK(I) = ZERO
   10 CONTINUE

C --- ON VERIFIE QUE LES RESULTATS SONT DE TYPE EVOL_THER :
C     ---------------------------------------------------
      CALL DISMOI('F','TYPE_RESU',TEMPE1,'RESULTAT',IBID,TYPRE1,IERD)
      IF (TYPRE1.NE.'EVOL_THER') THEN
        CALL U2MESS('F','UTILITAI3_54')
      END IF

      CALL DISMOI('F','TYPE_RESU',TEMPE2,'RESULTAT',IBID,TYPRE2,IERD)
      IF (TYPRE2.NE.'EVOL_THER') THEN
        CALL U2MESS('F','UTILITAI3_55')
      END IF

C --- RECUPERATION DU NOMBRE D'ORDRES DES RESULTATS :
C     ---------------------------------------------
      CALL RSUTNU(TEMPE1,' ',0,KNUM1,NBORDR,PREC,CRIT,IRET)
      IF (NBORDR.NE.1) THEN
        CALL U2MESK('F','UTILITAI3_56',1,TEMPE1)
      END IF

      CALL RSUTNU(TEMPE2,' ',0,KNUM2,NBORDR,PREC,CRIT,IRET)
      IF (NBORDR.NE.1) THEN
        CALL U2MESK('F','UTILITAI3_56',1,TEMPE2)
      END IF

C --- RECUPERATION DES CHAMPS DE TEMPERATURES DES RESULTATS :
C     -----------------------------------------------------
      CALL RSEXCH(TEMPE1,'TEMP',0,CHTEM1,IRET)
      IF (IRET.GT.0) THEN
        CALL U2MESK('F','UTILITAI3_52',1,TEMPE1)
      END IF

      CALL RSEXCH(TEMPE2,'TEMP',0,CHTEM2,IRET)
      IF (IRET.GT.0) THEN
        CALL U2MESK('F','UTILITAI3_52',1,TEMPE2)
      END IF

C --- RECUPERATION DU NUME_DDL ASSOCIE AU CHAMP DE TEMPERATURES :
C     ---------------------------------------------------------
      CALL DISMOI('F','PROF_CHNO',CHTEM1,'CHAM_NO',IBID,PRCHNO,IERD)

C --- RECUPERATION DU MODELE ASSOCIE AU NUME_DDL  :
C     ------------------------------------------
      CALL DISMOI('F','NOM_MODELE',PRCHNO,'PROF_CHNO',IBID,MODELE,IERD)

C --- RECUPERATION DU LIGREL DU MODELE  :
C     --------------------------------
      CALL DISMOI('F','NOM_LIGREL',MODELE,'MODELE',IBID,LIGRTH,IERD)

C --- CALCUL POUR CHAQUE ELEMENT DE LA SECTION DES 4 INTEGRALES
C --- SUIVANTES PERMETTANT LA DETERMINATION DU CENTRE DE
C --- TORSION/CISAILLEMENT ET DES COEFFICIENTS DE CISAILLEMENT :
C ---   I1 = SOMME_S_ELEMENT((D(PSI_Y)/DZ*Y - D(PSI_Y)/DY*Z).DS)
C ---   I2 = SOMME_S_ELEMENT((D(PSI_Z)/DZ*Y - D(PSI_Z)/DY*Z).DS)
C ---   I3 = SOMME_S_ELEMENT((D(PSI_Y)/DY**2 + D(PSI_Y)/DZ**2).DS)
C ---   I4 = SOMME_S_ELEMENT((D(PSI_Z)/DY**2 + D(PSI_Z)/DZ**2).DS)
C       ----------------------------------------------------------
      LPAIN(1) = 'PGEOMER'
      LCHIN(1) = CHGEOM
      LPAIN(2) = 'PTEMPE1'
      LCHIN(2) = CHTEM1
      LPAIN(3) = 'PTEMPE2'
      LCHIN(3) = CHTEM2
      LPAOUT(1) = 'PCASECT'
      LCHOUT(1) = '&&PECAP2.INTEG'

      CALL CALCUL('S','CARA_CISA',LIGRTH,3,LCHIN,LPAIN,1,LCHOUT,LPAOUT,
     &            'V')

C --- SOMMATION DES INTEGRALES PRECEDENTES SUR LA SECTION DE LA POUTRE
C ---   WORK(1) = SOMME_SECTION((D(PSI_Z)/DZ*Y - D(PSI_Z)/DY*Z).DS)
C ---   WORK(2) = SOMME_SECTION((D(PSI_Y)/DZ*Y - D(PSI_Y)/DY*Z).DS)
C ---   WORK(3) = SOMME_SECTION((D(PSI_Z)/DY**2 + D(PSI_Z)/DZ**2).DS)
C ---   WORK(4) = SOMME_SECTION((D(PSI_Y)/DY**2 + D(PSI_Y)/DZ**2).DS)
C       ------------------------------------------------------------
      CALL MESOMM(LCHOUT(1),9,IBID,WORK,CBID,0,IBID)
      EY = WORK(1)/IY
      EZ = WORK(2)/IZ
      AY = WORK(4)*S/IZ/IZ
      AZ = WORK(3)*S/IY/IY

C --- PASSAGE DE L'ANGLE FORME PAR LES AXES GLOBAUX AVEC LES AXES
C --- PRINCIPAUX D'INERTIE DE DEGRES EN RADIANS :
C     -----------------------------------------
      ALPHAR = ALPHA*R8DGRD()

C --- CONSTITUTION DE LA MATRICE DE PASSAGE DU REPERE GLOBAL
C --- AU REPERE D'INERTIE :
C     -------------------
      P(1,1) = COS(ALPHAR)
      P(2,1) = -SIN(ALPHAR)
      P(1,2) = SIN(ALPHAR)
      P(2,2) = COS(ALPHAR)

C --- VALEURS DES COORDONNEES DU CENTRE DE TORSION DANS LE REPERE
C --- GLOBAL INITIAL :
C     --------------
      PCTX = P(1,1)*EY + P(2,1)*EZ + XG
      PCTY = P(1,2)*EY + P(2,2)*EZ + YG

      CALL DETRSD('CHAMP_GD','&&PECAP2.INTEG')
C.============================ FIN DE LA ROUTINE ======================
      END
