      SUBROUTINE RSADPA(NOMSD,CEL,NPARA,LPARA,IORDR,ITYPE,LJEVEU,CTYPE)
      IMPLICIT   NONE
      INTEGER NPARA,IORDR,ITYPE,LJEVEU(*)
      CHARACTER*1 CEL
      CHARACTER*(*) NOMSD,LPARA(*),CTYPE(*)
C ----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF UTILITAI  DATE 08/06/2004   AUTEUR CIBHHLV L.VIVAN 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C RESPONSABLE VABHHTS J.PELLET

C      RECUPERATION DES ADRESSES JEVEUX DES PARAMETRES DE CALCUL
C      (OU DES VARIABLES D'ACCES)
C      D'UN RESULTAT-COMPOSE POUR LE NUMERO D'ORDRE : IORDR
C      ET POUR LA LISTE DE VARIABLES DE NOMS SYMBOLIQUES LPARA.
C ----------------------------------------------------------------------
C IN  : NOMSD  : NOM DE LA STRUCTURE "RESULTAT".
C IN  : CEL    : CONDITION D'ACCES AUX PARAMETRES :
C                    'L' : LECTURE, 'E' : ECRITURE.
C IN  : NPARA  : NOMBRE DE PARAMETRES CHERCHES.
C IN  : LPARA  : LISTE DES NOMS SYMBOLIQUES DES PARAMETRES.
C IN  : IORDR  : NUMERO D'ORDRE SOUHAITE.
C IN  : ITYPE  : CODE INDIQUANT QUE L'ON DESIRE LE TYPE
C                     = 0  PAS DE TYPE
C                    /= 0  ON FOURNIT LE TYPE
C OUT : LJEVEU : LISTE DES ADRESSES JEVEUX DANS ZI,ZR,...
C OUT : CTYPE  : CODE DU TYPE
C               R REAL,I INTEGER,C COMPLEXE,K8 K16 K24 K32 K80 CHARACTER
C-----------------------------------------------------------------------
C --------------- COMMUNS NORMALISES  JEVEUX  --------------------------
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
      CHARACTER*32 JEXNUM,JEXNOM,JEXATR,JEXR8
C ---------------- FIN COMMUNS NORMALISES  JEVEUX  --------------------
      INTEGER IBID,NBORDR,NRANG,JORDR,I,IPARA,IER,IRANG
      CHARACTER*8 K8B,NOMOBJ,K8DEBU,K8MAXI
      CHARACTER*16 PARAM,K16B
      CHARACTER*19 NOMS2
C ----------------------------------------------------------------------

      NOMS2 = NOMSD

C     --- RECUPERATION DU NUMERO DE RANGEMENT ---
      CALL RSUTRG(NOMSD,IORDR,IRANG)

      IF (CEL.EQ.'L') THEN
        IF (IRANG.EQ.0) THEN
          CALL UTDEBM('F','RSADPA','LE NUMERO D''ORDRE N''EXISTE PAS')
          CALL UTIMPI('S','    NUMERO : ',1,IORDR)
          CALL UTFINM()
        END IF
      ELSE
        IF (IRANG.EQ.0) THEN
          CALL JELIRA(NOMS2//'.ORDR','LONMAX',NBORDR,K8B)
          CALL JELIRA(NOMS2//'.ORDR','LONUTI',NRANG,K8B)
          NRANG = NRANG + 1
          IF (NRANG.GT.NBORDR) THEN
            CALL UTDEBM('F','RSADPA',' ')
            CALL UTIMPI('L','NUMERO D ARCHIVAGE: ',1,IORDR)
            CALL UTIMPI('S','SUPERIEUR AU MAX:,',1,NBORDR)
            CALL UTFINM()
          END IF
          CALL JEECRA(NOMS2//'.ORDR','LONUTI',NRANG,' ')
          CALL JEVEUO(NOMS2//'.ORDR','E',JORDR)
          ZI(JORDR+NRANG-1) = IORDR
          IRANG = NRANG
        END IF
      END IF

      CALL JELIRA(JEXNUM(NOMS2//'.TACH',1),'LONMAX',NBORDR,K8B)
      IF (IRANG.GT.NBORDR) THEN
        CALL UTDEBM('F','RSADPA',' ')
        CALL UTIMPI('L','NUMERO DE RANGEMENT : ',1,IRANG)
        CALL UTIMPI('S','SUPERIEUR AU MAX:,',1,NBORDR)
        CALL UTFINM()
      END IF

      DO 10,I = 1,NPARA
        PARAM = LPARA(I)
        CALL JENONU(JEXNOM(NOMS2//'.NOVA',PARAM),IPARA)
        IF (IPARA.EQ.0) THEN
          CALL DISMOI('F','TYPE_RESU',NOMSD,'RESULTAT',IBID,K16B,IER)
          CALL JEIMPO('RESULTAT',NOMS2//'.NOVA',' ',' ')
          CALL UTDEBM('F','RSADPA','VARIABLE INCONNUE: ')
          CALL UTIMPK('L','VARIABLE : ',1,PARAM)
          CALL UTIMPK('S','POUR LE TYPE : ',1,K16B)
          CALL UTFINM()
        END IF

        CALL EXTRS3(NOMS2,PARAM,IRANG,CEL,ITYPE,CTYPE(I),LJEVEU(I))

   10 CONTINUE

      END
