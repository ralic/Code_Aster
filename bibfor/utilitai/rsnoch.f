      SUBROUTINE RSNOCH(NOMSD,NOMSY,IORDR)
      IMPLICIT NONE
      INCLUDE 'jeveux.h'

      CHARACTER*32 JEXNUM,JEXNOM
      INTEGER IORDR
      CHARACTER*(*) NOMSD,NOMSY
C ----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF UTILITAI  DATE 09/11/2012   AUTEUR DELMAS J.DELMAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE PELLET J.PELLET

C  BUT : "NOTER" UN CHAMP DANS UNE SD_RESULTAT
C        ON VERIFIE QUE :
C           - LA PLACE EST LICITE (NOMSY OK ET IORDR<=NBORDR_MAX)
C           - LE CHAMP QUI VA ETRE "NOTE" DANS NOMSD
C             (REGLE DE NOMMAGE DE RSUTCH.F)
C             EXISTE REELLEMENT (EXISD.F)
C ----------------------------------------------------------------------
C IN  : NOMSD  : NOM DE LA STRUCTURE "RESULTAT"
C IN  : NOMSY  : NOM SYMBOLIQUE DU CHAMP A NOTER.
C IN  : IORDR  : NUMERO D'ORDRE DU CHAMP A NOTER.
C ----------------------------------------------------------------------

      CHARACTER*16 NOMS2
      CHARACTER*19 NOMD2,CHNOTE
      CHARACTER*24 VALK(2)
      CHARACTER*8  REPK
      CHARACTER*1  K1BID
      INTEGER NORMAX,IRETOU,NORDR,IRANG,JORDR,IRET,IBID,JTACH
C ----------------------------------------------------------------------

      CALL JEMARQ()

      NOMS2 = NOMSY
      NOMD2 = NOMSD


C     -- CALCUL ET VALIDATION DU NUMERO DE RANGEMENT :IRANG
C     -----------------------------------------------------
      CALL JELIRA(NOMD2//'.ORDR','LONMAX',NORMAX,K1BID)
      CALL RSUTRG(NOMD2,IORDR,IRETOU,NORDR)
      IF (IRETOU.EQ.0) THEN
        IRANG = NORDR + 1
        IF (IRANG.GT.NORMAX) CALL U2MESS('F','UTILITAI4_42')
        CALL JEECRA(NOMD2//'.ORDR','LONUTI',IRANG,' ')
        CALL JEVEUO(NOMD2//'.ORDR','E',JORDR)
C       -- ON VERIFIE QUE LE NOUVEAU IORDR EST SUPERIEUR
C          AU DERNIER IORDR DEJA STOCKE (IORDR CROISSANTS) :
        IF ( IRANG.GT.1 ) THEN
          CALL ASSERT(ZI(JORDR+IRANG-2).LT.IORDR)
        ENDIF
        ZI(JORDR-1+IRANG) = IORDR
      ELSE
        IRANG = IRETOU
      END IF


C     -- ON VERIFIE LE NOM SYMBOLIQUE :
C     -------------------------------------------
      CALL JENONU(JEXNOM(NOMD2//'.DESC',NOMS2),IRET)
      IF (IRET.EQ.0) THEN
        VALK(1) = NOMS2
        VALK(2) = NOMD2
        CALL U2MESK('F','UTILITAI4_43', 2 ,VALK)
      ENDIF


C     -- CHNOTE : NOM QUE DOIT AVOIR LE CHAMP A NOTER :
C        (REGLE DE NOMMAGE DE RSUTCH.F)
C     -------------------------------------------------
      CALL RSEXCH(' ',NOMD2,NOMS2,IORDR,CHNOTE,IRET)

C     -- ON VERIFIE L'EXISTENCE DE CHNOTE :
C     -------------------------------------------
      IF (IRET.EQ.100) CALL U2MESK('F','UTILITAI_55',1,CHNOTE)
      CALL ASSERT(IRET.EQ.0)


C     --- ON STOCKE LE NOM DU CHAMP :
C     ------------------------------
      CALL JENONU(JEXNOM(NOMD2//'.DESC',NOMS2),IBID)
      CALL JEVEUO(JEXNUM(NOMD2//'.TACH',IBID),'E',JTACH)

      ZK24(JTACH+IRANG-1)(1:19) = CHNOTE


C     -- SI LE CHAMP EST UN CHAM_ELEM MPI_INCOMPLET, ON LE COMPLETE:
C     --------------------------------------------------------------
      CALL DISMOI('F','TYPE_CHAMP',CHNOTE,'CHAMP',IBID,REPK,IRET)
      IF (REPK(1:2).EQ.'EL') CALL SDMPIC('CHAM_ELEM',CHNOTE)

      CALL JEDEMA()
      END
