      SUBROUTINE PSTYPA ( NBPASE, INPSCO, NOSIMP, NOPASE, TYPEPS )
C
C     PARAMETRES SENSIBLES - TYPE DE SENSIBILITE - ARCHIVAGE
C     *          *           ***                   *
C ----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF UTILITAI  DATE 29/09/2006   AUTEUR VABHHTS J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2002  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE GNICOLAS G.NICOLAS
C ----------------------------------------------------------------------
C     ARCHIVE LE TYPE DE SENSIBILITE CORRESPONDANT A UN PARAMETRE
C     DANS LA STRUCTURE INPSCO
C ----------------------------------------------------------------------
C IN  NBPASE  : NOMBRE DE PARAMETRES SENSIBLES
C IN  INPSCO  : STRUCTURE CONTENANT LA LISTE DES NOMS
C               VOIR LA DEFINITION DANS SEGICO
C IN  NOSIMP  : NOM SIMPLE IMPACTE PAR LA DERIVATION
C IN  NOPASE  : NOM DU PARAMETRE DE SENSIBILITE
C IN  TYPEPS  : TYPE DE SENSIBILITE POUR CE PARAMETRE
C
C ----------------------------------------------------------------------
C     ARBORESCENCE DE LA GESTION DES PARAMETRES SENSIBLES :
C  NTTYSE --!
C  METYSE --> PSTYSE --> PSTYPR --> SEGICO
C
C  NTDOTH --!
C  NMDOME --> PSTYPA --> SEGICO
C                    --> PSREMC
C ----------------------------------------------------------------------
C
      IMPLICIT   NONE
C
C 0.1. ==> ARGUMENTS
C
      INTEGER NBPASE
C
      CHARACTER*(*) NOSIMP, NOPASE, TYPEPS
      CHARACTER*(*) INPSCO
C
C 0.2. ==> COMMUNS
C
C --- DEBUT DECLARATIONS NORMALISEES JEVEUX ----------------------------
C
      INTEGER            ZI
      COMMON  / IVARJE / ZI(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C --- FIN DECLARATIONS NORMALISEES JEVEUX ------------------------------
C
C 0.3. ==> VARIABLES LOCALES
C
      CHARACTER*6 NOMPRO
      PARAMETER ( NOMPRO = 'PSTYPA' )
C
      INTEGER NBPSCO
C
      INTEGER LXLGUT
C
      INTEGER ADPSCO, ADRAUX, NRPASE
      INTEGER IAUX, JAUX
      INTEGER ADMOSI, ADVALE, ADMOFA
      INTEGER ADTSEK, ADTSKK, ADTSEI, ADTSII
      INTEGER NBMOSI, NBCONT
C
      CHARACTER*4 SAUX04
      CHARACTER*8 SAUX08
      CHARACTER*24 SAUX24
      CHARACTER*24 BLAN24
      CHARACTER*24 STTSEK, STTSEI
      CHARACTER*24 LIMOSI, LIVALE, LIMOFA
C
      LOGICAL LOGAUX
C
C                    123456789012345678901234
      DATA BLAN24 / '                        ' /
C
C====
C 1. ECRITURE/LECTURE AU BON ENDROIT ...
C====
C
C 1.1. ==> CARACTERISTIQUES DE LA STRUCTURE DE SAUVEGARDE
C
C 1.1.1. ==> NOMBRE D'INFORMATIONS ENREGISTREES : NBPSCO
C
      CALL SEGICO ( 2,
     &              SAUX08, IAUX, SAUX24, SAUX24,
     &              INPSCO, NBPSCO, JAUX )
C
C 1.1.2. ==> ADRESSE DE LA STRUCTURE DE MEMORISATION : ADPSCO
C
      CALL SEGICO ( 3,
     &              SAUX08, IAUX, SAUX24, SAUX24,
     &              INPSCO, ADPSCO, JAUX )
C
C 1.2. ==> PARMI TOUS LES PARAMETRES SENSIBLES ENREGISTRES, ON CHERCHE
C          OU EST CELUI QUI NOUS INTERESSE ICI. ON REPERE SON NUMERO.
C          SI ON NE LE TROUVE PAS, PROBLEME ...
C
      DO 12 , IAUX = 1 , NBPASE
C
        ADRAUX = ADPSCO + (NBPSCO+1)*IAUX
        IF ( NOPASE.EQ.ZK24(ADRAUX+1) ) THEN
          NRPASE = IAUX
          GOTO 20
        ENDIF
C
   12 CONTINUE
C
      CALL U2MESK('A','UTILITAI3_98',1,NOPASE(1:8))
      CALL U2MESS('F','MODELISA_67')
C
C====
C 2. ARCHIVAGE
C====
C
   20 CONTINUE
C
C 2.1. ==> RECUPERATION DES MOTS-CLES ET VALEURS ASSOCIES AU COUPLE
C          (NOM SIMPLE,PARAMETRE SENSIBLE)
C          LE RESULTAT EST SOUS LA FORME DE TROIS LISTES DE K80
C          EVOLUANT EN PARALLELE. LEUR LONGUEUR EST NBMOSI. ELLES
C          SONT ALLOUEES EN-DESSOUS DE PSREMC
C
C               12   345678   9012345
      LIMOSI = '&&'//NOMPRO//'_LIMOSI'
      LIVALE = '&&'//NOMPRO//'_LIVALE'
      LIMOFA = '&&'//NOMPRO//'_LIMOFA'
C
      CALL PSREMC ( NOSIMP, NOPASE,
     &              NBMOSI, LIMOSI, LIVALE, LIMOFA, IAUX )
C
C 2.2. ==> LE NOM DES STRUCTURES QUI MEMORISENT LE TYPE DE SENSIBILITE
C          POUR CE PARAMETRE ET LA COMMANDE EN COURS
C
C 2.2.1. ==> LA BASE DU NOM DES STRUCTURES
C            ELLE EST CREEE AU PREMIER COUP, ET RECUPEREE ENSUITE
C
      IF ( ZK24(ADRAUX+2).EQ.BLAN24 ) THEN
C
        CALL CODENT ( NRPASE, 'D0', SAUX04 )
        IAUX = LXLGUT(INPSCO)
        SAUX24 = BLAN24
        SAUX24(1:IAUX+4) = INPSCO(1:IAUX)//SAUX04
C                                          1234
        ZK24(ADRAUX+2) = SAUX24
        LOGAUX = .TRUE.
C
      ELSE
C
        SAUX24 = ZK24(ADRAUX+2)
        LOGAUX = .FALSE.
C
      ENDIF
C
C 2.2.2. ==> LES NOMS COMPLETS
C
      IAUX = LXLGUT(SAUX24)
C
      STTSEI = SAUX24
      STTSEI(IAUX+1:IAUX+2) = '.I'
C
      STTSEK = SAUX24
      STTSEK(IAUX+1:IAUX+4) = '.K80'
C
C 2.3. ==> ALLOCATION
C 2.3.1. ==> AU PREMIER COUP, ON CREE LES DEUX STRUCTURES
C
      IF ( LOGAUX ) THEN
C
        NBCONT = 1
        CALL WKVECT ( STTSEI,'V V I',NBCONT,ADTSEI )
        CALL JEECRA ( STTSEI,'LONUTI',NBCONT,SAUX08 )
        ADTSII = ADTSEI
C
        CALL WKVECT ( STTSEK,'V V K80',3*NBMOSI+1,ADTSEK )
        CALL JEECRA ( STTSEK,'LONUTI',3*NBMOSI+1,SAUX08 )
        ADTSKK = ADTSEK - 1
C
C 2.3.2. ==> AUX PASSAGES SUIVANTS, ON ALLONGE LES STRUCTURES
C
      ELSE
C
        CALL JELIRA ( STTSEI, 'LONUTI', NBCONT, SAUX08 )
        NBCONT = NBCONT + 1
        CALL JUVECA ( STTSEI,NBCONT )
        CALL JEECRA ( STTSEI,'LONUTI',NBCONT,SAUX08 )
        CALL JEVEUO ( STTSEI, 'E', ADTSEI )
        ADTSII = ADTSEI + NBCONT - 1
C
        CALL JELIRA ( STTSEK, 'LONUTI', IAUX, SAUX08 )
        IAUX = IAUX + 3*NBMOSI + 1
        CALL JUVECA ( STTSEK,IAUX )
        CALL JEECRA ( STTSEK,'LONUTI',IAUX,SAUX08 )
        CALL JEVEUO ( STTSEK, 'E', ADTSEK )
        ADTSKK = ADTSEK + IAUX - 3*NBMOSI - 2
C
      ENDIF
C
C 2.4. ==> ON REMPLIT LES STRUCTURES POUR CETTE CONTRIBUTUION
C
C 2.4.1. ==> LE NOMBRE DE MOTS-CLES SIMPLES ASSOCIES A CE PARAMETRE
C
      ZI(ADTSII) = NBMOSI
C
C 2.4.2. ==> LE TYPE DE PARAMETRE SENSIBLE
C
      IAUX = LXLGUT(TYPEPS)
      ADTSKK = ADTSKK + 1
      ZK80(ADTSKK)(1:IAUX) = TYPEPS(1:IAUX)
C
C 2.4.3. ==> LES NBMOSI TRIPLETS (MOT-CLE,VALEUR, MOT-CLE FACTEUR)
C
      IF ( NBMOSI.GT.0 ) THEN
C
        CALL JEVEUO ( LIMOSI, 'L', ADMOSI )
        CALL JEVEUO ( LIVALE, 'L', ADVALE )
        CALL JEVEUO ( LIMOFA, 'L', ADMOFA )
        DO 24 , IAUX = 1 , NBMOSI
          ADTSKK = ADTSKK + 1
          ZK80(ADTSKK) = ZK80(ADMOSI+IAUX-1)
          ADTSKK = ADTSKK + 1
          ZK80(ADTSKK) = ZK80(ADVALE+IAUX-1)
          ADTSKK = ADTSKK + 1
          ZK80(ADTSKK) = ZK80(ADMOFA+IAUX-1)
   24   CONTINUE
C
      ENDIF
C
C 2.5. ==> MENAGE
C
      CALL JEDETR ( LIMOSI )
      CALL JEDETR ( LIVALE )
      CALL JEDETR ( LIMOFA )
C
      END
