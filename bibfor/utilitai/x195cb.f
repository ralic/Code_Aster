      SUBROUTINE X195CB(TYCHR,NOMGD,CHOU)
      IMPLICIT  NONE
C     -----------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF UTILITAI  DATE 07/09/2009   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2009  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE PELLET J.PELLET
C     BUT : TRAITER :
C          - OPTION 'COMB' DE LA COMMANDE CREA_CHAMP
C     -----------------------------------------------------------------

C     ----- DEBUT COMMUNS NORMALISES  JEVEUX --------------------------
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
      CHARACTER*32 JEXNUM,JEXNOM
C     -----  FIN  COMMUNS NORMALISES  JEVEUX --------------------------

      CHARACTER*8 CHOU
      CHARACTER*4 TYCHR,TYCH1
      INTEGER IB,NBOCC,IOCC,IBID,K,JVALE1,JVALE2,N1,N2
      REAL*8 COEFR
      CHARACTER*8 NOMGD,NOMGD1,K8B,MA1,MA2
      CHARACTER*8 KBID
      CHARACTER*3 TSCA
      CHARACTER*19 CH1,CH2,PFCN1,PFCN2
      LOGICAL IDENSD

C     -----------------------------------------------------------------

      CALL JEMARQ()
      CALL GETFAC('COMB',NBOCC)
      CALL ASSERT(NBOCC.GE.1)
      CALL ASSERT(TYCHR.EQ.'NOEU')


C     -- 1. CREATION DU CHAMP "OUT" SUR LE MODELE DU 1ER CHAMP "IN"
C     -------------------------------------------------------------
C     -- ATTENTION : LA COMMANDE PEUT ETRE REENTRANTE,
C        IL NE FAUT PAS DETRUIRE CHOU TOUT DE SUITE.
      CALL GETVID('COMB','CHAM_GD',1,1,1,CH1,IB)
      CH2='&&X195CB.CHOU'
      CALL COPISD('CHAMP','V',CH1,CH2)
C     -- INITIALISATION A ZERO :
      CALL JEVEUO(CH2//'.VALE','E',JVALE2)
      CALL JELIRA(CH2//'.VALE','LONMAX',N1,KBID)
      CALL JERAZO(CH2//'.VALE',N1,1)
      CALL DISMOI('F','NOM_MAILLA',CH2,'CHAMP',IB,MA2,IB)
      CALL DISMOI('F','PROF_CHNO',CH2,'CHAM_NO',IB,PFCN2,IB)


C     -- 2. CALCUL DU CONTENU DE CH2 :
C     ---------------------------------
      DO 10,IOCC = 1,NBOCC
        CALL GETVID('COMB','CHAM_GD',IOCC,1,1,CH1,IB)

C       -- QUELQUES VERIFICATIONS DE COHERENCE :
        CALL DISMOI('F','TYPE_CHAMP',CH1,'CHAMP',IB,TYCH1,IB)
        IF (TYCH1.NE.'NOEU') CALL U2MESS('F','MODELISA5_10')

        CALL DISMOI('F','NOM_MAILLA',CH1,'CHAMP',IB,MA1,IB)
        IF (MA1.NE.MA2) CALL U2MESS('F','MODELISA5_13')

        CALL DISMOI('F','PROF_CHNO',CH1,'CHAM_NO',IB,PFCN1,IB)
        IF (.NOT.IDENSD('PROF_CHNO',PFCN1,PFCN2))
     &       CALL U2MESS('F','MODELISA5_12')

        CALL JELIRA(CH1//'.VALE','LONMAX',N2,KBID)
        IF (N2.NE.N1) CALL ASSERT(.FALSE.)

        CALL DISMOI('F','NOM_GD',CH1,'CHAMP',IB,NOMGD1,IB)
        IF (NOMGD1.NE.NOMGD) CALL U2MESS('F','MODELISA5_11')

        CALL DISMOI('F','TYPE_SCA',NOMGD1,'GRANDEUR',IB,TSCA,IB)
        CALL ASSERT(TSCA.EQ.'R'.OR.TSCA.EQ.'C')

C       -- CUMUL DES VALEURS :
        CALL JEVEUO(CH1//'.VALE','L',JVALE1)
        CALL GETVR8('COMB','COEF_R',IOCC,1,1,COEFR,IB)
        CALL ASSERT(IB.EQ.1)
        IF (TSCA.EQ.'R') THEN
          DO 11, K=1,N1
            ZR(JVALE2-1+K)=ZR(JVALE2-1+K)+COEFR*ZR(JVALE1-1+K)
 11       CONTINUE
        ELSEIF (TSCA.EQ.'C') THEN
          DO 12, K=1,N1
            ZC(JVALE2-1+K)=ZC(JVALE2-1+K)+COEFR*ZC(JVALE1-1+K)
 12       CONTINUE
        ENDIF
 10   CONTINUE


C     -- RECOPIE DE CH2 DANS CHOU :
C     -------------------------------
      CALL COPISD('CHAMP','G',CH2,CHOU)
      CALL DETRSD('CHAMP',CH2)

      CALL JEDEMA()
      END
