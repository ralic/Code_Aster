      SUBROUTINE PSTYPR ( NBPASE, INPSCO, NOPASE,
     >                    TYPEPS, NBMOSI, LIMOSI, LIVALE, LIMOFA )
C
C     PARAMETRES SENSIBLES - TYPE DE SENSIBILITE - RECUPERATION
C     *          *           ***                   *
C ----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF UTILITAI  DATE 01/07/2003   AUTEUR GNICOLAS G.NICOLAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2002  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE GNICOLAS G.NICOLAS
C ----------------------------------------------------------------------
C     RECUPERE LE TYPE DE SENSIBILITE CORRESPONDANT A UN PARAMETRE
C     DANS LA STRUCTURE INPSCO
C ----------------------------------------------------------------------
C IN  NBPASE  : NOMBRE DE PARAMETRES SENSIBLES
C IN  INPSCO  : STRUCTURE CONTENANT LA LISTE DES NOMS
C               VOIR LA DEFINITION DANS SEGICO
C IN  NOPASE  : NOM DU PARAMETRE DE SENSIBILITE
C OUT TYPEPS  : TYPE DE SENSIBILITE POUR CE PARAMETRE
C OUT NBMOSI  : NOMBRE DE MOTS-CLES SIMPLES CONCERNES
C OUT LIMOSI  : NOM DE LA STRUCTURE K80 CONTENANT LES MOTS_CLES SIMPLES
C OUT LIVALE  : NOM DE LA STRUCTURE K80 CONTENANT LES VALEURS ASSOCIEES
C OUT LIMOFA  : LA STRUCTURE K80 CONTENANT LES MOTS-CLES FACTEURS
C ----------------------------------------------------------------------
C
C     ARBORESENCE :
C  NTTYSE --!
C  METYSE --> PSTYSE --> PSTYPR --> SEGICO
C ----------------------------------------------------------------------
C
      IMPLICIT   NONE
C
C 0.1. ==> ARGUMENTS
C
      INTEGER NBPASE, NBMOSI
C
      CHARACTER*(*) NOPASE, TYPEPS, LIMOSI, LIVALE, LIMOFA
      CHARACTER*(*) INPSCO
C
C 0.2. ==> COMMUNS
C
C --- DEBUT DECLARATIONS NORMALISEES JEVEUX ----------------------------
C
      INTEGER            ZI
      COMMON  / IVARJE / ZI(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C --- FIN DECLARATIONS NORMALISEES JEVEUX ------------------------------
C
C 0.3. ==> VARIABLES LOCALES
C
      CHARACTER*6 NOMPRO
      PARAMETER ( NOMPRO = 'PSTYPR' )
C
      INTEGER NBPSCO
C
      INTEGER LXLGUT
C
      INTEGER ADPSCO, ADRAUX
      INTEGER NRPASE, NBCONT
      INTEGER ADTSEK, ADTSKK, ADTSEI, ADTSII
      INTEGER ADMOSI, ADMOMM, ADVALE, ADVAVV, ADMOFA, ADMOFF
      INTEGER NBMCMX, NBMCPA
      INTEGER LGTYPS
      INTEGER IAUX, JAUX, KAUX
C
      CHARACTER*8 SAUX08
      CHARACTER*24 SAUX24
      CHARACTER*24 STTSEK, STTSEI
      CHARACTER*80 SAUX80
C
C====
C 1. RECUPERATION AU BON ENDROIT ...
C====
C
C 1.1. ==> CARACTERISTIQUES DE LA STRUCTURE DE SAUVEGARDE
C 1.1.1. ==> NOMBRE D'INFORMATIONS ENREGISTREES
C
      CALL SEGICO ( 2,
     >              SAUX08, IAUX, SAUX24, SAUX24,
     >              INPSCO, NBPSCO, JAUX )
C
C 1.1.2. ==> ADRESSE DE LA TABLE DE MEMORISATION
C
      CALL SEGICO ( 3,
     >              SAUX08, IAUX, SAUX24, SAUX24,
     >              INPSCO, ADPSCO, JAUX )
C
C 1.2. ==> REPERAGE DU NUMERO DE PARAMETRE SENSIBLE CORRESPONDANT
C          A NOPASE
C
      DO 12 , NRPASE = 1 , NBPASE
C
        ADRAUX = ADPSCO + (NBPSCO+1)*NRPASE
        IF ( NOPASE.EQ.ZK24(ADRAUX+1) ) THEN
          GOTO 20
        ENDIF
C
   12 CONTINUE
C
      CALL UTMESS ( 'A', NOMPRO,
     >'CE PARAMETRE DE SENSIBILITE EST INTROUVABLE : '//NOPASE(1:8) )
      CALL UTMESS ( 'F', NOMPRO, 'ERREUR DE PROGRAMMATION' )
C
C====
C 2. RECUPERATION
C====
C
   20 CONTINUE
C
C 2.1. ==> A PRIORI, PAS DE TYPE DE SENSIBILITE
C
      KAUX = LEN(TYPEPS)
      DO 21 , IAUX = 1 , KAUX
        TYPEPS(IAUX:IAUX) = ' '
   21 CONTINUE
C
C 2.2. ==> LA BASE DU NOM DES STRUCTURES
C          ON EN DEDUIT LES NOMS DES STRUCTURES QUI MEMORISENT LE TYPE
C          DE SENSIBILITE POUR CE PARAMETRE ET LA COMMANDE EN COURS
C          ON RECUPERE NBCONT, NOMBRE DE CONTRIBUTIONS PILOTEES PAR LE
C          PARAMETRE SENSIBLE CONCERNE.
C
      SAUX24 = ZK24(ADRAUX+2)
      IAUX = LXLGUT(SAUX24)
C
      IF ( IAUX.NE.0 ) THEN
C
        STTSEI = SAUX24
        STTSEI(IAUX+1:IAUX+2) = '.I'
        CALL JEVEUO ( STTSEI, 'L', ADTSEI )
        ADTSII = ADTSEI - 1
        CALL JELIRA ( STTSEI, 'LONUTI', NBCONT, SAUX08 )
C
        STTSEK = SAUX24
        STTSEK(IAUX+1:IAUX+4) = '.K80'
        CALL JEVEUO ( STTSEK, 'L', ADTSEK )
        ADTSKK = ADTSEK - 1
C
      ELSE
C
        NBCONT = 0
        NBMOSI = 0
C
      ENDIF
C
C 2.3. ==> DECODAGE DES INFORMATIONS STOCKEES
C
      DO 23 , IAUX = 1 , NBCONT
C
C 2.3.1. ==> LE TYPE DE SENSIBILITE
C
        ADTSKK = ADTSKK + 1
        JAUX = LXLGUT(ZK80(ADTSKK))
C
C 2.3.1.1. ==> VERIFICATION DE LA TAILLE DE LA CHAINE
C
        IF ( KAUX.LT.JAUX ) THEN
          CALL UTDEBM ( 'A', NOMPRO, 'PROBLEME DE DECLARATION' )
          CALL UTIMPI ( 'L', 'LA CHAINE TYPEPS EST DECLAREE A ', 1,KAUX)
          CALL UTIMPI ( 'L',
     >'ON VEUT Y METTRE '//ZK24(ADRAUX+2)//' QUI EN CONTIENT ', 1, JAUX)
          CALL UTFINM
          CALL UTMESS ( 'F', NOMPRO, 'ERREUR DE PROGRAMMATION' )
C
C 2.3.1.2. ==> A LA PREMIERE CONTRIBUTION, ON ARCHIVE
C
        ELSEIF ( IAUX.EQ.1 ) THEN
          LGTYPS = JAUX
          TYPEPS(1:LGTYPS) = ZK80(ADTSKK)(1:LGTYPS)
          SAUX80 = ZK80(ADTSKK)
C
C 2.3.1.3. ==> AUX CONTRIBUTIONS SUIVANTES, ON VERIFIE QUE C'EST
C              TOUJOURS LE MEME TYPE SI LA SENSIBILITE EST ISSUE DE
C              PLUSIEURS CAUSES. PAR EXEMPLE, UNE DONNEE SENSIBLE DANS
C              PLUSIEURS CHARGEMENTS.
C              IL FAUT QUE CE SOIT TOUJOURS LE MEME TYPE.
C
        ELSE
          IF ( ( LGTYPS.NE.JAUX ) .OR.
     >         ( TYPEPS(1:LGTYPS).NE.ZK80(ADTSKK)(1:LGTYPS) ) ) THEN
            CALL UTMESS ( 'A', NOMPRO,
     >     'LES TYPES DE SENSIBILITE POUR L''INFLUENCE DE '//NOPASE(1:8)
     >     //  ' SONT INCOHERENTS.' )
            CALL UTMESS ( 'A', NOMPRO,
     >      'ON A '//SAUX80(1:LGTYPS)//' ET '//ZK80(ADTSKK)(1:JAUX))
            CALL UTMESS ( 'F', NOMPRO, 'ERREUR DE DONNEES' )
          ENDIF
        ENDIF
C
C 2.3.2. ==> LE NOMBRE DE MOTS-CLES SIMPLES POUR CETTE CONTRIBUTION
C
        ADTSII = ADTSII + 1
        NBMCPA = ZI(ADTSII)
C
C 2.3.2. ==> LES MOTS-CLES SIMPLES ASSOCIES
C 2.3.2.1. ==> ALLOCATION DES STRUCTURES
C              ON ALLOUE PLUTOT GRAND LA PREMIERE FOIS ; ENSUITE, ON
C              DOUBLERA LA TAILLE SI BESOIN.
C
        IF ( IAUX.EQ.1 ) THEN
C
          NBMOSI = NBMCPA
          NBMCMX = 5*NBMOSI + 1
C
          CALL WKVECT ( LIMOSI,'V V K80',NBMCMX,ADMOSI )
          CALL JEECRA ( LIMOSI,'LONUTI',NBMOSI,SAUX08 )
          ADMOMM = ADMOSI - 1
C
          CALL WKVECT ( LIVALE,'V V K80',NBMCMX,ADVALE )
          CALL JEECRA ( LIVALE,'LONUTI',NBMOSI,SAUX08 )
          ADVAVV = ADVALE - 1
C
          CALL WKVECT ( LIMOFA,'V V K80',NBMCMX,ADMOFA )
          CALL JEECRA ( LIMOFA,'LONUTI',NBMOSI,SAUX08 )
          ADMOFF = ADMOFA - 1
C
        ELSE
C
          CALL JELIRA ( LIMOSI, 'LONUTI', NBMOSI, SAUX08 )
          NBMOSI = NBMOSI + NBMCPA
          IF ( NBMOSI.GT.NBMCMX ) THEN
            NBMCMX = 2*NBMCMX
            CALL JUVECA ( LIMOSI,NBMCMX )
            CALL JEVEUO ( LIMOSI, 'E', ADMOSI )
            ADMOMM = ADMOSI + NBMOSI - NBMCPA - 1
            CALL JUVECA ( LIVALE,NBMCMX )
            CALL JEVEUO ( LIVALE, 'E', ADVALE )
            ADVAVV = ADVALE + NBMOSI - NBMCPA - 1
            CALL JUVECA ( LIMOFA,NBMCMX )
            CALL JEVEUO ( LIMOFA, 'E', ADMOFA )
            ADMOFF = ADMOFA + NBMOSI - NBMCPA - 1
          ENDIF
          CALL JEECRA ( LIMOSI,'LONUTI',NBMOSI,SAUX08 )
          CALL JEECRA ( LIVALE,'LONUTI',NBMOSI,SAUX08 )
          CALL JEECRA ( LIMOFA,'LONUTI',NBMOSI,SAUX08 )
C
        ENDIF
C
        DO 232 , JAUX = 1 , NBMCPA
          ADMOMM = ADMOMM + 1
          ADTSKK = ADTSKK + 1
          ZK80(ADMOMM) = ZK80(ADTSKK)
          ADVAVV = ADVAVV + 1
          ADTSKK = ADTSKK + 1
          ZK80(ADVAVV) = ZK80(ADTSKK)
          ADMOFF = ADMOFF + 1
          ADTSKK = ADTSKK + 1
          ZK80(ADMOFF) = ZK80(ADTSKK)
  232   CONTINUE
C
   23 CONTINUE
C
      END
