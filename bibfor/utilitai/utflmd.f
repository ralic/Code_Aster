      SUBROUTINE UTFLMD(MAILLA,LIMAIL,DIM,NBTROU,LITROU)
      IMPLICIT NONE
      INTEGER DIM,NBTROU
      CHARACTER*8 MAILLA
      CHARACTER*(*) LITROU,LIMAIL
C ----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF UTILITAI  DATE 29/03/2011   AUTEUR DELMAS J.DELMAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE DELMAS J.DELMAS
C
C     BUT:
C       FILTRER UNE LISTE DE MAILLE D'APRES LEUR DIMENSION
C       *           *        *                   *
C       IDEM QUE UTFLM2 MAIS AVEC UN VECTEUR JEVEUX
C
C
C     ARGUMENTS:
C     ----------
C
C      ENTREE :
C-------------
C IN   MAILLA    : NOM DU MAILLAGE
C IN   LIMAIL    : LISTE DES MAILLES (OBJET JEVEUX)
C IN   NDIM      : DIMENSION DES MAILLES A TROUVER (0,1,2,3)
C
C      SORTIE :
C-------------
C OUT  NBTROU    : NOMBRE DE MAILLE TROUVEES
C OUT  LITROU    : LISTE DES MAILLES TROUVEES (OBJET JEVEUX)
C                  SI NBTROU = 0, L'OBJET JEVEUX N'EST PAS CREE
C
C.......................................................................
C
C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ---------------------
C
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)

      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
      CHARACTER*32 JEXNUM
C
C --------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ---------------------
C
      INTEGER NBTYP,I,II,IRET,NBMAIL,ITROU
      INTEGER ITYPMA,IDIMTO,JTYPMA,ILIMAI,IT,ITEMPO,ITRMA

      CHARACTER*1 K1BID
      CHARACTER*8 K8BID
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
C
      CALL JELIRA('&CATA.TM.NOMTM','NOMMAX',NBTYP,K1BID)
C
      CALL WKVECT('&&UTDIMM.TYPE_MAILLE','V V K8',NBTYP,ITYPMA)
      CALL WKVECT('&&UTDIMM.DIME_TOPO','V V I',NBTYP,IDIMTO)
C
C ----RECUPERATION DE TOUS LES TYPES DE MAILLE
C      ET DE LEUR DIMENSION TOPOLOGIQUE
C
      DO 10 I = 1,NBTYP
        CALL JENUNO(JEXNUM('&CATA.TM.NOMTM',I),ZK8(ITYPMA-1+I))
        CALL DISMOI('F','DIM_TOPO',ZK8(ITYPMA-1+I),'TYPE_MAILLE',
     &                                       ZI(IDIMTO-1+I),K8BID,IRET)
   10 CONTINUE
C
C ----RECUPERATION DE LA LISTE DE MAILLE ET DE SA LONGUEUR
C
      CALL JEVEUO(LIMAIL,'L',ILIMAI)
      CALL JELIRA(LIMAIL,'LONMAX',NBMAIL,K1BID)
C
C ----RECUPERATION DE LA LISTE DES TYPES DE MAILLE DU MAILLAGE
C
      CALL JEVEUO(MAILLA//'.TYPMAIL        ','L',JTYPMA)

      CALL WKVECT('&&UTDIMM.LISTE_TYPMAI','V V I',NBMAIL,IT)
      CALL WKVECT('&&UTDIMM.LISTE_M_TEMP','V V I',NBMAIL,ITEMPO)

      NBTROU = 0
      II = 1
      DO 20 I = 1, NBMAIL
C
C ------RECUPERATION DU TYPE DE LA MAILLE I
C
        ZI(IT-1+I) = ZI(JTYPMA-1+ZI(ILIMAI-1+I))
C
C ------SI LA DIMENSION TOPOLOGIQUE EST LA BONNE ON GARDE LA MAILLE
C
        IF (ZI(IDIMTO-1+ZI(IT-1+I)).EQ.DIM) THEN
          ZI(ITEMPO-1+II) = ZI(ILIMAI-1+I)
          NBTROU = NBTROU + 1
          II = II + 1
        ENDIF
   20 CONTINUE

      IF (NBTROU.EQ.0) GOTO 9999
C
C ----SI LA LISTE N'EST PAS VIDE ON RECOPIE DANS LITROU DE TAILLE NBTROU
C
      CALL WKVECT(LITROU,'V V I',NBTROU,ITRMA)

      DO 30 ITROU = 1, NBTROU
          ZI(ITRMA-1+ITROU) = ZI(ITEMPO-1+ITROU)
   30 CONTINUE
C
 9999 CONTINUE
C
      CALL JEDETR('&&UTDIMM.TYPE_MAILLE')
      CALL JEDETR('&&UTDIMM.DIME_TOPO')
      CALL JEDETR('&&UTDIMM.LISTE_M_TEMP')
      CALL JEDETR('&&UTDIMM.LISTE_TYPMAI')
C
      CALL JEDEMA()
C
      END
