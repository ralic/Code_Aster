      SUBROUTINE MEMRES(LIMPR,LDYN,TITRE,PREC,TMAX)
      IMPLICIT NONE
      CHARACTER*(*) TITRE,LIMPR,LDYN
      REAL*8 TMAX,PREC
C TOLE CRS_505 CRP_4
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF DEBUG  DATE 30/07/2012   AUTEUR LEFEBVRE J-P.LEFEBVRE 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE PELLET J.PELLET
C   BUT: CALCULER LA QUANTITE DE MEMOIRE ENCORE LIBRE
C-------------------------------------------------------------
C IN  LIMPR: /'NON' : ON SE CONTENTE DE CALCULER TMAX
C            /'OUI' : ON IMPRIME TMAX (+ 2 AUTRES INFOS)
C               IMPRIME SUR .MESS 3 TAILLES (EN MO) :
C                 * MEM JEVEUX DYNAMIQUE ALLOUEE
C                 * MEM JEVEUX INDISPENSABLE ("U")
C                 * MEM ENCORE DISPONIBLE (TMAX)
C IN  TITRE : CHAINE DE CARACTERES IMPRIMEE AU DEBUT DES LIGNES
C            SI LIMPR='OUI'
C IN  LDYN : /'OUI' : ON FORCE LA LIBERATION DE LA MEMOIRE JEVEUX
C               DYNAMIQUE QUI N'EST PAS "U". (APPEL A JJLDYN)
C            /'NON' : ON NE FORCE PAS LA LIBERATION DE LA MEMOIRE JEVEUX
C               DYNAMIQUE
C IN  PREC : PRECISION SOUHAITEE POUR TMAX (EN MO)
C OUT TMAX : TAILLE (EN MO) DE LA MEMOIRE ENCORE DISPONIBLE
C
C REMARQUES IMPORTANTES :
C ---------------------
C 1) CETTE ROUTINE NE PEUT FONCTIONNER QUE SI LE "SYSTEME" LIMITE LA
C MEMOIRE VIRTUELLE QUE L'ON PEUT ALLOUER.
C EN BATCH SUR BULL, C'EST RMS QUI LIMITE LA MEMOIRE
C EN INTERACTIF, IL FAUT SE FIXER SOI-MEME LA LIMITE EN FAISANT
C PAR EXEMPLE : ULIMIT -V 800000 (800 MO)
C
C 2) SUR LES MACHINES AVEC DES INTEGER*4, SI LA MEMOIRE DISPONIBLE
C EST TROP GRANDE, IL PEUT ARRIVER QUE L'ON DEPASSE LA CAPACITE DES I4.
C NORMALEMENT, ON EST ALORS ARRETE PAR UN ASSERT DANS MEMRES.F
C-------------------------------------------------------------

      REAL*8 RVAL(2)
      CHARACTER*8 K8TAB(2)
      INTEGER LON1,IDEP,INCR,IRET
      INTEGER K,JAD,IERR,IBID,NBFREE,LTOT
      INTEGER LOISEM

      CALL ASSERT(LIMPR.EQ.'OUI' .OR. LIMPR.EQ.'NON')
      CALL ASSERT(LDYN.EQ.'OUI' .OR. LDYN.EQ.'NON')
      CALL ASSERT(PREC.GT.0.D0)

      TMAX=-99.D0
      IF (LDYN.EQ.'OUI') CALL JJLDYN(0,-1,LTOT)

      NBFREE=0
      IBID=0
      IDEP=0
      INCR=PREC*1024*1024/LOISEM()/2


   10 CONTINUE
      LON1=INCR
      DO 30,K=1,40

C        -- ON VERIFIE LA CAPACITE DES ENTIERS :
        LON1=LON1*2
        CALL ASSERT(LON1.GT.0)
        CALL ASSERT(IDEP+LON1.GT.0)

C       -- TENTATIVE D'ALLOCATION :
        CALL HPALLOC(JAD,IDEP+LON1,IERR,0)
        IF (IERR.EQ.0) THEN
          CALL HPDEALLC(JAD,NBFREE,IBID)
          GOTO 30
        ELSE
          CALL ASSERT(IERR.EQ.-2)
          IDEP=IDEP+LON1/2
          GOTO 20
        ENDIF

   20   CONTINUE
        IF (K.GT.1) THEN
          GOTO 10
        ELSE
          TMAX=DBLE(IDEP)
          GOTO 40
        ENDIF
   30 CONTINUE


   40 CONTINUE

C      -- ON VERIFIE QUE TMAX EST ASSEZ BIEN EVALUE (+/- PREC):
      IF (.TRUE.) THEN
        CALL HPALLOC(JAD,IDEP-2*INCR,IERR,0)
        CALL ASSERT(IERR.EQ.0)
        CALL HPDEALLC(JAD,NBFREE,IBID)
        CALL HPALLOC(JAD,IDEP+2*INCR,IERR,0)
        CALL ASSERT(IERR.NE.0)
      ENDIF

C     -- CONVERSION EN MO :
      TMAX=TMAX*LOISEM()/1024/1024


      IF (LIMPR.EQ.'OUI') THEN
        K8TAB(1)='COUR_JV'
        K8TAB(2)='CUSE_JV'
        CALL UTGTME(2,K8TAB,RVAL,IRET)
        WRITE (6,9000)'<MEMRES>',TITRE,RVAL(1),RVAL(2),TMAX
      ENDIF

 9000 FORMAT (A8,1X,A,3(1X,F12.2))
      END
