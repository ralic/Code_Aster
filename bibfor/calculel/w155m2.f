      SUBROUTINE W155M2(CHIN,CARELE,LIGREL,CHEXTR,NOMSYM,NOCMP,TYMAXI)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF CALCULEL  DATE 19/09/2011   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE PELLET J.PELLET
C ======================================================================
      IMPLICIT NONE
      CHARACTER*8 CARELE,NOCMP,TYMAXI
      CHARACTER*16 NOMSYM
      CHARACTER*19 CHIN,CHEXTR,LIGREL

C ----------------------------------------------------------------------
C BUT : EXTRACTION DU CHAM_ELEM CORRESPONDANT AU MIN/MAX
C       SUR LES SOUS-POINTS

C IN/JXIN  CHIN  : CHAM_ELEM (PLUSIEURS SOUS-POINTS) DANS LEQUEL
C                  ON DOIT EXTRAIRE CHEXTR
C IN/JXIN  CARELE  : CARA_ELEM ASSOCIE A CHIN
C IN/JXIN  LIGREL  : LIGREL SUR LEQUEL CREER CHEXTR
C IN/JXVAR CHEXTR  : CHAM_ELEM (1 SEUL SOUS-POINT) A REMPLIR
C IN       NOCMP   : NOM DE LA COMPOSANTE DONT ON CHERCHE LE MIN/MAX
C IN       TYMAXI  : /'MAXI' /'MINI' /'MAXI_ABS', /'MINI_ABS'
C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ---------------------
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C --------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ---------------------
      CHARACTER*24 LINUMA,LINUTE,VALK(5)
      CHARACTER*19 CES1,CES2,CES3,CES4
      CHARACTER*16 OPTION
      CHARACTER*8 KBID,LICMP(5),MA,NOMGD,TSCA,NOMPAR
      CHARACTER*3 EXITUY
      INTEGER IRET,NBMA,IBID,NBMAT,NUMA,KMA
      INTEGER NBPT,KPT,KCMP,NNCP,JLITE
      INTEGER IAD1,IAD4,JLIMA,NCMP
      INTEGER JCE2L,JCE2D,JCE2V,JCE3K,JCE3L,JCE3D,JCE3V,JCE3C
      INTEGER JCE4L,JCE4C,JCE4D,JCE4V,NBSPMX,NUCMP
      INTEGER ISP,NBSP,KSP,NUSEC,NUCOU,NUFIB,POSIC,POSIS,KCMP2
      REAL*8 VAL,VMIMA

C ----------------------------------------------------------------------
      CALL JEMARQ()
      CALL DISMOI('F','NOM_MAILLA',CHIN,'CHAM_ELEM',IBID,MA,IRET)
      CALL DISMOI('F','NOM_GD',CHIN,'CHAM_ELEM',IBID,NOMGD,IRET)
      CALL DISMOI('F','TYPE_SCA',CHIN,'CHAM_ELEM',IBID,TSCA,IRET)
      CALL DISMOI('F','MXNBSP',CHIN,'CHAM_ELEM',NBSPMX,KBID,IRET)
      CALL DISMOI('F','EXI_TUYAU',LIGREL,'LIGREL',IBID,EXITUY,IRET)
      IF (NBSPMX.LE.1) CALL U2MESS('F','CALCULEL2_15')
      CALL DISMOI('F','NB_MA_MAILLA',MA,'MAILLAGE',NBMAT,KBID,IRET)
      CALL ASSERT(TSCA.EQ.'R')
      CALL ASSERT(EXITUY.EQ.'OUI' .OR. EXITUY.EQ.'NON')


C     1.  LISTE DES MAILLES A TRAITER :
C     ---------------------------------
      LINUMA='&&W155M2.LIMA'
      LINUTE='&&W155M2.LITE'
      CALL LIGLMA(LIGREL,NBMA,LINUMA,LINUTE)
      CALL ASSERT(NBMA.GT.0)
      CALL JEVEUO(LINUMA,'L',JLIMA)
      CALL JEVEUO(LINUTE,'L',JLITE)


C     2.  NOMBRE DE COUCHES, SECTEURS ET FIBRES  DES ELEMENTS :
C     -----------------------------------------------------------
      CES1='&&W155M2.CES1'
      CES2='&&W155M2.CES2'
      CALL CELCES(CARELE//'.CANBSP','V',CES1)

C     -- L'ORDRE DES CMPS EST IPORTANT (UTILISE DANS W155M3)
      LICMP(1)='COQ_NCOU'
      LICMP(2)='TUY_NCOU'
      LICMP(3)='TUY_NSEC'
      LICMP(4)='NBFIBR'
      LICMP(5)='GRI_NCOU'
      CALL CESRED(CES1,NBMA,ZI(JLIMA),5,LICMP,'V',CES2)
      CALL DETRSD('CHAM_ELEM_S',CES1)
      CALL JEVEUO(CES2//'.CESD','L',JCE2D)
      CALL JEVEUO(CES2//'.CESV','L',JCE2V)
      CALL JEVEUO(CES2//'.CESL','L',JCE2L)


C     3. CHIN -> CES3 :
C     ------------------
      CES3='&&W155M2.CES3'
      CALL CELCES(CHIN,'V',CES3)
      CALL JEVEUO(CES3//'.CESK','L',JCE3K)
      CALL JEVEUO(CES3//'.CESD','L',JCE3D)
      CALL JEVEUO(CES3//'.CESC','L',JCE3C)
      CALL JEVEUO(CES3//'.CESL','L',JCE3L)
      CALL JEVEUO(CES3//'.CESV','L',JCE3V)
      CALL JELIRA(CES3//'.CESC','LONMAX',NCMP,KBID)


C     4. REMPLISSAGE DU CHAM_ELEM_S RESULTAT CES4 :
C     ---------------------------------------------
      CES4='&&W155M2.CES4'
      CALL CELCES(CHEXTR,'V',CES4)
      CALL JEVEUO(CES4//'.CESD','L',JCE4D)
      CALL JEVEUO(CES4//'.CESV','L',JCE4V)
      CALL JEVEUO(CES4//'.CESL','L',JCE4L)
      CALL JEVEUO(CES4//'.CESC','L',JCE4C)
      DO 10,KCMP=1,NCMP
        IF (ZK8(JCE3C-1+KCMP).EQ.NOCMP) THEN
          NUCMP=KCMP
          GOTO 20

        ENDIF
   10 CONTINUE
      VALK(1)=NOCMP
      VALK(2)=NOMSYM
      CALL U2MESK('F','CALCULEL2_18',2,VALK)

   20 CONTINUE
      DO 60,KMA=1,NBMA
        NUMA=ZI(JLIMA-1+KMA)
        CALL ASSERT(NUMA.GE.1 .AND. NUMA.LE.NBMAT)
        NBPT=ZI(JCE3D-1+5+4*(NUMA-1)+1)
        NBSP=ZI(JCE3D-1+5+4*(NUMA-1)+2)
        IF (NBSP.EQ.0)GOTO 60

        DO 50,KPT=1,NBPT
C         -- 4.1 CALCUL DE VMIMA ET ISP :
C            VMIMA : VALEUR MIN/MAX ATTEINTE SUR LES SOUS-POINTS
C            ISP   : NUMERO DU SOUS-POINT REALISANT LE MIN/MAX
          DO 30,KSP=1,NBSP
            CALL CESEXI('C',JCE3D,JCE3L,NUMA,KPT,KSP,NUCMP,IAD1)
            IF (IAD1.GT.0) THEN
              VAL=ZR(JCE3V-1+IAD1)
              IF (TYMAXI(5:8).EQ.'_ABS')VAL=ABS(VAL)
              IF (KSP.EQ.1) THEN
                VMIMA=VAL
                ISP=KSP
              ELSE
                IF (TYMAXI(1:4).EQ.'MAXI') THEN
                  IF (VAL.GT.VMIMA) THEN
                    VMIMA=VAL
                    ISP=KSP
                  ENDIF
                ELSEIF (TYMAXI(1:4).EQ.'MINI') THEN
                  IF (VAL.LT.VMIMA) THEN
                    VMIMA=VAL
                    ISP=KSP
                  ENDIF
                ELSE
                  CALL ASSERT(.FALSE.)
                ENDIF
              ENDIF
            ENDIF
   30     CONTINUE

C         -- 4.2  CALCUL DE NUCOU, NUSEC, ... A PARTIR DE ISP :
          CALL W155M3(NUMA,JCE2D,JCE2L,JCE2V,ISP,NUCOU,NUSEC,NUFIB,
     &                POSIC,POSIS)

C         -- 4.3 STOCKAGE DE VMIMA, NUCOU, NUSEC, ...
          DO 40,KCMP2=1,6
            CALL CESEXI('C',JCE4D,JCE4L,NUMA,KPT,1,KCMP2,IAD4)
            CALL ASSERT(IAD4.GT.0)
            IF (KCMP2.EQ.1) THEN
              CALL ASSERT(ZK8(JCE4C-1+KCMP2).EQ.'VAL')
              ZR(JCE4V-1+IAD4)=VMIMA
            ELSEIF (KCMP2.EQ.2) THEN
              CALL ASSERT(ZK8(JCE4C-1+KCMP2).EQ.'NUCOU')
              ZR(JCE4V-1+IAD4)=DBLE(NUCOU)
            ELSEIF (KCMP2.EQ.3) THEN
              CALL ASSERT(ZK8(JCE4C-1+KCMP2).EQ.'NUSECT')
              ZR(JCE4V-1+IAD4)=DBLE(NUSEC)
            ELSEIF (KCMP2.EQ.4) THEN
              CALL ASSERT(ZK8(JCE4C-1+KCMP2).EQ.'NUFIBR')
              ZR(JCE4V-1+IAD4)=DBLE(NUFIB)
            ELSEIF (KCMP2.EQ.5) THEN
              CALL ASSERT(ZK8(JCE4C-1+KCMP2).EQ.'POSIC')
              ZR(JCE4V-1+IAD4)=DBLE(POSIC)
            ELSEIF (KCMP2.EQ.6) THEN
              CALL ASSERT(ZK8(JCE4C-1+KCMP2).EQ.'POSIS')
              ZR(JCE4V-1+IAD4)=DBLE(POSIS)
            ELSE
              CALL ASSERT(.FALSE.)
            ENDIF
   40     CONTINUE
   50   CONTINUE
   60 CONTINUE


C     5 CES4 -> CHEXTR :
C     ------------------------------------
      CALL DISMOI('F','NOM_OPTION',CHEXTR,'CHAM_ELEM',IBID,OPTION,IBID)
      CALL DISMOI('F','NOM_PARAM',CHEXTR,'CHAM_ELEM',IBID,NOMPAR,IBID)
      CALL DETRSD('CHAM_ELEM',CHEXTR)
      CALL CESCEL(CES4,LIGREL,OPTION,NOMPAR,'OUI',NNCP,'G',CHEXTR,'F',
     &            IRET)
      CALL ASSERT(NNCP.EQ.0)


C     6. MENAGE :
C     ------------
      CALL DETRSD('CHAM_ELEM_S',CES2)
      CALL DETRSD('CHAM_ELEM_S',CES3)
      CALL DETRSD('CHAM_ELEM_S',CES4)
      CALL JEDETR(LINUMA)
      CALL JEDETR(LINUTE)

      CALL JEDEMA()
      END
