      SUBROUTINE CCVEPO(MODELE,RESUIN,LISCHA,NBCHAR,TYPESD,
     &                  NBCHRE,IOCCUR,SUROPT,LIGREL,EXIPOU)
      IMPLICIT NONE
C     --- ARGUMENTS ---
      INCLUDE 'jeveux.h'
      LOGICAL      EXIPOU
      INTEGER      NBCHRE,IOCCUR,NBCHAR
      CHARACTER*8  MODELE,RESUIN
      CHARACTER*16 TYPESD
      CHARACTER*19 LISCHA
      CHARACTER*24 SUROPT,LIGREL
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF CALCULEL  DATE 10/09/2012   AUTEUR SELLENET N.SELLENET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C ----------------------------------------------------------------------
C  CALC_CHAMP - VERIFICATION POUR LES POUTRES
C  -    -       --                    --
C ----------------------------------------------------------------------
C
C  ROUTINE PERMETTANT DE SAVOIR SI DES POUTRES SONT DANS LE LIGREL
C   REDUIT ET DE VERIFIER LES CHARGES REPARTIES
C
C IN  :
C   MODELE  K8   NOM DU MODELE
C   RESUIN  K8   NOM DE LA STRUCTURE DE DONNEES RESULTAT IN
C   LISCHA  K19  NOM DE L'OBJET JEVEUX CONTENANT LES CHARGES
C   NCHARG  I    NOMBRE DE CHARGES
C   TYPESD  K16  TYPE DE LA STRUCTURE DE DONNEES RESULTAT
C
C OUT :
C   NBCHRE  I    NOMBRE DE CHARGES REPARTIES (POUTRES)
C   IOCCUR  I    NUMERO D'OCCURENCE OU SE TROUVE LE CHARGE REPARTIE
C   SUROPT  K24  SUROPTION
C   LIGREL  K24  NOM DU LIGREL A CREER
C   EXIPOU  L    LOGIQUE INDIQUANT LE PRESENCE DE POUTRES
C ----------------------------------------------------------------------
C RESPONSABLE SELLENET N.SELLENET
      INTEGER      IBID,IERD,JREFE,JCHA,LTYMO,JMAILL,NBMAMO,NBMAAL
      INTEGER      JLISMA,IMA,GETEXM,N1,N2
C
      CHARACTER*8  K8B
      CHARACTER*16 TYPEMO
      CHARACTER*19 REFE,MASSE,CHDYNR,CHDEPL
      CHARACTER*24 NOOJB
C
      CALL JEMARQ()
C
      TYPEMO = ' '
      IF ( TYPESD.EQ.'MODE_MECA' ) THEN
        CALL RSADPA(RESUIN,'L',1,'TYPE_MODE',1,0,LTYMO,K8B)
        TYPEMO=ZK16(LTYMO)
      ENDIF
C
C     REDUCTION DU LIGREL SUR LA BASE DE GROUP_MA, GROUP_NO, ETC.
      N1 = GETEXM(' ','GROUP_MA')
      N2 = GETEXM(' ','MAILLE')
      IF ( N1+N2.NE.0 ) THEN
        CALL EXLIMA(' ',0,'V',MODELE,LIGREL)
      ELSE
        CALL JEVEUO(MODELE//'.MAILLE','L',JMAILL)
        CALL JELIRA(MODELE//'.MAILLE','LONMAX',NBMAMO,K8B)
        CALL WKVECT('&&CCVEPO.LISTE_MAILLES','V V I',NBMAMO,JLISMA)
        NBMAAL=0
        DO 10 IMA=1,NBMAMO
          IF ( ZI(JMAILL+IMA-1).NE.0 ) THEN
            ZI(JLISMA+NBMAAL)=IMA
            NBMAAL=NBMAAL+1
          ENDIF
  10    CONTINUE
        NOOJB='12345678.LIGR000000.LIEL'
        CALL GNOMSD(NOOJB,14,19)
        LIGREL=NOOJB(1:19)
        CALL ASSERT(LIGREL.NE.' ')
        CALL EXLIM1(ZI(JLISMA),NBMAAL,MODELE,'V',LIGREL)
        CALL JEDETR('&&CCVEPO.LISTE_MAILLES')
      ENDIF
C
      CALL DISMOI('F','EXI_POUX',LIGREL,'LIGREL',IBID,K8B,IERD)
      EXIPOU = .FALSE.
C     SPECIAL POUTRE A LA POUX...
      IF (K8B(1:3).EQ.'OUI') THEN
        EXIPOU = .TRUE.
C       ON VERIFIE SI DERIERE LE TYPESD MODE_MECA ON TROUVE UN MODE_DYN
        IF ((TYPESD.EQ.'MODE_MECA'.AND.TYPEMO(1:8).EQ.'MODE_DYN') .OR.
     &      TYPESD.EQ.'DYNA_TRANS' .OR. TYPESD.EQ.'MODE_ACOU' .OR.
     &      TYPESD.EQ.'DYNA_HARMO') THEN
          REFE=RESUIN
          SUROPT='MASS_MECA'
          CALL JEVEUO(REFE//'.REFD','L',JREFE)
          MASSE=ZK24(JREFE+1)(1:19)
          IF (MASSE.NE.' ') THEN
            CALL DISMOI('C','SUR_OPTION',MASSE,'MATR_ASSE',
     &                  IBID,SUROPT,IERD)
          ENDIF
          CALL RSEXCH('F',RESUIN,'DEPL',1,CHDEPL,IERD)
          CHDYNR='&&MECALM.M.GAMMA'
          CALL COPICH('V',CHDEPL(1:19),CHDYNR)
        ENDIF
        CALL JEVEUO(LISCHA,'L',JCHA)
C       VERIFIE L'UNICITE DE LA CHARGE REPARTIE
        IOCCUR=0
        CALL COCHRE(ZK8(JCHA),NBCHAR,NBCHRE,IOCCUR)
        IF (NBCHRE.GT.1) THEN
          CALL U2MESS('F','CALCULEL2_92')
        ENDIF
      ENDIF
C
      CALL JEDEMA()
C
      END
