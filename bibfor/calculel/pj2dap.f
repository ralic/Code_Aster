      SUBROUTINE PJ2DAP(INO2,GEOM2,MA2,GEOM1,TRIA3,COBARY,ITR3,NBTROU,
     &                  BTDI,BTVR,BTNB,BTLC,BTCO,IFM,NIV,LDMAX,DISTMA,
     &                  LOIN,DMIN)
      IMPLICIT NONE
      REAL*8 COBARY(3),GEOM1(*),GEOM2(*),BTVR(*)
      INTEGER ITR3,NBTROU,BTDI(*),BTNB(*),BTLC(*),BTCO(*),TRIA3(*)
      INTEGER IFM,NIV
      CHARACTER*8 MA2
C ----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF CALCULEL  DATE 16/06/2008   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C     BUT :
C       TROUVER LE TRIA3 QUI SERVIRA A INTERPOLER LE NOEUD INO2
C       AINSI QUE LES COORDONNEES BARYCENTRIQUES DE INO2 DANS CE TRIA3

C  IN   INO2       I  : NUMERO DU NOEUD DE M2 CHERCHE
C  IN   MA2        K8 : NOM DU MAILLAGE M2
C  IN   GEOM2(*)   R  : COORDONNEES DES NOEUDS DU MAILLAGE M2
C  IN   GEOM1(*)   R  : COORDONNEES DES NOEUDS DU MAILLAGE M1
C  IN   TRIA3(*)   I  : OBJET '&&PJXXCO.TRIA3'
C  IN   BTDI(*)    I  : OBJET .BT2DDI DE LA SD BOITE_2D
C  IN   BTVR(*)    R  : OBJET .BT2DVR DE LA SD BOITE_2D
C  IN   BTNB(*)    I  : OBJET .BT2DNB DE LA SD BOITE_2D
C  IN   BTLC(*)    I  : OBJET .BT2DLC DE LA SD BOITE_2D
C  IN   BTCO(*)    I  : OBJET .BT2DCO DE LA SD BOITE_2D
C  IN   IFM        I  : NUMERO LOGIQUE DU FICHIER MESSAGE
C  IN   NIV        I  : NIVEAU D'IMPRESSION POUR LES "INFO"
C  IN   LDMAX      L  : .TRUE. : IL FAUT PRENDRE DISTMA EN COMPTE
C  IN   DISTMA     R  : DISTANCE AU DELA DE LAQUELLE LE NOEUD INO2
C                       NE SERA PAS PROJETE.
C  OUT  NBTROU     I  : 2 -> ON A TROUVE 1 TETR4 QUI CONTIENT INO2
C                     : 1 -> ON A TROUVE 1 TETR4 ASSEZ PROCHE DE INO2
C                     : 0 -> ON N'A PAS TROUVE DE TETR4 ASSEZ PROCHE
C  OUT  ITR3       I  : NUMERO DU TRIA3 SOLUTION
C  OUT  COBARY(4)  R  : COORDONNEES BARYCENTRIQUES DE INO2 DANS ITR3
C  OUT  DMIN       R  : DISTANCE DE INO2 AU BORD DE ITR3 SI INO2 EST
C                       EXTERIEUR A ITR3.
C  OUT  LOIN       L  : .TRUE. SI DMIN > 10% DIAMETRE(ITR3)

C  REMARQUE :
C    SI NBTROU=0, INO2 NE SERA PAS PROJETE CAR IL EST AU DELA DE DISTMA
C    ALORS : DMIN=0, LOIN=.FALSE.
C ----------------------------------------------------------------------
C --- DEBUT DECLARATIONS NORMALISEES JEVEUX ----------------------------

      CHARACTER*32 JEXNUM,JEXNOM,JEXR8,JEXATR
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)

C --- FIN DECLARATIONS NORMALISEES JEVEUX ------------------------------
      REAL*8 COBAR2(3),DMIN,D2,DX,DY,XMIN,YMIN,R8MAEM,SURF,RTR3
      INTEGER P,Q,P1,Q1,P2,Q2,INO2,NTR3,I,K,IPOSI,NX,NTRBT,IBID
      CHARACTER*8 KB,NONO2,ALARME
      LOGICAL OK

      LOGICAL LDMAX,LOIN
      REAL*8 DISTMA,VALR(2)
C DEB ------------------------------------------------------------------
      NBTROU=0
      LOIN=.FALSE.
      DMIN=0.D0

      NX=BTDI(1)
      DX=BTVR(5)
      DY=BTVR(6)
      XMIN=BTVR(1)
      YMIN=BTVR(3)


C     -- 1. : ON CHERCHE UN TRIA3 ITR3 QUI CONTIENNE INO2 :
C     -------------------------------------------------------
C     -- PARCOURS DES MAILLES CANDIDATES :
C     DO 1,I=1,NTR3
      P=INT((GEOM2(3*(INO2-1)+1)-XMIN)/DX)+1
      Q=INT((GEOM2(3*(INO2-1)+2)-YMIN)/DY)+1
      NTRBT=BTNB((Q-1)*NX+P)
      IPOSI=BTLC((Q-1)*NX+P)
      DO 10,K=1,NTRBT
        I=BTCO(IPOSI+K)
        CALL PJ2DA1(INO2,GEOM2,I,GEOM1,TRIA3,COBAR2,OK)
        IF (OK) THEN
          ITR3=I
          NBTROU=2
          COBARY(1)=COBAR2(1)
          COBARY(2)=COBAR2(2)
          COBARY(3)=COBAR2(3)
          GOTO 9999
        ENDIF
   10 CONTINUE



C     -- 2. : SI ECHEC DE LA RECHERCHE PRECEDENTE, ON
C        CHERCHE LE TRIA3 ITR3 LE PLUS PROCHE DE INO2 :
C     -------------------------------------------------------
      IF (LDMAX) THEN
        DMIN=DISTMA
      ELSE
        DMIN=R8MAEM()
      ENDIF
C     -- PARCOURS DES MAILLES CANDIDATES :
C     DO 2,I=1,NTR3  % ON PARCOURT TOUS LES TRIA3

C     -- ON RECHERCHE LA GROSSE BOITE CANDIDATE :
      CALL PJ2DGB(INO2,GEOM2,GEOM1,TRIA3,BTDI,BTVR,BTNB,BTLC,BTCO,P1,
     &            Q1,P2,Q2)
      DO 50,P=P1,P2
        DO 40,Q=Q1,Q2
          NTRBT=BTNB((Q-1)*NX+P)
          IPOSI=BTLC((Q-1)*NX+P)
          DO 30,K=1,NTRBT
            I=BTCO(IPOSI+K)
            CALL PJ2DA2(INO2,GEOM2,I,GEOM1,TRIA3,COBAR2,D2,SURF)
            IF (SQRT(D2).LT.DMIN) THEN
              RTR3=SURF
              ITR3=I
              DMIN=SQRT(D2)
              NBTROU=1
              COBARY(1)=COBAR2(1)
              COBARY(2)=COBAR2(2)
              COBARY(3)=COBAR2(3)
            ENDIF
   30     CONTINUE
   40   CONTINUE
   50 CONTINUE


      IF (NBTROU.EQ.1) THEN
        IF (RTR3.EQ.0) THEN
          LOIN=.TRUE.
        ELSE
          RTR3 = RTR3** (1.D0/2.D0)
          IF (DMIN/RTR3.GT.1.D-1) LOIN=.TRUE.
        END IF
      ELSE
        DMIN=0.D0
      ENDIF

9999  CONTINUE

      END
