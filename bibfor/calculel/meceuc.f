      SUBROUTINE MECEUC(STOP,POUX,OPTION,CARAEZ,LIGREL,NIN,LCHIN,LPAIN,
     &                  NOU,LCHOU,LPAOU,BASE)

      IMPLICIT NONE

      INTEGER NIN,NOU
      CHARACTER*1 STOP
      CHARACTER*8 POUX,CARAEL
      CHARACTER*(*) BASE,OPTION
      CHARACTER*(*) LCHIN(*),LCHOU(*),LPAIN(*),LPAOU(*),LIGREL,CARAEZ

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF CALCULEL  DATE 11/10/2011   AUTEUR SELLENET N.SELLENET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C ----------------------------------------------------------------------
C     ENTREES:
C        STOP   :  /'S' : ON S'ARRETE SI AUCUN ELEMENT FINI DU LIGREL
C                         NE SAIT CALCULER L'OPTION.
C                  /'C' : ON CONTINUE SI AUCUN ELEMENT FINI DU LIGREL
C                         NE SAIT CALCULER L'OPTION. IL N'EXISTE PAS DE
C                         CHAMP "OUT" DANS CE CAS.
C        OPTIO  :  NOM D'1 OPTION
C        LIGREL :  NOM DU LIGREL SUR LEQUEL ON DOIT FAIRE LE CALCUL
C        NIN    :  NOMBRE DE CHAMPS PARAMETRES "IN"
C        NOU    :  NOMBRE DE CHAMPS PARAMETRES "OUT"
C        LCHIN  :  LISTE DES NOMS DES CHAMPS "IN"
C        LCHOU  :  LISTE DES NOMS DES CHAMPS "OUT"
C        LPAIN  :  LISTE DES NOMS DES PARAMETRES "IN"
C        LPAOU  :  LISTE DES NOMS DES PARAMETRES "OUT"
C        BASE   :  'G' , 'V' OU 'L'

C     SORTIES:
C       ALLOCATION ET CALCUL DES OBJETS CORRESPONDANT AUX CHAMPS "OUT"
C     CETTE ROUTINE MET EN FORME LES CHAMPS EVENTUELLEMENT COMPLEXE
C     POUR L APPEL A CALCUL
C-----------------------------------------------------------------------
C     ----- DEBUT COMMUNS NORMALISES  JEVEUX  --------------------------
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
      CHARACTER*32 JEXNUM

C     ----- FIN COMMUNS NORMALISES  JEVEUX  --------------------------

      CHARACTER*19 CH1,CH2,CHDYNR,CHDYNI,CHREEL,CHIMAG
      CHARACTER*19 CHACCR,CHACCI,CHDEPR,CHDEPI
      CHARACTER*16 OPTIO2
      CHARACTER*14 VALK(1)
      CHARACTER*8 NOMGD
      INTEGER IBID,I,IDESC,GD,INDIC1,INDIC2,INDIC3
      INTEGER INDIC4
C ----------------------------------------------------------------------
C
      CALL JEMARQ()

      OPTIO2=OPTION
      CHIMAG='&&MECEUC.CHIMAG'
      CHREEL='&&MECEUC.CHREEL'
      CH1='&&MECEUC.CH1'
      CH2='&&MECEUC.CH2'
      CHDYNR='&&MECEUC.CHDYNR'
      CHDYNI='&&MECEUC.CHDYNI'
      CHACCR='&&MECEUC.CHACCR'
      CHACCI='&&MECEUC.CHACCI'
      CHDEPR='&&MECEUC.CHDEPR'
      CHDEPI='&&MECEUC.CHDEPI'
      INDIC1=0
      INDIC2=0
      INDIC3=0
      INDIC4=0
      CARAEL=CARAEZ

C     1. CHAMP COMPLEXE = DEPL
C     -----------------------------------------------
      IF ((OPTIO2.EQ.'SIEF_ELGA') .OR.
     &    (OPTIO2.EQ.'EPSI_ELNO') .OR.
     &    (OPTIO2.EQ.'EPSI_ELGA') .OR.
     &    (OPTIO2.EQ.'EFGE_ELNO') .OR.
     &    (OPTIO2.EQ.'EFGE_ELNO_SENS') .OR.
     &    (OPTIO2.EQ.'SIPO_ELNO') .OR.
     &    (OPTIO2.EQ.'SIPM_ELNO') .OR.
     &    (OPTIO2.EQ.'SIPO_ELNO_SENS') .OR.
     &    (OPTIO2.EQ.'EPOT_ELEM') .OR.
     &    (OPTIO2.EQ.'ECIN_ELEM')) THEN
         DO 10 I=1,NIN
            IF (LPAIN(I).EQ.'PDEPLAR')INDIC1=I
10       CONTINUE
         CALL ASSERT(INDIC1.GT.0)
         IF ( LCHIN(INDIC1).EQ.' ' ) THEN
            VALK(1) = OPTION
            CALL U2MESK('F','UTILITAI8_10',1,VALK)
         ENDIF
         CALL JEVEUO(LCHIN(INDIC1)(1:19)//'.DESC','L',IDESC)
         GD=ZI(IDESC)
         CALL JENUNO(JEXNUM('&CATA.GD.NOMGD',GD),NOMGD)

         IF (NOMGD.EQ.'DEPL_R') THEN
C           ----------------------------------
C           LE CHAMP EST REEL, ON FAIT UN APPEL A CALCUL ET ON SORT
            CALL CALCUL(STOP,OPTIO2,LIGREL,NIN,LCHIN,LPAIN,NOU,LCHOU,
     &                  LPAOU,BASE,'OUI')
            GOTO 70

         ELSEIF (NOMGD.EQ.'DEPL_C') THEN
C           ----------------------------------
C           LE CHAMP EST COMPLEXE, ON LE DIVISE EN PARTIE REELLE ET
C           PARTIE IMAGINAIRE PUIS ON APPELLE CALCUL SUR CHACUN
C           DES CHAMPS
            CALL ASSERT(NOU.EQ.1)
            CALL SEPACH(CARAEL,LCHIN(INDIC1),'V',CHREEL,CHIMAG)
            LCHIN(INDIC1)=CHREEL

            IF (POUX.EQ.'OUI') THEN
               DO 20 I=1,NIN
                  IF (LPAIN(I).EQ.'PCHDYNR')INDIC2=I
   20          CONTINUE
               CALL ASSERT(INDIC2.GT.0)
               CALL SEPACH(CARAEL,LCHIN(INDIC2),'V',CHDYNR,CHDYNI)
               LCHIN(INDIC2)=CHDYNR
            ENDIF

            IF (OPTIO2(11:14).EQ.'SENS') THEN
               DO 30 I=1,NIN
                  IF (LPAIN(I).EQ.'PACCSEN')INDIC3=I
   30          CONTINUE
               CALL ASSERT(INDIC3.GT.0)
               CALL SEPACH(CARAEL,LCHIN(INDIC3),'V',CHACCR,CHACCI)
               LCHIN(INDIC3)=CHACCR

               DO 40 I=1,NIN
                  IF (LPAIN(I).EQ.'PDEPSEN')INDIC4=I
   40          CONTINUE
               CALL ASSERT(INDIC4.GT.0)
               CALL SEPACH(CARAEL,LCHIN(INDIC4),'V',CHDEPR,CHDEPI)
               LCHIN(INDIC4)=CHDEPR
            ENDIF

C           -- CALCUL 1ERE PARTIE (R):
            CALL COPISD('CHAM_ELEM_S','V',LCHOU(1),CH1)
            CALL CALCUL(STOP,OPTIO2,LIGREL,NIN,LCHIN,LPAIN,NOU,CH1,
     &                  LPAOU,'V','OUI')

C           -- CALCUL 2EME PARTIE (I):
            LCHIN(INDIC1)=CHIMAG
            IF (POUX.EQ.'OUI')LCHIN(INDIC2)=CHDYNI
            IF (OPTIO2(11:14).EQ.'SENS') THEN
               LCHIN(INDIC3)=CHACCI
               LCHIN(INDIC4)=CHDEPI
            ENDIF
            CALL COPISD('CHAM_ELEM_S','V',LCHOU(1),CH2)
            CALL CALCUL(STOP,OPTIO2,LIGREL,NIN,LCHIN,LPAIN,NOU,CH2,
     &                  LPAOU,'V','OUI')

C           -- ASSEMBLAGE (R,I) OU CUMUL (R+I) :
            IF (  (OPTIO2.EQ.'EPOT_ELEM') .OR.
     &            (OPTIO2.EQ.'ECIN_ELEM')) THEN
               CALL BARYCH(CH1,CH2,1.D0,1.D0,LCHOU(1),'G')
            ELSE
               CALL ASSACH(CH1,CH2,BASE,LCHOU(1))
            ENDIF

         ELSE
            CALL U2MESS('F','CALCULEL3_28')
         ENDIF

C     2. CHAMP COMPLEXE = SIEF
C     -----------------------------------------------
      ELSEIF ((OPTIO2.EQ.'ENEL_ELGA') .OR.
     &        (OPTIO2.EQ.'ENEL_ELNO') .OR.
     &        (OPTIO2.EQ.'EFGE_ELGA') .OR.
     &        (OPTIO2.EQ.'SIGM_ELGA') .OR.
     &        (OPTIO2.EQ.'SIGM_ELNO') .OR.
     &        (OPTIO2.EQ.'SIEF_ELNO')) THEN
C     &        (OPTIO2.EQ.'EFGE_ELNO') .OR.

         DO 50 I=1,NIN
            IF (OPTIO2.EQ.'EFGE_ELGA') THEN
              IF (LPAIN(I).EQ.'PSIEFR')INDIC1=I
            ELSEIF (OPTIO2.EQ.'EFGE_ELNO') THEN
              IF (LPAIN(I).EQ.'PEFGAR')INDIC1=I
            ELSEIF (OPTIO2.EQ.'SIGM_ELGA') THEN
              IF (LPAIN(I).EQ.'PSIEFR')INDIC1=I
            ELSE
              IF (LPAIN(I).EQ.'PCONTRR')INDIC1=I
            ENDIF
   50    CONTINUE
         CALL ASSERT(INDIC1.GT.0)
         NOMGD=' '
         IF ( LCHIN(INDIC1).NE.' ' ) THEN
           CALL DISMOI('F','NOM_GD',LCHIN(INDIC1)(1:19),'CHAMP',IBID,
     &                 NOMGD,IBID)
         ENDIF

         IF (NOMGD.EQ.'SIEF_R') THEN
C        -------------------------------
            CALL CALCUL(STOP,OPTIO2,LIGREL,NIN,LCHIN,LPAIN,NOU,LCHOU,
     &                  LPAOU,BASE,'OUI')
            GOTO 70

         ELSEIF (NOMGD.EQ.'SIEF_C') THEN
C        -------------------------------
            CALL ASSERT(NOU.EQ.1)
            CALL SEPACH(CARAEL,LCHIN(INDIC1),'V',CHREEL,CHIMAG)
            LCHIN(INDIC1)=CHREEL

            IF (POUX.EQ.'OUI') THEN
               DO 60 I=1,NIN
                  IF (LPAIN(I).EQ.'PCHDYNR')INDIC2=I
   60          CONTINUE
               CALL ASSERT(INDIC2.GT.0)
               CALL SEPACH(CARAEL,LCHIN(INDIC2),'V',CHDYNR,CHDYNI)
               LCHIN(INDIC2)=CHDYNR
            ENDIF

C           -- CALCUL 1ERE PARTIE (R):
            CALL COPISD('CHAM_ELEM_S','V',LCHOU(1),CH1)
            CALL CALCUL(STOP,OPTIO2,LIGREL,NIN,LCHIN,LPAIN,NOU,CH1,
     &                  LPAOU,'V','OUI')

C           -- CALCUL 2EME PARTIE (I):
            LCHIN(INDIC1)=CHIMAG
            IF (POUX.EQ.'OUI')LCHIN(INDIC2)=CHDYNI
            CALL COPISD('CHAM_ELEM_S','V',LCHOU(1),CH2)
            CALL CALCUL(STOP,OPTIO2,LIGREL,NIN,LCHIN,LPAIN,NOU,CH2,
     &                  LPAOU,'V','OUI')


C           -- ASSEMBLAGE (R,I) OU CUMUL (R+I) :
            IF ((OPTIO2.EQ.'SIEF_ELNO').OR.
     &          (OPTIO2.EQ.'SIGM_ELGA').OR.
     &          (OPTIO2.EQ.'SIGM_ELNO').OR.
     &          (OPTIO2.EQ.'EFGE_ELGA').OR.
     &          (OPTIO2.EQ.'EFGE_ELNO').OR.
     &          (OPTIO2.EQ.'SIEF_ELGA')) THEN
               CALL ASSACH(CH1,CH2,BASE,LCHOU(1))
            ELSE
               CALL BARYCH(CH1,CH2,1.D0,1.D0,LCHOU(1),'G')
            ENDIF

         ELSEIF (NOMGD.EQ.' '.AND.LCHIN(INDIC1).EQ.' ') THEN

           GOTO 70

         ELSE
            CALL U2MESS('F','CALCULEL3_28')
         ENDIF
C     -----------------------------------------------
C     3. AUTRES OPTIONS :
      ELSE
C        POUR TOUTE LES AUTRES OPTIONS LE CALCUL EN COMPLEXE N EST PAS
C        ENCORE AUTORISE ET ON FAIT DONC UN APPEL A CALCUL
         CALL CALCUL(STOP,OPTIO2,LIGREL,NIN,LCHIN,LPAIN,NOU,LCHOU,
     &               LPAOU,BASE,'OUI')
      ENDIF

   70 CONTINUE
      CALL DETRSD('CHAMP',CH1)
      CALL DETRSD('CHAMP',CH2)
      CALL DETRSD('CHAMP',CHIMAG)
      CALL DETRSD('CHAMP',CHREEL)
      CALL DETRSD('CHAMP',CHDYNR)
      CALL DETRSD('CHAMP',CHDYNI)
      CALL DETRSD('CHAMP',CHACCR)
      CALL DETRSD('CHAMP',CHACCI)
      CALL DETRSD('CHAMP',CHDEPR)
      CALL DETRSD('CHAMP',CHDEPI)

      CALL JEDEMA()
      END
