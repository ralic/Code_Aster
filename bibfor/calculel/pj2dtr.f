      SUBROUTINE PJ2DTR(CORTR3,CORRES,NUTM2D,ELRF2D,GEOM1,GEOM2,LRAFF)
      IMPLICIT NONE
      INCLUDE 'jeveux.h'

      CHARACTER*32 JEXATR
      CHARACTER*16 CORRES,CORTR3
      INTEGER NBTM
      PARAMETER    (NBTM=6)
      CHARACTER*8 ELRF2D(NBTM)
      INTEGER NUTM2D(NBTM)
      REAL*8  GEOM1(*),GEOM2(*)
      LOGICAL LRAFF
C ----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF CALCULEL  DATE 19/12/2012   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C     BUT :
C       TRANSFORMER CORTR3 EN CORRES EN UTILISANT LES FONC. DE FORME
C       DES MAILLES DU MAILLAGE1 (EN 2D ISOPARAMETRIQUE)

C
C  IN/JXIN   CORTR3   K16 : NOM DU CORRESP_2_MAILLA FAIT AVEC LES TRIA3
C  IN/JXOUT  CORRES   K16 : NOM DU CORRESP_2_MAILLA FINAL
C  IN        NUTM2D(6) I  : NUMEROS DES 6 TYPES DE MAILLES 2D
C  IN        ELRF2D(6) K8 : NOMS DES 6 TYPES DE MAILLES 2D
C  IN        GEOM1        : geometrie des noeuds du maillage 1
C  IN        GEOM2        : GEOMETRIE DES NOEUDS DU MAILLAGE 2
C  IN        LRAFF     L  : .TRUE. => ON VA UTILISER REEREG.F
C                            POUR ESSAYER DE "RAFFINER" LA PRECISION
C                            INUTILISABLE DANS LE CAS 2.5D
C ----------------------------------------------------------------------
C
      LOGICAL            LEXT
      INTEGER        NBNOMX,    NBFAMX
      PARAMETER    ( NBNOMX=27, NBFAMX=20)
      CHARACTER*8  M1, M2, KB, ELREFA, FAPG(NBFAMX),ALARME
      CHARACTER*16  K16BID,NOMCMD
      INTEGER      NBPG(NBFAMX), CNQUAD(3,2)
      REAL*8  CRREFE(3*NBNOMX), KSI, ETA, XR1(2), XR2(2), XR3(2)
      REAL*8  FF(NBNOMX),COOELE(3*NBNOMX),X1,X2,VOL
      INTEGER I1COCF,I1CONB,J1XXK1,I1CONU,I1COTR,I2COCF,I2COCO
      INTEGER I2COM1,I2CONB,J2XXK1,I2CONU,IACNX1,IALIM1,IALIN1,IALIN2
      INTEGER IATR3,IATYMA,IDECA1,IDECA2,IE,ILCNX1,IMA1,INDIIS,INO,INO2
      INTEGER IRET,ITR,ITYPM,KDIM,KK,NBFPG,NBNO,NDIM,NMA1,NMA2,NNO
      INTEGER NNO1,NNO2,NNOS,NUNO,NUNO2,NUTM,IBID
      INTEGER      IARG
C --- DEB --------------------------------------------------------------

      CALL JEMARQ()

C     0. DECOUPAGE DES QUADRANGLES EN 2 TRIANGLES (VOIR PJ2DCO)
C     ----------------------------------------------------------

      CNQUAD(1,1)=1
      CNQUAD(2,1)=2
      CNQUAD(3,1)=3

      CNQUAD(1,2)=1
      CNQUAD(2,2)=3
      CNQUAD(3,2)=4

C     1. RECUPERATION DES INFORMATIONS GENERALES :
C     -----------------------------------------------
      CALL JEVEUO(CORTR3//'.PJXX_K1','L',J1XXK1)
      CALL JEVEUO(CORTR3//'.PJEF_NB','L',I1CONB)
      CALL JEVEUO(CORTR3//'.PJEF_NU','L',I1CONU)
      CALL JEVEUO(CORTR3//'.PJEF_CF','L',I1COCF)
      CALL JEVEUO(CORTR3//'.PJEF_TR','L',I1COTR)

      M1=ZK24(J1XXK1-1+1)(1:8)
      M2=ZK24(J1XXK1-1+2)(1:8)
      CALL DISMOI('F','NB_NO_MAILLA', M1,'MAILLAGE',NNO1,KB,IE)
      CALL DISMOI('F','NB_NO_MAILLA', M2,'MAILLAGE',NNO2,KB,IE)
      CALL DISMOI('F','NB_MA_MAILLA', M1,'MAILLAGE',NMA1,KB,IE)
      CALL DISMOI('F','NB_MA_MAILLA', M2,'MAILLAGE',NMA2,KB,IE)

      CALL JEVEUO('&&PJXXCO.LIMA1','L',IALIM1)
      CALL JEVEUO('&&PJXXCO.LINO1','L',IALIN1)
      CALL JEVEUO('&&PJXXCO.LINO2','L',IALIN2)
      CALL JEVEUO('&&PJXXCO.TRIA3','L',IATR3)

      CALL JEVEUO(M1//'.CONNEX','L',IACNX1)
      CALL JEVEUO(JEXATR(M1//'.CONNEX','LONCUM'),'L',ILCNX1)
      CALL JEVEUO(M1//'.TYPMAIL','L',IATYMA)


C     2. ALLOCATION DE CORRES :
C     -----------------------------------------------
      CALL WKVECT(CORRES//'.PJXX_K1','V V K24',5,J2XXK1)
      ZK24(J2XXK1-1+1)=M1
      ZK24(J2XXK1-1+2)=M2
      ZK24(J2XXK1-1+3)='COLLOCATION'


C     2.1 REMPLISSAGE DE .PJEF_NB ET .PJEF_M1:
C     -----------------------------------------
      CALL WKVECT(CORRES//'.PJEF_NB','V V I',NNO2,I2CONB)
      CALL WKVECT(CORRES//'.PJEF_M1','V V I',NNO2,I2COM1)
      IDECA2=0
      DO 10, INO2=1,NNO2
C       ITR : TRIA3 ASSOCIE A INO2
        ITR=ZI(I1COTR-1+INO2)
        IF (ITR.EQ.0) GO TO 10
C       IMA1 : MAILLE DE M1 ASSOCIE AU TRIA3 ITR
        IMA1=ZI(IATR3+4*(ITR-1)+4)
        NBNO=ZI(ILCNX1+IMA1)-ZI(ILCNX1-1+IMA1)
        ZI(I2CONB-1+INO2)=NBNO
        ZI(I2COM1-1+INO2)=IMA1
        IDECA2=IDECA2+NBNO
10    CONTINUE
      IF (IDECA2.EQ.0) CALL U2MESS('F','CALCULEL3_97')


C     2.2 ALLOCATION DE .PJEF_NU .PJEF_CF .PJEF_CO:
C         (ET REMPLISSAGE DE CES 3 OBJETS)
C     ------------------------------------------------------
      CALL WKVECT(CORRES//'.PJEF_NU','V V I',IDECA2,I2CONU)
      CALL WKVECT(CORRES//'.PJEF_CF','V V R',IDECA2,I2COCF)
      CALL WKVECT(CORRES//'.PJEF_CO','V V R',3*NNO2,I2COCO)
      IDECA1=0
      IDECA2=0
      DO 20, INO2=1,NNO2
C       ITR : TRIA3 ASSOCIE A INO2
        ITR = ZI(I1COTR-1+INO2)
        IF (ITR.EQ.0) GO TO 20
C       IMA1 : MAILLE DE M1 ASSOCIE AU TRIA3 ITR
        IMA1= ZI(IATR3+4*(ITR-1)+4)
C       ITYPM : TYPE DE LA MAILLE IMA1
        ITYPM = ZI(IATYMA-1+IMA1)
        NUTM   = INDIIS(NUTM2D,ITYPM,1,NBTM)
        ELREFA = ELRF2D(NUTM)
        NBNO   = ZI(ILCNX1+IMA1)-ZI(ILCNX1-1+IMA1)

        CALL ELRACA(ELREFA,NDIM,NNO,NNOS,NBFPG,FAPG,NBPG,CRREFE,VOL)

        CALL ASSERT ( NBNO .EQ. NNO )

C       2.2.1 DETERMINATION DES COORDONEES DE INO2 DANS L'ELEMENT
C             DE REFERENCE : KSI , ETA
C     -----------------------------------------------------------
        KSI=0.D0
        ETA=0.D0
C       -- NUMERO DU 2EME NOEUD DE IMA1 : NUNO2
        NUNO2=ZI(IACNX1+ ZI(ILCNX1-1+IMA1)-2+2)
C       SI NUNO2 EST IDENTIQUE AU 2EME NOEUD DU TRIA3
C       C'EST QUE LE TRIA3 EST EN "DESSOUS" :

        IF (ELREFA.EQ.'TR3' .OR. ELREFA.EQ.'TR6' .OR.
     &                           ELREFA.EQ.'TR7') THEN

          DO 771,KK=1,3
            X1 = CRREFE(NDIM*(KK-1)+1)
            X2 = CRREFE(NDIM*(KK-1)+2)
            KSI = KSI + ZR(I1COCF-1+IDECA1+KK)*X1
            ETA = ETA + ZR(I1COCF-1+IDECA1+KK)*X2
771       CONTINUE

        ELSE IF (ELREFA.EQ.'QU4' .OR. ELREFA.EQ.'QU8' .OR.
     &                                ELREFA.EQ.'QU9' ) THEN
          IF (NUNO2.EQ.ZI(IATR3+4*(ITR-1)+2)) THEN
C         -- SI 1ER TRIANGLE :
            DO 772,KK=1,3
              X1 = CRREFE(NDIM*(CNQUAD(KK,1)-1)+1)
              X2 = CRREFE(NDIM*(CNQUAD(KK,1)-1)+2)
              KSI = KSI + ZR(I1COCF-1+IDECA1+KK)*X1
              ETA = ETA + ZR(I1COCF-1+IDECA1+KK)*X2
772         CONTINUE
          ELSE
C         -- SI 2EME TRIANGLE :
            DO 773,KK=1,3
              X1 = CRREFE(NDIM*(CNQUAD(KK,2)-1)+1)
              X2 = CRREFE(NDIM*(CNQUAD(KK,2)-1)+2)
              KSI = KSI + ZR(I1COCF-1+IDECA1+KK)*X1
              ETA = ETA + ZR(I1COCF-1+IDECA1+KK)*X2
773         CONTINUE
          END IF

        ELSE
           CALL U2MESK('F','ELEMENTS_55',1,ELREFA)
        END IF


        XR1(1) = KSI
        XR1(2) = ETA

        IF (LRAFF) THEN
C         -- ON ESSAYE D'AMELIORER LA PRECISION DE XR1(*)
C            EN UTILISANT REEREG :
          DO 72,INO=1,NBNO
            NUNO = ZI(IACNX1+ ZI(ILCNX1-1+IMA1)-2+INO)
            DO 73,KDIM=1,NDIM
              COOELE(NDIM*(INO-1)+KDIM)=GEOM1(3*(NUNO-1)+KDIM)
73          CONTINUE
72        CONTINUE
          CALL REEREG('C',ELREFA,NNO,COOELE,GEOM2(3*(INO2-1)+1),
     &                NDIM,XR2,IRET)

C         -- ON REGARDE SI INO2 EST EXTERIEUR A IMA1 :
          ALARME='OUI'
          CALL GETRES(K16BID,K16BID,NOMCMD)
          IF (NOMCMD.EQ.'PROJ_CHAMP') THEN
             CALL GETVTX(' ','ALARME',1,IARG,1,ALARME,IBID)
          ENDIF
          CALL PJEFLO(ELREFA,NDIM,IRET,XR2,ALARME,M2,INO2,M1,IMA1,LEXT)

C         -- ON CHOISIT LA MEILLEURE APPROXIMATION ENTRE XR1 ET XR2:
          CALL PJEFMI(ELREFA,NNO,COOELE,GEOM2(3*(INO2-1)+1),
     &                NDIM,XR1,XR2,LEXT,XR3)
        ELSE
          XR3(1)=XR1(1)
          XR3(2)=XR1(2)
        ENDIF

        ZR(I2COCO-1+3*(INO2-1)+1)=XR3(1)
        ZR(I2COCO-1+3*(INO2-1)+2)=XR3(2)

C       2.2.2 :
C       CALCUL DES F. DE FORME AUX NOEUDS POUR LE POINT XR3
        CALL ELRFVF ( ELREFA, XR3, 27, FF, NNO )
        DO 22,INO=1,NBNO
          NUNO = ZI(IACNX1+ ZI(ILCNX1-1+IMA1)-2+INO)
          ZI(I2CONU-1+IDECA2+INO) = NUNO
          ZR(I2COCF-1+IDECA2+INO) = FF(INO)
22      CONTINUE

        IDECA1=IDECA1+3
        IDECA2=IDECA2+NBNO

20    CONTINUE

      CALL JEDEMA()
      END
