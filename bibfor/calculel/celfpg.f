      SUBROUTINE CELFPG(CELZ,NOMOBJ,IRET)
      IMPLICIT  NONE
      INCLUDE 'jeveux.h'

      CHARACTER*32 JEXNUM,JEXATR
      CHARACTER*(*) CELZ,NOMOBJ
      INTEGER IRET
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF CALCULEL  DATE 09/11/2012   AUTEUR DELMAS J.DELMAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE PELLET J.PELLET
C ------------------------------------------------------------------
C BUT : EXTRAIRE DU CHAM_ELEM (ELGA) CELZ UN OBJET JEVEUX CONTENANT
C       LE SCHEMA DE POINT DE GAUSS DES MAILLES
C ------------------------------------------------------------------
C     ARGUMENTS:
C CELZ    IN/JXIN  K19 : SD CHAM_ELEM A EXAMINER
C         REMARQUE:   SI CELZ N'EST PAS "ELGA", NOMOBJ N'EST PAS CREE
C
C NOMOBJ  IN/JXVAR K24 : OBJET JEVEUX A CREER (SUR LA BASE 'V')
C    EN SORTIE, L'OBJET NOMOBJ EST UN VECTEUR DE K16 DIMENSIONNE AU
C    NOMBRE DE MAILLES DU MAILLAGE.
C    V(IMA)(1: 8) : NOM DE L'ELREFE POUR LA MAILLE IMA (OU ' ')
C    V(IMA)(9:16) : NOM DE LA FAMILLE DE PG POUR LA MAILLE IMA (OU ' ')
C
C    SI LA FAMILLE DE PG EST UNE FAMILLE "LISTE" (PAR EXEMPLE MATER),
C    V(IMA) CONTIENT LE NUMERO (<0) DE LA FAMILLE. EX : "-185"
C
C    REMARQUE :
C      SI L'OBJET NOMOBJ EXISTE AVANT L'APPEL A CELFPG :
C      * ON VERFIE QUE LE LIGREL EST LE MEME QUE CELUI QUI A SERVI A
C      CREER NOMOBJ
C      * ON NE LE RECALCULE PAS MAIS ON VA VERIFIER
C      QUE LA FAMILLE DES ELEMENTS DE CELZ EST COHERENTE AVEC CELLE
C      DECRITE DANS NOMOBJ. (CAS D'UNE BOUCLE SUR LES NOMSYM)
C      * ON PEUT NE FAIRE LA VERIF QUE SUR LE 1ER ELEMENT DE CHAQUE GREL
C      PUISQUE LE LIGREL EST LE MEME.
C
C
C IRET    CODE RETOUR
C         0 : PAS D'ERREUR
C         1 : CELZ NE CORRESPOND PAS A NOMOBJ (S'IL EXISTE)
      LOGICAL LEXI
C     ------------------------------------------------------------------
C     VARIABLES LOCALES:
C     ------------------
      CHARACTER*8 MA,NOMGD,KBID
      CHARACTER*16 NOFPG
      CHARACTER*19 CEL,LIGREL,LIGRSV
      CHARACTER*24 NOMOSV
      INTEGER JOBJ,IBID,NBMA,JCELV,JCELD,NEC
      INTEGER IGR,IEL,IALIEL,ILLIEL,NUMAIL,NBELEM
      INTEGER JCELK,NBGR,IMOLO,JMOLO,NUMA,NBEL,KFPG
      INTEGER IEXI
      SAVE LIGRSV,NOMOSV

      NUMAIL(IGR,IEL)=ZI(IALIEL-1+ZI(ILLIEL+IGR-1)+IEL-1)
C     ------------------------------------------------------------------

      CALL JEMARQ()
      CEL=CELZ
      IRET=0

C     -- SI CE N'EST PAS UN CHAMP ELGA, IL N'Y A RIEN A FAIRE :
      CALL JEVEUO(CEL//'.CELK','L',JCELK)
      IF (ZK24(JCELK-1+3).NE.'ELGA')GOTO 30


C     1 CALCUL DE LIGREL,NBMA,NEC :
C     --------------------------------------------------------
      CALL DISMOI('F','NOM_MAILLA',CEL,'CHAM_ELEM',IBID,MA,IBID)
      CALL DISMOI('F','NOM_LIGREL',CEL,'CHAM_ELEM',IBID,LIGREL,IBID)
      CALL DISMOI('F','NB_MA_MAILLA',MA,'MAILLAGE',NBMA,KBID,IBID)
      CALL DISMOI('F','NOM_GD',CEL,'CHAM_ELEM',IBID,NOMGD,IBID)
      CALL DISMOI('F','NB_EC',NOMGD,'GRANDEUR',NEC,KBID,IBID)


C     2 RECUPERATION DES OBJETS DU CHAM_ELEM ET DU LIGREL :
C     -------------------------------------------------------
      CALL JEVEUO(CEL//'.CELV','L',JCELV)
      CALL JEVEUO(CEL//'.CELD','L',JCELD)
      CALL JEVEUO(LIGREL//'.LIEL','L',IALIEL)
      CALL JEVEUO(JEXATR(LIGREL//'.LIEL','LONCUM'),'L',ILLIEL)
      NBGR=ZI(JCELD-1+2)


C     3 REPLISSAGE DE NOMOBJ :
C     -------------------------------------------------------

      CALL JEEXIN(NOMOBJ,IEXI)
      LEXI=(IEXI.GT.0)

      IF (.NOT.LEXI) THEN
        CALL WKVECT(NOMOBJ,'V V K16',NBMA,JOBJ)
        LIGRSV=LIGREL
        NOMOSV=NOMOBJ
      ELSE
        CALL ASSERT(NOMOBJ.EQ.NOMOSV)
        CALL ASSERT(LIGREL.EQ.LIGRSV)
        CALL JEVEUO(NOMOBJ,'L',JOBJ)
      ENDIF

      DO 20,IGR=1,NBGR
        NBEL=NBELEM(LIGREL,IGR)
        IMOLO=ZI(JCELD-1+ZI(JCELD-1+4+IGR)+2)
        IF (IMOLO.EQ.0)GOTO 20
        CALL JEVEUO(JEXNUM('&CATA.TE.MODELOC',IMOLO),'L',JMOLO)
        KFPG=ZI(JMOLO-1+4+NEC+1)
        IF (KFPG.GT.0) THEN
          CALL JENUNO(JEXNUM('&CATA.TM.NOFPG',KFPG),NOFPG)
        ELSE
          CALL CODENT(KFPG,'G',NOFPG)
        ENDIF

        IF (.NOT.LEXI) THEN
          DO 10,IEL=1,NBEL
            NUMA=NUMAIL(IGR,IEL)
            IF (NUMA.GT.0)ZK16(JOBJ-1+NUMA)=NOFPG
   10     CONTINUE

        ELSE
          IEL=1
          NUMA=NUMAIL(IGR,IEL)
          IF (ZK16(JOBJ-1+NUMA).NE.NOFPG) THEN
            IRET=1
            GOTO 30
          ENDIF
        ENDIF
   20 CONTINUE


   30 CONTINUE
      CALL JEDEMA()
      END
