      SUBROUTINE CNOCRE(MAZ,NOMGDZ,NBNOZ,LINOE,NCMPZ,LICMP,CNOCMP,BASEZ,
     &                  PROF,CNOZ)
C RESPONSABLE VABHHTS J.PELLET
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF CALCULEL  DATE 26/04/2011   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
      IMPLICIT NONE
      CHARACTER*(*) MAZ,NOMGDZ,CNOZ,BASEZ,PROF
      INTEGER NCMPZ,NBNOZ,LINOE(NBNOZ),CNOCMP(NBNOZ*NCMPZ)
      CHARACTER*(*) LICMP(NCMPZ)
C ------------------------------------------------------------------
C BUT : CREER UN CHAM_NO A VALEURS NULLES SUR UN PROFIL DEJA EXISTANT
C OU NON (SI LE PROFIL N'EXISTE PAS -- BLANCS ' ' -- IL EST CREE)
C ------------------------------------------------------------------
C     ARGUMENTS:
C MAZ     IN/JXIN  K8  : MAILLAGE DE CNOZ
C NOMGDZ  IN       K8  : NOM DE LA GRANDEUR DE CNOZ
C NBNOZ   IN       I   : NOMBRE DE NOEUDS VOULUES DANS CNOZ
C LINOE   IN       L_I : NOMS DES NOEUDS VOULUES DANS CNOZ
C NCMPZ   IN       I   : NOMBRE DE CMPS VOULUES DANS CNOZ
C LICMP   IN       L_K8: NOMS DES CMPS VOULUES DANS CNOZ
C BASEZ   IN       K1  : BASE DE CREATION POUR CNOZ : G/V/L
C PROF    IN/JXVAR K19 : SD PROF_CHNO SUR LAQUELLE LE CHAM_NO EST CREE
C CNOZ    IN/JXOUT K19 : SD CHAM_NO A CREER
C---------------- COMMUNS NORMALISES  JEVEUX  --------------------------
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C     ------------------------------------------------------------------
C     VARIABLES LOCALES:
C     ------------------
      INTEGER IBID,NBNO,JCNSK,JCNSD,INO
      INTEGER I,K,JCNSL,JCNSV,NCMP
      CHARACTER*3 TSCA
      CHARACTER*8 NOMGD
      CHARACTER*19 CNS
C     ------------------------------------------------------------------

      CALL JEMARQ()

      CNS = '&&CNOCRE.CNS'
      CALL CNSCRE(MAZ,NOMGDZ,NCMPZ,LICMP,'V',CNS)

      CALL JEVEUO(CNS//'.CNSK','L',JCNSK)
      CALL JEVEUO(CNS//'.CNSD','L',JCNSD)
      CALL JEVEUO(CNS//'.CNSV','E',JCNSV)
      CALL JEVEUO(CNS//'.CNSL','E',JCNSL)

      NOMGD = ZK8(JCNSK-1+2)
      NBNO = ZI(JCNSD-1+1)
      NCMP = ZI(JCNSD-1+2)

      CALL DISMOI('F','TYPE_SCA',NOMGD,'GRANDEUR',IBID,TSCA,IBID)

      CALL ASSERT((TSCA.EQ.'R') .OR. (TSCA.EQ.'C'))
      IF (TSCA.EQ.'R') THEN
C         -----------
        IF (NBNOZ.EQ.0) THEN
          DO 20,K = 1,NCMP
            DO 10,INO = 1,NBNO
              ZL(JCNSL-1+ (INO-1)*NCMP+K) = .TRUE.
              ZR(JCNSV-1+ (INO-1)*NCMP+K) = 0.0D0
   10       CONTINUE
   20     CONTINUE

        ELSE
          DO 40,I = 1,NBNOZ
            INO = LINOE(I)
            DO 30,K = 1,NCMP
              IF (CNOCMP((I-1)*NCMP+K).EQ.1) THEN
                ZL(JCNSL-1+ (INO-1)*NCMP+K) = .TRUE.
                ZR(JCNSV-1+ (INO-1)*NCMP+K) = 0.0D0
              ENDIF
   30       CONTINUE
   40     CONTINUE
        ENDIF
C
      ELSEIF (TSCA.EQ.'C') THEN
C             -----------
        IF (NBNOZ.EQ.0) THEN
          DO 60,K = 1,NCMP
            DO 50,INO = 1,NBNO
              ZL(JCNSL-1+ (INO-1)*NCMP+K) = .TRUE.
              ZC(JCNSV-1+ (INO-1)*NCMP+K) = (0.0D0,0.0D0)
   50       CONTINUE
   60     CONTINUE

        ELSE
          DO 80,I = 1,NBNOZ
            INO = LINOE(I)
            DO 70,K = 1,NCMP
              IF (CNOCMP((I-1)*NCMP+K).EQ.1) THEN
                ZL(JCNSL-1+ (INO-1)*NCMP+K) = .TRUE.
                ZC(JCNSV-1+ (INO-1)*NCMP+K) = (0.0D0,0.0D0)
              ENDIF
   70       CONTINUE
   80     CONTINUE
        ENDIF
      ENDIF


      CALL CNSCNO(CNS,PROF,'NON',BASEZ,CNOZ,'F',IBID)
      CALL DETRSD('CHAM_NO_S',CNS)

      CALL JEDEMA()
      END
