      SUBROUTINE RESLO2(MODELE,LIGREL,LCHAR,CHVOIS,CVOISX,TABIDO)
      IMPLICIT NONE
      INTEGER       TABIDO(5)
      CHARACTER*8   MODELE,LCHAR(*)
      CHARACTER*24  CHVOIS
      CHARACTER*(*) LIGREL
C ----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF CALCULEL  DATE 22/12/2008   AUTEUR DELMAS J.DELMAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2003  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C
C     BUT:
C         PREPARER LE CALCUL DE L'ESTIMATEUR D'ERREUR EN RESIDU.
C         RECHERCHER LES VOISINS DE CHAQUE ELEMENT ET RECUPERER
C         DES ADRESSES.


C     ARGUMENTS:
C     ----------
C
C      ENTREE :
C-------------
C IN   MODELE : NOM DU MODELE
C IN   LIGREL : NOM DU LIGREL
C IN   LCHAR  : LISTE DES CHARGEMENTS
C
C      SORTIE :
C-------------
C OUT  CHVOIS : NOM DU CHAMP DES VOISINS
C OUT  CVOISX : POUR XFEM, NOM DU CHAMP DES VOISINS DES SOUS-ELEMENTS
C OUT  TABIDO : TABLEAU D'ENTIERS CONTENANT DES ADRESSES
C           1 : IATYMA : ADRESSE DU VECTEUR TYPE MAILLE (NUMERO <-> NOM)
C           2 : IAGD   : ADRESSE DU VECTEUR GRANDEUR (NUMERO <-> NOM)
C           3 : IACMP  : ADRESSE DU VECTEUR NOMBRE DE COMPOSANTES 
C                  (NUMERO DE GRANDEUR <-> NOMBRE DE COMPOSANTES)
C           4 : ICONX1 : ADRESSE DE LA COLLECTION CONNECTIVITE
C           5 : ICONX2 : ADRESSE DU POINTEUR DE LONGUEUR DE LA
C                        CONNECTIVITE
C ......................................................................
C
C --------- DEBUT DECLARATIONS NORMALISEES JEVEUX ----------------------
C
      CHARACTER*32 JEXNUM,JEXATR
      INTEGER        ZI
      COMMON /IVARJE/ZI(1)
      REAL*8         ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16     ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL        ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8    ZK8
      CHARACTER*16          ZK16
      CHARACTER*24                  ZK24
      CHARACTER*32                          ZK32
      CHARACTER*80                                  ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C --------- FIN DECLARATIONS NORMALISEES JEVEUX ------------------------
C
      CHARACTER*6 NOMPRO
      PARAMETER ( NOMPRO = 'RESLO2' )

      INTEGER NBPIN
      PARAMETER ( NBPIN = 2 )
      INTEGER NBPOUT
      PARAMETER ( NBPOUT = 1 )

      INTEGER      IRET,IBID,IER,NBTM,ITY,NBGD,IGD,NCMP
      INTEGER      IACMP,IAGD,IATYMA,ICONX1,ICONX2
      CHARACTER*1  BASE
      CHARACTER*8  LPAIN(NBPIN),LPAOUT(NBPOUT),MA,TYPEMA,GD
      CHARACTER*16 OPT
      CHARACTER*19 CNSETO,LONCHA
      CHARACTER*24 LCHIN(NBPIN),LCHOUT(NBPOUT),CHGEOM,KBID
      CHARACTER*24 BLAN24,CVOISX
      
      LOGICAL EXIGEO
C
C ----------------------------------------------------------------------
C
C               123456789012345678901234
      BLAN24 = '                        '
C
      BASE = 'V'
      CALL MEGEOM(MODELE,LCHAR(1),EXIGEO,CHGEOM)
      IF (.NOT.EXIGEO) CALL U2MESS('F','CALCULEL2_81')
C
C ------- RECHERCHE DES VOISINS ----------------------------------------
C
      LPAIN(1) = 'PGEOMER'
      LCHIN(1) = CHGEOM

      LPAOUT(1) = 'PVOISIN'
      CHVOIS = '&&'//NOMPRO//'.CH_VOISIN'
      LCHOUT(1) = CHVOIS
      OPT = 'INIT_MAIL_VOIS'
      CALL CALCUL('C',OPT,LIGREL,1,LCHIN,LPAIN,1,LCHOUT,LPAOUT,BASE)
      CALL EXISD('CHAMP_GD',LCHOUT(1),IRET)
      IF (IRET.EQ.0) THEN
          CALL U2MESK('A','CALCULEL2_88',1,OPT)
           GOTO 9999
      END IF

      CALL DISMOI('F','NOM_MAILLA',MODELE,'MODELE',IBID,MA,IER)
      CALL RESVOI(MODELE,MA,CHVOIS)
C
C ------- SI LE MODELE EST XFEM ON CALCULE LA SD VOISIN ----------------
C ---------- POUR LES SOUS-ELEMENTS DES ELEMENTS XFEM ------------------
C      
      CVOISX = BLAN24
      CALL JEEXIN(MODELE//'.FISS',IRET)
C      
      IF(IRET.NE.0) THEN        
        OPT = 'CHVOIS_XFEM'
        CNSETO = MODELE//'.TOPOSE.CNS'
        LONCHA = MODELE//'.TOPOSE.LON'
        CVOISX = '&&'//NOMPRO//'.CH_CNINVX'
C        
        LPAIN(1) = 'PCNSETO'
        LCHIN(1) = CNSETO
        LPAIN(2) = 'PLONCHA'
        LCHIN(2) = LONCHA        
        LPAOUT(1) = 'PCVOISX'
        LCHOUT(1) = CVOISX     
C        
        CALL CALCUL('C',OPT,LIGREL,2,LCHIN,LPAIN,1,LCHOUT,LPAOUT,BASE)
C        
        CALL EXISD('CHAMP_GD',LCHOUT(1),IRET)
C
        IF (IRET.EQ.0) THEN
          CALL U2MESK('A','CALCULEL2_88',1,OPT)
          GOTO 9999
        END IF
C        
      END IF
C
C ----- CALCUL DE 5 ADRESSES : -----------------------------------------
C      IATYMA : ADRESSE DU VECTEUR TYPE MAILLE (NUMERO <-> NOM)
C      IAGD   : ADRESSE DU VECTEUR GRANDEUR (NUMERO <-> NOM)
C      IACMP : ADRESSE DU VECTEUR NOMBRE DE COMPOSANTES
C                     (NUMERO DE GRANDEUR <-> NOMBRE DE COMPOSANTES)
C      ICONX1 : ADRESSE DE LA COLLECTION CONNECTIVITE
C      ICONX2 : ADRESSE DU POINTEUR DE LONGUEUR DE LA CONNECTIVITE
C
      CALL JELIRA('&CATA.TM.NOMTM','NOMMAX',NBTM,KBID)
      CALL WKVECT('&&'//NOMPRO//'.TYPEMA','V V K8',NBTM,IATYMA)
C
      DO 1 ITY=1,NBTM
        CALL JENUNO (JEXNUM('&CATA.TM.NOMTM',ITY),TYPEMA)
        ZK8(IATYMA-1+ITY) = TYPEMA
1     CONTINUE
C
      CALL JELIRA('&CATA.GD.NOMGD','NOMMAX',NBGD,KBID)
      CALL WKVECT('&&'//NOMPRO//'.GD','V V K8',NBGD,IAGD)
C
      DO 2 IGD=1,NBGD
        CALL JENUNO (JEXNUM('&CATA.GD.NOMGD',IGD),GD)
        ZK8(IAGD-1+IGD) = GD
2     CONTINUE
C
      CALL WKVECT('&&'//NOMPRO//'.NBCMP','V V I',NBGD,IACMP)
C
      DO 3 IGD=1,NBGD
        CALL JELIRA (JEXNUM('&CATA.GD.NOMCMP',IGD),'LONMAX',NCMP,KBID)
        ZI(IACMP-1+IGD) = NCMP
3     CONTINUE
C
      CALL JEVEUO(MA//'.CONNEX','L',ICONX1)
      CALL JEVEUO(JEXATR(MA//'.CONNEX','LONCUM'),'L',ICONX2)
C
C ----- REMPLISSAGE DU TABLEAU -----------------------------------------
C
      TABIDO(1) = IATYMA
      TABIDO(2) = IAGD
      TABIDO(3) = IACMP
      TABIDO(4) = ICONX1
      TABIDO(5) = ICONX2
C
9999  CONTINUE
C
      END
