      SUBROUTINE ECLPGC(CH1,CH2,LIGREL,MA2,PRCHNO,NOMFPG)
      IMPLICIT   NONE
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF CALCULEL  DATE 13/12/2011   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE PELLET J.PELLET
C---------------------------------------------------------------------
C BUT : "ECLATER" LE CHAM_ELEM_ELGA CH1 POUR CREER LE CHAM_NO CH2 SUR
C        SUR LE MAILLAGE MA2.
C
C ARGUMENTS :
C  IN/JXN   CH1 : CHAM_ELEM_ELGA A ECLATER
C  IN/JXOUT CH2 : NOM DU CHAM_NO A CREER
C  IN/JXIN  LIGREL : NOM DU LIGREL CORRESPONDANT AUX MAILLES QUI
C           INTERESSENT L'UTILISATEUR.
C           LIGREL EST EVENTUELLEMMENT UN "SOUS" LIGREL DU LIGREL
C           ASSOCIE A CH1.
C  IN/JXIN  MA2 : NOM DU MAILLAGE QUI "PORTERA" LE CHAM_NO CH2
C  IN/JXIN  PRCHNO : NOM DE LA SD_PROF_CHNO QUI SERA ASSOCIEE A CH2
C  IN/JXIN  NOMFPG : NOM D'UN OBJET JEVEUX CONTENANT LE NOM DE LA
C           FAMILLE DE PG A UTILISER POUR CHAQUE MAILLE DE LIGREL.
C
C REMARQUES :
C  * MA2 IL EST TRES IMPORTANT QUE LE MAILLAGE MA2 FOURNI SOIT CELUI
C      QUI A ETE OBTENU PAR LA ROUTINE ECLPGM (AVEC LE MEME
C      LIGREL EN ENTREE).
C      LA "JUSTESSE" DU CHAMP CREE (CH2) PROVIENT DU FAIT QUE DANS
C      LES 2 ROUTINES ECLPGM ET ECLPGC, ON PARCOURT LES MEMES MAILLES
C      DANS LE MEME ORDRE.
C  * NOMFPG PEUT ETRE OBTENU PAR LA ROUTINE CELFPG
C  * LIGREL PEUT ETRE OBTENU PAR LA ROUTINE EXLIMA OU BIEN ON
C           L'EXTRAIT DE CH1 (DISMOI).
C  * PRCHNO PEUT ETRE ' '. DANS CE CAS, ON EN CREERA UN DIFFERENT
C           A CHAQUE APPEL A ECLPGC.
C           CHOISIR PRCHNO /= ' ' PERMET D'ECONOMISER CETTE SD SI
C           PLUSIEURS CHAMPS PEUVENT LA PARTAGER.
C           C'EST LE CAS EN GENERAL POUR LA BOUCLE SUR LES NUME_ORDRE

C
C --- DEBUT DECLARATIONS NORMALISEES JEVEUX ---------------------------
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
      CHARACTER*32 JEXNOM,JEXNUM,JEXATR
C --- FIN DECLARATIONS NORMALISEES JEVEUX -----------------------------

C ---------------------------------------------------------------------
C     VARIABLES NECESSAIRES A L'APPEL DE ECLATY :
C     ON COMPREND LE SENS DE CES VARIABLES EN REGARDANT ECLATY
      INTEGER MXNBN2,MXNBPG,MXNBPI,MXNBTE
C     MXNBN2 : MAX DU NOMBRE DE NOEUDS D'UN SOUS-ELEMENT (HEXA8)
      PARAMETER(MXNBN2=8)
C     MXNBPG : MAX DU NOMBRE DE POINTS DE GAUSS D'UN TYPE_ELEM
      PARAMETER(MXNBPG=27)
C     MXNBPI : MAX DU NOMBRE DE POINT_I (HEXA A 27 POINTS DE GAUSS)
C     MXNBPI = 4X4X4
      PARAMETER(MXNBPI=64)
C     MXNBTE : MAX DU NOMBRE DE TERMES DE LA C.L. DEFINISSANT 1 POINT_I
C              AU PLUS LES 8 SOMMETS D'UN HEXA8
      PARAMETER(MXNBTE=8)
      INTEGER CONNX(MXNBN2,MXNBPG),NSOMM1(MXNBPI,MXNBTE)
      INTEGER NTERM1(MXNBPI),NBNO2(MXNBPG),KK
      REAL*8 CSOMM1(MXNBPI,MXNBTE)
      INTEGER TYMA(MXNBPG)
C ---------------------------------------------------------------------
      LOGICAL EXISDG,LVARI
      INTEGER NUMA,JNOFPG,NUMAIL
      INTEGER K,NBGREL,TE,TYPELE,NPG1,NPOINI,IDECA2
      INTEGER IGR,JMACO,JCMACO,JLIEL,JCLIEL,NBELEM,JCNSL2
      INTEGER IBID,NBPG,INO,NBGR,INOGL
      INTEGER NUMGLM,IPG,IAMOL1,JCELV1,JCNSV2,MXCMP
      INTEGER IMA,NBELGR,NBNOMA,JVAL2,NBNO,NDDL,IDDL,ADIEL
      INTEGER IIPG,JCELD1,JCELK1,MOLOC1,NCMPMX
      PARAMETER(MXCMP=100)
      INTEGER NUDDL(MXCMP),MXVARI,IEL,NCMP,JNOCMP,JCORR1
      CHARACTER*8 MA2,KBID,NOMG1,NOMG2,ELREFA,FAPG
      CHARACTER*16 NOMTE
      CHARACTER*16 OPTIO,PARAM
      CHARACTER*19 LIGREL,CH1,CH2S,CH2,PRCHNO,CH1B
      CHARACTER*24 VALK(2),NOMFPG
C     FONCTIONS FORMULES :
C     NBNOMA(IMA)=NOMBRE DE NOEUDS DE LA MAILLE IMA
      NBNOMA(IMA)=ZI(JCMACO-1+IMA+1)-ZI(JCMACO-1+IMA)
C     NUMGLM(IMA,INO)=NUMERO GLOBAL DU NOEUD INO DE LA MAILLE IMA
C                     IMA ETANT UNE MAILLE DU MAILLAGE.
      NUMGLM(IMA,INO)=ZI(JMACO-1+ZI(JCMACO+IMA-1)+INO-1)
      NUMAIL(IGR,IEL)=ZI(JLIEL-1+ZI(JCLIEL+IGR-1)+IEL-1)
C DEB -----------------------------------------------------------------
      CALL JEMARQ()


      CH1B=CH1
      CALL DISMOI('F','NOM_GD',CH1B,'CHAMP',IBID,NOMG1,IBID)
      IF (NOMG1(5:6).NE.'_R') CALL U2MESS('F','CALCULEL2_39')
      NOMG2=NOMG1
      LVARI=(NOMG1.EQ.'VARI_R')
      IF (LVARI) NOMG2='VAR2_R'



C     -- ON VERIFIE QUE LE CHAM_ELEM N'EST PAS TROP DYNAMIQUE :
      CALL CELVER(CH1B,'NBSPT_1','COOL',KK)
      IF (KK.EQ.1) THEN
        CALL U2MESK('I','PREPOST_36',1,NOMG1)
        CALL CELCEL('PAS_DE_SP',CH1B,'V','&&ECLPGC.CH1B1')
        CH1B='&&ECLPGC.CH1B1'
      ENDIF

C     -- PROJECTION SUR LE LIGREL REDUIT SI NECESSAIRE :
      CALL JEVEUO(CH1B//'.CELK','L',JCELK1)
      IF (ZK24(JCELK1-1+3)(1:4).NE.'ELGA') CALL U2MESS('F',
     &    'CALCULEL2_41')
      IF ( ZK24(JCELK1-1+1)(1:19).NE.LIGREL) THEN
        OPTIO=ZK24(JCELK1-1+2)
        PARAM=ZK24(JCELK1-1+6)
        CALL CHLIGR(CH1B,LIGREL,OPTIO,PARAM,'V','&&ECLPGC.CH1B2')
        CH1B='&&ECLPGC.CH1B2'
      ENDIF

      CALL JEEXIN(CH1B//'.CELD',IBID)
      IF (IBID.EQ.0)  GOTO 90

      CALL JEVEUO(CH1B//'.CELV','L',JCELV1)
      CALL JEVEUO(CH1B//'.CELD','L',JCELD1)
      CALL JEVEUO(CH1B//'.CELD','L',JCELD1)
C     -- MXVARI : NOMBRE MAXI DE VXX SI VARI_R
      MXVARI=MAX(1,ZI(JCELD1-1+4))


C     -- ON CHERCHE LES CMPS PRESENTES DANS LE CHAM_ELEM CH1B :
C         NCMP : NOMBRE DE CMPS PRESENTES
C         '&&ECLPGC.CORR1': CONTIENT LA CORRESPONDANCE ENTRE LE
C                           NUMERO D'1 CMP DU CHAM_ELEM ET LE
C                           NUMERO D'1 CMP DU CHAM_ELEM_S
C         '&&ECLPGC.NOM_CMP': CONTIENT LES NOMS DES CMPS DU CHAM_ELEM_S
C     -----------------------------------------------------------------
      IF (.NOT.LVARI) THEN


        CALL CMPCHA(CH1B,'&&ECLPGC.NOM_CMP','&&ECLPGC.CORR1',
     &            '&&ECLPGC.CORR2',NCMP,NCMPMX)
        CALL JEVEUO('&&ECLPGC.NOM_CMP','L',JNOCMP)
        CALL JEVEUO('&&ECLPGC.CORR1','L',JCORR1)
      ELSE
C       -- POUR VARI_R :
        CALL ASSERT(NOMG2.EQ.'VAR2_R')
        CALL DISMOI('F','NB_CMP_MAX',NOMG2,'GRANDEUR',NCMPMX,KBID,IBID)
        CALL ASSERT(MXVARI.LE.NCMPMX)
        NCMP=MXVARI
        CALL JEVEUO(JEXNOM('&CATA.GD.NOMCMP',NOMG2),'L',JNOCMP)
        CALL WKVECT('&&ECLPGC.CORR1','V V I',NCMP,JCORR1)
        DO 77,K=1,NCMP
          ZI(JCORR1-1+K)=K
 77     CONTINUE
      ENDIF
      CALL ASSERT(NCMP.LE.MXCMP)




C       -- CREATION D'UN CHAM_NO_S : CH2S
C       -----------------------------------------------------
      CH2S='&&ECLPGC.CH2S'
      CALL CNSCRE(MA2,NOMG2,NCMP,ZK8(JNOCMP),'V',CH2S)
      CALL JEVEUO(CH2S//'.CNSV','E',JCNSV2)
      CALL JEVEUO(CH2S//'.CNSL','E',JCNSL2)



C     -- REMPLISSAGE DU CHAM_NO :
C     ---------------------------
      CALL JEVEUO(NOMFPG,'L',JNOFPG)
      CALL JEVEUO(LIGREL//'.LIEL','L',JLIEL)
      CALL JEVEUO(JEXATR(LIGREL//'.LIEL','LONCUM'),'L',JCLIEL)
      CALL JEVEUO(MA2//'.CONNEX','L',JMACO)
      CALL JEVEUO(JEXATR(MA2//'.CONNEX','LONCUM'),'L',JCMACO)
      IPG=0
      NBGR=NBGREL(LIGREL)
      DO 80,IGR=1,NBGR
        MOLOC1=ZI(JCELD1-1+ZI(JCELD1-1+4+IGR)+2)
        IF (MOLOC1.EQ.0)GOTO 80

        IF (.NOT.LVARI) CALL ASSERT(MXVARI.EQ.1)

        CALL JEVEUO(JEXNUM('&CATA.TE.MODELOC',MOLOC1),'L',IAMOL1)
        CALL ASSERT(ZI(IAMOL1-1+1).EQ.3)
        NBPG=ZI(IAMOL1-1+4)

        NUMA=NUMAIL(IGR,1)
        ELREFA=ZK16(JNOFPG-1+NUMA)(1:8)
        FAPG=ZK16(JNOFPG-1+NUMA)(9:16)
        IF (FAPG.EQ.' ')GOTO 80

C           -- ON VERIFIE QUE C'EST UN CHAMP "ELGA/IDEN" :
C           ----------------------------------------------
        CALL ASSERT(.NOT.((NBPG.LT.0).OR.(NBPG.GT.10000)))

C           -- ON ECLATE LE TYPE_ELEM :
C           ---------------------------
        TE=TYPELE(LIGREL,IGR)
        CALL JENUNO(JEXNUM('&CATA.TE.NOMTE',TE),NOMTE)
        CALL ECLATY(NOMTE,ELREFA,FAPG,NPG1,NPOINI,NTERM1,NSOMM1,CSOMM1,
     &              TYMA,NBNO2,CONNX,MXNBN2,MXNBPG,MXNBPI,MXNBTE)
        IF (NPG1.NE.0) THEN
          IF (NBPG.NE.NPG1) THEN
            VALK(1)=NOMTE
            CALL U2MESK('F','CALCULEL2_42',1,VALK)
          ENDIF
        ELSE
C            -- ON IGNORE LES AUTRES ELEMENTS :
          NBPG=0
          GOTO 80

        ENDIF
        NBELGR=NBELEM(LIGREL,IGR)

C            -- QUELLES SONT LES CMPS PORTEES PAR LES POINTS DE GAUSS ?
C            ----------------------------------------------------------
        IF (LVARI) THEN
          NDDL=MXVARI
          DO 20,K=1,NDDL
            NUDDL(K)=K
   20     CONTINUE
        ELSE
          NDDL=0
          DO 30,K=1,NCMPMX
            IF (EXISDG(ZI(IAMOL1-1+4+1),K)) THEN
              NDDL=NDDL+1
              NUDDL(NDDL)=ZI(JCORR1-1+K)
            ENDIF
   30     CONTINUE
        ENDIF


C          -- BOUCLE SUR TOUS LES POINTS DE GAUSS DU GREL :
C          ------------------------------------------------
        DO 70,IEL=1,NBELGR
          IF (LVARI) NDDL=ZI(JCELD1-1+ZI(JCELD1-1+4+IGR)+4+
     &                               4*(IEL-1)+2)

          DO 60,IIPG=1,NBPG
            IPG=IPG+1
C            -- AU POINT DE GAUSS IPG CORRESPOND LA MAILLE NUMERO IPG
C               DANS MA2.
            IMA=IPG
            NBNO=NBNOMA(IMA)
            IF (NBNO.GT.27) CALL U2MESS('F','CALCULEL2_43')
            DO 50,INO=1,NBNO
              INOGL=NUMGLM(IMA,INO)
              DO 40,IDDL=1,NDDL
                IDECA2=NCMP*(INOGL-1)+NUDDL(IDDL)
                JVAL2=JCNSV2-1+IDECA2
                ZL(JCNSL2-1+IDECA2)=.TRUE.
                ADIEL=ZI(JCELD1-1+ZI(JCELD1-1+4+IGR)+4+4*(IEL-1)+4)
                ZR(JVAL2)=ZR(JCELV1-1+ADIEL-1+NDDL*(IIPG-1)+IDDL)
   40         CONTINUE
   50       CONTINUE
   60     CONTINUE
   70   CONTINUE
   80 CONTINUE

C     -- ON ESSAYE DE FAIRE UN PEU DE PLACE EN MEMOIRE :
      CALL DETRSD('CHAMP_GD','&&ECLPGC.CH1B1')
      CALL DETRSD('CHAMP_GD','&&ECLPGC.CH1B2')
      CALL JELIBE(CH1//'.CELD')
      CALL JELIBE(CH1//'.CELV')
      CALL JELIBE(MA2//'.CONNEX')


C         -- ON TRANSFORME CH2S EN VRAI CHAM_NO :
C         ----------------------------------------
C     -- 2 JELIBE POUR ECONOMISER LA MEMOIRE EN DESSOUS (MARQUE):
      CALL JELIBE(CH2S//'.CNSL')
      CALL JELIBE(CH2S//'.CNSV')
      CALL CNSCNO(CH2S,PRCHNO,'NON','G',CH2,'F',IBID)
      CALL DETRSD('CHAM_NO_S',CH2S)

      CALL JEDETR('&&ECLPGC.NOM_CMP')
      CALL JEDETR('&&ECLPGC.CORR1')
      CALL JEDETR('&&ECLPGC.CORR2')


   90 CONTINUE
      CALL DETRSD('CHAMP_GD','&&ECLPGC.CH1B1')
      CALL DETRSD('CHAMP_GD','&&ECLPGC.CH1B2')
      CALL JEDEMA()
      END
