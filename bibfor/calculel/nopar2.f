      CHARACTER*8 FUNCTION NOPAR2(NOMOPT,NOMGD,STATUT)
      IMPLICIT NONE
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF CALCULEL  DATE 13/06/2012   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE                            VABHHTS J.PELLET
      INCLUDE 'jeveux.h'
      CHARACTER*(*)  NOMOPT,NOMGD,STATUT
C ----------------------------------------------------------------------
C     ENTREES:
C     NOMOPT   : NOM D'1 OPTION
C     NOMGD    : NOM D'1 GRANDEUR
C     STATUT : 'IN'/'OUT'/'INOUT'

C     SORTIES:
C     NOPAR2 : NOM DU PARAMETRE DE L'OPTION NOMOPT
C              QUI CORRESPOND A LA GRANDEUR NOMGD ET AU STATUT STATUT
C              RQUE : ERREUR <F> SI ON EN TROUVE : 0,2,3,...
C              RQUE : SI INOUT, ON CHERCHE D'ABORD DANS OUT ET SI ON NE
C                     TROUVE PAS, ON CHERCHE DANS IN

C     SI NOMGD=' ' ET SI STATUT='OUT' :
C         - SI L'OPTION N'A QU'UN PARAMETRE 'OUT', ON LE REND
C         - SI L'OPTION A PLUSIEURS PARAMETRES 'OUT' => ERREUR <F>

C ----------------------------------------------------------------------


C     VARIABLES LOCALES:
C     ------------------
      INTEGER OPT,NBIN,NBOUT,NBTROU,ITROU,GD,GD2 ,IADESC,IAOPPA,KK
      CHARACTER*16 NOMOP2
      CHARACTER*8  NOMGD2
      CHARACTER*8  STATU2,OUTROU
      CHARACTER*24 VALK(3)

C DEB-------------------------------------------------------------------
      NOMOP2=NOMOPT
      NOMGD2=NOMGD
      STATU2=STATUT

      CALL JENONU(JEXNOM('&CATA.OP.NOMOPT',NOMOP2),OPT)
      IF (NOMGD2.NE.' ') CALL JENONU(JEXNOM('&CATA.GD.NOMGD',NOMGD2),GD)
      CALL JEVEUO(JEXNUM('&CATA.OP.DESCOPT',OPT),'L',IADESC)
      CALL JEVEUO(JEXNUM('&CATA.OP.OPTPARA',OPT),'L',IAOPPA)
      NBIN = ZI(IADESC-1+2)
      NBOUT = ZI(IADESC-1+3)
      NBTROU=0
      OUTROU=' '


      IF (STATU2.EQ.'OUT') THEN
        IF (NOMGD2.EQ.' ') THEN
          CALL ASSERT(NBOUT.EQ.1)
          NBTROU=NBTROU+1
          ITROU=1
          OUTROU='OUT'
        ELSE
          DO 1,KK=1,NBOUT
            GD2 = ZI(IADESC-1+4+NBIN+KK)
            IF (GD.EQ.GD2) THEN
              NBTROU=NBTROU+1
              ITROU=KK
              OUTROU='OUT'
            END IF
1         CONTINUE
        END IF

      ELSE IF (STATU2.EQ.'IN') THEN
        DO 2,KK=1,NBIN
          GD2 = ZI(IADESC-1+4+KK)
          IF (GD.EQ.GD2) THEN
            NBTROU=NBTROU+1
            ITROU=KK
            OUTROU='IN'
          END IF
2       CONTINUE

      ELSE IF (STATU2.EQ.'INOUT') THEN
        DO 11,KK=1,NBOUT
          GD2 = ZI(IADESC-1+4+NBIN+KK)
          IF (GD.EQ.GD2) THEN
            NBTROU=NBTROU+1
            ITROU=KK
            OUTROU='OUT'
          END IF
11      CONTINUE

        IF (NBTROU.EQ.0) THEN
          DO 12,KK=1,NBIN
            GD2 = ZI(IADESC-1+4+KK)
            IF (GD.EQ.GD2) THEN
              NBTROU=NBTROU+1
              ITROU=KK
              OUTROU='IN'
            END IF
12        CONTINUE
        END IF

      ELSE
        CALL ASSERT(.FALSE.)
      END IF

      IF (NBTROU.EQ.0) THEN
         VALK(1) = STATU2
         VALK(2) = NOMGD2
         VALK(3) = NOMOP2
         CALL U2MESK('F','CALCULEL3_84', 3 ,VALK)
      ENDIF
      IF (NBTROU.GT.1) THEN
        VALK(1) = STATU2
        VALK(2) = NOMGD2
        VALK(3) = NOMOP2
        CALL U2MESK('F','CALCULEL3_85', 3 ,VALK)
      ENDIF
      IF (OUTROU.EQ.'OUT') THEN
        NOPAR2=ZK8(IAOPPA-1+NBIN+ITROU)

      ELSE IF (OUTROU.EQ.'IN') THEN
        NOPAR2=ZK8(IAOPPA-1+ITROU)

      ELSE
        CALL ASSERT(.FALSE.)
      END IF

      END
