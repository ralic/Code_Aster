      SUBROUTINE ETENCA(CHINZ,LIGRLZ,IRET)
      IMPLICIT NONE

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF CALCULEL  DATE 20/02/2007   AUTEUR LEBOUVIER F.LEBOUVIER 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE                            VABHHTS J.PELLET
C     ARGUMENTS:
C     ----------
      CHARACTER*19 LIGREL,CHIN
      CHARACTER*(*) LIGRLZ,CHINZ
      INTEGER IRET
C ----------------------------------------------------------------------
C     ENTREES:
C     CHINZ  : NOM DE LA CARTE A ETENDRE
C     LIGRLZ : NOM DU LIGREL SUR LEQUEL ETENDRE LA CARTE

C     SORTIES:
C     ON CREE LES OBJETS CHIN.PTMA ET CHIN.PTMS SUR LA VOLATILE

C        (SI LES OBJETS .PTMA ET/OU .PTMS EXISTENT DEJA, C'EST QUE LA
C         CARTE EST DEJA ETENDUE. CAS OU LA CARTE EXISTE PLUSIEURS FOIS
C         DANS LA LISTE LCHIN(*). ON SUPPOSE QU'ELLE EST ETENDUE SUR LE
C         BON LIGREL PUISQUE CALCUL CES OBJETS EN FIN D'ALGORITHME.)


C     IRET : CODE RETOUR   0 --> OK
C                          1 --> PROBLEME
C ----------------------------------------------------------------------

C     FONCTIONS EXTERNES:
C     -------------------
      CHARACTER*8 MAILLA
      CHARACTER*32 JEXNUM,JEXATR

C     VARIABLES LOCALES:
C     ------------------
      INTEGER NMA,NMS,NBEDIT,IGD,CODE,IENT,I,II,NB
      INTEGER DESC,GRPMA,LIMA,IALIMA,ILLIMA
      INTEGER PTMA,PTMS,IERD,IBID,NOLI
      LOGICAL BONLIG,LALLOC
      CHARACTER*8 MA,KBID
      CHARACTER*24 LIGRI
C---------------- COMMUNS NORMALISES  JEVEUX  --------------------------
      COMMON /IVARJE/ZI(1)
      COMMON /RVARJE/ZR(1)
      COMMON /CVARJE/ZC(1)
      COMMON /LVARJE/ZL(1)
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
      INTEGER ZI
      INTEGER VALI(3)
      REAL*8 ZR
      COMPLEX*16 ZC
      LOGICAL ZL
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*24 VALK
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      CHARACTER*1 K1BID


      CALL JEMARQ()
      LIGREL = LIGRLZ
      CHIN = CHINZ

      IRET = 0
      CALL JEVEUO(CHIN//'.NOLI','L',NOLI)


C     ----ALLOCATION DES OBJETS PTMA ET PTMS:
      CALL DISMOI('F','NB_MA_MAILLA',LIGREL,'LIGREL',NMA,KBID,IERD)
      CALL DISMOI('F','NB_MA_SUP',LIGREL,'LIGREL',NMS,KBID,IERD)

      LALLOC=.FALSE.
      IF (NMA.GT.0) THEN
        CALL JEEXIN(CHIN//'.PTMA',IBID)
        IF (IBID.EQ.0)  THEN
          LALLOC=.TRUE.
          CALL WKVECT(CHIN//'.PTMA','V V I',NMA,PTMA)
        END IF
      END IF

      IF (NMS.GT.0) THEN
        CALL JEEXIN(CHIN//'.PTMS',IBID)
        IF (IBID.EQ.0)  THEN
          LALLOC=.TRUE.
          CALL WKVECT(CHIN//'.PTMS','V V I',NMS,PTMS)
        END IF
      END IF

C       -- LA CARTE EST DEJA ETENDUE:
      IF (.NOT.LALLOC) GO TO 9999


C     ----MISE EN MEMOIRE DE LA COLLECTION .LIMA :
      CALL JEEXIN(CHIN//'.LIMA',IBID)
      IF (IBID.GT.0) THEN
        CALL JEVEUO(CHIN//'.LIMA','L',IALIMA)
        CALL JEVEUO(JEXATR(CHIN//'.LIMA','LONCUM'),'L',ILLIMA)
      END IF


C     ----REMPLISSAGE DES OBJETS PTMA ET PTMS:
      CALL JEVEUO(CHIN//'.DESC','L',DESC)
      NBEDIT = ZI(DESC-1+3)
      DO 60 IGD = 1,NBEDIT
        CODE = ZI(DESC-1+3+2*IGD-1)
        IENT = ZI(DESC-1+3+2*IGD)

C        ------- ON NOTE SI LE LIGREL ASSOCIE A IGD EST LE MEME
C        QUE CELUI SUR LEQUEL ON ETEND:
        LIGRI = ZK24(NOLI-1+IGD)
        IF (LIGRI(1:19).EQ.LIGREL) THEN
          BONLIG = .TRUE.
        ELSE
          BONLIG = .FALSE.
        END IF

C        ------ GROUPE PREDEFINI "TOUT":
        IF (CODE.EQ.1) THEN
          DO 10 I = 1,NMA
            ZI(PTMA-1+I) = IGD
   10     CONTINUE
          GO TO 60
        END IF
        IF ((CODE.EQ.-1) .AND. BONLIG) THEN
          DO 20 I = 1,NMS
            ZI(PTMS-1+I) = IGD
   20     CONTINUE
          GO TO 60
        END IF

C        ------- GROUPE DE MAILLES DU MAILLAGE:
        IF (CODE.EQ.2) THEN
          MA = MAILLA(LIGREL)
          CALL JELIRA(JEXNUM(MA//'.GROUPEMA',IENT),'LONMAX',NB,K1BID)
          CALL JEVEUO(JEXNUM(MA//'.GROUPEMA',IENT),'L',GRPMA)
          DO 30 I = 1,NB
            II = ZI(GRPMA-1+I)
            ZI(PTMA-1+II) = IGD
   30     CONTINUE
          GO TO 60
        END IF

C        ------- LISTE TARDIVE DE MAILLES ASSOCIEE A LA CARTE:
        IF (ABS(CODE).EQ.3) THEN
          NB = ZI(ILLIMA+IENT) - ZI(ILLIMA+IENT-1)
          LIMA=IALIMA+ZI(ILLIMA-1+IENT)-1

          IF (CODE.GT.0) THEN
            DO 40 I = 1,NB
              II = ZI(LIMA-1+I)
              IF (II.LE.0) THEN
                VALK = CHIN
                VALI (1) = IENT
                VALI (2) = I
                VALI (3) = II
                CALL U2MESG('F', 'CALCULEL5_86',1,VALK,3,VALI,0,0.D0)
              END IF
              ZI(PTMA-1+II) = IGD
   40       CONTINUE
          ELSE
            IF (BONLIG) THEN
              DO 50 I = 1,NB
                II = ZI(LIMA-1+I)
                IF (II.GE.0) THEN
                  CALL U2MESS('F','CALCULEL2_50')
                END IF
                ZI(PTMS-1-II) = IGD
   50         CONTINUE
            END IF
          END IF
          GO TO 60
        END IF
   60 CONTINUE
 9999 CONTINUE
      CALL JEDEMA()
      END
