      SUBROUTINE AGCAR2(NGDMXN, CHINZ)
      IMPLICIT NONE
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF CALCULEL  DATE 20/03/2006   AUTEUR VABHHTS J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2005  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
      INTEGER NGDMXN
      CHARACTER*19 CHIN
      CHARACTER*(*) CHINZ
C --------------------------------------------------------------------
C   AGRANDISSEMENT DE LA CARTE CHIN, NGDMXN ETANT LE NOUVEAU NOMBRE
C   MAXIMUM DE COUPLES (ENTITE,VALEUR) A STOCKER
C --------------------------------------------------------------------
C  NGDMXN       - IN     - I    - : NOUVEAU NOMBRE MAX DE COUPLES
C               -        -      -   (ENTITE,VALEUR) A STOCKER
C --------------------------------------------------------------------
C  CHINZ        - IN     - K*(*)- : NOM DE LA CARTE A REDIMENSIONNER -
C               - JXVAR  -      -   ON REALLOUE ET ON RECOPIE LEURS
C               -        -      -   ANCIENNES VALEURS POUR LES OBJETS-
C               -        -      -   CHIN.DESC
C               -        -      -   CHIN.VALE
C               -        -      -   CHIN.NOMA
C               -        -      -   CHIN.NOLI
C               -        -      -   CHIN.LIMA
C --------------------------------------------------------------------
C
C     FONCTIONS EXTERNES:
C     -------------------
      INTEGER NBEC
      CHARACTER*32 JEXNUM
C
C---------------- COMMUNS NORMALISES  JEVEUX  --------------------------
      COMMON /IVARJE/ZI(1)
      COMMON /RVARJE/ZR(1)
      COMMON /CVARJE/ZC(1)
      COMMON /LVARJE/ZL(1)
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
      INTEGER ZI
      REAL*8 ZR
      COMPLEX*16 ZC
      LOGICAL ZL
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
C
      CHARACTER*1 BASE
      CHARACTER*24 TRAV, CLIMA,DESCAV
      CHARACTER*1 K1BID
      INTEGER JDESCA,JDESC,NEC,IEC,NGDMXA,NEDIT,IED,IDECA,IDEC
      INTEGER IBID,NCMP,NMXMA ,IGD
C ----------------------------------------------------------------------
      CALL JEMARQ()
      CHIN = CHINZ
      TRAV = CHIN//'.TRAV'
      CLIMA = CHIN//'.LIMA'
      CALL JELIRA(CHIN//'.DESC','CLAS',IBID,BASE)

C
C --- AGRANDISSEMENT DE .DESC:
C ------------------------------
      DESCAV='&&AGCAR2.DESCAV'
      CALL JEDUPO(CHIN//'.DESC', 'V', DESCAV, .FALSE.)
      CALL JEVEUO(DESCAV,'E',JDESCA)
      IGD = ZI(JDESCA-1+1)
      NEC = NBEC(IGD)
      NGDMXA = ZI(JDESCA-1+2)
      NEDIT = ZI(JDESCA-1+3)
      CALL ASSERT(NGDMXN.GT.NGDMXA)

      CALL JEDETR(CHIN//'.DESC')
      CALL WKVECT(CHIN//'.DESC',BASE//' V I',3+NGDMXN*(2+NEC),JDESC)
      CALL JEECRA(CHIN//'.DESC','DOCU',IBID,'CART')

      ZI(JDESC-1+1)=IGD
      ZI(JDESC-1+2)=NGDMXN
      ZI(JDESC-1+3)=NEDIT

      DO 7, IED=1,NEDIT
         ZI(JDESC-1+3+(IED-1)*2+1)=ZI(JDESCA-1+3+(IED-1)*2+1)
         ZI(JDESC-1+3+(IED-1)*2+2)=ZI(JDESCA-1+3+(IED-1)*2+2)
 7    CONTINUE

      DO 8, IED=1,NEDIT
         IDECA=3+2*NGDMXA + NEC*(IED-1)
         IDEC =3+2*NGDMXN + NEC*(IED-1)
         DO 9, IEC=1,NEC
            ZI(JDESC-1+IDEC+IEC)=ZI(JDESCA-1+IDECA+IEC)
 9       CONTINUE
 8    CONTINUE
      CALL JEDETR(DESCAV)



C ---  AGRANDISSEMENT DE VALE:
C ------------------------------
      CALL JELIRA(JEXNUM('&CATA.GD.NOMCMP',IGD),'LONMAX',NCMP,K1BID)
      CALL JUVECA(CHIN//'.VALE',NGDMXN*NCMP)


C ---  AGRANDISSEMENT DE NOLI
C ------------------------------
      CALL JUVECA(CHIN//'.NOLI',NGDMXN)


C ---  AGRANDISSEMENT DE LIMA
C ------------------------------
      CALL JEDUPO(CLIMA,'V',TRAV,.FALSE.)
      CALL JELIRA(CLIMA,'LONT',NMXMA,K1BID)
      CALL JEDETR(CHIN//'.LIMA')
      CALL COCOPG(TRAV, CLIMA, NGDMXN, NMXMA,BASE)
      CALL JEDETR(CHIN//'.TRAV')
C
      CALL JEDEMA()

      END
