      SUBROUTINE MEAMME(OPTION,MODELE,NCHAR,LCHAR,MATE,CARA,EXITIM,TIME,
     &                  RIGIEL,MASSEL,MATEL)
      IMPLICIT REAL*8 (A-H,O-Z)
      INTEGER NCHAR
      REAL*8 TIME
      CHARACTER*(*) MODELE,OPTION,CARA,MATE,RIGIEL
      CHARACTER*8 LCHAR(*)
      CHARACTER*(*) MASSEL,MATEL
      LOGICAL EXITIM
C ----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF CALCULEL  DATE 28/03/2007   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.

C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.

C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C       CALCUL DES MATRICES ELEMENTAIRES D' AMOR_MECA
C           OU DES MATRICES ELEMENTAIRES DE RIGI_MECA_HYST

C ----------------------------------------------------------------------
C IN  : OPTION : 'AMOR_MECA' OU 'RIGI_MECA_HYST'
C IN  : MODELE : NOM DU MODELE
C IN  : NCHAR  : NOMBRE DE CHARGES
C IN  : LCHAR  : LISTE DES CHARGES
C IN  : MATE   : CHAM_MATER
C IN  : CARA   : CARA_ELEM
C IN  : MATEL  : NOM DU MATEL (N RESUELEM) PRODUIT
C IN  : EXITIM : VRAI SI L'INSTANT EST DONNE
C IN  : TIME   : INSTANT DE CALCUL
C IN  : RIGIEL : MATR_ELEM_DEPL_R DE RIGI_MECA
C VAR : MASSEL : MATR_ELEM_DEPL_R DE MASS_MECA
C ----------------------------------------------------------------------
C     ----- DEBUT COMMUNS NORMALISES  JEVEUX  --------------------------
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
      CHARACTER*32 JEXNUM,JEXNOM,JEXATR
C     ------------------------------------------------------------------
      CHARACTER*8 K8B,LPAIN(10),LPAOUT(3)
      CHARACTER*16 OPTIO2
      CHARACTER*19 LIGRE1,CHVARC
      CHARACTER*24 RIGICH,MASSCH,LIGRMO,LCHIN(10),LCHOUT(3),LIGRCH
      CHARACTER*24 CHGEOM,CHCARA(15),CHHARM,ARGU

C DEB-------------------------------------------------------------------
      CALL JEMARQ()
      IF (MODELE(1:1).EQ.' ') CALL U2MESS('F','CALCULEL2_82')
      CHVARC='&&MEAMME.CHVARC'
      OPTIO2 = OPTION
      NH = 0

      CALL MECHAM(OPTIO2,MODELE(1:8),NCHAR,LCHAR,CARA(1:8),NH,CHGEOM,
     &            CHCARA,CHHARM,ICODE)
      CALL VRCINS(MODELE(1:8),MATE(1:8),CARA(1:8),NCHAR,
     &            LCHAR,TIME,CHVARC)

C     -- ON RECUPERE EVENTUELLEMENT LE NOM DES RESUELEM DE RIGI/MASS:
C     ---------------------------------------------------------------
      RIGICH = ' '
      IF (RIGIEL(1:1).NE.' ') THEN
        CALL JEEXIN(RIGIEL(1:8)//'.LISTE_RESU',IRET)
        IF (IRET.GT.0) THEN
          CALL JEVEUO(RIGIEL(1:8)//'.LISTE_RESU','L',IALIR1)
          CALL JELIRA(RIGIEL(1:8)//'.LISTE_RESU','LONUTI',NBRES1,K8B)
          DO 10 I = 1,NBRES1
            RIGICH = ZK24(IALIR1-1+I)
            CALL DISMOI('F','NOM_LIGREL',RIGICH(1:19),'RESUELEM',IBID,
     &                  LIGRE1,IED)
            IF (LIGRE1(1:8).EQ.MODELE(1:8)) GO TO 20
   10     CONTINUE
          CALL U2MESS('F','CALCULEL2_83')
   20     CONTINUE
        END IF
      END IF

      MASSCH = ' '
      IF (MASSEL(1:1).NE.' ') THEN
        CALL JEEXIN(MASSEL(1:8)//'.LISTE_RESU',IRET)
        IF (IRET.GT.0) THEN
          CALL JEVEUO(MASSEL(1:8)//'.LISTE_RESU','L',IALIR1)
          CALL JELIRA(MASSEL(1:8)//'.LISTE_RESU','LONUTI',NBRES1,K8B)
          DO 30 I = 1,NBRES1
            MASSCH = ZK24(IALIR1-1+I)
            CALL DISMOI('F','NOM_LIGREL',MASSCH(1:19),'RESUELEM',IBID,
     &                  LIGRE1,IED)
            IF (LIGRE1(1:8).EQ.MODELE(1:8)) GO TO 40
   30     CONTINUE
          CALL U2MESS('F','CALCULEL2_84')
   40     CONTINUE
        END IF
      END IF

      CALL JEEXIN(MATEL(1:8)//'.REFE_RESU',IRET)
      IF (IRET.GT.0) THEN
        CALL JEDETR(MATEL(1:8)//'.REFE_RESU')
        CALL JEDETR(MATEL(1:8)//'.LISTE_RESU')
      END IF
      CALL MEMARE('G',MATEL(1:8),MODELE(1:8),MATE,CARA(1:8),'AMOR_MECA')
      CALL WKVECT(MATEL(1:8)//'.LISTE_RESU','G V K24',NCHAR+1,JLIRES)

      IF (OPTIO2(1:9).EQ.'AMOR_MECA') THEN
        LPAOUT(1) = 'PMATUUR'
      ELSE IF (OPTIO2.EQ.'RIGI_MECA_HYST') THEN
        LPAOUT(1) = 'PMATUUC'
      ELSE
        CALL U2MESS('F','ASSEMBLA_17')
      END IF
      LPAOUT(2) = 'PMATUUR'

      LCHOUT(1) = MATEL(1:8)//'.ME001'
      LCHOUT(2) = MATEL(1:8)//'.ME002'
      ILIRES = 0

      LIGRMO = MODELE(1:8)//'.MODELE'
      LPAIN(1) = 'PGEOMER'
      LCHIN(1) = CHGEOM
      LPAIN(2) = 'PMATERC'
      LCHIN(2) = MATE
      LPAIN(3) = 'PCAORIE'
      LCHIN(3) = CHCARA(1)
      LPAIN(4) = 'PCADISA'
      LCHIN(4) = CHCARA(4)
      LPAIN(5) = 'PCAGNPO'
      LCHIN(5) = CHCARA(6)
      LPAIN(6) = 'PCACOQU'
      LCHIN(6) = CHCARA(7)
      LPAIN(7) = 'PVARCPR'
      LCHIN(7) = CHVARC
      LPAIN(8) = 'PRIGIEL'
      LCHIN(8) = RIGICH
      LPAIN(9) = 'PMASSEL'
      LCHIN(9) = MASSCH
      LPAIN(10) = 'PCADISK'
      LCHIN(10) = CHCARA(2)
      IF (OPTIO2.EQ.'AMOR_MECA_ABSO') THEN
        NOP = 2
      ELSE
        NOP = 10
      ENDIF
      CALL CALCUL('S',OPTIO2,LIGRMO,NOP,LCHIN,LPAIN,1,LCHOUT,LPAOUT,'G')
      CALL EXISD('CHAMP_GD',LCHOUT(1) (1:19),IRET)
      IF (IRET.NE.0) THEN
        ILIRES = ILIRES + 1
        ZK24(JLIRES-1+ILIRES) = LCHOUT(1)
        CALL JEECRA(MATEL(1:8)//'.LISTE_RESU','LONUTI',ILIRES,' ')
      END IF

C --- PRISE EN COMPTE DES MATRICES DE BLOCAGE DANS LE CAS D'UNE
C --- RIGIDITE HYSTERETIQUE :
C     ---------------------
      IF (OPTIO2.EQ.'RIGI_MECA_HYST') THEN
        DO 50 ICHA = 1,NCHAR
          LIGRCH = LCHAR(ICHA) (1:8)//'.CHME.LIGRE'
          ARGU = LCHAR(ICHA) (1:8)//'.CHME.LIGRE.LIEL'
          CALL JEEXIN(ARGU,IRET)
          IF (IRET.LE.0) GO TO 50
          LCHIN(1) = LCHAR(ICHA) (1:8)//'.CHME.CMULT'
          ARGU = LCHAR(ICHA) (1:8)//'.CHME.CMULT.DESC'
          CALL JEEXIN(ARGU,IRET)
          IF (IRET.LE.0) GO TO 50

          LPAIN(1) = 'PDDLMUR'
          ILIRES = ILIRES + 1
          CALL CODENT(ILIRES,'D0',LCHOUT(2) (12:14))
          OPTIO2 = 'MECA_DDLM_R'
          CALL CALCUL('S',OPTIO2,LIGRCH,1,LCHIN,LPAIN,1,LCHOUT(2),
     &                LPAOUT(2),'G')
          CALL EXISD('CHAMP_GD',LCHOUT(2) (1:19),IRET)
          IF (IRET.NE.0) THEN
            ZK24(JLIRES-1+ILIRES) = LCHOUT(2)
            CALL JEECRA(MATEL(1:8)//'.LISTE_RESU','LONUTI',ILIRES,' ')
          ELSE
            ILIRES = ILIRES - 1
          END IF
   50   CONTINUE
      END IF

   60 CONTINUE
      CALL DETRSD('CHAMP_GD',CHVARC)

      CALL JEDEMA()
      END
