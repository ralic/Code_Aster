      SUBROUTINE ALRESL(OPT,LIGREL,NOCHOU,NOMPAR,BASE)
      IMPLICIT NONE

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF CALCULEL  DATE 09/11/2012   AUTEUR DELMAS J.DELMAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE PELLET J.PELLET
C     ARGUMENTS:
C     ----------
      INCLUDE 'jeveux.h'

      CHARACTER*32 JEXNUM
      INTEGER OPT
      CHARACTER*19 LIGREL,NOCHOU
      CHARACTER*8 NOMPAR
      CHARACTER*(*) BASE
C ----------------------------------------------------------------------
C     ENTREES:
C      OPT   : OPTION
C     LIGREL : NOM DE LIGREL
C     NOCHOU : NOM DU RESUELEM A ALLOUER
C      NOMPAR: NOM DU PARAMETRE
C      BASE  : 'G', 'V' OU 'L'

C     SORTIES:
C     + CREATION DU RESUELEM DE NOM NOCHOU
C       (LE RESUELEM PEUT ETRE "VIDE")

C ----------------------------------------------------------------------

C     FONCTIONS EXTERNES:
C     -------------------
      INTEGER TYPELE,NBELEM,MODATT,DIGDE2,NBGREL,INPARA,GRDEUR
      CHARACTER*1 BAS2
      CHARACTER*8 SCALAI
      CHARACTER*16 NOMOPT,CODVOI,NOMTE
      INTEGER        IAMACO,ILMACO,IAMSCO,ILMSCO,IALIEL,ILLIEL
      COMMON /CAII03/IAMACO,ILMACO,IAMSCO,ILMSCO,IALIEL,ILLIEL
      INTEGER EVFINI,CALVOI,JREPE,JPTVOI,JELVOI
      COMMON /CAII19/EVFINI,CALVOI,JREPE,JPTVOI,JELVOI

C     VARIABLES LOCALES:
      INTEGER NGREL,IGR,TE,NEL,MODE,NCMPEL,IPAR
      INTEGER DESC,GD,JNOLI,IEL,IDESC
      INTEGER LON1,LONTOT,IPARMX,IBID
      INTEGER DIGDE4,JRSVI,DIM1,DIGDE3
      CHARACTER*8 SCAL,NOMGD,TYMAT
      LOGICAL LMATVF


      CALL JEMARQ()
      BAS2 = BASE

      CALL JENUNO(JEXNUM('&CATA.OP.NOMOPT',OPT),NOMOPT)
      NGREL = NBGREL(LIGREL)
      GD = GRDEUR(NOMPAR)
      SCAL = SCALAI(GD)


C     -- LE RESUELEM DOIT-IL ETRE CREE ?
C     ----------------------------------
      IPARMX = 0
      DO 10 IGR = 1,NGREL
        TE = TYPELE(LIGREL,IGR)
        IPAR = INPARA(OPT,TE,'OUT',NOMPAR)
        IPARMX = MAX(IPARMX,IPAR)
   10 CONTINUE
      IF (IPARMX.EQ.0) GO TO 30


C     -- LE RESUELEM EST-IL "VOISIN_VF" ET DE TYPE "MATRICE" ?
C        (I.E. LMATVF=.TRUE.)
      CALL JENUNO(JEXNUM('&CATA.GD.NOMGD',GD),NOMGD)
      CALL DISMOI('F','TYPE_MATRICE',NOMGD,'GRANDEUR',IBID,TYMAT,IBID)
      LMATVF=.FALSE.
      IF (EVFINI.EQ.1) THEN
        IF (TYMAT.EQ.' ') THEN
C         -- C'EST UN RESUELEM DE TYPE "VECTEUR"
          LMATVF=.FALSE.
        ELSE
          LMATVF=.TRUE.
          IF (TYMAT.NE.'NON_SYM') CALL U2MESS('F','CALCULEL4_12')
        ENDIF
      ENDIF


C     ----CREATION DE L'OBJET NOLI :
      CALL WKVECT(NOCHOU//'.NOLI',BAS2//' V K24',4,JNOLI)
      ZK24(JNOLI-1+1) = LIGREL
      ZK24(JNOLI-1+2) = NOMOPT
      ZK24(JNOLI-1+3) = 'MPI_COMPLET'
      IF (LMATVF)  ZK24(JNOLI-1+4) = 'VOISIN_VF'

C     ----CREATION DE L'OBJET DESC :
      CALL WKVECT(NOCHOU//'.DESC',BAS2//' V I',2+NGREL,IDESC)
      CALL JEECRA(NOCHOU//'.DESC','DOCU',IBID,'RESL')

C     ---CREATION DE LA COLLECTION DIPERSEE RESL  :
      CALL JECREC(NOCHOU//'.RESL',BAS2//' V '//SCAL(1:4),'NU',
     +            'DISPERSE','VARIABLE',NGREL)

      IF (LMATVF) THEN
        CALL JECREC(NOCHOU//'.RSVI',BAS2//' V I','NU',
     +            'CONTIG','VARIABLE',NGREL)
        LONTOT=0
        DO 22 IGR = 1,NGREL
          NEL = NBELEM(LIGREL,IGR)
          LONTOT=LONTOT+NEL+1
   22   CONTINUE
        CALL JEECRA(NOCHOU//'.RSVI','LONT',LONTOT,' ')
      ENDIF


C     -- REMPLISSAGE DE DESC ET ALLOCATION DE .RESL:
C     ----------------------------------------------
      CALL JEVEUO(NOCHOU//'.DESC','E',DESC)
      ZI(DESC-1+1) = GD
      ZI(DESC-1+2) = NGREL
      DO 20 IGR = 1,NGREL
        NEL = NBELEM(LIGREL,IGR)
        TE = TYPELE(LIGREL,IGR)
        IPAR = INPARA(OPT,TE,'OUT',NOMPAR)

C        -- SI LE TYPE_ELEMENT NE CONNAIT PAS LE PARAMETRE:
        IF (IPAR.EQ.0) THEN
          ZI(DESC-1+2+IGR) = 0
C          -- LES COLLECTIONS NUMEROTEES CONTIG DOIVENT ETRE COMPLETES:
          IF (LMATVF) THEN
            CALL JEECRA(JEXNUM(NOCHOU//'.RSVI',IGR),'LONMAX',1,' ')
            CALL JEVEUO(JEXNUM(NOCHOU//'.RSVI',IGR),'E',JRSVI)
          ENDIF
        ELSE
          MODE = MODATT(OPT,TE,'OUT',IPAR)
          CALL ASSERT(MODE.GT.0)
          ZI(DESC-1+2+IGR) = MODE
          CALL JECROC(JEXNUM(NOCHOU//'.RESL',IGR))
          IF (LMATVF) CALL JECROC(JEXNUM(NOCHOU//'.RSVI',IGR))

          IF (.NOT.LMATVF) THEN
            NCMPEL = DIGDE2(MODE)
            CALL JEECRA(JEXNUM(NOCHOU//'.RESL',IGR),'LONMAX',
     +                NCMPEL*NEL,' ')
          ELSE
            CALL JEECRA(JEXNUM(NOCHOU//'.RSVI',IGR),'LONMAX',NEL+1,' ')
            CALL JEVEUO(JEXNUM(NOCHOU//'.RSVI',IGR),'E',JRSVI)
            CALL JENUNO(JEXNUM('&CATA.TE.NOMTE',TE),NOMTE)
            CALL TEATTR(NOMTE,'S','TYPE_VOISIN',CODVOI,IBID)

C           -- CALCUL DE LA LONGUEUR DU GREL : LONTOT
            LONTOT=0
            ZI(JRSVI-1+1)=LONTOT+1
            DIM1=DIGDE3(MODE,'L')
            DO 21, IEL=1,NEL
              LON1=DIGDE4(IGR,IEL,OPT,IPAR,DIM1,CODVOI)
              LONTOT=LONTOT+LON1
              ZI(JRSVI-1+IEL+1)=LONTOT+1
   21       CONTINUE
            CALL JEECRA(JEXNUM(NOCHOU//'.RESL',IGR),'LONMAX',
     +                LONTOT,' ')
          ENDIF
        ENDIF
   20 CONTINUE
   30 CONTINUE
      CALL JEDEMA()
      END
