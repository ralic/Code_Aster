      SUBROUTINE MERIT1(MODELE,NCHAR,LCHAR,MATE,CARA,TIME,MATEL,NH,
     &                  PREFCH,NUMERO,BASE)
      IMPLICIT REAL*8 (A-H,O-Z)

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF CALCULEL  DATE 26/04/2011   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.

C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.

C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================

C     ARGUMENTS:
C     ----------
      CHARACTER*8 MODELE,CARA,LCHARZ
      CHARACTER*19 MATEL,PREFCH
      CHARACTER*(*) LCHAR(*),MATE
      CHARACTER*24 TIME
      CHARACTER*1 BASE
      INTEGER NCHAR,NUMERO
C ----------------------------------------------------------------------

C     CALCUL DES MATRICES ELEMENTAIRES DE RIGIDITE THERMIQUE (1)
C            ( ISO     , 'RIGI_THER'  )
C            ( CAL_TI  , 'THER_DDLM_R')

C     LES RESUELEM PRODUITS S'APPELLENT :
C           PREFCH(1:8).ME000I , I=NUMERO+1,NUMERO+N

C     ENTREES:

C     LES NOMS QUI SUIVENT SONT LES PREFIXES UTILISATEUR K8:
C        MODELE : NOM DU MODELE
C        NCHAR  : NOMBRE DE CHARGES
C        LCHAR  : LISTE DES CHARGES
C        MATE   : CHAMP DE MATERIAUX
C        CARA   : CHAMP DE CARAC_ELEM
C        MATEL  : NOM DU MATR_ELEM (N RESUELEM) PRODUIT
C        PREFCH : PREFIXE DES NOMS DES RESUELEM STOCKES DANS MATEL
C        NUMERO : NUMERO D'ORDRE A PARTIR DUQUEL ON NOMME LES RESUELEM
C        TIME   : CHAMPS DE TEMPSR

C     SORTIES:
C        MATEL  : EST REMPLI.

C ----------------------------------------------------------------------

C     FONCTIONS EXTERNES:
C     -------------------

C     VARIABLES LOCALES:
C     ------------------
C---------------- COMMUNS NORMALISES  JEVEUX  --------------------------
      COMMON /IVARJE/ZI(1)
      COMMON /RVARJE/ZR(1)
      COMMON /CVARJE/ZC(1)
      COMMON /LVARJE/ZL(1)
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
      INTEGER ZI
      REAL*8 ZR
      COMPLEX*16 ZC
      LOGICAL ZL,EXIGEO,EXICAR
      CHARACTER*8 ZK8,LPAIN(6),LPAOUT(1)
      CHARACTER*16 ZK16,OPTION
      CHARACTER*24 ZK24,CHGEOM,CHHARM,LCHIN(6),LCHOUT(1)
      CHARACTER*24 LIGRMO,LIGRCH,CHCARA(18)
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80

      CALL JEMARQ()

C     -- ON VERIFIE LA PRESENCE PARFOIS NECESSAIRE DE CARA_ELEM
C        ET CHAM_MATER :
      CALL MEGEOM(MODELE,LCHAR(1),EXIGEO,CHGEOM)
      CALL MECARA(CARA,EXICAR,CHCARA)
      CALL MEHARM(MODELE,NH,CHHARM)

      CALL JEEXIN(MATEL//'.RERR',IRET)
      IF (IRET.GT.0) THEN
        CALL JEDETR(MATEL//'.RERR')
        CALL JEDETR(MATEL//'.RELR')
      END IF
      CALL MEMARE('V',MATEL,MODELE,MATE,CARA,'RIGI_THER')

      LPAOUT(1) = 'PMATTTR'
      LCHOUT(1) = PREFCH(1:8)//'.ME000'
      ILIRES = 0
      IF (MODELE.NE.'       ') THEN
        LPAIN(1) = 'PGEOMER'
        LCHIN(1) = CHGEOM
        LPAIN(2) = 'PMATERC'
        LCHIN(2) = MATE(1:24)
        LPAIN(3) = 'PCACOQU'
        LCHIN(3) = CHCARA(7)
        LPAIN(4) = 'PTEMPSR'
        LCHIN(4) = TIME
        LPAIN(5) = 'PHARMON'
        LCHIN(5) = CHHARM
        LPAIN(6) = 'PCAMASS'
        LCHIN(6) = CHCARA(12)
        LIGRMO = MODELE//'.MODELE'
        OPTION = 'RIGI_THER'
        ILIRES=ILIRES+1
        CALL CODENT(ILIRES+NUMERO,'D0',LCHOUT(1) (12:14))
        CALL CALCUL('S',OPTION,LIGRMO,6,LCHIN,LPAIN,1,LCHOUT,LPAOUT,
     &              BASE,'OUI')
        CALL REAJRE(MATEL,LCHOUT(1),BASE)
      END IF
      IF (LCHAR(1) (1:8).NE.'        ') THEN
        DO 10 ICHA = 1,NCHAR
          LPAIN(1) = 'PDDLMUR'
          LCHARZ = LCHAR(ICHA)
          CALL EXISD('CHAMP_GD',LCHARZ//'.CHTH.CMULT',IRET)
          IF (IRET.EQ.0) GO TO 10
          LCHIN(1) = LCHAR(ICHA)//'.CHTH.CMULT     '
          ILIRES=ILIRES+1
          CALL CODENT(ILIRES+NUMERO,'D0',LCHOUT(1) (12:14))
          LIGRCH = LCHAR(ICHA)//'.CHTH.LIGRE'
          OPTION = 'THER_DDLM_R'
          CALL CALCUL('S',OPTION,LIGRCH,1,LCHIN,LPAIN,1,LCHOUT,LPAOUT,
     &                BASE,'OUI')
          CALL REAJRE(MATEL,LCHOUT(1),BASE)
   10   CONTINUE
      END IF

      CALL JEDEMA()
      END
