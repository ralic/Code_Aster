      SUBROUTINE MEFICG(OPTIOZ,RESULT,MODELE,DEPLA,THETA,MATE,NCHAR,
     &                  LCHAR,SYMECH,FONDF,EXTIM,TIME,IORD,NBPRUP,
     &                  NOPRUP)
      IMPLICIT NONE

      CHARACTER*8 MODELE,LCHAR(*),FONDF,RESULT,SYMECH
      CHARACTER*16 OPTIOZ,NOPRUP(*)
      CHARACTER*24 DEPLA,MATE,THETA
      REAL*8 TIME
      INTEGER IORD,NCHAR,NBPRUP
      LOGICAL EXTIM
C ......................................................................
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF CALCULEL  DATE 11/09/2002   AUTEUR VABHHTS J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.

C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.

C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================

C     - FONCTION REALISEE:   CALCUL DES COEFFICIENTS D'INTENSITE DE
C                            CONTRAINTES K1 ET K2 EN 2D

C IN   OPTION  --> CALC_K_G   (SI CHARGES REELLES)
C              --> CALC_K_G_F (SI CHARGES FONCTIONS)
C IN   RESULT  --> NOM UTILISATEUR DU RESULTAT ET TABLE
C IN   MODELE  --> NOM DU MODELE
C IN   DEPLA   --> CHAMPS DE DEPLACEMENT
C IN   THETA   --> CHAMP THETA
C IN   MATE    --> CHAMP DE MATERIAUX
C IN   NCHAR   --> NOMBRE DE CHARGES
C IN   LCHAR   --> LISTE DES CHARGES
C IN   SYMECH  --> SYMETRIE DU CHARGEMENT
C IN   FONDF   --> FOND DE FISSURE
C IN   EXTIM   --> VRAI SI L'INSTANT EST DONNE
C IN   TIME    --> INSTANT DE CALCUL
C IN   IORD    --> NUMERO D'ORDRE DE LA SD
C ......................................................................

      INTEGER I,IRET,IBID,IFIC,INORMA,IERD,INIT,IFM,NIV
      INTEGER IADRMA,IADRFF,ICOODE,IADRCO,IADRNO
      INTEGER JTEMP,LOBJ2,NDIMTE,NUNOFF,NDIM,NCHIN

      REAL*8 FIC(5),RCMP(4),VAL(5)

      COMPLEX*16 CBID

      LOGICAL EXIGEO,EXITHE,EXITRF,FONC,EPSI

      CHARACTER*8 NOMA,FOND,LICMP(4),K8B
      CHARACTER*8 LPAIN(13),LPAOUT(1),REPK,KBID
      CHARACTER*16 OPTION
      CHARACTER*24 CHGEOM,CHFOND,CHTEMP,CHTREF
      CHARACTER*24 LCHIN(13),LCHOUT(1),LIGRMO,NOMNO,NORMA
      CHARACTER*24 OBJ1,OBJ2,COORD,COORN,TEMPE,CHTIME
      CHARACTER*24 PAVOLU,PA1D2D,PAPRES
      CHARACTER*24 CF1D2D,CHPRES,CHROTA,CHPESA,CHVOLU,CF2D3D,CHEPSI

C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ---------------------

      CHARACTER*32 JEXNOM
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
      CHARACTER*1 K1BID

C --------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ---------------------

      CALL JEMARQ()
      OPTION = OPTIOZ

C- RECUPERATION DU CHAMP GEOMETRIQUE

      CALL MEGEOM(MODELE,' ',EXIGEO,CHGEOM)
      NOMA = CHGEOM

C- RECUPERATION DE L'ETAT INITIAL

      CALL GETFAC('ETAT_INIT',INIT)
      IF (INIT.NE.0) THEN
        CALL UTMESS('F','MEFICG','CALC_K_G : CHAMP INITIAL IMPOSSIBLE')
      END IF

C- RECUPERATION (S'ILS EXISTENT) DES CHAMP DE TEMPERATURES (T,TREF)

      TEMPE = ' '
      CHTEMP = '&&MEFICG.CH_TEMP_R'
      DO 10 I = 1,NCHAR
        CALL JEEXIN(LCHAR(I)//'.CHME.TEMPE.TEMP',IRET)
        IF (IRET.NE.0) THEN
          CALL JEVEUO(LCHAR(I)//'.CHME.TEMPE.TEMP','L',JTEMP)
          TEMPE = ZK8(JTEMP)
        END IF
   10 CONTINUE
      CALL METREF(MATE,NOMA,EXITRF,CHTREF)
      CALL METEMP(NOMA,TEMPE,EXTIM,TIME,CHTREF,EXITHE,CHTEMP(1:19))
      CALL DISMOI('F','ELAS_F_TEMP',MATE,'CHAM_MATER',IBID,REPK,IERD)
      IF (REPK.EQ.'OUI') THEN
        IF (.NOT.EXITHE) THEN
          CALL UTMESS('F','MEFICG',
     &                'LE MATERIAU DEPEND DE LA TEMPERATURE'//
     &                '! IL N''Y A PAS DE CHAMP DE TEMPERATURE '//
     &                '! LE CALCUL EST IMPOSSIBLE ')
        END IF
        IF (.NOT.EXITRF) THEN
          CALL UTMESS('A',' MEFICG',
     &                'LE MATERIAU DEPEND DE LA TEMPERATURE'//
     &                ' IL N''Y A PAS DE TEMPERATURE DE REFERENCE'//
     &                ' ON PRENDRA DONC LA VALEUR 0')
        END IF
      END IF

C - TRAITEMENT DES CHARGES

      CALL GCHARG(MODELE,NCHAR,LCHAR,CHVOLU,CF1D2D,CF2D3D,CHPRES,CHEPSI,
     &            CHPESA,CHROTA,FONC,EPSI)
      IF (FONC) THEN
        PAVOLU = 'PFFVOLU'
        PA1D2D = 'PFF1D2D'
        PAPRES = 'PPRESSF'
        OPTION = 'CALC_K_G_F'
      ELSE
        PAVOLU = 'PFRVOLU'
        PA1D2D = 'PFR1D2D'
        PAPRES = 'PPRESSR'
      END IF

C OBJET DECRIVANT LE MAILLAGE

      OBJ1 = MODELE//'.MODELE    .NOMA'
      CALL JEVEUO(OBJ1,'L',IADRMA)
      NOMA = ZK8(IADRMA)
      NOMNO = NOMA//'.NOMNOE'
      COORN = NOMA//'.COORDO    .VALE'
      COORD = NOMA//'.COORDO    .DESC'
      CALL JEVEUO(COORN,'L',IADRCO)
      CALL JEVEUO(COORD,'L',ICOODE)
      NDIM = -ZI(ICOODE-1+2)

C OBJET CONTENANT LES NOEUDS DU FOND DE FISSURE

      FOND = FONDF(1:8)
      OBJ2 = FOND//'.FOND      .NOEU'
      CALL JELIRA(OBJ2,'LONMAX',LOBJ2,K1BID)
      IF (LOBJ2.NE.1) THEN
        CALL UTMESS('F',' MEFICG',
     &            'LE FOND DE FISSURE DOIT CONTENIR UN NOEUD ET UN SEUL'
     &              )
      END IF
      CALL JEVEUO(OBJ2,'L',IADRNO)
      CALL JENONU(JEXNOM(NOMNO,ZK8(IADRNO)),NUNOFF)

C OBJET CONTENANT LA NORMALE AU FOND DE FISSURE

      NORMA = FOND//'.NORMALE'
      CALL JEVEUO(NORMA,'L',INORMA)
      IF (INORMA.EQ.0) THEN
        CALL UTMESS('F','MEFICG',
     &              'IL FAUT DEFINIR LA NORMALE AU FOND DE FISSURE')
      END IF

C CREATION OBJET CONTENANT COORDONNEES DU NOEUD DE FOND
C DE FISSURE ET LA NORMALE A LA FISSURE

      CALL WKVECT('&&MEFICG.FOND','V V R8',4,IADRFF)

      LICMP(1) = 'XA'
      LICMP(2) = 'YA'
      LICMP(3) = 'XNORM'
      LICMP(4) = 'YNORM'
      RCMP(1) = ZR(IADRCO+NDIM* (NUNOFF-1))
      RCMP(2) = ZR(IADRCO+NDIM* (NUNOFF-1)+1)
      RCMP(3) = ZR(INORMA)
      RCMP(4) = ZR(INORMA+1)
      ZR(IADRFF) = RCMP(1)
      ZR(IADRFF+1) = RCMP(2)
      ZR(IADRFF+2) = RCMP(3)
      ZR(IADRFF+3) = RCMP(4)

      CHFOND = '&&MEFICG.FOND'
      CALL MECACT('V',CHFOND,'MAILLA',NOMA,'FISS_R',4,LICMP,IBID,RCMP,
     &            CBID,' ')

      NDIMTE = 5
      CALL WKVECT('&&MEFICG.VALG','V V R8',NDIMTE,IFIC)
      LPAOUT(1) = 'PGTHETA'
      LCHOUT(1) = '&&FICGELE'

      LPAIN(1) = 'PGEOMER'
      LCHIN(1) = CHGEOM
      LPAIN(2) = 'PDEPLAR'
      LCHIN(2) = DEPLA
      LPAIN(3) = 'PTHETAR'
      LCHIN(3) = THETA
      LPAIN(4) = 'PMATERC'
      LCHIN(4) = MATE
      LPAIN(5) = 'PTEMPER'
      LCHIN(5) = CHTEMP
      LPAIN(6) = 'PTEREF'
      LCHIN(6) = CHTREF
      LPAIN(7) = PAVOLU
      LCHIN(7) = CHVOLU
      LPAIN(8) = PA1D2D
      LCHIN(8) = CF1D2D
      LPAIN(9) = PAPRES
      LCHIN(9) = CHPRES
      LPAIN(10) = 'PPESANR'
      LCHIN(10) = CHPESA
      LPAIN(11) = 'PROTATR'
      LCHIN(11) = CHROTA
      LPAIN(12) = 'PFISSR'
      LCHIN(12) = CHFOND

      LIGRMO = MODELE//'.MODELE'
      NCHIN = 12
      CHTIME = '&&MEFICG.CH_INST_R'
      IF (OPTION.EQ.'CALC_K_G_F') THEN
        CALL MECACT('V',CHTIME,'MODELE',LIGRMO,'INST_R  ',1,'INST   ',
     &              IBID,TIME,CBID,KBID)
        LPAIN(NCHIN+1) = 'PTEMPSR'
        LCHIN(NCHIN+1) = CHTIME
        NCHIN = NCHIN + 1
      END IF

      CALL CALCUL('S',OPTION,LIGRMO,NCHIN,LCHIN,LPAIN,1,LCHOUT,LPAOUT,
     &            'V')

C  SOMMATION DES FIC ET G ELEMENTAIRES

      CALL MESOMM(LCHOUT(1),5,IBID,FIC,CBID,0,IBID)

      DO 20 I = 1,5
        ZR(IFIC+I-1) = FIC(I)
   20 CONTINUE

      IF (SYMECH.EQ.'SYME') THEN
        ZR(IFIC) = 2.D0*FIC(1)
        ZR(IFIC+1) = 2.D0*FIC(2)
        ZR(IFIC+2) = 0.D0
        ZR(IFIC+3) = 2.D0*FIC(4)
        ZR(IFIC+4) = 0.D0
      ELSE IF (SYMECH.EQ.'ANTI') THEN
        ZR(IFIC) = 2.D0*FIC(1)
        ZR(IFIC+1) = 0.D0
        ZR(IFIC+2) = 2.D0*FIC(3)
        ZR(IFIC+3) = 0.D0
        ZR(IFIC+4) = 2.D0*FIC(5)
      END IF

C IMPRESSION DE K1,K2,G ET ECRITURE DANS LA TABLE RESU

      CALL INFNIV(IFM,NIV)
      IF (NIV.GE.2) THEN
        CALL IMPFIC(ZR(IFIC),LCHOUT(1),ZK8(IADRNO),RCMP,IFM)
      END IF

      IF (NBPRUP.EQ.4) THEN
        VAL(1) = ZR(IFIC)
        VAL(2) = ZR(IFIC+3)
        VAL(3) = ZR(IFIC+4)
        VAL(4) = ZR(IFIC+1)*ZR(IFIC+1) + ZR(IFIC+2)*ZR(IFIC+2)
      ELSE
        VAL(1) = TIME
        VAL(2) = ZR(IFIC)
        VAL(3) = ZR(IFIC+3)
        VAL(4) = ZR(IFIC+4)
        VAL(5) = ZR(IFIC+1)*ZR(IFIC+1) + ZR(IFIC+2)*ZR(IFIC+2)
      END IF
      CALL TBAJLI(RESULT,NBPRUP,NOPRUP,IORD,VAL,CBID,K8B,0)

      CALL DETRSD('CHAMP_GD',CHTEMP)
      CALL DETRSD('CHAMP_GD',CHTIME)
      CALL JEDETR('&&MEFICG.VALG')
      CALL JEDETR('&&MEFICG.FOND')

      CALL JEDEMA()
      END
