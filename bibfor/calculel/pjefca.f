      SUBROUTINE PJEFCA(MOA1,LIMA1,IOCC,NCAS)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF CALCULEL  DATE 04/09/2012   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE PELLET J.PELLET
C---------------------------------------------------------------------
C BUT : DETERMINER LE "CAS DE FIGURE" DE LA PROJECTION :
C ----
C  "3D" : ON UTILISE LES MAILLES VOLUMIQUES  : HEXA, ...
C         LE MAILLAGE EST 3D (X,Y,Z)
C  "2D" : ON UTILISE LES MAILLES SURFACIQUES : TRIA, ...
C         LE MAILLAGE EST 2D (X,Y)
C  "2.5D" : ON UTILISE LES MAILLES SURFACIQUES : TRIA, ...
C         MAIS LE MAILLAGE EST 3D (X,Y,Z)
C  "1.5D" : ON UTILISE LES MAILLES LINEIQUES : SEG
C         LE MAILLAGE PEUT ETRE 2D (X,Y) OU 3D (X,Y,Z)

C ON ESSAIE DE DETERMINER LE CAS DE FIGURE EN FONCTION
C DES MAILLES DE MOA1.
C MAIS ON SCRUTE AUSSI LE MOT CLE CAS_FIGURE QUI PERMET A L'UTILISATEUR
C DE FORCER CE PARAMETRE.
C
C  ARGUMENTS :
C  -----------
C  IN MOA1 : NOM DU MODELE (OU DU MAILLAGE) CONTENANT LES
C            MAILLES A PROJETER
C  IN IOCC  : 0 OU NUMERO D'OCCURENCE DE VIS_A_VIS
C  IN LIMA1 : NOM DE OBJET JEVEUX CONTENANT LA LISTE DES NUMEROS DE
C             MAILLES A PROJETER (OU ' ' SI IOCC=0).
C  OUT NCAS : CAS DE FIGURE : 3D/2D/2.5D/1.5D
C---------------------------------------------------------------------
      IMPLICIT   NONE
      INCLUDE 'jeveux.h'
      CHARACTER*(*) MOA1,NCAS,LIMA1
      INTEGER IOCC
C---------------- VARIABLES LOCALES  --------------------------
      CHARACTER*8 MOA,NOMO1,NOMA1,CDIM1,KBID
      INTEGER NDIM,IAGMA1,IE,IBID,NBMA1,DITOPO,TYPM1,DIM1,JTYPM1
      INTEGER NB1,KMA,JTMDIM,JREPE,N1,IEXI
      INTEGER      IARG
C-----------------------------------------------------------------------
      CALL JEMARQ()

      MOA=MOA1
      CALL JEEXIN(MOA//'.MODELE    .REPE',IEXI)
      IF (IEXI.GT.0) THEN
        NOMO1=MOA
        CALL DISMOI('F','NOM_MAILLA',NOMO1,'MODELE',IBID,NOMA1,IE)
      ELSE
        NOMO1=' '
        NOMA1=MOA
      ENDIF
      CALL JEVEUO(NOMA1//'.TYPMAIL','L',JTYPM1)


C     -- SI L'UTILISATEUR A UTILISE  CAS_FIGURE :
C     -------------------------------------------
      IF (IOCC.EQ.0) THEN
        CALL GETVTX(' ','CAS_FIGURE',1,IARG,1,NCAS,N1)
        IF (N1.EQ.1)GOTO 30
      ELSE
        CALL GETVTX('VIS_A_VIS','CAS_FIGURE',IOCC,IARG,1,NCAS,N1)
        IF (N1.EQ.1)GOTO 30
      ENDIF


C     CALCUL DE LVAVIS, IAGMA1, NBMA1 (MAILLES CONCERNEES) :
C     ---------------------------------------------------------
      IF (IOCC.GT.0) THEN
        CALL JEVEUO(LIMA1,'L',IAGMA1)
        CALL JELIRA(LIMA1,'LONMAX',NBMA1,KBID)
      ELSE
        CALL DISMOI('F','NB_MA_MAILLA',NOMA1,'MAILLAGE',NB1,KBID,IE)
        IF (NOMO1.NE.' ') THEN
          CALL JEVEUO(NOMO1//'.MODELE    .REPE','L',JREPE)
        ENDIF

        CALL WKVECT('&&PJEFCA.LIMA1','V V I',NB1,IAGMA1)
        NBMA1=0
        DO 10,KMA=1,NB1
          IF (NOMO1.NE.' ') THEN
C          -- SI C'EST UNE MAILLE DU MODELE :
            IF (ZI(JREPE-1+2*(KMA-1)+1).GT.0) THEN
              NBMA1=NBMA1+1
              ZI(IAGMA1-1+NBMA1)=KMA
            ENDIF
          ELSE
            NBMA1=NBMA1+1
            ZI(IAGMA1-1+NBMA1)=KMA
          ENDIF
   10   CONTINUE
      ENDIF


C     DETERMINATION DE LA DIMENSION DE L'ESPACE (NDIM) :
C     --------------------------------------------------------
      CALL DISMOI('F','Z_CST',NOMA1,'MAILLAGE',IBID,CDIM1,IE)
      IF (CDIM1.EQ.'OUI') THEN
        NDIM=2
      ELSE
        NDIM=3
      ENDIF



C     DETERMINATION DU CAS DE FIGURE : 2D, 3D , 2.5D OU 1.5D :
C     --------------------------------------------------------
      CALL JEVEUO('&CATA.TM.TMDIM','L',JTMDIM)

C     -- ON PARCOURT LES MAILLES DE LIMA1 POUR DETERMINER
C        LA PLUS GRANDE DIMENSION TOPOLOGIQUE : 3,2,1 : DITOPO
      DITOPO=0
      DO 20,KMA=1,NBMA1
        TYPM1=ZI(JTYPM1-1+ZI(IAGMA1-1+KMA))
        DIM1=ZI(JTMDIM-1+TYPM1)
        DITOPO=MAX(DITOPO,DIM1)
   20 CONTINUE

      IF (DITOPO.EQ.3) THEN
        CALL ASSERT(NDIM.EQ.3)
        NCAS='3D'
      ELSEIF (DITOPO.EQ.1) THEN
        NCAS='1.5D'
      ELSEIF (DITOPO.EQ.2) THEN
        IF (NDIM.EQ.2) THEN
          NCAS='2D'
        ELSEIF (NDIM.EQ.3) THEN
          NCAS='2.5D'
        ELSE
          CALL ASSERT(.FALSE.)
        ENDIF
      ELSE
        CALL ASSERT(.FALSE.)
      ENDIF


      CALL JEDETR('&&PJEFCA.LIMA1')

   30 CONTINUE
      CALL JEDEMA()
      END
