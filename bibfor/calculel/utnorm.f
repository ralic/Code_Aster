      SUBROUTINE UTNORM(IGEOM,NSOMM,NARET,INO,POINC1,POINC2,JNO,MNO,
     &                  ZRINO2,ZRINO1,ZRJNO2,ZRJNO1,X3,Y3,HF,XN,YN,JAC,
     &                  LAXI,JACOB,IFM,NIV)
C-----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF CALCULEL  DATE 29/09/2006   AUTEUR VABHHTS J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE BOITEAU O.BOITEAU
C TOLE CRP_21
C-----------------------------------------------------------------------
C    - FONCTION REALISEE:  UTILITAIRE DE CALCUL DE LA NORMALE A UN
C                          SEGMENT EN SES NOEUDS. POUR AERER TE0003
C
C IN IGEOM    : ADRESSE JEVEUX DU CHAMP GEOMETRIE
C IN NSOMM    : NOMBRE DE SOMMETS PAR ARETE
C IN NARET    : NOMBRE D'ARETES
C IN INO      : NUMERO DU NOEUD DE DEPART DE L'ARETE
C IN POINC1/2 : POIDS DE NEWTON-COTES
C   COORDONNEES POINTS D'INTERPOLATION
C IN LAXI     : LOGICAL POUR MODELISATION AXISYMETRIQUE
C IN JACOB    : REEL DEFINISSANT ORIENTATION (1 NORMAL, -1 INVERSEE)
C IN IFM/NIV  : PARAMETRES D'IMPRESSION
C OUT JNO     : NUMERO DU NOEUD EXTREME DE L'ARETE
C OUT MNO     : NUMERO DU NOEUD MILIEU DE L'ARETE
C OUT ZRINO2/1 : ABSCISSE/ORDONNEE POINT DE DEPART DE L'ARETE INO
C OUT ZRJNO2/1 : ABSCISSE/ORDONNEE POINT EXTREME DE L'ARETE JNO
C OUT X3/Y3    : ABSCISSE/ORDONNEE POINT MILIEU DE L'ARETE MNO
C OUT HF       : LONGUEUR DE L'ARETE
C OUT XN/YN/JAC : VALEUR DES COORD DE LA NORMALE EXTERNE ET DU
C                 JACOBIEN AUX POINTS CI-DESSUS
C   -------------------------------------------------------------------
C     SUBROUTINES APPELLEES:
C       MSG:UTMESS.
C       ENVIMA:R8MIEM.
C
C     FONCTIONS INTRINSEQUES:
C       SQRT.
C   -------------------------------------------------------------------
C     ASTER INFORMATIONS:
C       03/07/01 (OB): CREATION POUR SIMPLIFIER TE0003.F.
C----------------------------------------------------------------------
C CORPS DU PROGRAMME
      IMPLICIT NONE

C DECLARATION PARAMETRES D'APPELS
      INTEGER IGEOM,NSOMM,NARET,INO,JNO,MNO,IFM,NIV
      REAL*8 ZRINO2,ZRINO1,ZRJNO2,ZRJNO1,X3,Y3,XN(9),YN(9),JAC(9),HF,
     &       POINC1,POINC2,JACOB
      LOGICAL LAXI

C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ---------------------
      INTEGER            ZI
      COMMON  / IVARJE / ZI(1)
      REAL*8             ZR
      COMMON  / RVARJE / ZR(1)
      COMPLEX*16         ZC
      COMMON  / CVARJE / ZC(1)
      LOGICAL            ZL
      COMMON  / LVARJE / ZL(1)
      CHARACTER*8        ZK8
      CHARACTER*16                ZK16
      CHARACTER*24                          ZK24
      CHARACTER*32                                    ZK32
      CHARACTER*80                                              ZK80
      COMMON  / KVARJE / ZK8(1) , ZK16(1) , ZK24(1) , ZK32(1) , ZK80(1)
C --------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ---------------------

C DECLARATION VARIABLES LOCALES
      INTEGER I
      REAL*8 X(3),Y(3),AUX,OVFL,R8MIEM,JACOB1

C INIT
      OVFL = R8MIEM()
      IF (INO.EQ.NARET) THEN
        JNO = 1
      ELSE
        JNO = INO+1
      ENDIF
      ZRJNO1 = ZR(IGEOM+2*JNO-1)
      ZRJNO2 = ZR(IGEOM+2*JNO-2)
      ZRINO1 = ZR(IGEOM+2*INO-1)
      ZRINO2 = ZR(IGEOM+2*INO-2)

C CALCUL DU POINT MILIEU
      IF (NSOMM.EQ.3) THEN
        MNO = NARET+INO
        X3 = ZR(IGEOM+2*MNO-2)
        Y3 = ZR(IGEOM+2*MNO-1)
      ELSE
        MNO = 0
        X3 = (ZRINO2+ZRJNO2)*0.5D0
        Y3 = (ZRINO1+ZRJNO1)*0.5D0
      ENDIF

C CALCUL DE HF
      HF = SQRT((ZRINO2-ZRJNO2)**2 +(ZRINO1-ZRJNO1)**2)

C CALCUL NORMALE, TANGENTE ET JACOBIEN PREMIER POINT D'INTEGRATION
      JACOB1 = -1.D0*JACOB
      X(1) =  0.5D0*(ZRJNO1 + 3.D0*ZRINO1 - 4.D0*Y3 )
      Y(1) = -0.5D0*(ZRJNO2 + 3.D0*ZRINO2 - 4.D0*X3 )
      AUX = SQRT(Y(1)**2 + X(1)**2)
      IF (AUX.GT.OVFL) THEN
        JAC(1) = AUX*POINC1
        IF (LAXI) JAC(1) = JAC(1)*ZRINO2
        AUX = 1.D0/AUX
      ELSE
        CALL U2MESS('F','CALCULEL5_22')
      ENDIF
      XN(1) = X(1) * AUX * JACOB1
      YN(1) = Y(1) * AUX * JACOB1

C CALCUL NORMALE, TANGENTE ET JACOBIEN DEUXIEME POINT D'INTEGRATION
      X(2) = -0.5D0*(3.D0*ZRJNO1 + ZRINO1 - 4.D0*Y3)
      Y(2) =  0.5D0*(3.D0*ZRJNO2 + ZRINO2 - 4.D0*X3)
      AUX = SQRT(Y(2)**2 + X(2)**2)
      IF (AUX.GT.OVFL) THEN
        JAC(2) = AUX*POINC1
        IF (LAXI) JAC(2) = JAC(2)*ZRJNO2
        AUX = 1.D0/AUX
      ELSE
        CALL U2MESS('F','CALCULEL5_23')
      ENDIF
      XN(2) = X(2) * AUX * JACOB1
      YN(2) = Y(2) * AUX * JACOB1

      IF (NSOMM.EQ.3) THEN

C CALCUL NORMALE, TANGENTE ET JACOBIEN TROISIEME POINT D'INTEGRATION
        IF (HF.GT.OVFL) THEN
          AUX = 1.D0/HF
        ELSE
          CALL U2MESS('F','CALCULEL5_24')
        ENDIF
        XN(3) = (ZRINO1 - ZRJNO1) * AUX * JACOB1
        YN(3) = (ZRJNO2 - ZRINO2) * AUX * JACOB1
        JAC(3) = HF*0.5D0*POINC2
        IF (LAXI) JAC(3) = JAC(3)*X3
      ELSE
        XN(3) = 0.D0
        YN(3) = 0.D0
        JAC(3) = 0.D0
      ENDIF

      IF (NIV.EQ.2) THEN
        WRITE(IFM,*)
        WRITE(IFM,*)'NUMERO D''ARETE/HF ',INO,HF
        WRITE(IFM,*)'NOMBRE DE SOMMETS ',NSOMM
        IF (NSOMM.EQ.3) THEN
          WRITE(IFM,*)'CONNECTIQUE ',INO,JNO,MNO
        ELSE
          WRITE(IFM,*)'CONNECTIQUE ',INO,JNO
        ENDIF
        WRITE(IFM,*)'XN  ',(XN(I),I=1,NSOMM)
        WRITE(IFM,*)'YN  ',(YN(I),I=1,NSOMM)
        WRITE(IFM,*)'JAC ',(JAC(I),I=1,NSOMM)
      ENDIF
      END
