      SUBROUTINE TE0553(OPTION,NOMTE)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 16/01/2012   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.

C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.

C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
      IMPLICIT REAL*8 (A-H,O-Z)
      CHARACTER*16 OPTION,NOMTE
C ......................................................................

C     BUT: CALCUL DES MATRICES ELEMENTAIRES EN MECANIQUE
C          CORRESPONDANT A UN AMORTISSEMENT
C          SUR DES FACES D'ELEMENTS ISOPARAMETRIQUES 2D

C          OPTION : 'AMOR_MECA'

C    - ARGUMENTS:
C        DONNEES:      OPTION       -->  OPTION DE CALCUL
C                      NOMTE        -->  NOM DU TYPE ELEMENT
C ......................................................................

      CHARACTER*8 NOMRES(3),FAMI,POUM
      INTEGER ICODRE(3),KPG,SPT
      REAL*8 POIDS,NX,NY,VALRES(3),E,NU,LAMBDA,MU
      REAL*8 RHOCP,RHOCS
      REAL*8 RHO,TAUX,TAUY,NUX,NUY,SCAL,VNX,VNY,VTX,VTY
      REAL*8 VITUNI(2,2),VECT(3,2,6),MATR(6,6),JAC
      INTEGER NNO,KP,NPG,IPOIDS,IVF,IDFDE,IGEOM
      INTEGER K,I,L,MATER

C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ---------------------
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C --------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ---------------------
      CALL ELREF4(' ','RIGI',NDIM,NNO,NNOS,NPG,IPOIDS,IVF,IDFDE,JGANO)
      CALL JEVECH('PGEOMER','L',IGEOM)
      CALL JEVECH('PMATERC','L',IMATE)
      CALL JEVECH('PMATUUR','E',IMATUU)

      MATER = ZI(IMATE)
      NOMRES(1) = 'E'
      NOMRES(2) = 'NU'
      NOMRES(3) = 'RHO'
      FAMI='FPG1'
      KPG=1
      SPT=1
      POUM='+'
      CALL RCVALB(FAMI,KPG,SPT,POUM,MATER,' ','ELAS',0,' ',R8B,3,
     &            NOMRES,VALRES,ICODRE,1)
      E = VALRES(1)
      NU = VALRES(2)
      RHO = VALRES(3)

      LAMBDA = E*NU/ (1.D0+NU)/ (1.D0-2.D0*NU)
      MU = E/2.D0/ (1.D0+NU)

      RHOCP = SQRT((LAMBDA+2.D0*MU)*RHO)
      RHOCS = SQRT(MU*RHO)

C     VITESSE UNITAIRE DANS LES 3 DIRECTIONS

      VITUNI(1,1) = 1.D0
      VITUNI(1,2) = 0.D0

      VITUNI(2,1) = 0.D0
      VITUNI(2,2) = 1.D0

      DO 30 I = 1,NNO
        DO 20 J = 1,2
          DO 10 K = 1,2*NNO
            VECT(I,J,K) = 0.D0
   10     CONTINUE
   20   CONTINUE
   30 CONTINUE

C    BOUCLE SUR LES POINTS DE GAUSS

      DO 70 KP = 1,NPG
        K = (KP-1)*NNO
        CALL VFF2DN(NDIM,NNO,KP,IPOIDS,IDFDE,ZR(IGEOM),NX,NY,POIDS)
        JAC = SQRT(NX*NX+NY*NY)

C        --- CALCUL DE LA NORMALE UNITAIRE ---

        NUX = NX/JAC
        NUY = NY/JAC

C        --- CALCUL DE V.N ---

        SCAL = 0.D0
        DO 60 I = 1,NNO
          DO 50 J = 1,2
            SCAL = NUX*ZR(IVF+K+I-1)*VITUNI(J,1)
            SCAL = SCAL + NUY*ZR(IVF+K+I-1)*VITUNI(J,2)

C        --- CALCUL DE LA VITESSE NORMALE ET DE LA VITESSE TANGENCIELLE

            VNX = NUX*SCAL
            VNY = NUY*SCAL

            VTX = ZR(IVF+K+I-1)*VITUNI(J,1)
            VTY = ZR(IVF+K+I-1)*VITUNI(J,2)

            VTX = VTX - VNX
            VTY = VTY - VNY


C        --- CALCUL DU VECTEUR CONTRAINTE

            TAUX = RHOCP*VNX + RHOCS*VTX
            TAUY = RHOCP*VNY + RHOCS*VTY


C        --- CALCUL DU VECTEUR ELEMENTAIRE

            DO 40 L = 1,NNO
              LL = 2*L - 1
              VECT(I,J,LL) = VECT(I,J,LL) + TAUX*ZR(IVF+K+L-1)*POIDS*JAC
              VECT(I,J,LL+1) = VECT(I,J,LL+1) +
     &                         TAUY*ZR(IVF+K+I-1)*POIDS*JAC
   40       CONTINUE
   50     CONTINUE
   60   CONTINUE

   70 CONTINUE

      DO 100 I = 1,NNO
        DO 90 J = 1,2
          DO 80 K = 1,2*NNO
            MATR(2* (I-1)+J,K) = VECT(I,J,K)
   80     CONTINUE
   90   CONTINUE
  100 CONTINUE


C       --- PASSAGE AU STOCKAGE TRIANGULAIRE

      DO 120 I = 1,2*NNO
        DO 110 J = 1,I
          IJ = (I-1)*I/2 + J
          ZR(IMATUU+IJ-1) = MATR(I,J)
  110   CONTINUE
  120 CONTINUE

      END
