      SUBROUTINE REEREF(ELREFP,NNOP,NNOS,IGEOM,XG,IDEPL,GRAND ,NDIM,HE,
     &                  FISNO,NFISS,NFH,NFE,DDLS,DDLM,FE,DGDGL,CINEM,
     &                  XE,FF,DFDI,F,EPS,GRAD)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 21/12/2010   AUTEUR MASSIN P.MASSIN 
C ======================================================================
C COPYRIGHT (C) 1991 - 2010  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C TOLE CRP_21

      IMPLICIT NONE
      CHARACTER*3  CINEM
      CHARACTER*8  ELREFP
      INTEGER      IGEOM,NNOP,NDIM,NFH,NFE,DDLS,IDEPL
      INTEGER      NFISS,FISNO(NNOP,NFISS)
      REAL*8       XG(NDIM),HE(NFISS),FE(4),DGDGL(4,NDIM)
      REAL*8       XE(NDIM),FF(NNOP),DFDI(NNOP,NDIM),F(3,3)
      REAL*8       EPS(6),GRAD(NDIM,NDIM)
      LOGICAL      GRAND
      INTEGER      NNOS,DDLM
C
C ----------------------------------------------------------------------
C
C TROUVER LES COORDONNEES DANS L'ELEMENT DE REFERENCE D'UN
C POINT DONNE DANS L'ELEMENT REEL PAR LA METHODE NEWTON
C ET CALCUL DES ELEMENTS CINEMATIQUES
C
C ----------------------------------------------------------------------
C
C
C IN  ELREFP : TYPE DE L'ELEMENT DE REF PARENT
C IN  NNOP   : NOMBRE DE NOEUDS DE L'ELT DE RÉF PARENT
C   L'ORDRE DES DDLS DOIT ETRE 'DC' 'H1' 'E1' 'E2' 'E3' 'E4' 'LAGC'
C IN  IGEOM  : COORDONNEES DES NOEUDS
C IN  XG     : COORDONNES DU POINT DANS L'ELEMENT REEL
C IN  DEPL   : DEPLACEMENT RÉEL À PARTIR DE LA CONF DE REF
C IN  GRAND  : INDICATEUR SI GRANDES TRANSFORMATIONS
C              SI GRAND = .FALSE.
C                --> MATRICE F: UNITE
C                --> DEFORMATION EPS PETITES
C IN  NDIM   : DIMENSION DE L'ESPACE
C IN  HE     : VALEUR DE LA FONCTION HEAVISIDE SUR LE SOUS-ÉLT
C IN  NFH    : NOMBRE DE FONCTIONS HEAVYSIDE (PAR NOEUD)
C IN  NFE    : NOMBRE DE FONCTIONS SINGULIÈRES D'ENRICHISSEMENT
C IN  DDLT   : NOMBRE DE DDLS TOTAL PAR NOEUD
C IN  FE     : VALEURS AUX NOEUDS DES FONCTIONS D'ENRICHISSEMENT
C IN  DGDGL  : DÉRIVÉES DES FONCTIONS D'ENRICHISSEMENT
C IN  CINEM  : CALCUL DES QUANTITÉS CINÉMATIQUES
C               'NON' : ON S'ARRETE APRES LE CALCUL DES FF
C               'DFF' : ON S'ARRETE APRES LE CALCUL DES DERIVEES DES FF
C               'OUI' : ON VA JUSQU'AU BOUT
C OUT XE     : COORDONNÉES DU POINT DANS L'ÉLÉMENT DE RÉF PARENT
C OUT FF     : FONCTIONS DE FORMES EN XE
C OUT DFDI   : DÉRIVÉES DES FONCTIONS DE FORMES EN XE
C OUT F      : GRADIENT DE LA TRANSFORMATION
C OUT EPS    : DÉFORMATIONS
C OUT GRAD   : GRADIENT DES DÉPLACEMENTS
C
C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ---------------------
C
      INTEGER            ZI
      COMMON  / IVARJE / ZI(1)
      REAL*8             ZR
      COMMON  / RVARJE / ZR(1)
      COMPLEX*16         ZC
      COMMON  / CVARJE / ZC(1)
      LOGICAL            ZL
      COMMON  / LVARJE / ZL(1)
      CHARACTER*8        ZK8
      CHARACTER*16                ZK16
      CHARACTER*24                          ZK24
      CHARACTER*32                                    ZK32
      CHARACTER*80                                              ZK80
      COMMON  / KVARJE / ZK8(1) , ZK16(1) , ZK24(1) , ZK32(1) , ZK80(1)
C
C --- FIN DECLARATIONS NORMALISEES JEVEUX ------------------------------
C
      INTEGER     NBNOMX
      PARAMETER   (NBNOMX = 27)
C
      REAL*8      ZERO,UN,RAC2
      INTEGER     I,J,K,N,P,IG,CPT
      INTEGER     NNO,NDERIV,IRET,NN
      REAL*8      INVJAC(3,3)
      REAL*8      DFF(3,NBNOMX)
      REAL*8      KRON(3,3),TMP,EPSTAB(3,3)
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
C
C --- INITIALISATIONS
C
      CALL ASSERT(CINEM.EQ.'NON'.OR.CINEM.EQ.'OUI'.OR.CINEM.EQ.'DFF'
     &             .OR.CINEM.EQ.'INI')
      ZERO = 0.D0
      UN   = 1.D0
      RAC2 = SQRT(2.D0)
C
C --- RECHERCHE DE XE PAR NEWTON-RAPHSON
C
      CALL REEREG('S',ELREFP,NNOP,ZR(IGEOM),XG,NDIM,XE,IRET)
C
C --- VALEURS DES FONCTIONS DE FORME EN XE: FF
C
      CALL ELRFVF(ELREFP,XE,NBNOMX,FF,NNO)
C
C --- DERIVEES PREMIERES DES FONCTIONS DE FORME EN XE: DFF
C
      CALL ELRFDF(ELREFP,XE,NDIM*NBNOMX,DFF,NNO,NDERIV)
C
C --- CALCUL DE L'INVERSE DE LA JACOBIENNE EN XE: INVJAC
C
      CALL INVJAX('S',NNO   ,NDIM  ,NDERIV,DFF   ,ZR(IGEOM),INVJAC,IRET)
C
      IF (CINEM.EQ.'NON') GOTO 9999
C
C --- DERIVEES DES FONCTIONS DE FORMES CLASSIQUES EN XE : DFDI
C
      CALL MATINI(NNOP,NDIM,ZERO,DFDI)
      DO 300 N=1,NNO
        DO 310 I=1,NDIM
          DO 311 K=1,NDIM
            DFDI(N,I)= DFDI(N,I) + INVJAC(K,I)*DFF(K,N)
 311      CONTINUE
 310    CONTINUE
 300  CONTINUE
C
      IF (CINEM.EQ.'DFF') GOTO 9999
C
C --- MATRICE IDENTITE
C
      CALL MATINI(3,3,ZERO,KRON)
      DO 10 P = 1,3
        KRON(P,P) = UN
 10   CONTINUE
C
C --- CALCUL DES GRADIENTS : GRAD(U) ET F
C
      DO 20 I=1,3
        DO 21 J=1,3
          F(I,J)    = KRON(I,J)
 21     CONTINUE
 20   CONTINUE
C
      DO 30 I=1,NDIM
        DO 31 J=1,NDIM
           GRAD(I,J) = ZERO
 31     CONTINUE
 30   CONTINUE

      DO 40 I=1,6
         EPS(I) = ZERO
 40   CONTINUE

      IF (CINEM.EQ.'INI') GOTO 9999
C
C --- L'ORDRE DES DDLS DOIT ETRE 'DC' 'H1' 'E1' 'E2' 'E3' 'E4' 'LAGC'
C
      DO 402 N=1,NNOP
C
C --- DDLM=-1 PERMET D'EVITER D'AVOIR A FOURNIR DDLM DANS CHAQUE CAS
        IF (DDLM.EQ.-1) THEN
          NN=DDLS*(N-1)
        ELSE
          CALL INDENT(N,DDLS,DDLM,NNOS,NN)
        ENDIF

        CPT=0

C -- DDLS CLASSIQUES
        DO 403 I=1,NDIM
          CPT = CPT+1
          DO 404 J=1,NDIM
            GRAD(I,J) = GRAD(I,J) + DFDI(N,J) *
     &                            ZR(IDEPL-1+NN+CPT)
 404      CONTINUE
 403    CONTINUE

C -- DDLS HEAVISIDE
        DO 405 IG=1,NFH
          DO 406 I=1,NDIM
            CPT = CPT+1
            DO 407 J=1,NDIM
              GRAD(I,J) = GRAD(I,J) + HE(FISNO(N,IG)) * DFDI(N,J) *
     &                            ZR(IDEPL-1+NN+CPT)
 407        CONTINUE
 406      CONTINUE
 405    CONTINUE

C -- DDL ENRICHIS EN FOND DE FISSURE
        DO 408 IG=1,NFE
          DO 409 I=1,NDIM
            CPT = CPT+1
            DO 410 J=1,NDIM
              GRAD(I,J) = GRAD(I,J) + ZR(IDEPL-1+NN+CPT) *
     &                    (DFDI(N,J) * FE(IG) + FF(N) * DGDGL(IG,J))
 410        CONTINUE
 409      CONTINUE
 408    CONTINUE
 402  CONTINUE

      IF (GRAND) THEN
        DO 420 I=1,NDIM
          DO 421 J=1,NDIM
            F(I,J) = F(I,J) + GRAD(I,J)
 421      CONTINUE
 420    CONTINUE
      ENDIF
C
C --- CALCUL DES DÉFORMATIONS : EPS
C
      DO 430 I=1,NDIM
        DO 431 J=1,I
          TMP = GRAD(I,J) + GRAD(J,I)
          IF (GRAND) THEN
            DO 432 K=1,NDIM
              TMP = TMP + GRAD(K,I)*GRAD(K,J)
 432        CONTINUE
          ENDIF
          EPSTAB(I,J) = 0.5D0*TMP
 431    CONTINUE
 430  CONTINUE
      CALL VECINI(6,ZERO,EPS)
      EPS(1) = EPSTAB(1,1)
      EPS(2) = EPSTAB(2,2)
      EPS(4) = EPSTAB(2,1)*RAC2
      IF (NDIM .EQ. 3) THEN
        EPS(3) = EPSTAB(3,3)
        EPS(5) = EPSTAB(3,1)*RAC2
        EPS(6) = EPSTAB(3,2)*RAC2
      ENDIF
C
 9999 CONTINUE
C
      CALL JEDEMA()
      END
