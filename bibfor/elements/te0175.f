      SUBROUTINE TE0175(OPTION,NOMTE)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 16/01/2012   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C.......................................................................
C
C     BUT: CALCUL DU VECTEUR INTENSITE ACTIVE AUX NOEUDS
C          ELEMENTS ISOPARAMETRIQUES 2D
C
C          OPTION : 'INTE_ELNO'
C
C     ENTREES  ---> OPTION : OPTION DE CALCUL
C              ---> NOMTE  : NOM DU TYPE ELEMENT
C.......................................................................

      IMPLICIT REAL*8(A-H,O-Z)
      INTEGER IDFDE,IGEOM,IDINO,KPG,SPT
      INTEGER IINTE,IPRES,IMATE,IFREQ,NPG,IPOIDS,IVF
      INTEGER NNO,INO,I,NDIM,NNOS,JGANO,MATER
      REAL*8 OMERHO,PI,DFDX(9),DFDY(9),JAC,R8B,R8PI,RHO
      INTEGER ICODRE
      CHARACTER*8 FAMI,POUM
      CHARACTER*16 NOMTE,OPTION
      COMPLEX*16 VITX,VITY

C---------------- COMMUNS NORMALISES  JEVEUX  --------------------------
      COMMON /IVARJE/ZI(1)
      COMMON /RVARJE/ZR(1)
      COMMON /CVARJE/ZC(1)
      COMMON /LVARJE/ZL(1)
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
      INTEGER ZI
      REAL*8 ZR
      COMPLEX*16 ZC
      LOGICAL ZL
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
C------------FIN  COMMUNS NORMALISES  JEVEUX  --------------------------
      CALL JEMARQ()

      CALL ELREF4(' ','NOEU',NDIM,NNO,NNOS,NPG,IPOIDS,IVF,IDFDE,JGANO)

      CALL JEVECH('PGEOMER','L',IGEOM)
      CALL JEVECH('PPRESSC','L',IPRES)
      CALL JEVECH('PMATERC','L',IMATE)
      CALL JEVECH('PINTER','E',IINTE)

      MATER=ZI(IMATE)
      FAMI='FPG1'
      KPG=1
      SPT=1
      POUM='+'
      CALL RCVALB(FAMI,KPG,SPT,POUM,MATER,' ','FLUIDE',0,'   ',R8B,1,
     &           'RHO',RHO,ICODRE,1)

      CALL JEVECH('PFREQR','L',IFREQ)
      PI=R8PI()
      OMERHO=2.D0*PI*ZR(IFREQ)*RHO

C    BOUCLE SUR LES NOEUDS
      DO 30 INO=1,NPG
        IDINO=IINTE+(INO-1)*4-1
        CALL DFDM2D(NNO,INO,IPOIDS,IDFDE,ZR(IGEOM),DFDX,DFDY,JAC)

        VITX=(0.0D0,0.0D0)
        VITY=(0.0D0,0.0D0)
        DO 20 I=1,NNO
          VITX=VITX+DFDX(I)*ZC(IPRES+I-1)
          VITY=VITY+DFDY(I)*ZC(IPRES+I-1)
   20   CONTINUE

        VITX=VITX*(0.D0,1.D0)/OMERHO
        VITY=VITY*(0.D0,1.D0)/OMERHO

        ZR(IDINO+1)=0.5D0*DBLE(ZC(IPRES+INO-1)*DCONJG(VITX))
        ZR(IDINO+2)=0.5D0*DBLE(ZC(IPRES+INO-1)*DCONJG(VITY))
        ZR(IDINO+3)=0.5D0*DIMAG(ZC(IPRES+INO-1)*DCONJG(VITX))
        ZR(IDINO+4)=0.5D0*DIMAG(ZC(IPRES+INO-1)*DCONJG(VITY))
   30 CONTINUE

      CALL JEDEMA()
      END
