      SUBROUTINE TE0440(OPTION,NOMTE)

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 22/03/2010   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2008  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================

      IMPLICIT NONE
      CHARACTER*16 OPTION,NOMTE
C......................................................................
C
C    - FONCTION REALISEE:  CALCUL DES VECTEURS ELEMENTAIRES
C                          OPTIONS  CHAR_MECA_FR3D3D ET CHAR_MECA_FF3D3D
C                                   CHAR_MECA_FR2D2D ET CHAR_MECA_FF2D2D
C                          POUR LES ÉLÉMENTS X-FEM
C
C    - ARGUMENTS:
C        DONNEES:      OPTION       -->  OPTION DE CALCUL
C                      NOMTE        -->  NOM DU TYPE ELEMENT
C ......................................................................


C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ---------------------
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C --------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ---------------------

      INTEGER I,J,NDIM,NNO,NNOP,NNOS,NNOM,NDDL,NPG,SINGU,IRET,IBID
      INTEGER DDLH,DDLS,NFE,DDLC,NIT,CPT,IT,NSE,ISE,IN,INO
      INTEGER JPINTT,JCNSET,JHEAVT,JLONCH,JLSN,JLST,JCOORS,JSTNO
      INTEGER IVECTU,IFORC,ITEMPS,IGEOM,IDECPG,NSEMAX(3)
      REAL*8  HE,RBID
      CHARACTER*8   ELREFP,ELRESE(3),FAMI(3)
      CHARACTER*24  COORSE
      LOGICAL FONC,LBID
      DATA    NSEMAX / 2 , 3 , 6 /
      DATA    ELRESE /'SE2','TR3','TE4'/
      DATA    FAMI   /'BID','RIGI','XINT'/

C-----------------------------------------------------------------------


C     ELEMENT DE REFERENCE PARENT
      CALL ELREF1(ELREFP)
      CALL ELREF4(' ','RIGI',NDIM,NNOP,IBID,IBID,IBID,IBID,IBID,IBID)

C     SOUS-ELEMENT DE REFERENCE : RECUP DE NNO, NPG
      CALL ELREF4(ELRESE(NDIM),FAMI(NDIM),IBID,NNO,NNOS,NPG,
     &                                           IBID,IBID,IBID,IBID)

C     INITIALISATION DES DIMENSIONS DES DDLS X-FEM
      CALL XTEINI(NOMTE,DDLH,NFE,SINGU,DDLC,NNOM,DDLS,NDDL)

C     PARAMETRE DU VECTEUR ELEMENTAIRE
C-------------------------------------
      CALL JEVECH('PVECTUR','E',IVECTU)

C     PARAMÈTRES PROPRES À X-FEM
C-------------------------------
      CALL JEVECH('PPINTTO','L',JPINTT)
      CALL JEVECH('PCNSETO','L',JCNSET)
      CALL JEVECH('PHEAVTO','L',JHEAVT)
      CALL JEVECH('PLONCHA','L',JLONCH)
      CALL JEVECH('PLSN',   'L',JLSN)
      CALL JEVECH('PLST',   'L',JLST)
      CALL JEVECH('PGEOMER','L',IGEOM)
      CALL JEVECH('PSTANO' ,'L',JSTNO)

C     PARAMÈTRES DES FORCES VOLUMIQUES
C-------------------------------------

      IF ((OPTION.EQ.'CHAR_MECA_FR3D3D').OR.
     &    (OPTION.EQ.'CHAR_MECA_FR2D2D')) THEN

        FONC=.FALSE.
        IF (NDIM.EQ.3) CALL TEFREP(OPTION,NOMTE,'PFR3D3D',IFORC)
        IF (NDIM.EQ.2) CALL TEFREP(OPTION,NOMTE,'PFR2D2D',IFORC)

      ELSEIF ((OPTION.EQ.'CHAR_MECA_FF3D3D').OR.
     &        (OPTION.EQ.'CHAR_MECA_FF2D2D')) THEN

        FONC=.TRUE.
        CALL JEVECH('PTEMPSR','L',ITEMPS)
        IF (NDIM.EQ.3) CALL JEVECH('PFF3D3D','L',IFORC)
        IF (NDIM.EQ.2) CALL JEVECH('PFF2D2D','L',IFORC)

      ENDIF


C     RÉCUPÉRATION DE LA SUBDIVISION L'ÉLÉMENT PARENT EN NIT TETRAS
      NIT=ZI(JLONCH-1+1)

      CPT=0
C     BOUCLE SUR LES NIT TETRAS
      DO 100 IT=1,NIT

C       RÉCUPÉRATION DU DÉCOUPAGE EN NSE SOUS-ÉLÉMENTS
        NSE=ZI(JLONCH-1+1+IT)

C       BOUCLE D'INTÉGRATION SUR LES NSE SOUS-ÉLÉMENTS
        DO 110 ISE=1,NSE

          CPT=CPT+1

C         COORD DU SOUS-ÉLT EN QUESTION
          COORSE='&&TE0440.COORSE'
          CALL WKVECT(COORSE,'V V R',NDIM*(NDIM+1),JCOORS)

C         BOUCLE SUR LES 4 (OU 3) SOMMETS DU SOUS-TETRA (OU SOUS-TRIA)
          DO 111 IN=1,NNO
            INO=ZI(JCNSET-1+(NDIM+1)*(CPT-1)+IN)
            DO 112 J=1,NDIM
              IF (INO.LT.1000) THEN
C               NOEUD DE L'ELEMENT PARENT
                ZR(JCOORS-1+NDIM*(IN-1)+J)=ZR(IGEOM-1+NDIM*(INO-1)+J)
              ELSE
C               POINT D'INTERSECTION
                ZR(JCOORS-1+NDIM*(IN-1)+J)=
     &                               ZR(JPINTT-1+NDIM*(INO-1000-1)+J)
              ENDIF
 112        CONTINUE
 111      CONTINUE

C         FONCTION HEAVYSIDE CSTE SUR LE SS-ÉLT
          HE = ZI(JHEAVT-1+NSEMAX(NDIM)*(IT-1)+ISE)
C         POSITION DANS LA FAMILLE 'XFEM' DU 1ER PG DU SE COURANT
          IDECPG = NPG * ( NSEMAX(NDIM)*(IT-1)+ (ISE-1))

          CALL XFOVOL(ELREFP,NDIM,COORSE,IGEOM,HE,DDLH,DDLC,NFE,
     &                NNOP,NPG,JLSN,JLST,IDECPG,IFORC,ITEMPS,IVECTU,
     &                FONC,.TRUE.)

          CALL JEDETR(COORSE)

 110    CONTINUE
 100  CONTINUE

C     SUPPRESSION DES DDLS SUPERFLUS
      CALL XTEDDL(NDIM,DDLH,NFE,DDLC,DDLS,NDDL,NNOP,NNOS,ZI(JSTNO),
     &                  LBID,OPTION,NOMTE,
     &                  RBID,ZR(IVECTU))

      END
