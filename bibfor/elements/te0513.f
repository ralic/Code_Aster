      SUBROUTINE TE0513(OPTION,NOMTE)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 20/03/2002   AUTEUR MJBHHPE J.L.FLEJOU 
C ======================================================================
C COPYRIGHT (C) 1991 - 2002  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C                                                                       
C                                                                       
C ======================================================================
      IMPLICIT REAL*8 (A-H,O-Z)
      CHARACTER*(*) OPTION,NOMTE
C     ------------------------------------------------------------------
C     CALCULE LA MATRICE DE RIGIDITE GEOMETRIQUE ELEMENTAIRE DES
C     ELEMENTS DE POUTRE MULTI-FIBRES DE TIMOSHENKO AVEC GAUCHISSEMENT.
C     ------------------------------------------------------------------
C IN  OPTION : K16 : NOM DE L'OPTION A CALCULER
C        'RIGI_MECA_GE' : CALCUL DE LA MATRICE DE RIGIDITE
C IN  NOMTE  : K16 : NOM DU TYPE ELEMENT
C        'MECA_POU_D_TGM': POUTRE DROITE MULTI-FIBRE DE TIMOSHENKO AVEC
C                          GAUCHISSEMENT
C     ------------------------------------------------------------------
C
C     ----- DEBUT COMMUNS NORMALISES  JEVEUX  --------------------------
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C     -----  FIN  COMMUNS NORMALISES  JEVEUX  --------------------------
C

      INTEGER NC,NNO
      PARAMETER ( NC = 7 , NNO = 2 )
      REAL*8 ZERO
      PARAMETER( ZERO = 0.D0 )
      CHARACTER*16 CH16
      REAL*8 A,IY,IZ,IYR2,IZR2,EZ,EY
      REAL*8 XL, MAT(105), PGL(3,3) , B(2*NC)
      INTEGER NCOMP,NBFIB,I,LSECT,LX,IAEFF,KP,LORIEN,LMAT,ADR
      REAL*8 KSI1,D1B3(2,3)
C     ------------------------------------------------------------------
C

      IF ( OPTION .NE. 'RIGI_MECA_GE' ) THEN
          CH16 = OPTION
          CALL UTMESS('F','ELEMENTS DE POUTRE (TE0513)',
     +                'L''OPTION "'//CH16//'" EST INCONNUE')
      END IF

C     --- RECUPERATION DES CARACTERISTIQUES GENERALES DES SECTIONS ---
      CALL JEVECH('PCAGNPO','L',LSECT)
      LSECT = LSECT - 1
      A   = ZR(LSECT+1)
      IY  = ZR(LSECT+2)
      IZ  = ZR(LSECT+3)
      EY  = -ZR(LSECT+6)
      EZ  = -ZR(LSECT+7)
      IYR2= ZR(LSECT+10)
      IZR2= ZR(LSECT+11)

C     --- RECUPERATION DES COORDONNEES DES NOEUDS ---
      CALL JEVECH('PGEOMER','L',LX)
      LX = LX - 1
      XL = SQRT((ZR(LX+4)-ZR(LX+1))**2+ (ZR(LX+5)-ZR(LX+2))**2+
     +     (ZR(LX+6)-ZR(LX+3))**2)
      IF (XL.EQ.ZERO) THEN
          CH16 = ' ?????????'
          CALL UTMESS('F','ELEMENTS DE POUTRE (TE0343)',
     +                'NOEUDS CONFONDUS POUR UN ELEMENT: '//CH16(:8))
      ENDIF

C     --- RECUPERATION DES CARACTERISTIQUES DES FIBRES
C     NOMBRE DE VARIABLES PAR POINT DE GAUSS EN PLUS DU NBFIB
      NCOMP = 7
      CALL JEVECH('PNBSP_I','L',I)
      NBFIB = ZI(I)

C     ON PROJETTE AVEC LES FCTS DE FORME
C     SUR LES NOEUDS DEBUT ET FIN DE L'ELEMENT
C     POUR LE POINT 1
      KSI1 = -SQRT( 5.D0 / 3.D0 )
      D1B3(1,1) = KSI1*(KSI1-1.D0)/2.0D0
      D1B3(1,2) = 1.D0-KSI1*KSI1
      D1B3(1,3) = KSI1*(KSI1+1.D0)/2.0D0
C     POUR LE POINT 2
      KSI1 = SQRT( 5.D0 / 3.D0 )
      D1B3(2,1) = KSI1*(KSI1-1.D0)/2.0D0
      D1B3(2,2) = 1.D0-KSI1*KSI1
      D1B3(2,3) = KSI1*(KSI1+1.D0)/2.0D0

      DO 100 I = 1 , 105
         MAT(I) = 0.D0 
100   CONTINUE

C --- CALCUL DE LA MATRICE DE RIGIDITE GEOMETRIQUE ---
      CALL JEVECH('PEFFORR','L',IAEFF)
C
C     ON RECUPERE LES EFFORTS GENERALISE CF OPTION SIEF_ELNO_ELGA

C !!! MAGOUILLE POUR STOCKER LES FORCES INTEGREES !!!
C     ELLES SONT DEJA CALCULEES ET STOCKEES APRES LES FIBRES
C       AU NOEUD 1 ON RECUPERE   -EFFORT STOCKE
C       AU NOEUD 2 ON RECUPERE   +EFFORT STOCKE
      DO 210 I = 1 , NC
        B(I)    = ZERO
        B(I+NC) = ZERO
        DO 212 KP = 1 , 3
          ADR = IAEFF+(NBFIB+NCOMP)*(KP-1)+ NBFIB + I - 1
          B(I)   = B(I)   -ZR(ADR)*D1B3(1,KP)
          B(I+NC)= B(I+NC)+ZR(ADR)*D1B3(2,KP)
212     CONTINUE
210   CONTINUE

      CALL PTKG20(B,A,IZ,IY,IYR2,IZR2,XL,EY,EZ,MAT)

C --- RECUPERATION DES ORIENTATIONS ALPHA,BETA,GAMMA ---
      CALL JEVECH('PCAORIE','L',LORIEN)

C --- PASSAGE DU REPERE LOCAL AU REPERE GLOBAL ---
      CALL MATROT ( ZR(LORIEN) , PGL )
      CALL JEVECH ( 'PMATUUR', 'E', LMAT )
      CALL UTPSLG ( NNO, NC, PGL, MAT, ZR(LMAT) )

      END
