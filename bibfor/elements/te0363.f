       SUBROUTINE TE0363(OPTION,NOMTE)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 07/10/2008   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C
      IMPLICIT   NONE
      CHARACTER*16 OPTION,NOMTE
C

C
C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX --------------------
C
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C --------- FIN  DECLARATIONS  NORMALISEES  JEVEUX --------------------
C
      INTEGER I,J,IN,NFAES,NFAMA,INCOCA,PL
      INTEGER NNE,NNES,NNM,NNC,NDDL,NDDLC,NDIM,INDCO,MEMCO
      INTEGER IGEOM,IDEPL,JPCPO,IGEOMT,IGEOES,IGEOMA
      INTEGER IDEPM,JPCPI,JPCAI,JPCCF,IAINES,IAINMA
      INTEGER INDNOR,IFACES,IFACMA,IGLISS
      INTEGER IPINES,IPINMA,XOULA,JOUT1,JOUT2,JOUT3
      INTEGER IRET,IBID,CFACE(5,3)
      REAL*8 PREC,NOOR,R8PREM
      REAL*8 XPR,YPR,XPC,YPC,JEU,XES(3),XMA(3)
      REAL*8 TAU1(3),TAU2(3),NORM(3)
      REAL*8 GEOMM(3),GEOME(3),RBID
      REAL*8 DEPLME(6),DEPLM(6),DEPLE(6),DEPLMM(6)
      REAL*8 FFPC(9),DFFPC(2,9),DDFFPC(3,9)
      REAL*8 FFPR(9),DFFPR(2,9),DDFFPR(3,9),DFDI,F
      REAL*8 FFES(9),EPS
      REAL*8 FFMA(9),GRAD
      CHARACTER*8 ESC,MAIT,ELC,ESQ
      CHARACTER*19 GEOM2,PINTES,PINTMA,COORDE,COORDM
      CHARACTER*19 AINTES,AINTMA,CFACES,CFACMA
      PARAMETER    (PREC=1.D-16)
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
C
C --- INFOS SUR LA MAILLE DE CONTACT
C
      CALL XMELET(NOMTE,
     &            NDIM,NDDL,NDDLC,ESC,ESQ,NNE,MAIT,NNM,ELC,NNC)
      CALL ASSERT (NDDL.LE.81)
C
C --- INITIALISATIONS
C      
      DO 7 I=1,5
        DO 8 J=1,3
          CFACE(I,J)=0
  8     CONTINUE
  7   CONTINUE
  
      DO 9 I = 1,NDIM
        GEOME(I)  = 0.D0
        GEOMM(I)  = 0.D0       
 9    CONTINUE
 
      INCOCA=0

      GEOM2 ='&&TE0363.GEOM2'
      PINTES='&&TE0363.PINES'
      PINTMA='&&TE0363.PINMA'
      COORDE='&&TE0363.GEOME'
      COORDM='&&TE0363.GEOMM'
      AINTES='&&TE0363.AINTE'
      AINTMA='&&TE0363.AINTM'
      CFACES='&&TE0363.CFACE'
      CFACMA='&&TE0363.CFACM'
C
C ---RECUPERATION DES DONNEES DE LA CARTE CONTACT 'POINT' (VOIR MMCART)
      CALL JEVECH('PCAR_PT','L',JPCPO)
      XPC      = ZR(JPCPO-1+1)
      YPC      = ZR(JPCPO-1+10)
      XPR      = ZR(JPCPO-1+2)
      YPR      = ZR(JPCPO-1+3)
      TAU1(1)  = ZR(JPCPO-1+4)
      TAU1(2)  = ZR(JPCPO-1+5)
      TAU1(3)  = ZR(JPCPO-1+6)
      TAU2(1)  = ZR(JPCPO-1+7)
      TAU2(2)  = ZR(JPCPO-1+8)
      TAU2(3)  = ZR(JPCPO-1+9)
      INDCO    = NINT(ZR(JPCPO-1+11))
C      LAMBDA   = ZR(JPCPO-1+12)
C      COEFCA   = ZR(JPCPO-1+13)
C      COEFFA   = ZR(JPCPO-1+14)
C      COEFFF   = ZR(JPCPO-1+15)
C      IFROTT   = NINT(ZR(JPCPO-1+16))
      INDNOR   = NINT(ZR(JPCPO-1+17))
C      IAXIS    = NINT(ZR(JPCPO-1+18))
C      HPG      = ZR(JPCPO-1+19)
      IGLISS   = NINT(ZR(JPCPO-1+29))
      MEMCO    = NINT(ZR(JPCPO-1+30))
C---LES NUMEROS DES FACETTES DE CONTACT (ESCLAVE ET MAITRE) DONT LE PTC
C--- ET SON PROJETE APPARTIENT
      NFAES    = NINT(ZR(JPCPO-1+31))
      NFAMA    = NINT(ZR(JPCPO-1+32))
C----------------------------------------------------------------------
C      IMA      = NINT(ZR(JPCPO-1+34))
      
C --RECUPERATION DES DONNEES DE LA CARTE CONTACT 'PINTER' (VOIR MMCART)
      CALL JEVECH('PCAR_PI','L',JPCPI)
      
C --RECUPERATION DES DONNEES DE LA CARTE CONTACT 'AINTER' (VOIR MMCART)
      CALL JEVECH('PCAR_AI','L',JPCAI)
      
C --RECUPERATION DES DONNEES DE LA CARTE CONTACT 'CFACE' (VOIR MMCART)
      CALL JEVECH('PCAR_CF','L',JPCCF)      
C
C --- RECUPERATION DE LA GEOMETRIE ET DES CHAMPS DE DEPLACEMENT
C
      CALL JEVECH('PGEOMER','E',IGEOM)
      CALL JEVECH('PDEPL_P','E',IDEPL)
      CALL JEVECH('PDEPL_M','L',IDEPM)
      
C-----RECUPERATION DES CHAMPS DE SORTIE

      CALL JEVECH('PINCOCA','E',JOUT1)
      CALL JEVECH('PINDCOT','E',JOUT2)
      CALL JEVECH('PINDMEM','E',JOUT3)
      
C----ON CREE UN CHAMP 'LOCAL' DE LA GÉOMETRIE (POUR MANIPULER PAR LA 
C----SUITE LES NOEUDS FICTIFS

      CALL WKVECT(GEOM2,'V V R',NDIM*(NNE+NNM),IGEOMT)
      DO 50 I=1,NDIM*(NNE+NNM)
        ZR(IGEOMT+I-1)=ZR(IGEOM+I-1)
  50  CONTINUE
C-----NOMBRE DE NOEUDS ESCLAVES SOMMETES(EN 3D IL FAUDRA MODIFIER ICI)
      NNES=NNE/2
C
C --- REACTUALISATION DE LA GEOMETRIE
C
      CALL XMREAC(NDIM,NNES,NNM,
     &            IGEOMT,IDEPM)
C
C --- FONCTIONS DE FORMES ET DERIVEES POUR LE
C --- POINT DE CONTACT
C
      CALL XMMFFD(ELC,XPC,YPC,
     &            FFPC,DFFPC,DDFFPC,IRET)
     
   
C --- FONCTIONS DE FORMES ET DERIVEES POUR LA
C --- PROJECTION DU POINT DE CONTACT DANS 'ELC'
C
      CALL XMMFFD(ELC,XPR,YPR,
     &            FFPR,DFFPR,DDFFPR,IRET)      
     
C----CONSTRUCTION DU CHAMP DES COORDONNÉES GÉOMETRIQUE POUR LA 
C----MAILLE ESCLAVE (SEULEMENT POUR LES NOEUDS SOMMETS)

      CALL WKVECT(COORDE,'V V R',NDIM*NNES,IGEOES)
      DO 60 I=1,NNES
        DO 70 J=1,NDIM
          ZR(IGEOES-1+(I-1)*NDIM+J)=ZR(IGEOMT-1+(I-1)*NDIM+J)
 70     CONTINUE
 60   CONTINUE     
 
C----CONSTRUCTION DU CHAMP DES COORDONNÉES GÉOMETRIQUE POUR LA 
C----MAILLE MAITRE

      CALL WKVECT(COORDM,'V V R',NDIM*NNM,IGEOMA)
      DO 80 I=1,NNM
        DO 90 J=1,NDIM
          ZR(IGEOMA-1+(I-1)*NDIM+J)=
     &                     ZR(IGEOMT-1+NDIM*NNE+(I-1)*NDIM+J)
 90     CONTINUE
 80   CONTINUE
 
C----CONSTRUCTION DES CHAMPS AINTES ET AINTMA (INFO SUR LES ARRETES 
C----COUPÉS DANS LA MAILLE ESCLAVE, RESPECTIVEMENT MAITRE )

      CALL WKVECT(AINTES,'V V R',24,IAINES)
      CALL WKVECT(AINTMA,'V V R',24,IAINMA)
      DO 81 I=1,24
        ZR(IAINES-1+I)= ZR(JPCAI-1+I)
        ZR(IAINMA-1+I)= ZR(JPCAI-1+24+I)
 81   CONTINUE

C----CONSTRUCTION DES CHAMPS CFACES ET CFACMA (INFO SUR LA 
C----CONNECTIVITÉ DES FACETTES DANS LA MAILLE ESCLAVE, 
C----RESPECTIVEMENT MAITRE )

      CALL WKVECT(CFACES,'V V R',24,IFACES)
      CALL WKVECT(CFACMA,'V V R',24,IFACMA)
      DO 82 I=1,15
        ZR(IFACES-1+I)= ZR(JPCCF-1+I)
        ZR(IFACMA-1+I)= ZR(JPCCF-1+15+I)
 82   CONTINUE
 
C----ON CONSTRUIT LA MATRICE DE CONNECTIVITÉ CFACE (MAILLE ESCLAVE)
C--!!!ATTENTION, POUR L'INSTANT CE QUI SUIT EST VALABLE QUE EN 2D!!!!
C      NFACE=1
      CFACE(1,1)=1
      CFACE(1,2)=2      
    
C      DO 83 I=1,NFACE
C        DO 84 J=1,NDIM
C          CFACE(I,J)=NINT(ZR(IFACES-1+NDIM*(I-1)+J))
C 84     CONTINUE
C 83   CONTINUE
C
C ---CALCUL DES COORDONNÉES RÉELES DES POINTS D'INTERSECTION ESCLAVES
      CALL WKVECT(PINTES,'V V R',NDIM*NDIM,IPINES)
      DO 100 I=1,NDIM*NDIM
        ZR(IPINES-1+I)=0.D0
 100  CONTINUE
      CALL XMPINT(ESC,NDIM,IGEOES,JPCPI,
     &               NFAES,1,NNES,IPINES)
C
C ---CALCUL DES COORDONNÉES RÉELES DES POINTS D'INTERSECTION MAITRES
      CALL WKVECT(PINTMA,'V V R',NDIM*NDIM,IPINMA)
      DO 110 I=1,NDIM*NDIM
        ZR(IPINMA-1+I)=0.D0
 110  CONTINUE
      CALL XMPINT(MAIT,NDIM,IGEOMA,JPCPI,
     &               NFAMA,2,NNM,IPINMA)
C
C----CALCUL DES COORDONNÉES RÉELES DU POINT DE CONTACT
      DO 120 I = 1,NDIM
        DO 130 J = 1,NNC
          GEOME(I) = GEOME(I) + 
     &               FFPC(J)*ZR(IPINES-1+(J-1)*NDIM+I)
 130    CONTINUE
 120  CONTINUE
C
C---- FONCTIONS DE FORME POUR LE POINT DE CONTACT DANS LA MAILLE ESCLAVE
C   
      CALL REEREF(ESC,NNES,IGEOES,GEOME,RBID,.FALSE.,NDIM,RBID,
     &            IBID,IBID,IBID,RBID,RBID,'NON',XES,FFES,DFDI,
     &            F,EPS,GRAD)
C
C----CALCUL DES COORDONNÉES RÉELES DU PROJETE DU POINT DE CONTACT
      DO 140 I = 1,NDIM
        DO 150 J = 1,NNC
          GEOMM(I) = GEOMM(I) +
     &               FFPR(J)*ZR(IPINMA-1+(J-1)*NDIM+I)
 150    CONTINUE 
 140  CONTINUE
C   
C---- FONCTIONS DE FORME POUR LE PROJETE DU POINT DE CONTACT
C-----DANS LA MAILLE MAITRE
     
      CALL REEREF(MAIT,NNM,IGEOMA,GEOMM,RBID,.FALSE.,NDIM,RBID,
     &            IBID,IBID,IBID,RBID,RBID,'NON',XMA,FFMA,DFDI,
     &            F,EPS,GRAD)
C
C
C --- CALCUL DE LA NORMALE
C
      CALL MMNORM(NDIM,TAU1,TAU2,NORM,NOOR)
      IF (NOOR.LE.R8PREM()) THEN
        CALL ASSERT(.FALSE.)
      ENDIF
C 
C --- CALCUL DES DEPLACEMENTS GEOMETRIQUES DU POINT DE CONTACT ET
C --- DE SON PROJETE
C
      DO 160 I = 1,2*NDIM
        DEPLM(I)  = 0.D0
        DEPLMM(I) = 0.D0          
 160  CONTINUE
      DO 170 I = 1,3*NDIM
        DEPLE(I)  = 0.D0
        DEPLME(I) = 0.D0
 170  CONTINUE
 
      DO 180 I = 1,2*NDIM
        DO 190 J = 1,NNES
          DEPLE(I)  = DEPLE(I)  +
     &                FFES(J)*ZR(IDEPL+(J-1)*(3*NDIM)+I-1)
          DEPLME(I) = DEPLME(I) +
     &                FFES(J)*ZR(IDEPM+(J-1)*(3*NDIM)+I-1)
 190    CONTINUE
 180  CONTINUE
 
      DO 200 I = 1,2*NDIM
        DO 210 J = 1,NNM
          DEPLM(I)  = DEPLM(I)  + 
     &     FFMA(J)*ZR(IDEPL+NNES*(4*NDIM)+(J-1)*2*NDIM+I-1)
          DEPLMM(I) = DEPLMM(I) +
     &     FFMA(J)*ZR(IDEPM+NNES*(4*NDIM)+(J-1)*2*NDIM+I-1) 
 210    CONTINUE
 200  CONTINUE 
C
C----CALCUL DES REACTIONS DE CONTACT (PRESSION ET FROTTEMENT)
C
      DO 220 I=1,NNC
C---!!!TYPE DE MAILLE QUADRATIQUE À INTRODUIRE DANS XOULA!!!!      
        IN=XOULA(CFACE,NFAES,I,IAINES,ESQ)
        CALL XPLMA2(NDIM,NNE,NNES,IN,PL)
        DEPLE(2*NDIM+1)=DEPLE(2*NDIM+1)+FFPC(I)*ZR(IDEPL-1+PL)
        DEPLME(2*NDIM+1)=DEPLME(2*NDIM+1)+FFPC(I)*ZR(IDEPM-1+PL)
 220  CONTINUE
C
C --- EVALUATION DES JEUX - CAS DU CONTACT
C
      CALL XMMJEU(NDIM,NORM,GEOME,GEOMM,DEPLE,DEPLM,
     &            DEPLME,DEPLMM,JEU,RBID,RBID)

C
C --- NOEUDS EXCLUS PAR PROJECTION HORS ZONE
C
      IF (INDNOR .EQ. 1) THEN
        ZR(JOUT2)= 0.D0
        GO TO 999
      ENDIF
C
C
C----SI LE CONTACT A ETE POSTULE, ON TESTE LA VALEUR DE LA PRESSION
C    DE CONTACT

      IF (INDCO.EQ.1) THEN
        IF (DEPLE(2*NDIM+1).GT.-1.D-3) THEN
          IF ((IGLISS.EQ.1).AND.(MEMCO.EQ.1)) THEN
            ZR(JOUT2) = 1.D0
            ZI(JOUT3) = 1
          ELSE IF (IGLISS.EQ.0) THEN
            ZR(JOUT2) = 0.D0
            INCOCA = 1
          ENDIF
        ELSE
          ZR(JOUT2) = 1.D0
          ZI(JOUT3) = 1
        END IF

C-----SI LE NON-CONTACT A ETE POSTULÉ, ON TESTE LA VALEUR DU JEU

      ELSE IF (INDCO .EQ. 0) THEN
        IF (JEU.GT.PREC) THEN
          ZR(JOUT2) = 1.D0
          ZI(JOUT3) = 1
          INCOCA = 1
        ELSE
          ZR(JOUT2) = 0.D0
        END IF
C
      ELSE
C-----SI INDCO N'EST NI EGAL A 0 NI EGAL A 1
        CALL U2MESS('F','DVP_98')
      ENDIF

C
 999  CONTINUE
 
C     ENREGISTREMENT DES CHAMPS DE SORTIE
      ZI(JOUT1)=INCOCA

      CALL JEDETR(GEOM2)
      CALL JEDETR(PINTES)
      CALL JEDETR(PINTMA)
      CALL JEDETR(COORDE)
      CALL JEDETR(COORDM)
      CALL JEDETR(AINTES)
      CALL JEDETR(AINTMA)
      CALL JEDETR(CFACES)
      CALL JEDETR(CFACMA)


      CALL JEDEMA()
      END
