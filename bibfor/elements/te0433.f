      SUBROUTINE TE0433(OPTION,NOMTE)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 04/09/2012   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
      IMPLICIT NONE
      INCLUDE 'jeveux.h'
      CHARACTER*16 OPTION,NOMTE
C ......................................................................
C    - FONCTION REALISEE:  CALCUL DES OPTIONS DE POST-TRAITEMENT :
C                                  - SIEF_ELGA
C                                  - EPOT_ELEM
C                                  - EPSI_ELGA
C                                  - MASS_INER
C                          POUR LES GRILLES MEMBRANES EXCENTREES OU NON
C    - ARGUMENTS:
C        DONNEES:      OPTION       -->  OPTION DE CALCUL
C                      NOMTE        -->  NOM DU TYPE ELEMENT
C ......................................................................

      INTEGER CODRES(2)
      CHARACTER*4 FAMI
      CHARACTER*8 NOMRES(2)
      INTEGER NDDL,NNO,NNOS,NPG,NDIM,I,J,N,KPG
      INTEGER JGANO,IPOIDS,IVF,IDFDE,IGEOM,IMATE
      INTEGER ICONTP,IRET,ICACOQ,IMASS,IDEPL,IDEFO,INR
      REAL*8 DFF(2,8),VFF(8),B(6,8),P(3,6),JAC
      REAL*8 ALPHA,BETA,DIR11(3),DENSIT,PGL(3,3),DISTN,VECN(3)
      REAL*8 EPSM,EPSG(9),EPSTHE,SIG,SIGG(9),RHO,VALRES(2),EPOT
      REAL*8 R8VIDE,R8DGRD,R8PREM
      REAL*8 X(8),Y(8),Z(8),VOLUME,CDG(3),PPG,XXI,YYI,ZZI,MATINE(6),VRO
      LOGICAL LEXC

C - BOOLEEN POUR LES GRILLES EXCENTREES

      LEXC = (NOMTE(1:4).EQ.'MEGC')

C - FONCTIONS DE FORMES ET POINTS DE GAUSS

      FAMI = 'RIGI'
      CALL ELREF4(' ','RIGI',NDIM,NNO,NNOS,NPG,IPOIDS,IVF,IDFDE,JGANO)

C - PARAMETRES EN ENTREE

      CALL JEVECH('PGEOMER','L',IGEOM)
      CALL JEVECH('PCACOQU','L',ICACOQ)

      IF ((OPTION.EQ.'SIEF_ELGA').OR.
     &    (OPTION.EQ.'EPOT_ELEM')) THEN
        CALL JEVECH('PDEPLAR','L',IDEPL)
        CALL JEVECH('PMATERC','L',IMATE)

      ELSEIF (OPTION.EQ.'EPSI_ELGA') THEN
        CALL JEVECH('PDEPLAR','L',IDEPL)
        CALL R8INIR(9,0.D0,EPSG,1)

      ELSEIF (OPTION.EQ.'MASS_INER') THEN
        CALL JEVECH('PMATERC','L',IMATE)
      ENDIF

C - PARAMETRES EN SORTIE

      IF (OPTION.EQ.'SIEF_ELGA') THEN
        CALL JEVECH('PCONTRR','E',ICONTP)
        CALL R8INIR(9,0.D0,SIGG,1)

      ELSEIF (OPTION.EQ.'EPOT_ELEM') THEN
        CALL JEVECH('PENERDR','E',INR)
        EPOT = 0.D0

      ELSEIF (OPTION.EQ.'EPSI_ELGA') THEN
        CALL JEVECH('PDEFOPG','E',IDEFO)

      ELSEIF (OPTION.EQ.'MASS_INER') THEN
        CALL JEVECH('PMASSINE','E',IMASS)
        CALL RCVALB(FAMI,KPG,1,'+',ZI(IMATE),' ','ELAS',0,' ',0.D0,1,
     &                 'RHO',RHO,CODRES, 1)
        IF(RHO.LE.R8PREM()) THEN
          CALL U2MESS('F','ELEMENTS5_45')
        ENDIF
      ENDIF

C - LE VECTEUR NORME QUI INDIQUE LA DIRECTION D'ARMATURE

      DENSIT = ZR(ICACOQ)
      ALPHA = ZR(ICACOQ+1) * R8DGRD()
      BETA  = ZR(ICACOQ+2) * R8DGRD()
      DIR11(1) = COS(BETA)*COS(ALPHA)
      DIR11(2) = COS(BETA)*SIN(ALPHA)
      DIR11(3) = SIN(BETA)

C - SI EXCENTREE : RECUPERATION DE LA NORMALE ET DE L'EXCENTREMENT

      IF (LEXC) THEN

        IF (NOMTE.EQ.'MEGCTR3') THEN
          CALL DXTPGL(ZR(IGEOM),PGL)
        ELSEIF (NOMTE.EQ.'MEGCQU4') THEN
          CALL DXQPGL(ZR(IGEOM),PGL)
        ENDIF

        DISTN = ZR(ICACOQ+3)
        DO 8 I=1,3
          VECN(I)=DISTN*PGL(3,I)
8       CONTINUE
        NDDL=6

      ELSE
        DISTN = R8VIDE()
        NDDL  = 3
      ENDIF

C - COORDONNEES PHYSIQUES DES NOEUDS

      IF (OPTION.EQ.'MASS_INER') THEN
        DO 40 I = 1,NNO
          X(I) = ZR(IGEOM+3* (I-1))
          Y(I) = ZR(IGEOM+3*I-2)
          Z(I) = ZR(IGEOM+3*I-1)
40      CONTINUE
        IF (LEXC) THEN
          X(I) = X(I) + VECN(1)
          Y(I) = Y(I) + VECN(2)
          Z(I) = Z(I) + VECN(3)
        ENDIF
        CALL R8INIR(3,0.D0,CDG,1)
        CALL R8INIR(6,0.D0,MATINE,1)
      ENDIF

      VOLUME = 0.D0

C - DEBUT DE LA BOUCLE SUR LES POINTS DE GAUSS

      DO 800 KPG=1,NPG

C --- MISE SOUS FORME DE TABLEAU DES VALEURS DES FONCTIONS DE FORME
C     ET DES DERIVEES DE FONCTION DE FORME

        DO 11 N=1,NNO
          VFF(N)  =ZR(IVF+(KPG-1)*NNO+N-1)
          DFF(1,N)=ZR(IDFDE+(KPG-1)*NNO*2+(N-1)*2)
          DFF(2,N)=ZR(IDFDE+(KPG-1)*NNO*2+(N-1)*2+1)
11      CONTINUE

C --- CALCUL DE LA MATRICE "B" : DEPL NODAL --> EPS11 ET DU JACOBIEN

        CALL NMGRIB(NNO,ZR(IGEOM),DFF,DIR11,LEXC,VECN,B,JAC,P)

C --- SIEF_ELGA, EPOT_ELEM : ON CALCULE LA CONTRAINTE AU PG

        IF ((OPTION.EQ.'SIEF_ELGA').OR.
     &      (OPTION.EQ.'EPOT_ELEM')) THEN

C         CALCUL DE LA DEFORMATION AU PG
          EPSM=0.D0
          DO 210 I=1,NNO
            DO 210 J=1,NDDL
              EPSM=EPSM+B(J,I)*ZR(IDEPL+(I-1)*NDDL+J-1)
210       CONTINUE

C         CALCUL DE LA CONTRAINTE
          CALL VERIFT(FAMI,KPG,1,'+',ZI(IMATE),'ELAS',1,EPSTHE,IRET)
          NOMRES(1) = 'E'
          CALL RCVALB(FAMI,KPG,1,'+',ZI(IMATE),' ','ELAS',
     &                0,' ',0.D0,1,
     &                NOMRES,VALRES,CODRES, 0)
          EPSM=EPSM-EPSTHE
          SIG = VALRES(1)*EPSM

          IF (OPTION.EQ.'EPOT_ELEM') THEN
            EPOT = EPOT+(SIG*EPSM*ZR(IPOIDS+KPG-1)*JAC*DENSIT)/2
          ELSE
            SIGG(KPG) = SIG
          ENDIF

C --- EPSI_ELGA : ON CALCULE LA DEFORMATION AU PG

        ELSEIF (OPTION.EQ.'EPSI_ELGA') THEN

C         CALCUL DE LA DEFORMATION AU PG
          DO 30 I=1,NNO
            DO 30 J=1,NDDL
              EPSG(KPG)=EPSG(KPG)+B(J,I)*ZR(IDEPL+(I-1)*NDDL+J-1)
30        CONTINUE

C --- MASS_INER : ON SOMME LA CONTRIBUTION DU PG A LA MASSE TOTALE

        ELSEIF (OPTION.EQ.'MASS_INER') THEN
          VOLUME = VOLUME + ZR(IPOIDS+KPG-1)*DENSIT*JAC
          PPG = ZR(IPOIDS+KPG-1)*JAC*DENSIT
          DO 300 I=1,NNO
            CDG(1) = CDG(1) + PPG*VFF(I)*X(I)
            CDG(2) = CDG(2) + PPG*VFF(I)*Y(I)
            CDG(3) = CDG(3) + PPG*VFF(I)*Z(I)
            XXI = 0.D0
            YYI = 0.D0
            ZZI = 0.D0
            DO 310 J=1,NNO
              XXI = XXI + X(I)*VFF(I)*VFF(J)*X(J)
              YYI = YYI + Y(I)*VFF(I)*VFF(J)*Y(J)
              ZZI = ZZI + Z(I)*VFF(I)*VFF(J)*Z(J)
              MATINE(2) = MATINE(2) + X(I)*VFF(I)*VFF(J)*Y(J)*PPG
              MATINE(4) = MATINE(4) + X(I)*VFF(I)*VFF(J)*Z(J)*PPG
              MATINE(5) = MATINE(5) + Y(I)*VFF(I)*VFF(J)*Z(J)*PPG
310         CONTINUE
            MATINE(1) = MATINE(1) + PPG*(YYI+ZZI)
            MATINE(3) = MATINE(3) + PPG*(XXI+ZZI)
            MATINE(6) = MATINE(6) + PPG*(XXI+YYI)
300       CONTINUE
        ENDIF

C - FIN DE LA BOUCLE SUR LES POINTS DE GAUSS
800   CONTINUE

      IF (OPTION.EQ.'SIEF_ELGA') THEN
        DO 510 KPG=1,NPG
          ZR(ICONTP+KPG-1)=SIGG(KPG)
510     CONTINUE

      ELSEIF (OPTION.EQ.'EPOT_ELEM') THEN
        ZR(INR) = EPOT

      ELSEIF (OPTION.EQ.'EPSI_ELGA') THEN
        DO 500 KPG=1,NPG
          ZR(IDEFO+KPG-1)=EPSG(KPG)
500     CONTINUE

      ELSEIF (OPTION.EQ.'MASS_INER') THEN
        VRO = RHO / VOLUME
        ZR(IMASS)   = RHO * VOLUME
        ZR(IMASS+1) = CDG(1)/VOLUME
        ZR(IMASS+2) = CDG(2)/VOLUME
        ZR(IMASS+3) = CDG(3)/VOLUME
        ZR(IMASS+4) = MATINE(1)*RHO - VRO*(CDG(2)*CDG(2)+CDG(3)*CDG(3))
        ZR(IMASS+5) = MATINE(3)*RHO - VRO*(CDG(1)*CDG(1)+CDG(3)*CDG(3))
        ZR(IMASS+6) = MATINE(6)*RHO - VRO*(CDG(1)*CDG(1)+CDG(2)*CDG(2))
        ZR(IMASS+7) = MATINE(2)*RHO - VRO*(CDG(1)*CDG(2))
        ZR(IMASS+8) = MATINE(4)*RHO - VRO*(CDG(1)*CDG(3))
        ZR(IMASS+9) = MATINE(5)*RHO - VRO*(CDG(2)*CDG(3))
      ENDIF

      END
