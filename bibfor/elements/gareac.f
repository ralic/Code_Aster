      SUBROUTINE GAREAC(XDM,XDP,DGAMMA)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 11/06/2012   AUTEUR CHEIGNON E.CHEIGNON 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
      IMPLICIT NONE
      REAL*8 XDM(3),XDP(3),DGAMMA
C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ---------------------
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C --------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ---------------------

      REAL*8  XDMNOR(3),XDPNOR(3),DD(3)
      REAL*8  NORMM    ,NORMP
      REAL*8  YTR(3)   ,ZTR(3)
      REAL*8  PTR2GL(3,3),PSTRX,PSTRY,PTRBTR(3,3)
      REAL*8  ANGLM(3),PGLM(3,3),VTEMP(3),PSLTY,PSLTZ,PLO2TR(3,3)
      REAL*8  YTEMP(3),YLOCP(3)
      REAL*8  ANGLP(3),PGLP(3,3)
      REAL*8  R8MIEM,R8PREM
      REAL*8  PSCAL,NORM
      REAL*8  DDOT,TRIGOM

C ----------------------------------------------------------------------

C --- SI L'ORIENTATION N'A PAS CHANGE ==> DGAMMA = 0
      CALL DCOPY(3,XDM,1,XDMNOR,1)
      CALL NORMEV(XDMNOR,NORMM)
      CALL DCOPY(3,XDP,1,XDPNOR,1)
      CALL NORMEV(XDPNOR,NORMP)
      CALL VDIFF(3,XDPNOR,XDMNOR,DD)
      IF (ABS(DDOT(3,DD,1,DD,1)).LT.R8PREM()) THEN
        DGAMMA = 0.D0
        GOTO 9999
      ENDIF

C --- CONSTRUCTION DE LA MATRICE DE PASSAGE DU REPERE GLOBAL
C     VERS (XDMNOR,YTR,ZTR)
C     YTR EST NORMAL A XDMNOR DANS LE PLAN FORME PAR (XDMNOR,XDPNOR)
C     ZTR EST NORMAL AU PLAN FORME PAR (XDMNOR,XDPNOR)

      PSCAL = DDOT(3,XDPNOR,1,XDMNOR,1)
      CALL DCOPY(3,XDPNOR,1,YTR   ,1)
      CALL DAXPY(3,-PSCAL,XDMNOR,1,YTR,1)
      CALL NORMEV(YTR,NORM)
      IF (NORM.LE.R8MIEM()) THEN
        DGAMMA = 0.D0
        GOTO 9999
      ENDIF
      CALL PROVEC(XDMNOR,YTR,ZTR)

      PTR2GL(1,1) = XDMNOR(1)
      PTR2GL(2,1) = XDMNOR(2)
      PTR2GL(3,1) = XDMNOR(3)
      PTR2GL(1,2) = YTR(1)
      PTR2GL(2,2) = YTR(2)
      PTR2GL(3,2) = YTR(3)
      PTR2GL(1,3) = ZTR(1)
      PTR2GL(2,3) = ZTR(2)
      PTR2GL(3,3) = ZTR(3)

C --- CONSTRUCTION DE LA MATRICE DE PASSAGE DE (XDMNOR,YTR,ZTR)
C     VERS (XDPNOR,YDPNOR,ZTR)
C     IL S'AGIT DE LA ROTATION AUTOUR DE ZTR
C     YDPNOR EST NORMAL A XDPNOR DANS LE PLAN NORMAL A ZTR

      PSTRX = DDOT(3,XDMNOR,1,XDPNOR,1)
      PSTRY = DDOT(3,YTR   ,1,XDPNOR,1)

      PTRBTR(1,1) =  PSTRX
      PTRBTR(1,2) = -PSTRY
      PTRBTR(1,3) =  0.D0
      PTRBTR(2,1) =  PSTRY
      PTRBTR(2,2) =  PSTRX
      PTRBTR(2,3) =  0.D0
      PTRBTR(3,1) =  0.D0
      PTRBTR(3,2) =  0.D0
      PTRBTR(3,3) =  1.D0

C --- CONSTRUCTION DE LA MATRICE DE PASSAGE DE (XDMNOR,YTR,ZTR)
C     VERS (XDMNOR,YLOCM,ZLOCM)
C     IL S'AGIT DE LA ROTATION AUTOUR DE XDMNOR
C     (YLOCM,ZLOCM) CONSTITUE LE REPERE LOCAL A T- POUR GAMMA=0

      CALL ANGVX(XDM,ANGLM(1),ANGLM(2))
      ANGLM(3) = 0.D0
      CALL MATROT(ANGLM,PGLM)
      VTEMP(1) = PGLM(2,1)
      VTEMP(2) = PGLM(2,2)
      VTEMP(3) = PGLM(2,3)
      PSLTY    = DDOT(3,YTR,1,VTEMP,1)
      VTEMP(1) = PGLM(3,1)
      VTEMP(2) = PGLM(3,2)
      VTEMP(3) = PGLM(3,3)
      PSLTZ    = DDOT(3,YTR,1,VTEMP,1)

      PLO2TR(1,1) =  1.D0
      PLO2TR(1,2) =  0.D0
      PLO2TR(1,3) =  0.D0
      PLO2TR(2,1) =  0.D0
      PLO2TR(2,2) =  PSLTY
      PLO2TR(2,3) =  PSLTZ
      PLO2TR(3,1) =  0.D0
      PLO2TR(3,2) = -PSLTZ
      PLO2TR(3,3) =  PSLTY

C --- CALCUL DE YLOC A T+

C     PTR2GL * PTRBTR * PLO2TR *  (0 1 0)T
      VTEMP(1) = PLO2TR(1,2)
      VTEMP(2) = PLO2TR(2,2)
      VTEMP(3) = PLO2TR(3,2)
      CALL PROMAT(PTRBTR,3,3,3,VTEMP,3,3,1,YTEMP)
      CALL PROMAT(PTR2GL,3,3,3,YTEMP,3,3,1,YLOCP)

C --- CALCUL DE LA BASE LOCALE A T+ EN CONSIDERANT GAMMA NUL
      CALL ANGVX(XDP,ANGLP(1),ANGLP(2))
      ANGLP(3) = 0.D0
      CALL MATROT(ANGLP,PGLP)
C     LES LIGNES DE PGLP SONT LES 3 VECTEURS DE LA BASE LOCAL

      VTEMP(1) = PGLP(2,1)
      VTEMP(2) = PGLP(2,2)
      VTEMP(3) = PGLP(2,3)
      PSCAL = DDOT(3,YLOCP,1,VTEMP,1)
      IF (PSCAL.GT.1.D0) THEN
        PSCAL = 1.0D0
      ELSEIF (PSCAL.LT.-1.D0) THEN
        PSCAL = -1.0D0
      ENDIF
      DGAMMA = TRIGOM('ACOS',PSCAL)
      VTEMP(1) = PGLP(3,1)
      VTEMP(2) = PGLP(3,2)
      VTEMP(3) = PGLP(3,3)
      IF (DDOT(3,YLOCP,1,VTEMP,1).LT.0.D0) THEN
         DGAMMA = -DGAMMA
      ENDIF

 9999 CONTINUE

      END
