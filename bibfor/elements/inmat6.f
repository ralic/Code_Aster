      SUBROUTINE INMAT6(ELREFA,FAPG,MGANOS)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 26/04/2011   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.

C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.

C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE VABHHTS J.PELLET
C----------------------------------------------------------------------
C BUT : CALCUL DE LA MATRICE MGANOS : GAUSS -> SOMMETS
C----------------------------------------------------------------------
      IMPLICIT NONE

      INTEGER    NBPGMX,    NBNOMX,    NBFAMX
      PARAMETER (NBPGMX=1000, NBNOMX=27, NBFAMX=20)
      INTEGER      NDIM,NNO,NNOS,NBFPG,NBPG(NBFAMX)
      INTEGER      I,KP,KDIM,LN,J,LM,NPG,IRET
      REAL*8       XNO(3*NBNOMX), VOL, FF(NBNOMX), M(NBPGMX*NBNOMX)
      REAL*8       P(NBPGMX*NBNOMX), MGANOS(NBPGMX,NBNOMX)
      REAL*8       XPG(3*NBPGMX), POIPG(NBPGMX), XG(3), DET
      CHARACTER*8  NOFPG(NBFAMX), ELREFA, ELREF2, FAPG

C     NBPGMX, NBNOMX, NBFAMX SE REFERER A ELRACA

C DEB ------------------------------------------------------------------

      CALL ELRACA(ELREFA,NDIM,NNO,NNOS,NBFPG,NOFPG,NBPG,XNO,VOL)
      CALL ELRAGA(ELREFA,FAPG,NDIM,NPG,XPG,POIPG)

      CALL ASSERT(NNO.LE.NBNOMX)
      CALL ASSERT(NPG.LE.NBPGMX)

C     CAS DU SHB8 ET DU SHB6 NON INVERSIBLE
      IF (FAPG.EQ.'SHB5'.OR.FAPG.EQ.'SHB6') THEN
        CALL R8INIR(NBNOMX*NBNOMX,0.D0,MGANOS,1)
        DO 10 I = 1,NNOS/2
          MGANOS(1,I) = 1.D0
   10   CONTINUE
        DO 20 I = NNOS/2+1,NNOS
          MGANOS(5,I) = 1.D0
   20   CONTINUE
        ELREF2 = ELREFA
        GO TO 100
      END IF

C     CAS DU QU4/FIS2 NON INVERSIBLE
      IF (ELREFA.EQ.'QU4'.AND. FAPG.EQ.'FIS2') THEN
        CALL R8INIR(NBNOMX*NBNOMX,0.D0,MGANOS,1)
        MGANOS(1,1) = 1.D0
        MGANOS(1,4) = 1.D0
        MGANOS(2,2) = 1.D0
        MGANOS(2,3) = 1.D0
        GO TO 100
      END IF


      IF ((ELREFA.EQ.'H20') .OR. (ELREFA.EQ.'H27')) THEN
        ELREF2 = 'HE8'
      ELSE IF ((ELREFA.EQ.'P15').OR.(ELREFA.EQ.'P18')) THEN
        ELREF2 = 'PE6'
      ELSE IF (ELREFA.EQ.'P13') THEN
        ELREF2 = 'PY5'
      ELSE IF (ELREFA.EQ.'T10') THEN
        ELREF2 = 'TE4'
      ELSE IF ((ELREFA.EQ.'TR6') .OR. (ELREFA.EQ.'TR7')) THEN
        ELREF2 = 'TR3'
      ELSE IF ((ELREFA.EQ.'QU8') .OR. (ELREFA.EQ.'QU9')) THEN
        ELREF2 = 'QU4'
      ELSE IF ((ELREFA.EQ.'SE3') .OR. (ELREFA.EQ.'SE4')) THEN
        ELREF2 = 'SE2'
      ELSE
        ELREF2 = ELREFA
      END IF


C     CALCUL DES MATRICES M ET P :
C     ----------------------------
      DO 30 I = 1,NNOS*NNOS
        M(I) = 0.D0
   30 CONTINUE

      DO 70 KP = 1,NPG
        DO 40,KDIM = 1,NDIM
          XG(KDIM) = XPG(NDIM* (KP-1)+KDIM)
   40   CONTINUE
        CALL ELRFVF(ELREF2,XG,NBNOMX,FF,NNO)
        LN = (KP-1)*NNOS
        DO 60 I = 1,NNOS
          P(LN+I) = FF(I)
          DO 50 J = 1,NNOS
            LM = NNOS* (I-1) + J
            M(LM) = M(LM) + FF(I)*FF(J)
   50     CONTINUE
   60   CONTINUE
   70 CONTINUE

C     CALCUL DE LA MATRICE M-1*P :
C     ----------------------------
      CALL MGAUSS('NFVP',M,P,NNOS,NNOS,NPG,DET,IRET)

      DO 90 I = 1,NNOS
        DO 80 KP = 1,NPG
          MGANOS(KP,I) = P((KP-1)*NNOS+I)
   80   CONTINUE
   90 CONTINUE

  100 CONTINUE

      END
