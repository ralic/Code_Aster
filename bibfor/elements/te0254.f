      SUBROUTINE TE0254(OPTION,NOMTE)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 16/01/2012   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C.......................................................................
      IMPLICIT REAL*8 (A-H,O-Z)
C
C     BUT: CALCUL DES MATRICES DE MASSE ELEMENTAIRES EN MECANIQUE
C          ELEMENTS  DE FLUIDE ISOPARAMETRIQUES 2D
C
C          OPTION : 'MASS_MECA '
C
C     ENTREES  ---> OPTION : OPTION DE CALCUL
C          ---> NOMTE  : NOM DU TYPE ELEMENT
C.......................................................................
C
      PARAMETER         ( NBRES=2 )
      CHARACTER*8        NOMRES(NBRES),FAMI,POUM
      INTEGER            ICODRE(NBRES),KPG,SPT
      CHARACTER*16       NOMTE,OPTION
      REAL*8             VALRES(NBRES),A(2,2,9,9)
      REAL*8             DFDX(9),DFDY(9),POIDS,RHO,CELER
      INTEGER            IPOIDS,IVF,IDFDE,IGEOM,IMATE
      INTEGER            NNO,NDIM,KP,NPG2,NNOS,IK,IJKL,I,J,K,L
      INTEGER            IMATUU,JGANO
      LOGICAL            LTEATT
C
C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ---------------------
      INTEGER            ZI
      COMMON  / IVARJE / ZI(1)
      REAL*8             ZR
      COMMON  / RVARJE / ZR(1)
      COMPLEX*16         ZC
      COMMON  / CVARJE / ZC(1)
      LOGICAL            ZL
      COMMON  / LVARJE / ZL(1)
      CHARACTER*8        ZK8
      CHARACTER*16                ZK16
      CHARACTER*24                          ZK24
      CHARACTER*32                                    ZK32
      CHARACTER*80                                              ZK80
      COMMON  / KVARJE / ZK8(1) , ZK16(1) , ZK24(1) , ZK32(1) , ZK80(1)
C --------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ---------------------
C
      CALL ELREF4(' ','RIGI',NDIM,NNO,NNOS,NPG2,IPOIDS,IVF,IDFDE,JGANO)

C
      CALL JEVECH('PGEOMER','L',IGEOM)
      CALL JEVECH('PMATERC','L',IMATE)
      CALL JEVECH('PMATUUR','E',IMATUU)
C
      NOMRES(1)='RHO'
      NOMRES(2)='CELE_R'
      FAMI='FPG1'
      KPG=1
      SPT=1
      POUM='+'
      CALL RCVALB(FAMI,KPG,SPT,POUM, ZI(IMATE),' ','FLUIDE',0,' ',
     &            R8BID,2,NOMRES,VALRES,ICODRE, 1)
      RHO    = VALRES(1)
      CELER = VALRES(2)
C
      DO 113 K=1,2
         DO 113 L=1,2
            DO 113 I=1,NNO
            DO 113 J=1,I
               A(K,L,I,J) = 0.D0
113   CONTINUE
C
C    BOUCLE SUR LES POINTS DE GAUSS
C
      DO 101 KP=1,NPG2
        K=(KP-1)*NNO
        CALL DFDM2D(NNO,KP,IPOIDS,IDFDE,ZR(IGEOM),DFDX,DFDY,POIDS)
C%
        IF ( LTEATT(' ','AXIS','OUI') ) THEN
           R = 0.D0
           DO 102 I=1,NNO
             R = R + ZR(IGEOM+2*(I-1))*ZR(IVF+K+I-1)
102        CONTINUE
           POIDS = POIDS*R
        ENDIF
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C    TERME EN -RHO*(GRAD(PHI)**2)          C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
           DO 106 I=1,NNO
             DO 107 J=1,I
               A(2,2,I,J) = A(2,2,I,J) -
     &         POIDS * (DFDX(I)*DFDX(J) + DFDY(I)*DFDY(J))*RHO
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C    TERME EN   (P*PHI)/(CEL**2)       C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
               A(1,2,I,J) = A(1,2,I,J) +
     &         POIDS * ZR(IVF+K+I-1) * ZR(IVF+K+J-1)/ CELER / CELER

107          CONTINUE
C
106        CONTINUE
C
101   CONTINUE
C
      DO 108 I=1,NNO
        DO 109 J=1,I
          A(2,1,I,J) = A(1,2,I,J)
109     CONTINUE
108   CONTINUE
C
C PASSAGE DU STOCKAGE RECTANGULAIRE (A) AU STOCKAGE TRIANGULAIRE (ZR)
C
      DO 112 K=1,2
         DO 112 L=1,2
            DO 112 I=1,NNO
                IK = ((2*I+K-3) * (2*I+K-2)) / 2
            DO 112 J=1,I
                IJKL = IK + 2 * (J-1) + L
                ZR(IMATUU+IJKL-1) = A(K,L,I,J)
112          CONTINUE
C
      END
