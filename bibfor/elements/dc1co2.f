      SUBROUTINE DC1CO2(MOM,DEPS,PLAMOM,DC,DTG,ZEROG,DC12)
      IMPLICIT NONE
      COMMON /TDIM/ N,ND
      REAL*8 DC12(3),MOM(3),DEPS(6),PLAMOM(3),DC(3,3)
      REAL*8 LAMBDA,U(3),HDCU(3),A0,A1,A2,DF(3),R1,R2
      REAL*8 DDEPS(3),DCU(3),SQRTDE,DELTA,ZEROG,DTG(6,6)
      REAL*8 MOMNEW(3),H(3,3),S,TMP(3),FPLAS,GPLAS,MATMP(3,6)
      INTEGER N, ND
C-----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 18/11/2003   AUTEUR LEBOUVIE F.LEBOUVIER 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C-----------------------------------------------------------------------
C     BUT    :  CALCULS DE LAMBDA ET DE L'INCREMENT DE LA COURBURE 
C               PLASTIQUE QUAND LE CRITERE DE PLASTICITE VAUT 1 
C               PAR UNE METHODE EXPLICITE DE SECOND ORDRE  
C
C IN    R  MOM    : MOMENT - MOMENT DE RAPPEL
C       R  DC     : CONSTANTE DE PRAGER
C       R  DEPS   : INCREMENT DE DEFORMATION (MEMBRANE, COURBURE)
C       R  DTG    : MATRICE TANGENTE
C       R  PLAMOM : MOMENT LIMITE ELASTIQUE
C        
C OUT   R  DC12   : INCREMENT DE COURBURE PLASTIQUE
C-----------------------------------------------------------------------
C 
      CALL INITMA(3,3,H)
C
      H(2,1)=-1.D0
      H(1,2)=-1.D0
      H(3,3)=2.D0
C
C     LA DIRECTION DE LA COURBURE PLASTIQUE EST FIXEE
C     -----------------------------------------------
      IF (PLAMOM(1).GT.0.D0)THEN
         U(1) = 1.D0
         U(2) = 1.D0
         U(3) = 0.D0
      ELSE
         U(1) = -1.D0
         U(2) = -1.D0
         U(3) = 0.D0
      ENDIF
      CALL DFPLAS(MOM,PLAMOM,DF)
C
      CALL EXTMAT(DTG,6,3,6,'IG',MATMP)
      CALL PRMRVE(MATMP,3,6,DEPS,DDEPS)
      N=3
      CALL PMAVEC('ZERO',3,DC,U,DCU)
      CALL PMAVEC('ZERO',3,H,DCU,HDCU)
C
C     COEFFICIENTS DE L'EQUATION DU 2D DEGRE
C     --------------------------------------
      N = 3
      CALL LCSOVE(MOM,DDEPS,TMP)
      A0=FPLAS(TMP,PLAMOM)
      CALL LCPRSC(DF,DCU,R1)
      CALL LCPRSC(DDEPS,HDCU,R2)
      A1=-R1-R2
      CALL LCPRSC(DCU,HDCU,S)
      A2=0.5D0*S
C
C     RESOLUTION DE L'EQUATION
C     ------------------------
C     DISCRIMINANT
      DELTA=A1**2-4.D0*A0*A2
      IF (DELTA.LT.0.D0)THEN
C        PAS DE RACINES REELLES: DC12=0
         CALL INITVE(3,DC12)
         GOTO 9999
      ENDIF
      SQRTDE=SQRT(DELTA)
      IF(A0.GT.0)THEN
C        SEULEMENT UNE RACINE POSITIVE
C        ----------------------------
         LAMBDA=(-A1-SQRTDE)/(2.D0*A2)
      ELSEIF(A1.GT.0.D0)THEN
C        DEUX RACINES POSITIVES
C        ----------------------
         LAMBDA=(-A1+SQRTDE)/(2.D0*A2)
C        PRODUIT SCALAIRE VECTEUR : DC12
         CALL LCPRSV(LAMBDA,U,DC12)
C        PRODUIT MATRICE VECTEUR : TMP
         N=3
         CALL PMAVEC('ZERO',3,DC,DC12,TMP)
C
         CALL LCSOVE(MOM,DDEPS,MOMNEW)
C
         CALL LCDIVE(MOMNEW,TMP,MOMNEW)
C        SI LA PREMIERE RACINE NE CONVIENT PAS, ON ESSAIE L'AUTRE
         IF(GPLAS(MOMNEW,PLAMOM).GT.ZEROG)THEN
            LAMBDA=(-A1-SQRTDE)/(2.D0*A2)
         ENDIF
      ELSE
C        DEUX RACINES NEGATIVES
C        ----------------------
         LAMBDA=0.D0
      ENDIF
C     
      CALL LCPRSV(LAMBDA,U,DC12)
C
9999  CONTINUE

      END
