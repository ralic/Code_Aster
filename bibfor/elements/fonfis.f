      SUBROUTINE FONFIS(NOMA,NBNOFF,FONOEU,FONDFI)

      IMPLICIT NONE
      INCLUDE 'jeveux.h'

      CHARACTER*32 JEXNOM
      INTEGER        NBNOFF
      CHARACTER*8    NOMA
      CHARACTER*24   FONDFI,FONOEU

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 09/11/2012   AUTEUR DELMAS J.DELMAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C
C     ----------------------------------------------------------------
C FONCTION REALISEE:
C
C     POUR CHAQUE NOEUD DU FOND DE FISSURE ON RECUPERE SES
C     COORDONNEES ET ON CALCULE SON ABSCISSE CURVILIGNE
C
C     ------------------------------------------------------------------
C ENTREE:
C        NOMA   : NOM DU MAILLAGE
C        NBNOFF : NOMBRE DE NOEUDS AU FOND DE FISSURE
C        FONOEU : NOMS DES NOEUDS DU FOND DE FISSURE
C
C SORTIE:
C        FONDFI : VECTEUR .FONDFISS CONTENANT LES COORDONNEES ET LES
C                 ABSCISSES CURVILIGNES DES NOEUDS DU FOND
C     ------------------------------------------------------------------
C
C
      INTEGER        I,IFON,JCOOR,JNOE,NI,NJ
      REAL*8         ABSCI,COORI(3),COORJ(3),NORM,XIJ,YIJ,ZIJ
C
C
C
      CALL JEMARQ()

C     ADRESSES DES COORDONNEES DES NOEUDS DU MAILLAGE
      CALL JEVEUO( NOMA//'.COORDO    .VALE','L', JCOOR )

C     ALLOCATION DU VECTEUR DES COORDONNEES ET DES ABSCISSES CURVILIGNES
C     DES NOEUDS DU FOND
      CALL WKVECT (FONDFI,'G V R',4*NBNOFF,IFON)

C     RECUPERATION DES NOMS DES NOEUDS DU FOND DE FISSURE
      CALL JEVEUO ( FONOEU, 'L', JNOE )

C     RECUPERATION DES COORDONNNES DE NI
      CALL JENONU (JEXNOM(NOMA//'.NOMNOE',ZK8(JNOE)),NI)
      COORI(1) = ZR(JCOOR-1 + (NI-1)*3 + 1)
      COORI(2) = ZR(JCOOR-1 + (NI-1)*3 + 2)
      COORI(3) = ZR(JCOOR-1 + (NI-1)*3 + 3)

C     REMPLISSAGE DE .FONDFISS DANS LA SD_FOND_FISS :
C     DONNEES DU CAS 2D OU DU PREMIER NOEUD POUR LE CAS 3D
      ZR(IFON-1 + 4*(1-1) + 1) = COORI(1)
      ZR(IFON-1 + 4*(1-1) + 2) = COORI(2)
      ZR(IFON-1 + 4*(1-1) + 3) = COORI(3)
      ZR(IFON-1 + 4*(1-1) + 4) = 0.D0

C     REMPLISSAGE DE .FONDFISS DANS LA SD_FOND_FISS: CAS 3D
      IF (NBNOFF.NE.1)THEN
        DO 10 I=2,NBNOFF

C         NUMEROS (ABSOLUS) DES NOEUDS DU FOND: NI ET NJ
          CALL JENONU (JEXNOM(NOMA//'.NOMNOE',ZK8(JNOE-1+I-1)),NI)
          CALL JENONU (JEXNOM(NOMA//'.NOMNOE',ZK8(JNOE-1+I)),NJ)

C         COORDONNEES DES NOEUDS I ET J
          COORI(1) = ZR(JCOOR-1 + (NI-1)*3 + 1)
          COORI(2) = ZR(JCOOR-1 + (NI-1)*3 + 2)
          COORI(3) = ZR(JCOOR-1 + (NI-1)*3 + 3)

          COORJ(1) = ZR(JCOOR-1 + (NJ-1)*3 + 1)
          COORJ(2) = ZR(JCOOR-1 + (NJ-1)*3 + 2)
          COORJ(3) = ZR(JCOOR-1 + (NJ-1)*3 + 3)

C         CALCUL DES ABSCISSES CURVILIGNES
          ZR(IFON-1 + 4*(I-1) + 1) = COORJ(1)
          ZR(IFON-1 + 4*(I-1) + 2) = COORJ(2)
          ZR(IFON-1 + 4*(I-1) + 3) = COORJ(3)

          XIJ = COORJ(1) - COORI(1)
          YIJ = COORJ(2) - COORI(2)
          ZIJ = COORJ(3) - COORI(3)
          NORM = SQRT(XIJ*XIJ + YIJ*YIJ + ZIJ*ZIJ)
          ABSCI = ZR(IFON-1 + 4*(I-2) + 4)

          ZR(IFON-1 + 4*(I-1) + 4) = ABSCI + NORM
 10     CONTINUE
      ENDIF
C
      CALL JEDEMA()
      END
