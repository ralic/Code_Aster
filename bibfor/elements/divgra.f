         SUBROUTINE DIVGRA(E1,E2,DFDE,DFDK,VIBARN,DIVSIG)
         IMPLICIT NONE
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 06/04/2004   AUTEUR DURAND C.DURAND 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C...............................................................
C
C BUT: CALCUL DE LA DIVERGENCE SURFACIQUE DU GRADIENT DE PHIBARRE
C
C     ENTREES  ---> DERIVEES DE FONCTIONS DE FORME
C...............................................................
C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ---------------------
      INTEGER            ZI
      COMMON  / IVARJE / ZI(1)
      REAL*8             ZR
      COMMON  / RVARJE / ZR(1)
      COMPLEX*16         ZC
      COMMON  / CVARJE / ZC(1)
      LOGICAL            ZL
      COMMON  / LVARJE / ZL(1)
      CHARACTER*8        ZK8
      CHARACTER*16                ZK16
      CHARACTER*24                          ZK24
      CHARACTER*32                                    ZK32
      CHARACTER*80                                              ZK80
      COMMON  / KVARJE / ZK8(1) , ZK16(1) , ZK24(1) , ZK32(1) , ZK80(1)
C --------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ---------------------
C
      REAL*8        DFDE(9,9),DFDK(9,9),VIBARN(2,9)
      REAL*8        DIVSIG(9)
      REAL*8        COVA(3,3),METR(2,2),A(2,2),JC,CNVA(3,3)
      REAL*8        DVIBAR(2,9),E1(3,9),E2(3,9)
      INTEGER       I,J,ITEMP,IPG,KDEC,IDEC,IDIR,JDIR
      INTEGER       NDIM,NNO,NNOS,NPG1,IPOIDS,IVF,IDFDX,IDFDY,JGANO
C-----------------------------------------------------------------------
C
      CALL ELREF4(' ','RIGI',NDIM,NNO,NNOS,NPG1,IPOIDS,IVF,IDFDX,JGANO)
      IDFDY  = IDFDX  + 1
C
      CALL JEVECH('PTEMPER','L',ITEMP)
C
C --- CALCUL DE VITESSE PERMANENTE FLUIDE AUX NOEUDS
C
      DO 10 I=1,NNO
         VIBARN(1,I)=0.0D0
         VIBARN(2,I)=0.0D0
         DO 20 J=1,NNO
            VIBARN(1,I)=VIBARN(1,I)+(ZR(ITEMP+J-1)*DFDE(J,I))
            VIBARN(2,I)=VIBARN(2,I)+(ZR(ITEMP+J-1)*DFDK(J,I))
20       CONTINUE
10    CONTINUE

C --- CALCUL DE LA DERIVEE DE LA COMPOSANTE DE VITESSE COVARIANTE
C     AU POINT DE GAUSS
      DO 30 IPG=1,NPG1
         KDEC=(IPG-1)*NNO*NDIM
         DVIBAR(1,IPG)=0.0D0
         DVIBAR(2,IPG)=0.0D0
         DO 31 I=1,NNO
            IDEC=(I-1)*NDIM

         DVIBAR(1,IPG)=DVIBAR(1,IPG)+VIBARN(1,I)*ZR(IDFDX+KDEC+IDEC)
         DVIBAR(2,IPG)=DVIBAR(2,IPG)+VIBARN(2,I)*ZR(IDFDY+KDEC+IDEC)

31       CONTINUE
30    CONTINUE

C --- CALCUL  DE LA DIVERGENCE SURFACIQUE
C     DU GRADIENT DU POTENTIEL PERMANENT EVALUE AU NOEUD

      DO 40 IPG=1,NPG1
         DIVSIG(IPG)=0.D0
         DO 41 I=1,3
            COVA(I,1)=E1(I,IPG)
            COVA(I,2)=E2(I,IPG)
41       CONTINUE
C
C        KDEC=(IPG-1)*NNO*NDIM

C ------ ON CALCULE LE TENSEUR METRIQUE AU POINT DE GAUSS

         CALL SUMETR(COVA,METR,JC)

C ------ CALCUL DE LA BASE CONTRAVARIANTE AU POINT DE GAUSS

         CALL SUBACV(COVA,METR,JC,CNVA,A)

         DO 51 IDIR=1,2
            DO 61 JDIR=1,2
               DIVSIG(IPG)= DIVSIG(IPG)+A(IDIR,JDIR)*DVIBAR(IDIR,IPG)
61          CONTINUE
51       CONTINUE
40    CONTINUE
C
      END
