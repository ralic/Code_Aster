      SUBROUTINE FONNO2 (MACOFO,NOMA,NBMAC,NBNOFF,NBNOSE,NBMAX,NOEU,
     &                   TABLEV)
      IMPLICIT NONE
      CHARACTER*8         NOMA, NOEU
      CHARACTER*19        MACOFO
      INTEGER             NBMAC,NBNOFF,NBNOSE,NBMAX,TABLEV(2)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 06/09/2011   AUTEUR GENIAUT S.GENIAUT 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C       ----------------------------------------------------------------
C      PARMI LES MAILLES CONNECTEES AU SEGMENT DU FOND, FILTRAGE DES
C          MAILLES CONNECTEES A 1 LEVRE (CAD AYANT UNE FACE LIBRE) 
C          -> REMPLISSAGE DE TABLEV
C       ----------------------------------------------------------------
C    ENTREES
C       MACOFO : VECTEUR DES MAILLES (PRINCIPALES) CONNECTEES AU SEGMENT
C                DU FOND DE FISSURE COURANT
C       NOMA   : NOM DU MAILLAGE
C       NBMAC  : NOMBRE DE MAILLES CONNECTEES AU SEGMENT DU FOND ET DE
C                DE DIMENSION NDIM
C       NBNOFF : NOMBRE DE NOEUD EN FOND DE FISSURE
C       NBNOSE : NOMBRE DE NOEUD PAR SEGMENT
C       NBMAX  : NOMBRE DE NOEUDS MAX COMMUNS A DEUX MAILLES CONNEXES
C       NOEU   : NOM DU NOEUD SOMMET COURANT
C    SORTIE
C       TABLEV : VECTEUR CONTNANT LES NUMEROS DES DEUX MAILLES
C                CONNECTEES AU NOEUD SOMMET COURANT ET AUX LEVRES
        
C     ----- DEBUT COMMUNS NORMALISES  JEVEUX  --------------------------
      INTEGER          ZI
      COMMON  /IVARJE/ ZI(1)
      REAL*8           ZR
      COMMON  /RVARJE/ ZR(1)
      COMPLEX*16       ZC
      COMMON  /CVARJE/ ZC(1)
      LOGICAL          ZL
      COMMON  /LVARJE/ ZL(1)
      CHARACTER*8      ZK8
      CHARACTER*16             ZK16
      CHARACTER*24                      ZK24
      CHARACTER*32                               ZK32
      CHARACTER*80                                        ZK80
      COMMON  /KVARJE/ ZK8(1), ZK16(1), ZK24(1), ZK32(1), ZK80(1)
      CHARACTER*32     JEXNUM
C     ----- FIN COMMUNS NORMALISES  JEVEUX  ----------------------------
C
      INTEGER       JMACO,IATYMA,JNO1,JNO2,TYP11,TYP22
      INTEGER       INP,INQ,INR,INS,NBNO1,NBNO2,IRET
      INTEGER       COMP2,COMP3,COMP4
      CHARACTER*8   K8B,TYP1,TYP2
      CHARACTER*9   VALK(1)

C     -----------------------------------------------------------------
C           
      CALL JEMARQ() 
      
C
C     RECUPERATION DE L'ADRESSE DES TYPFON DE MAILLES
      CALL JEVEUO ( NOMA//'.TYPMAIL','L',IATYMA)
C
C     RECUPERATION DU VECTEUR DES MAILLES CONNECTEES AU SEGMENT DU FOND 
      CALL JEVEUO(MACOFO,'L',JMACO)
      
      COMP4=0
      DO 120 INP=1,NBMAC
        COMP3=0
        CALL JEVEUO(JEXNUM( NOMA//'.CONNEX',ZI(JMACO-1 + INP)),
     &     'L',JNO1)
C       NOMBRE DE NOEUDS LA MAILLE
        TYP11= IATYMA-1+ZI(JMACO-1 + INP)      
        CALL JENUNO(JEXNUM('&CATA.TM.NOMTM',ZI(TYP11)),TYP1)
        CALL DISMOI('F','NBNO_TYPMAIL',TYP1,'TYPE_MAILLE',
     &  NBNO1,K8B,IRET)
C       POUR CHAQUE MAILLE VOISINE (NOEUD FOND COMMUN ET MEME
C       DIMENSION TOPO
        DO 121 INQ=1,NBMAC
          COMP2=0
          CALL JEVEUO(JEXNUM( NOMA//'.CONNEX',ZI(JMACO-1 + INQ)),
     &         'L',JNO2)  
          TYP22= IATYMA-1+ZI(JMACO-1 + INQ)
          CALL JENUNO(JEXNUM('&CATA.TM.NOMTM',ZI(TYP22)),TYP2)
          CALL DISMOI('F','NBNO_TYPMAIL',TYP2,'TYPE_MAILLE',
     &  NBNO2,K8B,IRET)
C         ON COMPTE LE NOMBRE DE NOEUDS COMMUNS AFIN D'ISOLER 
C         LES MAILLES DE BORD
          DO 122 INR=1,NBNO1
            DO 123 INS=1,NBNO2
              IF (ZI(JNO1-1+INR).EQ.ZI(JNO2-1+INS)) THEN
                COMP2=COMP2+1
              ENDIF
 123        CONTINUE      
 122      CONTINUE
C         SI LES DEUX MAILLES ONT DES NOEUDS EN COMMUN EN DEHORS DU 
C         FOND MAIS PAS TOUS
          IF ( ((NBNOFF.EQ.1).AND.(COMP2.NE.NBNO1)
     &       .AND.(COMP2.GE.NBNOSE)) .OR.
     &         ((NBNOFF.GT.1).AND.(COMP2.NE.NBNO1)
     &       .AND.(COMP2.GE.NBMAX))  .OR.
     &         (NBMAC.EQ.1)  ) THEN  
            COMP3=COMP3+1
          ENDIF
 121    CONTINUE   
C       ON GARDE LES MAILLES CONNECTEES QU'A 1 SEULE AUTRE MAILLE
        IF (COMP3.EQ.1) THEN 
          COMP4=COMP4+1
          CALL ASSERT(COMP4.LE.2)
          TABLEV(COMP4)=ZI(JMACO-1 + INP)
        ENDIF   
 120  CONTINUE

C     SI AUCUNE MAILLE DE CE TYPE N'EST TROUVE
      IF (COMP4.EQ.0) THEN
        VALK(1) = NOEU
        CALL U2MESK('F','RUPTURE0_31',1,VALK(1))
      ENDIF
      CALL JEDEMA()
      END
