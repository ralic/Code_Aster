      SUBROUTINE DKTB ( CARAT3 , IGAU, JACGAU, BMAT )
      IMPLICIT  NONE
      INTEGER   IGAU
      REAL*8    BMAT(8,1), CARAT3(*), JACGAU
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 14/10/2005   AUTEUR CIBHHLV L.VIVAN 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C     ------------------------------------------------------------------
C --- CALCUL DE LA MATRICE (B) RELIANT LES DEFORMATIONS DU PREMIER 
C --- ORDRE AUX DEPLACEMENTS AU POINT D'INTEGRATION D'INDICE IGAU  
C --- POUR UN ELEMENT DE TYPE DKT 
C --- (I.E. (EPS_1) = (B)*(UN))
C --- D'AUTRE_PART, ON CALCULE LE PRODUIT NOTE JACGAU = JACOBIEN*POIDS
C     ------------------------------------------------------------------
C     IN  IGAU          : INDICE DU POINT D'INTEGRATION
C     OUT JACGAU        : PRODUIT JACOBIEN*POIDS AU POINT D'INTEGRATION
C                         COURANT
C     OUT BMAT(8,1)     : MATRICE (B) AU POINT D'INTEGRATION COURANT
C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ---------------------
      CHARACTER*32       JEXNUM , JEXNOM , JEXR8 , JEXATR
      INTEGER            ZI
      COMMON  / IVARJE / ZI(1)
      REAL*8             ZR
      COMMON  / RVARJE / ZR(1)
      COMPLEX*16         ZC
      COMMON  / CVARJE / ZC(1)
      LOGICAL            ZL
      COMMON  / LVARJE / ZL(1)
      CHARACTER*8        ZK8
      CHARACTER*16                ZK16
      CHARACTER*24                          ZK24
      CHARACTER*32                                    ZK32
      CHARACTER*80                                              ZK80
      COMMON  / KVARJE / ZK8(1) , ZK16(1) , ZK24(1) , ZK32(1) , ZK80(1)
C --------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ---------------------
      INTEGER  NDIM,NNO,NNOS,NPG,IPOIDS,ICOOPG,IVF,IDFDX,IDFD2,JGANO
      INTEGER  I, J
      REAL*8   QSI, ETA, BM(3,6),  BF(3,9), BC(2,9)
C     ------------------------------------------------------------------
C
      CALL ELREF5(' ','RIGI',NDIM,NNO,NNOS,NPG,IPOIDS,ICOOPG,
     +                                         IVF,IDFDX,IDFD2,JGANO)
C
C --- COORDONNEES DU POINT D'INTEGRATION COURANT :
C     ------------------------------------------
      QSI = ZR(ICOOPG-1+NDIM*(IGAU-1)+1)
      ETA = ZR(ICOOPG-1+NDIM*(IGAU-1)+2)
C
C --- PRODUIT JACOBIEN*POIDS
C     ----------------------
      JACGAU = ZR(IPOIDS+IGAU-1)*CARAT3(7)
C
C --- CALCUL DE LA MATRICE B_MEMBRANE NOTEE, ICI, (BM)
C     ------------------------------------------------
      CALL DXTBM ( CARAT3(9) , BM)
C
C --- CALCUL DE LA MATRICE B_FLEXION RELATIVE AUX INCONNUES W, BETAX
C --- ET BETAY.
C --- CETTE MATRICE PREND EN COMPTE L'INTERPOLATION QUADRATIQUE
C --- DES ROTATIONS EN FONCTION DES INCONNUES ALFA.
C --- ON RAPPELLE QUE CES INCONNUES SONT DEFINIES AU MILIEU DES COTES
C --- DE TELLE MANIERE QUE LA COMPOSANTE DES ROTATIONS LE LONG DES
C --- COTES EST UNE FONCTION QUADRATIQUE DE L'ABSCISSE CURVILIGNE.
C --- CES INCONNUES ALFA SONT ELIMINEES GRACE A LA RELATION :
C ---  (ALFA) = (AN)*(UN)
C --- AVEC (UN) = (...,W_I,BETAX_I,BETAY_I,...)
C --- CETTE MATRICE (AN) EST CALCULEE IMPLICITEMENT DANS DKTBF
C     --------------------------------------------------------
      CALL DKTBF ( QSI, ETA, CARAT3, BF )
C
C --- MISE A ZERO DE LA PARTIE (BC) DE LA MATRICE (B) RELATIVE
C --- AU CISAILLEMENT
C     ---------------
      DO 10 I = 1, 2
         DO 20 J = 1, 9
            BC(I,J) = 0.0D0
 20      CONTINUE
 10   CONTINUE
C
C --- AFFECTATION DE LA MATRICE B COMPLETE, NOTEE (BMAT) 
C --- AVEC LES MATRICES (BM), (BF) ET (BC)
C     ------------------------------------
      CALL BCOQAF ( BM, BF, BC, NNO, BMAT)
C
      END
