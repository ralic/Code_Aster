      SUBROUTINE ERMEV3(NNO,IPG,IVF,ISIG,NBCMP,DFDX,DFDY,DFDZ,
     &                  DSX,DSY,DSZ,NORME)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 29/09/2009   AUTEUR GNICOLAS G.NICOLAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2006  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C =====================================================================
C  ERREUR EN MECANIQUE - TERME VOLUMIQUE - DIMENSION 3
C  **        **                *                     *
C =====================================================================
C     BUT:
C         PREMIER TERME DE L'ESTIMATEUR D'ERREUR EN RESIDU EXPLICITE : 
C         CALCUL DE LA DIVERGENCE ET DE LA NORME DE SIGMA EN UN POINT
C         DE GAUSS EN 3D.
C
C
C     ARGUMENTS:
C     ----------
C
C      ENTREE :
C-------------
C IN   NNO    : NOMBRE DE NOEUDS DE LA_MAILLE VOLUMIQUE
C IN   IPG    : NUMERO DU POINT DE GAUSS
C IN   IVF    : ADRESSE DANS ZR DU TABLEAU DES FONCTIONS DE FORME
C IN   ISIG   : ADRESSE DANS ZR DU TABLEAU DES CONTRAINTES AUX NOEUDS
C IN   NBCMP  : NOMBRE DE COMPOSANTES
C IN   DFDX   : DERIVEES DES FONCTIONS DE FORME / X
C IN   DFDY   : DERIVEES DES FONCTIONS DE FORME / Y
C IN   DFDZ   : DERIVEES DES FONCTIONS DE FORME / Z
C
C      SORTIE :
C-------------
C OUT  DSX    : PREMIERE COMPOSANTE DE DIVERGENCE SIGMA
C OUT  DSY    : SECONDE COMPOSANTE DE DIVERGENCE SIGMA
C OUT  DSZ    : TROISEME COMPOSANTE DE DIVERGENCE SIGMA
C OUT  NORME  : NORME DE SIGMA AU POINT DE GAUSS
C
C ......................................................................
C
      IMPLICIT NONE
      INTEGER NNO,IPG,IVF,ISIG,NBCMP
      REAL*8  DFDX(27),DFDY(27),DFDZ(27),DSX,DSY,DSZ,NORME
C
C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ---------------------

      INTEGER        ZI
      COMMON /IVARJE/ZI(1)
      REAL*8         ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16     ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL        ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8    ZK8
      CHARACTER*16          ZK16
      CHARACTER*24                  ZK24
      CHARACTER*32                          ZK32
      CHARACTER*80                                  ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)

C --------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ---------------------

      INTEGER I,K

      REAL*8 DSIG11,DSIG12,DSIG13
      REAL*8 DSIG21,DSIG22,DSIG23
      REAL*8 DSIG31,DSIG32,DSIG33
      REAL*8 SPG11,SPG22,SPG33,SPG12,SPG13,SPG23
      REAL*8 SIG11,SIG22,SIG33,SIG12,SIG13,SIG23

C ----------------------------------------------------------------------
C
      DSIG11=0.D0
      DSIG12=0.D0
      DSIG13=0.D0
      DSIG21=0.D0
      DSIG22=0.D0
      DSIG23=0.D0
      DSIG31=0.D0
      DSIG32=0.D0
      DSIG33=0.D0
C
      SPG11=0.D0
      SPG22=0.D0
      SPG33=0.D0
      SPG12=0.D0
      SPG13=0.D0
      SPG23=0.D0
C
      DO 10 I=1,NNO
        K=(IPG-1)*NNO
        SIG11=ZR(ISIG-1+NBCMP*(I-1)+1)
        SIG22=ZR(ISIG-1+NBCMP*(I-1)+2)
        SIG33=ZR(ISIG-1+NBCMP*(I-1)+3)
        SIG12=ZR(ISIG-1+NBCMP*(I-1)+4)
        SIG13=ZR(ISIG-1+NBCMP*(I-1)+5)
        SIG23=ZR(ISIG-1+NBCMP*(I-1)+6)
C
        DSIG11=DSIG11+SIG11*DFDX(I)
        DSIG12=DSIG12+SIG12*DFDY(I)
        DSIG13=DSIG13+SIG13*DFDZ(I)
        DSIG21=DSIG21+SIG12*DFDX(I)
        DSIG22=DSIG22+SIG22*DFDY(I)
        DSIG23=DSIG23+SIG23*DFDZ(I)
        DSIG31=DSIG31+SIG13*DFDX(I)
        DSIG32=DSIG32+SIG23*DFDY(I)
        DSIG33=DSIG33+SIG33*DFDZ(I)
C
        SPG11=SPG11+SIG11*ZR(IVF+K+I-1)
        SPG22=SPG22+SIG22*ZR(IVF+K+I-1)
        SPG33=SPG33+SIG33*ZR(IVF+K+I-1)
        SPG12=SPG12+SIG12*ZR(IVF+K+I-1)
        SPG13=SPG13+SIG13*ZR(IVF+K+I-1)
        SPG23=SPG23+SIG23*ZR(IVF+K+I-1)
C
  10  CONTINUE
C
      DSX=DSIG11+DSIG12+DSIG13
      DSY=DSIG21+DSIG22+DSIG23
      DSZ=DSIG31+DSIG32+DSIG33
C
      NORME=SPG11**2+SPG22**2+SPG33**2+
     &      2.D0*(SPG12**2+SPG13**2+SPG23**2)
C
      END
