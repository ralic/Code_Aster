      SUBROUTINE EF0042(NOMTE)
      IMPLICIT      NONE
      INCLUDE 'jeveux.h'
      CHARACTER*16 NOMTE
C     ------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 08/10/2012   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C     CALCUL DE EFGE_ELNO
C     ------------------------------------------------------------------
C
      INTEGER NBTERM,NNO,NC,NEQ,IREP,I,NDIM,IBID
      INTEGER LDIS,LORIEN,JEFFO,JDEPL,INFODI,ITYPE
      REAL*8 ULR(12),FLR(12)
      REAL*8 PGL(3,3),KLC(144),MAT(144),R8BID
      CHARACTER*8 K8BID
      CHARACTER*16 CH16
C     ------------------------------------------------------------------

C     ON VERIFIE QUE LES CARACTERISTIQUES ONT ETE AFFECTEES
C     LE CODE DU DISCRET
      CALL INFDIS('CODE',IBID,R8BID,NOMTE)
C     LE CODE STOKE DANS LA CARTE
      CALL INFDIS('TYDI',INFODI,R8BID,K8BID)
      IF (INFODI.NE.IBID) THEN
        CALL U2MESK('F+','DISCRETS_25',1,NOMTE)
        CALL INFDIS('DUMP',IBID,R8BID,'F+')
      ENDIF
C     DISCRET DE TYPE RAIDEUR
      CALL INFDIS('DISK',INFODI,R8BID,K8BID)
      IF (INFODI.EQ.0) THEN
        CALL U2MESK('A+','DISCRETS_27',1,NOMTE)
        CALL INFDIS('DUMP',IBID,R8BID,'A+')
      ENDIF

C     MATRICE DE RAIDEUR SYMETRIQUE OU PAS
      INFODI=1
      CALL INFDIS('SYMK',INFODI,R8BID,K8BID)
C --- INFORMATIONS SUR LES DISCRETS :
C        NBTERM  = NOMBRE DE COEFFICIENTS DANS K
C        NNO     = NOMBRE DE NOEUDS
C        NC      = NOMBRE DE COMPOSANTE PAR NOEUD
C        NDIM    = DIMENSION DE L'ELEMENT
C        ITYPE = TYPE DE L'ELEMENT
      CALL INFTED(NOMTE,INFODI,NBTERM,NNO,NC,NDIM,ITYPE)
      NEQ=NNO*NC

C     --- MATRICE DE RIGIDITE LOCALE ---
      CALL JEVECH('PCADISK','L',LDIS)
      CALL JEVECH('PCAORIE','L',LORIEN)
      CALL MATROT(ZR(LORIEN),PGL)

C     --- ABSOLU VERS LOCAL ? ---
C     --- IREP = 1 = MATRICE EN REPERE GLOBAL ==> PASSER EN LOCAL ---
      CALL INFDIS('REPK',IREP,R8BID,K8BID)
      IF (IREP.EQ.1) THEN
        IF (NDIM.EQ.3) THEN
          IF (INFODI.EQ.1) THEN
            CALL UTPSGL(NNO,NC,PGL,ZR(LDIS),MAT)
          ELSEIF (INFODI.EQ.2) THEN
            CALL UTPPGL(NNO,NC,PGL,ZR(LDIS),MAT)
          ENDIF
        ELSEIF (NDIM.EQ.2) THEN
          IF (INFODI.EQ.1) THEN
            CALL UT2MGL(NNO,NC,PGL,ZR(LDIS),MAT)
          ELSEIF (INFODI.EQ.2) THEN
            CALL UT2PGL(NNO,NC,PGL,ZR(LDIS),MAT)
          ENDIF
        ENDIF
      ELSE
        DO 10 I=1,NBTERM
          MAT(I)=ZR(LDIS+I-1)
   10   CONTINUE
      ENDIF

C     ---- MATRICE RIGIDITE LIGNE > MATRICE RIGIDITE CARRE
      IF (INFODI.EQ.1) THEN
        CALL VECMA(MAT,NBTERM,KLC,NEQ)
      ELSEIF (INFODI.EQ.2) THEN
        CALL VECMAP(MAT,NBTERM,KLC,NEQ)
      ENDIF

C     --- CALCUL DES VECTEURS ELEMENTAIRES ----

      CALL JEVECH('PEFFORR','E',JEFFO)
      CALL JEVECH('PDEPLAR','L',JDEPL)

C        --- VECTEUR DEPLACEMENT LOCAL  ULR = PGL * UG  ---
      IF (NDIM.EQ.3) THEN
        CALL UTPVGL(NNO,NC,PGL,ZR(JDEPL),ULR)
      ELSEIF (NDIM.EQ.2) THEN
        CALL UT2VGL(NNO,NC,PGL,ZR(JDEPL),ULR)
      ENDIF

C        --- VECTEUR EFFORT      LOCAL  FLR = KLC * ULR  ---
      CALL PMAVEC('ZERO',NEQ,KLC,ULR,FLR)

C     ON CHANGE LE SIGNE DES EFFORTS SUR LE PREMIER NOEUD, POUR LES
C     ELEMENTS A 2 NOEUDS
      IF (NNO.EQ.1) THEN
        DO 20 I=1,NEQ
          ZR(JEFFO+I-1)=FLR(I)
   20   CONTINUE
      ELSE
        DO 30 I=1,NC
          ZR(JEFFO+I-1)=-FLR(I)
          ZR(JEFFO+I+NC-1)=FLR(I+NC)
   30   CONTINUE
      ENDIF
      END
