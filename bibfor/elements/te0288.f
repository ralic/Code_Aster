      SUBROUTINE TE0288(OPTION,NOMTE)
      IMPLICIT NONE
      INCLUDE 'jeveux.h'
      CHARACTER*16 OPTION,NOMTE
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 28/08/2012   AUTEUR TRAN V-X.TRAN 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C
C    - FONCTION REALISEE:  CALCUL DES OPTIONS DE POST-TRAITEMENT
C                          EN MÉCANIQUE DE LA RUPTURE
C                          POUR LES ÉLÉMENTS X-FEM (OPTION CALC_G)
C
C             ATTENTION : PAS D'ETAT INITIAL
C                         PAS DE GRANDES DEF, GRANDES ROT
C                         PAS DE COMP_INCR
C
C
C    - ARGUMENTS:
C        DONNEES:      OPTION       -->  OPTION DE CALCUL
C                      NOMTE        -->  NOM DU TYPE ELEMENT
C ......................................................................



      INTEGER NDIM,NNO,NNOP,NPG,NPTF,JBASEC,NFISS,JFISNO
      INTEGER NFH,NFE,DDLC,NSE,ISE,IN,INO
      INTEGER JPINTT,JCNSET,JHEAVT,JLONCH,JBASLO,IGEOM,IDEPL
      INTEGER IPRES,IPREF,ITEMPS,JPTINT,JAINT,JCFACE,JLONGC,IMATE,ICOMP
      INTEGER ITHET,I,J,COMPT,IGTHET,IBID,JLSN,JLST,ICODE
      INTEGER NINTER,NFACE,CFACE(5,3),IFA,SINGU,JPMILT,IRESE,DDLM
      REAL*8  THET,R8PREM,VALRES(3),DEVRES(3),PRESN(27),VALPAR(4)
      REAL*8  PRES,RHO,FNO(81),COORSE(81)
      INTEGER   ICODRE(3)
      CHARACTER*8   ELREFP,ELRESE(6),FAMI(6),NOMRES(3),NOMPAR(4),ENR
      CHARACTER*16  COMPOR(4)
      LOGICAL       ISMALI,GRAND,INCR

      DATA    ELRESE /'SE2','TR3','TE4','SE3','TR6','TE4'/
      DATA    FAMI   /'BID','RIGI','XINT','BID','RIGI','XINT'/
      DATA    NOMRES /'E','NU','ALPHA'/


      CALL ELREF1(ELREFP)
      CALL JEVECH('PTHETAR','L',ITHET)
      CALL ELREF4(' ','RIGI',NDIM,NNOP,IBID,IBID,IBID,IBID,IBID,IBID)

C     SI LA VALEUR DE THETA EST NULLE SUR L'ÉLÉMENT, ON SORT
      COMPT = 0
      DO 10 I = 1,NNOP
        THET = 0.D0
        DO 11 J = 1,NDIM
          THET = THET + ABS(ZR(ITHET+NDIM*(I-1)+J-1))
 11     CONTINUE
        IF (THET.LT.R8PREM()) COMPT = COMPT + 1
 10   CONTINUE
      IF (COMPT.EQ.NNOP) GOTO 9999

C     SOUS-ELEMENT DE REFERENCE : RECUP DE NNO, NPG ET IVF
      IF (.NOT.ISMALI(ELREFP).AND. NDIM.LE.2) THEN
        IRESE=3
      ELSE
        IRESE=0
      ENDIF
      CALL ELREF4(ELRESE(NDIM+IRESE),FAMI(NDIM+IRESE),IBID,NNO,IBID,NPG,
     &                                          IBID,IBID,IBID,IBID)

C     INITIALISATION DES DIMENSIONS DES DDLS X-FEM
      CALL XTEINI(NOMTE,NFH,NFE,SINGU,DDLC,IBID,IBID,IBID,
     &            DDLM,NFISS,IBID)


      CALL JEVECH('PCOMPOR','L',ICOMP)
      CALL JEVECH('PGEOMER','L',IGEOM)
      CALL JEVECH('PDEPLAR','L',IDEPL)
      CALL JEVECH('PMATERC','L',IMATE)
      CALL JEVECH('PGTHETA','E',IGTHET)

      DO 50 I = 1,4
        COMPOR(I) = ZK16(ICOMP+I-1)
   50 CONTINUE

      INCR  = COMPOR(4).EQ.'COMP_INCR'
      GRAND = COMPOR(3).EQ.'GROT_GDEP'

      IF (INCR)  CALL U2MESS('F','XFEM_48')
      IF (GRAND) CALL U2MESS('F','XFEM_49')

C     ------------------------------------------------------------------
C              CALCUL DE G SUR L'ELEMENT MASSIF
C     ------------------------------------------------------------------
C
C     PARAMÈTRES PROPRES À X-FEM

      CALL JEVECH('PPINTTO','L',JPINTT)
      CALL JEVECH('PCNSETO','L',JCNSET)
      CALL JEVECH('PHEAVTO','L',JHEAVT)
      CALL JEVECH('PLONCHA','L',JLONCH)
      CALL JEVECH('PBASLOR','L',JBASLO)
      CALL JEVECH('PLSN','L',JLSN)
      CALL JEVECH('PLST','L',JLST)

C     PROPRES AUX ELEMENTS 1D ET 2D (QUADRATIQUES)
      CALL TEATTR (NOMTE,'S','XFEM',ENR,IBID)
      IF (IBID.EQ.0 .AND.(NOMTE(3:4).NE.'AX').AND.(ENR.EQ.'XH'
     & .OR.ENR.EQ.'XHT'.OR.ENR.EQ.'XT'.OR.ENR.EQ.'XHC').AND. NDIM.LE.2)
     &  CALL JEVECH('PPMILTO','L',JPMILT)
      IF (NFISS.GT.1) CALL JEVECH('PFISNO','L',JFISNO)

C     CALCUL DES FORCES NODALES CORRESPONDANT AUX CHARGES VOLUMIQUES
      CALL XCGFVO(OPTION,NDIM,NNOP,FNO,RHO)

C     RÉCUPÉRATION DE LA SUBDIVISION DE L'ÉLÉMENT EN NSE SOUS ELEMENT
      NSE=ZI(JLONCH-1+1)

C       BOUCLE D'INTEGRATION SUR LES NSE SOUS-ELEMENTS
      DO 110 ISE=1,NSE

C       BOUCLE SUR LES SOMMETS DU SOUS-TRIA (DU SOUS-SEG)
        DO 111 IN=1,NNO
          INO=ZI(JCNSET-1+NNO*(ISE-1)+IN)
          DO 112 J=1,NDIM
            IF (INO.LT.1000) THEN
              COORSE(NDIM*(IN-1)+J)=ZR(IGEOM-1+NDIM*(INO-1)+J)
            ELSEIF (INO.GT.1000 .AND. INO.LT.2000) THEN
              COORSE(NDIM*(IN-1)+J)=ZR(JPINTT-1+NDIM*(INO-1000-1)+J)
            ELSEIF (INO.GT.2000 .AND. INO.LT.3000) THEN
              COORSE(NDIM*(IN-1)+J)=ZR(JPMILT-1+NDIM*(INO-2000-1)+J)
            ELSEIF (INO.GT.3000) THEN
              COORSE(NDIM*(IN-1)+J)=ZR(JPMILT-1+NDIM*(INO-3000-1)+J)
            ENDIF
 112      CONTINUE
 111    CONTINUE

        CALL XGELEM(ELREFP,NDIM,COORSE,IGEOM,JHEAVT,ISE,NFH,
     &              DDLC,DDLM,NFE,ZR(JBASLO),NNOP,IDEPL,
     &              ZR(JLSN),ZR(JLST),IGTHET,FNO,NFISS,JFISNO)


 110  CONTINUE

C     ------------------------------------------------------------------
C              CALCUL DE G SUR LES LEVRES
C     ------------------------------------------------------------------

      IF (OPTION.EQ.'CALC_G') THEN
C       SI LA PRESSION N'EST CONNUE SUR AUCUN NOEUD, ON LA PREND=0.
        CALL JEVECD('PPRESSR',IPRES,0.D0)
      ELSEIF (OPTION.EQ.'CALC_G_F') THEN
        CALL JEVECH('PPRESSF','L',IPREF)
        CALL JEVECH('PTEMPSR','L',ITEMPS)

C       RECUPERATION DES PRESSIONS AUX NOEUDS PARENTS
        NOMPAR(1)='X'
        NOMPAR(2)='Y'
        IF (NDIM.EQ.3) NOMPAR(3)='Z'
        IF (NDIM.EQ.3) NOMPAR(4)='INST'
        IF (NDIM.EQ.2) NOMPAR(3)='INST'
        DO 70 I = 1,NNOP
          DO 80 J = 1,NDIM
            VALPAR(J) = ZR(IGEOM+NDIM*(I-1)+J-1)
 80       CONTINUE
          VALPAR(NDIM+1)= ZR(ITEMPS)
          CALL FOINTE('FM',ZK8(IPREF),4,NOMPAR,VALPAR,PRESN(I),ICODE)
 70     CONTINUE
      ENDIF

C     SI LA VALEUR DE LA PRESSION EST NULLE SUR L'ÉLÉMENT, ON SORT
      COMPT = 0
      DO 90 I = 1,NNOP
        IF (OPTION.EQ.'CALC_G')   PRES = ABS(ZR(IPRES-1+I))
        IF (OPTION.EQ.'CALC_G_F') PRES = ABS(PRESN(I))
        IF (PRES.LT.R8PREM()) COMPT = COMPT + 1
 90   CONTINUE
      IF (COMPT.EQ.NNOP) GOTO 9999

C     PARAMETRES PROPRES A X-FEM
      CALL JEVECH('PPINTER','L',JPTINT)
      CALL JEVECH('PAINTER','L',JAINT)
      CALL JEVECH('PCFACE' ,'L',JCFACE)
      CALL JEVECH('PLONGCO','L',JLONGC)
      CALL JEVECH('PBASECO','L',JBASEC)

C     RÉCUPÉRATIONS DES DONNÉES SUR LA TOPOLOGIE DES FACETTES
      NINTER=ZI(JLONGC-1+1)
      NFACE=ZI(JLONGC-1+2)
      NPTF=ZI(JLONGC-1+3)
      IF (NINTER.LT.NDIM) GOTO 9999

      DO 20 I=1,NFACE
        DO 21 J=1,NPTF
          CFACE(I,J)=ZI(JCFACE-1+NDIM*(I-1)+J)
 21     CONTINUE
 20   CONTINUE

C     RECUPERATION DES DONNEES MATERIAU AU 1ER POINT DE GAUSS DE
C     DE L'ELEMENT PARENT !!
C     LE MATÉRIAU DOIT ETRE HOMOGENE DANS TOUT L'ELEMENT
      CALL RCVAD2('RIGI',1,1,'+',ZI(IMATE),'ELAS',3,NOMRES,
     &            VALRES,DEVRES,ICODRE)
      IF ((ICODRE(1).NE.0) .OR. (ICODRE(2).NE.0)) THEN
        CALL U2MESS('F','RUPTURE1_25')
      END IF
      IF (ICODRE(3) .NE. 0) THEN
        VALRES(3) = 0.D0
        DEVRES(3) = 0.D0
      END IF

C     BOUCLE SUR LES FACETTES
      DO 200 IFA=1,NFACE
        CALL XSIFLE(NDIM,IFA,JPTINT,JAINT,CFACE,IGEOM,NFH,SINGU,
     &              NFE,DDLC,DDLM,JLST,IPRES,IPREF,ITEMPS,IDEPL,NNOP,
     &              VALRES,ZR(JBASLO),ITHET,NOMPAR,PRESN,OPTION,IGTHET,
     &              JBASEC)
 200  CONTINUE


9999  CONTINUE


      END
