      SUBROUTINE VDESGA(NOMTE,KWGT,INTE,INTSN,NB1,NB2,XI,DEPL,BTILD,
     &                    INDITH,ALPHA,TEMPGA,EPSILN,SIGMA,VECTT)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 03/07/2012   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
      IMPLICIT NONE
C
C
      INCLUDE 'jeveux.h'
      CHARACTER*(*) NOMTE
      REAL*8 XI(3,*),DEPL(*),BTILD(5,42),MATC(5,5),TEMPGA(*),VECTT(3,3)
      REAL*8 EPSI(5),EPSTOT(5),SIGM(5),EPSILN(6,*),SIGMA(6,*),KAPPA
C
C-----------------------------------------------------------------------
      INTEGER I ,INDITH ,INTE ,INTSN ,JCARA ,K ,KWGT 
      INTEGER NB1 ,NB2 
      REAL*8 ALPHA ,DEUX 
C-----------------------------------------------------------------------
      DEUX = 2.0D0
C
      DO 10 I=1,5
         EPSI(I)=0.D0
      DO 20 K=1,5*NB1+2
         EPSI(I)=EPSI(I)+BTILD(I,K)*DEPL(K)
 20   CONTINUE
 10   CONTINUE
C
         EPSILN(1,KWGT)=EPSI(1)
         EPSILN(2,KWGT)=EPSI(2)
         EPSILN(3,KWGT)=0.D0
         EPSILN(4,KWGT)=EPSI(3)/DEUX
         EPSILN(5,KWGT)=EPSI(4)/DEUX
         EPSILN(6,KWGT)=EPSI(5)/DEUX
C
      CALL JEVECH ('PCACOQU' , 'L' , JCARA)
      KAPPA = ZR(JCARA+3)
C
      CALL MATRC(NOMTE,NB2,KAPPA,MATC,VECTT)
C
      IF (INDITH.EQ.-1) THEN
C
C     PAS DE CONTRAINTES THERMIQUES
C
      DO 30 I=1,5
         SIGM(I)=0.D0
      DO 40 K=1,5
         SIGM(I)=SIGM(I)+MATC(I,K)*EPSI(K)
 40   CONTINUE
 30   CONTINUE
C
      ELSE IF (INDITH.EQ.0) THEN
C
C     AVEC CONTRAINTES THERMIQUES
C
C     POUR CELA ON PASSE PAR LES DEFORMATIONS TOTALES
C     AU LIEU DE FAIRE ARIMETHIQUEMENT  SIGMA(ELAS) + SIGMA(THER)
C     CECI EN RAISON DE L'HETEROGENEITE ENTRE DEFORMATION ELASTIQUE
C     ET THERMIQUE (L'UN PEUT ETRE LINEAIE, L'AUTRE QUADRATQUE EN KSI3
C     DEFORMATIONS TOTALES = DEFOR(ELAS) - DEFOR(THER)
C     PUIS CONTRAINTES VRAIES = H * DEFORMATIONS TOTALES
C
         EPSTOT(1)=EPSI(1)-ALPHA*TEMPGA(KWGT)
         EPSTOT(2)=EPSI(2)-ALPHA*TEMPGA(KWGT)
         EPSTOT(3)=EPSI(3)
         EPSTOT(4)=EPSI(4)
         EPSTOT(5)=EPSI(5)
C
      DO 35 I=1,5
         SIGM(I)=0.D0
      DO 45 K=1,5
         SIGM(I)=SIGM(I)+MATC(I,K)*EPSTOT(K)
 45   CONTINUE
 35   CONTINUE
C
      ENDIF
C
         SIGMA(1,KWGT)=SIGM(1)
         SIGMA(2,KWGT)=SIGM(2)
         SIGMA(3,KWGT)=0.D0
         SIGMA(4,KWGT)=SIGM(3)
         SIGMA(5,KWGT)=SIGM(4)
         SIGMA(6,KWGT)=SIGM(5)
C
C
      END
