      SUBROUTINE KLG (NNO,NBRDDL,PGL,K)
      IMPLICIT REAL*8 (A-H,O-Z)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 20/12/2010   AUTEUR PELLET J.PELLET 
C TOLE CRS_1404
C ======================================================================
C COPYRIGHT (C) 1991 - 2010  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C
C PASSAGE DE LA MATRICE K DU REPERE LOCAL AU REPERE GLOBAL
C POUR LES DDL DE POUTRE, ON LAISSE LES DDL DE COQUE DANS
C LE REPERE LOCAL.
C ENTREE        : NNO = NBRE DE NOEUDS
C                 PGL = MATRICE DE PASSAGE
C ENTREE-SORTIE : K   = MATRICE DE RIGIDITE
C
      INTEGER            I,J,L,NNO,NBRDDL,M
CJMP      PARAMETER          (NBRDDL=63)
      REAL*8             K(NBRDDL,NBRDDL),P(NBRDDL,NBRDDL),PGL(3,3)
      REAL*8             KTEMP(NBRDDL,NBRDDL)
C
C  INITIALISATION A L'IDENTITE DE LA MATRICE DE PASSAGE P
C
      DO 10, I=1, NBRDDL
         DO 20 J=1, NBRDDL
         IF ( I.EQ.J ) THEN
             P(I,J)=1.D0
         ELSE
             P(I,J)=0.D0
         ENDIF
20       CONTINUE
10    CONTINUE
C
C  REMPLISSAGE DES DE BLOC DE LA MATRICE P CORRESPONDANT AUX DDL
C  DE POUTRE (UX, UY, UZ, TETAX, TETAY, ET TETAZ) PAR LA MATRICE
C  DE PASSAGE (3*3) PGL.
C
      DO 30, L=1,NNO
         M=(L-1)*NBRDDL/NNO
         DO 40, I=1,3
            DO 50, J=1,3
            P(M+I,M+J)=PGL(I,J)
            P(M+3+I,M+3+J)=PGL(I,J)
50          CONTINUE
40       CONTINUE
30    CONTINUE
C
C INITIALISATION A ZERO DE LA MATRICE KTEMP
C
      DO 60, I=1,NBRDDL
         DO 70, J=1,NBRDDL
            KTEMP(I,J)=0.D0
70       CONTINUE
60    CONTINUE
C
C CALCUL DE KTEMP = PRODUIT (TRANSPOSEE P) * K
C
      DO 80, I=1,NBRDDL
         DO 90, J=1,NBRDDL
            DO 100, L=1,NBRDDL
            KTEMP(I,J)=KTEMP(I,J)+P(L,I)*K(L,J)
100         CONTINUE
90       CONTINUE
80    CONTINUE
C
C  INITIALISATION A ZERO DE LA MATRICE K
C
      DO 110, I=1,NBRDDL
         DO 120, J=1,NBRDDL
            K(I,J)=0.D0
120       CONTINUE
110    CONTINUE
C
C CALCUL DE K = PRODUIT KTEMP * P = (TRANSPOSEE P) * K * P
C
      DO 130, I=1,NBRDDL
         DO 140, J=1,NBRDDL
            DO 150, L=1,NBRDDL
            K(I,J)=K(I,J)+KTEMP(I,L)*P(L,J)
150         CONTINUE
140       CONTINUE
130    CONTINUE
C
C
9999  CONTINUE
      END
