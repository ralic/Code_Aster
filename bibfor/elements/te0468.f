      SUBROUTINE TE0468(OPTION,NOMTE)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 26/04/2011   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C ======================================================================
      IMPLICIT REAL*8 (A-H,O-Z)
      CHARACTER*16 OPTION,NOMTE
C ......................................................................

C     BUT: CALCUL DU FLUX HYDRAULIQUE NORMAL
C          SUR DES SEG2
C          OPTION : 'FLHN_ELGA'

C    - ARGUMENTS:
C        DONNEES:      OPTION       -->  OPTION DE CALCUL
C                      NOMTE        -->  NOM DU TYPE ELEMENT
C ......................................................................

      REAL*8 POIDS,NX,NY,FLX(4),FLY(4),FLUN
      INTEGER NNO,KP,NPG,IPOIDS,IVF,IDFDE,IGEOM
      INTEGER IFLUX,IVECTU,K,I,IAD
      CHARACTER*24 VALKM(3)

C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ---------------------
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C --------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ---------------------

      CALL ELREF4('SE2','RIGI',NDIM,NNO,NNOS,NPG,IPOIDS,IVF,IDFDE,JGANO)
      CALL JEVECH('PGEOMER','L',IGEOM)
      CALL JEVECH('PCONTR','L',IFLUX)
      CALL JEVECH('PFLHN','E',IVECTU)
C
C  CALCUL DU NBRE DE CMP CALCULEES DU FLUX
      IF(NOMTE.EQ.'HM_D_PLAN_SE3'.OR.NOMTE.EQ.'THM_D_PLAN_SE3'
     &.OR.NOMTE.EQ.'HM_AXIS_SE3'.OR.NOMTE.EQ.'THM_AXIS_SE3') THEN
         NBFLUX=1
      ELSEIF(NOMTE.EQ.'THV_D_PLAN_SE3'.OR.NOMTE.EQ.'THV_AXIS_SE3') THEN
         NBFLUX=2
      ELSEIF(NOMTE.EQ.'HHM_D_PLAN_SE3'.OR.NOMTE.EQ.'THH_D_PLAN_SE3'
     &.OR.NOMTE.EQ.'THHM_D_PLAN_SE3'.OR.NOMTE.EQ.'HH_D_PLAN_SE3'
     &.OR.NOMTE.EQ.'HHM_AXIS_SE3'.OR.NOMTE.EQ.'THH_AXIS_SE3'
     &.OR.NOMTE.EQ.'THHM_AXIS_SE3'.OR.NOMTE.EQ.'HH_AXIS_SE3') THEN
         NBFLUX=3
      ELSEIF(NOMTE.EQ.'HH2M_D_PLAN_SE3'.OR.NOMTE.EQ.'THH2_D_PLAN_SE3'
     &.OR.NOMTE.EQ.'THH2M_D_PLAN_SE3'.OR.NOMTE.EQ.'HH2_D_PLAN_SE3'
     &.OR.NOMTE.EQ.'HH2M_AXIS_SE3'.OR.NOMTE.EQ.'THH2_AXIS_SE3'
     &.OR.NOMTE.EQ.'THH2M_AXIS_SE3'.OR.NOMTE.EQ.'HH2_AXIS_SE3') THEN
         NBFLUX=4
      ELSE
         VALKM(1)=OPTION
         VALKM(2)=NOMTE
         VALKM(3)='TE0468'
         CALL U2MESK('F','CALCULEL7_2',3,VALKM)
      ENDIF
C    BOUCLE SUR LES CMP
      DO 100 IFL = 1,NBFLUX

C    BOUCLE SUR LES POINTS DE GAUSS
        DO 40 KP = 1,NPG
          K = (KP-1)*NNO
C CALCUL DES FLUX AU POINT DE GAUSS KP A PARTIR DES FLUX AUX NOEUDS
          S = 0.D0
          T = 0.D0
          DO 10 I = 1,NNO
            IAD = IFLUX+2*(IFL-1)+2*NBFLUX*(I-1)
            S = S + ZR(IAD  )*ZR(IVF+K+I-1)
            T = T + ZR(IAD+1)*ZR(IVF+K+I-1)
   10     CONTINUE
          FLX(KP) = S
          FLY(KP) = T
C CALCUL DE LA NORMALE
          CALL VFF2DN(NDIM,NNO,KP,IPOIDS,IDFDE,ZR(IGEOM),NX,NY,POIDS)
          FLUN = NX*FLX(KP) + NY*FLY(KP)
          ZR(IVECTU+NBFLUX*(KP-1)+IFL-1) = FLUN
   40   CONTINUE
100   CONTINUE
      END
