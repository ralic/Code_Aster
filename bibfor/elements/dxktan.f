      SUBROUTINE DXKTAN(DELAS,MP1,MP2,NBACKN,NCRIT,DCC1,DCC2,DSIDEP)
C-----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 16/12/2004   AUTEUR VABHHTS J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C ----------------------------------------------------------------------
C     REALISE LE CALCUL DE LA MATRICE TANGENTE DANS LE CAS DE LA LOI 
C     DE COMPORTEMENT GLRC
C
C IN  DELAS  : MATRICE ELASTIQUE EN MEMBRANE, FLEXION ET COUPLAGE
C IN  MP1    : MOMENTS LIMITES ELASTIQUES EN FLEXION POSITIVE
C IN  MP2    : MOMENTS LIMITES ELASTIQUES EN FLEXION NEGATIVE
C IN  NBACKN : MOMENT DE RAPPEL
C IN  NCRIT  : TYPE DU CRITERE DE PLASTICITE
C IN  DCC1   : MATRICE ELASTIQUE + CONSTANTES DE PRAGER (FLEXION +)
C IN  DCC2   : MATRICE ELASTIQUE + CONSTANTES DE PRAGER (FLEXION -)
C
C OUT DSIDEP : MATRICE TANGENTE
C ----------------------------------------------------------------------
      IMPLICIT NONE
      REAL*8 MP1(3),MP2(3),NBACKN(6)
      REAL*8 DFPLA1(6)
      REAL*8 DFPLA2(6)
      REAL*8 DELAS(6,6),DSIDEP(6,6)
      REAL*8 MAT(6,6),MATA(6,6),MATB(6,6),MATC(6,6),MATD(6,6)
      REAL*8 DCC1(3,3),DCC2(3,3),DC1(6,6),DC2(6,6)
      REAL*8 VECT(6)
      REAL*8 SCAL,SCALA,SCALB
      COMMON /TDIM/ N,ND
C
      INTEGER NCRIT,I,J,N,ND
C
C--- INITIALISATION
C
      N=6
      DO 100 I = 1,6
        DFPLA1(I) = 0.D0
        DFPLA2(I) = 0.D0
        DO 110 J=1,6
          DC1(I,J) = 0.D0
          DC2(I,J) = 0.D0
  110   CONTINUE
  100 CONTINUE
C      
      CALL DCOPY(36,DELAS,1,DC1,1)
      CALL DCOPY(36,DELAS,1,DC2,1)
C            
      DO 120 I=1,3
        DO 130 J=1,3
          DC1(I+3,J+3) = DCC1(I,J)
          DC2(I+3,J+3) = DCC2(I,J)
  130   CONTINUE
  120 CONTINUE
C
      IF (NCRIT .EQ. 0) THEN
C - CAS ELASTIQUE
        CALL DCOPY(36,DELAS,1,DSIDEP,1)
C
      ELSE IF(NCRIT .EQ. 1) THEN 
        CALL DFPLAS(NBACKN(4),MP1,DFPLA1(4))
        CALL LCPRTE(DFPLA1,DFPLA1,MATA) 
        CALL PMAT(6,DELAS,MATA,MATB)
        CALL PMAT(6,MATB,DELAS,MATC)
        CALL PMAVEC('ZERO',6,DC1,DFPLA1,VECT)
        CALL LCPRSC(DFPLA1,VECT,SCAL)
C
        DO 10 I=1,6
          DO 20 J=1,6
            DSIDEP(I,J)=DELAS(I,J)-MATC(I,J)/SCAL
 20       CONTINUE
 10     CONTINUE

      ELSE IF(NCRIT .EQ. 2) THEN 
        CALL DFPLAS(NBACKN(4),MP2,DFPLA2(4))
        CALL LCPRTE(DFPLA2,DFPLA2,MATA) 
        CALL PMAT(6,DELAS,MATA,MATB)
        CALL PMAT(6,MATB,DELAS,MATC)
        CALL PMAVEC('ZERO',6,DC2,DFPLA2,VECT)
        CALL LCPRSC(DFPLA2,VECT,SCAL)
C
        DO 30 I=1,6
          DO 40 J=1,6
            DSIDEP(I,J)=DELAS(I,J)-MATC(I,J)/SCAL
 40       CONTINUE
 30     CONTINUE
C
       ELSE IF (NCRIT .EQ. 12) THEN

C - NUMERATEUR
        CALL DFPLAS(NBACKN(4),MP1,DFPLA1(4))
        CALL DFPLAS(NBACKN(4),MP2,DFPLA2(4))
        CALL DXPRD1(DFPLA1,DFPLA2,DC2,DFPLA2,DFPLA1,MATA)
        CALL DXPRD1(DFPLA1,DFPLA1,DC2,DFPLA2,DFPLA2,MATB)
        CALL DXPRD1(DFPLA2,DFPLA1,DC1,DFPLA1,DFPLA2,MATC)
        CALL DXPRD1(DFPLA2,DFPLA2,DC1,DFPLA1,DFPLA1,MATD)
C - DENOMINATEUR
        CALL DXPRD2(DFPLA1,DC1,DFPLA1,DFPLA2,DC2,DFPLA2,SCALA)
        CALL DXPRD2(DFPLA2,DC1,DFPLA1,DFPLA1,DC2,DFPLA2,SCALB)
C
        SCAL = SCALA*SCALB
C
        DO 50 I=1,6
          DO 60 J=1,6
            MAT(I,J)=(MATA(I,J)-MATB(I,J)+MATC(I,J)-MATD(I,J))/SCAL
 60       CONTINUE
 50     CONTINUE
C
        CALL PMAT(6,DELAS,MAT,MATA)
        CALL PMAT(6,MATA,DELAS,MATB)
C
        DO 70 I=1,6
          DO 80 J=1,6
            DSIDEP(I,J)=DELAS(I,J)-MATB(I,J)
 80       CONTINUE
 70     CONTINUE
C      
      ENDIF
      END
