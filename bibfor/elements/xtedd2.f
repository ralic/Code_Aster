      SUBROUTINE XTEDD2(NDIM,JNNE,NDEPLE,JNNM,NDDL,
     &           OPTION,LESCLX,LMAITX,LCONTX,STANO,LACT,JDDLE,JDDLM,
     &           NFHE,NFHM,LMULTI,HEAVNO,MMAT,VTMP)

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 13/06/2012   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE MASSIN P.MASSIN
C
C     BUT: SUPPRIMER LES DDLS "EN TROP" (VOIR BOOK III 09/06/04
C     ET  BOOK IV  30/07/07) POUR LE CONTACT XFEM GRAND GLISSEMENT
C
C     TRAVAIL EFFECTUE EN COLLABORATION AVEC I.F.P.
C
      IMPLICIT NONE
      INCLUDE 'jeveux.h'
      INTEGER       NDIM,JNNM(3),NDDL,NDLS,STANO(*),LACT(8)
      CHARACTER*16  OPTION
      REAL*8        MMAT(336,336),VTMP(336)
      LOGICAL       LESCLX,LMAITX,LCONTX,LMULTI
      INTEGER       JNNE(3),NDEPLE,JDDLE(2)
      INTEGER       JDDLM(2),NFHE,NFHM,HEAVNO(8)

C IN   NDIM   : DIMENSION DE L'ESPACE
C IN   NNES   : NOMBRE DE NOEUDS SOMMETS DE LA MAILLE ESCLAVE
C IN   NNM    : NOMBRE DE NOEUDS DE LA MAILLE MAITRE
C IN   NDDL   : NOMBRE DE DDL TOTAL DE L'ÉLÉMENT
C IN   NDLS   : NOMBRE DE DDLS D'UN NOEUD SOMMET ESCLAVE
C IN   OPTION : OPTION DE CALCUL DU TE
C IN   STANO  : LITE DU STATUT DES NOEUDS
C IN   LACT   : LITE DES LAGRANGES ACTIFS
C IN   LESCLX : LA MAILLE ESCLAVE EST HEAVISIDE CRACK-TIP
C IN   LMAITX : LA MAILLE MAITRE EST HEAVISIDE CRACK-TIP
C IN   LCONTX : CERTAINS DDL DE CONTACT NE SONT PAS ACTIFS
C IN/OUT  MAT : MATRICE DE RIGIDITÉ
C IN/OUT VECT : VECTEUR SECOND MEMBRE
C
C
C
C
       INTEGER       I,J,NDDLE,IN,NNES,NNEM,DDLES,DDLEM,NNM,DDLMAX
       PARAMETER     (DDLMAX=336)
       INTEGER       DDLMS,DDLMM,IFH,POSDDL(DDLMAX)
       REAL*8        R8MAEM,DMAX,DMIN
C
C----------------------------------------------------------------------
C
      CALL JEMARQ()
C
      NNES=JNNE(2)
      NNEM=JNNE(3)
      NNM=JNNM(1)
      DDLES=JDDLE(1)
      DDLEM=JDDLE(2)
      DDLMS=JDDLM(1)
      DDLMM=JDDLM(2)
      NDDLE = DDLES*NNES+DDLEM*NNEM
C
C --- REMPLISSAGE DU VECTEUR POS : POSITION DES DDLS A SUPPRIMER
C
      CALL ASSERT(NDDL.LE.DDLMAX)
      DO 9 I=1,DDLMAX
        POSDDL(I)=0
   9  CONTINUE

      IF (LESCLX) THEN
        DO 10 I=1,NDEPLE
          CALL INDENT(I,DDLES,DDLEM,NNES,IN)

          IF (STANO(I).EQ.1) THEN
C --- NOEUD HEAVISIDE, ON ELIMINE LES DDL CRACK-TIP
            DO 20 J=1,NDIM
              POSDDL(IN+2*NDIM+J)=1
  20        CONTINUE
          ELSEIF (STANO(I).EQ.2) THEN
C --- NOEUD CRACK-TIP, ON ELIMINE LES DDL HEAVISIDE
            DO 30 J=1,NDIM
              POSDDL(IN+NDIM+J)=1
  30        CONTINUE
          ENDIF
  10    CONTINUE
      ELSE
        DO 130 I=1,NDEPLE
          CALL INDENT(I,DDLES,DDLEM,NNES,IN)
          DO 135 IFH = 1,NFHE
            IF (STANO(NFHE*(I-1)+IFH).EQ.0) THEN
C --- DANS LE CAS DE MAILLE HEAVISIDE, ON ELIMINE LE DDL HEAVISIDE
              DO 140 J=1,NDIM
                POSDDL(IN+NDIM*IFH+J)=1
 140          CONTINUE
            ENDIF
 135      CONTINUE
 130    CONTINUE
      ENDIF
      IF (LMAITX) THEN
        DO 40 I=1,NNM
          CALL INDENT(I,DDLMS,DDLMM,NNM,IN)
          IN = IN + NDDLE
          IF (STANO(NDEPLE+I).EQ.1) THEN
C --- NOEUD HEAVISIDE, ON ELIMINE LES DDL CRACK-TIP
            DO 50 J=1,NDIM
              POSDDL(IN+2*NDIM+J)=1
  50        CONTINUE
          ELSEIF (STANO(NDEPLE+I).EQ.2) THEN
C --- LE NOEUD EST CRACK-TIP, ON ELIMINE HEAVISIDE
            DO 60 J=1,NDIM
              POSDDL(IN+NDIM+J)=1
  60        CONTINUE
          ENDIF
  40    CONTINUE
      ELSE
        DO 150 I=1,NNM
          CALL INDENT(I,DDLMS,DDLMM,NNM,IN)
          IN = IN + NDDLE
          DO 155 IFH = 1,NFHM
            IF (STANO(NFHE*NDEPLE+NFHM*(I-1)+IFH).EQ.0) THEN
C --- DANS LE CAS DE MAILLE HEAVISIDE, ON ELIMINE LE DDL HEAVISIDE
              DO 160 J=1,NDIM
                POSDDL(IN+NDIM*IFH+J)=1
 160          CONTINUE
            ENDIF
 155      CONTINUE
 150    CONTINUE
      ENDIF
      IF (LCONTX) THEN
        DO 110 I=1,NNES
          IF (LACT(I).EQ.0) THEN
C --- CONTACT NON ACTIF POUR CE NOEUD, ON ELIMINE LES DDL DE CONTACT
            DO 120 J=1,NDIM
              IF (.NOT.LMULTI) POSDDL(DDLES*I-NDIM+J)=1
              IF(LMULTI)POSDDL(DDLES*(I-1)+NDIM*(NFHE+HEAVNO(I))+J)=1
 120        CONTINUE
          ENDIF
 110    CONTINUE
      ENDIF
C
      IF ( OPTION.EQ.'RIGI_CONT'.OR.OPTION.EQ.'RIGI_FROT') THEN
C --- CALCUL DU COEFFICIENT DIAGONAL POUR
C --- L'ELIMINATION DES DDLS HEAVISIDE
C --- MISE A ZERO DES TERMES HORS DIAGONAUX (I,J)
C --- ET MISE A UN DES TERMES DIAGONAUX (I,I)
C        DMIN=R8MAEM()
        DMAX=1.D0
C          DO 70 I = 1, NDDL
C            DMAX = DMAX + ABS(MMAT(I,I))
C  70      CONTINUE
        DO 80 I = 1,NDDL
          IF (POSDDL(I).EQ.0) GOTO 80
          DO 90 J = 1,NDDL
            IF (J.NE.I) THEN
              MMAT(I,J) = 0.D0
              MMAT(J,I) = 0.D0
            ENDIF
            IF (J.EQ.I) MMAT(I,J) = DMAX
 90      CONTINUE
 80    CONTINUE
C
      ELSE IF (OPTION.EQ.'CHAR_MECA_CONT' .OR.
     &         OPTION.EQ.'CHAR_MECA_FROT') THEN
C --- MISE A ZERO DES TERMES I
        DO 100 I = 1,NDDL
          IF (POSDDL(I).EQ.0) GOTO 100
            VTMP(I) = 0.D0
 100    CONTINUE
      ENDIF
C
      CALL JEDEMA()
      END
