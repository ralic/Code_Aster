      SUBROUTINE DRAACN(DEG,POLY,NBROOT,ROOT)

      IMPLICIT NONE

C-----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 19/05/2009   AUTEUR SFAYOLLE S.FAYOLLE 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C======================================================================
C
C     EVALUE LES RACINES DU POLYNOME DE DEGRE DEG PAR L ALGORITHME
C     DE BAIRSTOW AVEC POLISSAGE DES RACINES PAR L ALGO DE NEWTON
C
C
C IN  DEG : DEGRE DU POLYNOME
C IN  POLY : COEFFICIENT DU POLYNOME
C
C OUT NBROOT : NOMBRE DE RACINE DU POLYNOME
C OUT ROOT : RACINE DU POLYNOME

      INTEGER I, J, K, N, NROOT, IRAN(20)
      INTEGER I2, KKK
      INTEGER DEG, NBROOT

      REAL*8 POLY(DEG+1), ROOT(DEG)
      REAL*8 A(DEG+1),B(DEG+3),C(DEG+3)
      REAL*8 DP1,DQ,D,E,F,P1,Q,TTT,U,X,Y,Z
      REAL*8 REALRO(DEG),CCOEF,COEF(DEG+1)

      REAL*8 ZERO

      N = DEG
      ZERO = 1.0D-8
      CCOEF=0.D0

C     COPIE DES COEFFICIENTS DU POLYNOME ET
C     RECHERCHE DU PLUS GRAND COEFFICIENT EN VALEUR ABSOLUE
      DO 20, I = 1, DEG+1
        A(I) = POLY(I)
        CCOEF = MAX(CCOEF,ABS(A(I)))
 20   CONTINUE

      IF(ABS(A(1)) .LE. ZERO*CCOEF) THEN
        DO 24, N = DEG, 1, -1
          DO 23, I = 1, N
            A(I) = A(I+1)
 23       CONTINUE

          A(N+1)=0.D0
 24     CONTINUE
      ELSE
        CCOEF=A(1)
        DO 30, I = 1,N+1
          A(I)=A(I)/CCOEF
 30     CONTINUE
      ENDIF

      P1 = 0.D0
      Q = 0.D0
      K = 100
      E = 1.D-4

C     INITIALISATION DES TABLEAUX
      CALL R8INIR(DEG,0.0D0,ROOT,1)
      CALL R8INIR(DEG,0.0D0,REALRO,1)

      NBROOT=0

      DO 59, N = N, 3, -2

        J=0
        F=E+1.0D0

        DO 49, KKK = 1,10000000
          IF(F .LE. E) GOTO 50

          IF (J .GT. K) GOTO 60

          J=J+1
          B(1) = 0.D0
          B(2) = 0.D0
          C(1) = 0.D0
          C(2) = 0.D0

          DO 40, I2 = 1, N+1
            I = I2 + 2
            B(I) = A(I-2)-P1*B(I-1)-Q*B(I-2)
            C(I) = -B(I-1)-P1*C(I-1)-Q*C(I-2)
 40       CONTINUE

          X = B(N+2)
          Y = B(N+3)
          Z = C(N+1)
          TTT = C(N+2)
          U = C(N+3)
          D = TTT*TTT-Z*(U+X)

          IF (D .EQ. 0.D0) GOTO 60

          DP1=(Z*Y-X*TTT)/D
          DQ=(-X*(Q*Z+P1*TTT)-Y*TTT)/D
          P1=P1+DP1
          Q=Q+DQ
          F=(ABS(DP1)+ABS(DQ))/(ABS(P1)+ABS(Q))
 49     CONTINUE

 50     CONTINUE

C     RECHERCHE DES RACINES DU POLYNOME DU SECOND DEGRE
C     Y = X**2 + P1 * X + Q
        CALL DRAAC2(1.D0,P1,Q,
     &        REALRO(NBROOT+1),REALRO(NBROOT+2),NROOT)

        NBROOT=NBROOT+NROOT

        DO 55, I = 1,N-1
          A(I) = B(I+2)
 55     CONTINUE
 59   CONTINUE

 60   CONTINUE

      IF (N .EQ. 2) THEN
C     RECHERCHE DES RACINES DU POLYNOME DU SECOND DEGRE
C     Y = A(1) * X**2 + A(2) * X + A(3)
        CALL DRAAC2(A(1),A(2),A(3),
     &        REALRO(NBROOT+1),REALRO(NBROOT+2),NROOT)
        NBROOT = NBROOT+NROOT
      ELSEIF (N .EQ. 1) THEN
        NBROOT = NBROOT + 1
        REALRO(NBROOT) = -A(2)/A(1)
      ENDIF

      IF (NBROOT .GT. 0) THEN
        DO 65, I=1,DEG+1
          COEF(I) = POLY(DEG-I+2)
 65     CONTINUE

        DO 67, I=1,NBROOT
C     RAFFINE LA RECHERCHE DES RACINES PAR LA METHODE DE NEWTON
          CALL NWTPOL(DEG,COEF,REALRO(I))
 67     CONTINUE
      ENDIF

      IF (NBROOT .GT. 1) THEN
C     EVALUE L ORDRE DES RACINES DANS IRAN
        CALL DCLASS(NBROOT,REALRO,IRAN)

        DO 70, I = 1,NBROOT
          ROOT(I)=REALRO(IRAN(I))
 70     CONTINUE
      ENDIF

      END
