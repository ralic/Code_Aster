      SUBROUTINE TE0534(OPTION,NOMTE)
      IMPLICIT   NONE
      INCLUDE 'jeveux.h'

      CHARACTER*16 OPTION,NOMTE

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 19/12/2012   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE GENIAUT S.GENIAUT
C TOLE CRP_20

C.......................................................................
C
C               CALCUL DES SECONDS MEMBRES DE CONTACT FROTTEMENT
C                   POUR X-FEM  (METHODE CONTINUE)
C
C
C  OPTION : 'CHAR_MECA_CONT' (CALCUL DU SECOND MEMBRE DE CONTACT)
C  OPTION : 'CHAR_MECA_FROT' (CALCUL DU SECOND MEMBRE DE FROTTEMENT)
C
C  ENTREES  ---> OPTION : OPTION DE CALCUL
C           ---> NOMTE  : NOM DU TYPE ELEMENT
C
C.......................................................................
C

      INTEGER     I,J,IFA,IPGF,ISSPG
      INTEGER      JINDCO,JDONCO,JLST,IPOIDS,IVF,IDFDE,JGANO,IGEOM
      INTEGER      IDEPM,IDEPL,JPTINT,JAINT,JCFACE,JLONCH
      INTEGER     IVFF,IADZI,IAZK24,IBID,IVECT,JBASEC
      INTEGER      NDIM,NFH,DDLC,DDLS,NDDL,NNO,NNOS,NNOM,NNOF,DDLM
      INTEGER     NPG,NPGF,FAC(6,4),NBF,JSEUIL
      INTEGER     INDCO,NINTER,NFACE,CFACE(5,3),IBID2(12,3)
      INTEGER     NFE,SINGU,JSTNO,NVIT,ALGOCR,ALGOFR,NVEC
      INTEGER     NNOL,PLA(27),LACT(8),NLACT,JCOHES
      INTEGER     JMATE,NFISS,JFISNO,CONTAC,NBSPG,NSPFIS
      INTEGER     JHEANO,IFISS,NCOMPH,NCOMPV,VSTNC(32)
      INTEGER      JTAB(2),IRET,NCOMPD,NCOMPP,NCOMPA,NCOMPB,NCOMPC
      INTEGER     JHEAFA,NPTF
      REAL*8     VTMP(400),REAC,REAC12(3),JAC
      REAL*8     ND(3),FFP(27),FFC(8),SEUIL,COEFCP,COEFCR,COEFFP
      REAL*8     MU,TAU1(3),TAU2(3),COEFFR
      REAL*8     RR,RBID,COHES(3),RELA
      LOGICAL     LBID,LELIM
      CHARACTER*8     ELREF,TYPMA,ELREFC
      CHARACTER*8 ELC,FPG
      CHARACTER*16 ENR

C.......................................................................

      CALL JEMARQ()

C --- INITIALISATIONS
C
      DO 5 I  = 1,8
        LACT(I) = 0
 5    CONTINUE
      CALL VECINI(27,0.D0,FFP)
      RR = 0.D0
      NCOMPH = 0
      LELIM = .FALSE.
      NBSPG = 0
      CALL VECINI(3,0.D0,TAU2)
      CALL ELREF1(ELREF)
      CALL ELREF4(' ','RIGI',NDIM,NNO,NNOS,NPG,IPOIDS,IVF,IDFDE,JGANO)
C
C     INITIALISATION DES DIMENSIONS DES DDLS X-FEM
      CALL XTEINI(NOMTE,NFH,NFE,SINGU,DDLC,NNOM,DDLS,NDDL,
     &            DDLM,NFISS,CONTAC)
C
      CALL TECAEL(IADZI,IAZK24)
      TYPMA=ZK24(IAZK24-1+3+ZI(IADZI-1+2)+3)

      IF (NDIM .EQ. 3) THEN
         CALL CONFAC(TYPMA,IBID2,IBID,FAC,NBF)
      ENDIF

      DO 40 J=1,NDDL
        VTMP(J)=0.D0
40    CONTINUE
C
C --- ROUTINE SPECIFIQUE P2P1
C
      CALL ELELIN(CONTAC,ELREF,ELREFC,IBID,IBID)
C
C --- RECUPERATION DES ENTRÉES / SORTIE
C
      CALL JEVECH('PGEOMER','L',IGEOM)
C     DEPLACEMENT A L'EQUILIBRE PRECEDENT  (DEPMOI)       : 'PDEPL_M'
      CALL JEVECH('PDEPL_M','L',IDEPM)
C     INCREMENT DE DEP DEPUIS L'EQUILIBRE PRECEDENT (DEPDEL) :'PDEPL_P'
      CALL JEVECH('PDEPL_P','L',IDEPL)
      CALL JEVECH('PINDCOI','L',JINDCO)
      CALL JEVECH('PDONCO','L',JDONCO)
      CALL TECACH('OOO','PDONCO',2,JTAB,IBID)
      NCOMPD = JTAB(2)
      CALL JEVECH('PSEUIL','L',JSEUIL)
      CALL JEVECH('PLST','L',JLST)
      CALL JEVECH('PPINTER','L',JPTINT)
      CALL JEVECH('PAINTER','L',JAINT)
      CALL JEVECH('PCFACE','L',JCFACE)
      CALL JEVECH('PLONCHA','L',JLONCH)
      CALL JEVECH('PBASECO','L',JBASEC)
      CALL JEVECH('PVECTUR','E',IVECT)
      IF (NFISS.GT.1) THEN
        CALL JEVECH('PFISNO','L',JFISNO)
        CALL JEVECH('PHEAVNO','L',JHEANO)
        CALL JEVECH('PHEAVFA','L',JHEAFA)
        CALL TECACH('OOO','PHEAVFA',2,JTAB,IRET)
        NCOMPH = JTAB(2)
      ENDIF
C     DIMENSSION DES GRANDEURS DANS LA CARTE
      CALL TECACH('OOO','PDONCO',2,JTAB,IRET)
      NCOMPD = JTAB(2)
      CALL TECACH('OOO','PPINTER',2,JTAB,IRET)
      NCOMPP = JTAB(2)
      CALL TECACH('OOO','PAINTER',2,JTAB,IRET)
      NCOMPA = JTAB(2)
      CALL TECACH('OOO','PBASECO',2,JTAB,IRET)
      NCOMPB = JTAB(2)
      CALL TECACH('OOO','PCFACE',2,JTAB,IRET)
      NCOMPC = JTAB(2)

C     STATUT POUR L'ÉLIMINATION DES DDLS DE CONTACT
      DO 30 I=1,MAX(1,NFH)*NNOS
        VSTNC(I) = 1
  30  CONTINUE
      CALL TEATTR(NOMTE,'S','XFEM',ENR,IBID)

C
C --- BOUCLE SUR LES FISSURES
C
      DO 90 IFISS = 1,NFISS
C
C --- RECUPERATION DIVERSES DONNEES CONTACT
C
         CALL XDOCON(ALGOCR,ALGOFR,CFACE ,CONTAC,COEFCP,
     &               COEFFP,COEFCR,COEFFR,ELC   ,FPG   ,
     &               IFISS ,IVFF  ,JCFACE,JDONCO,JLONCH,
     &               MU    ,NSPFIS,NCOMPD,NDIM  ,NFACE ,
     &               NINTER,NNOF  ,NOMTE ,NPGF  ,NPTF  ,
     &               RELA)
         IF(NINTER.EQ.0) GOTO 91
C
C --- RECUPERATION MATERIAU ET VARIABLES INTERNES COHESIF
C
        IF(ALGOCR.EQ.3) THEN
          CALL JEVECH('PMATERC','L',JMATE)
          CALL JEVECH('PCOHES' ,'L',JCOHES)
          CALL TECACH('OOO','PCOHES',2,JTAB,IRET)
          NCOMPV = JTAB(2)
        ENDIF
C
C --- RECUP MULTIPLICATEURS ACTIFS ET LEURS INDICES
C
         CALL XMULCO(CONTAC,DDLC,DDLM,JAINT,IFISS,
     &               JHEANO,VSTNC,LACT,.TRUE.,LELIM,NDIM,NFE,
     &               NFH,NFISS,NINTER,NLACT,NNO,
     &               NNOL,NNOM,NNOS,PLA,TYPMA)
C
C --- BOUCLE SUR LES FACETTES
C
        DO 100 IFA=1,NFACE
C
C --- BOUCLE SUR LES POINTS DE GAUSS DES FACETTES
C
          DO 110 IPGF=1,NPGF
C
C --- RECUPERATION DES STATUTS POUR LE POINT DE GAUSS
C
          ISSPG = NPGF*(IFA-1)+IPGF
          INDCO = ZI(JINDCO-1+NBSPG+ISSPG)
          IF(ALGOFR.NE.0) SEUIL = ZR(JSEUIL-1+NBSPG+ISSPG)
          IF(ALGOCR.EQ.3) THEN
            DO 2 I=1,NCOMPV
              COHES(I) = ZR(JCOHES+NCOMPV*(NBSPG+ISSPG-1)-1+I)
2           CONTINUE
          ENDIF
C
C --- PREPARATION DU CALCUL
C
          CALL XMPREP(CFACE ,CONTAC,ELREF ,ELREFC,ELC   ,
     &                FFC   ,FFP   ,FPG   ,JAINT ,JBASEC,
     &                JPTINT,IFA   ,IGEOM ,IPGF  ,JAC   ,
     &                JLST  ,LACT  ,ND    ,NDIM  ,NINTER,
     &                NLACT ,NNO   ,NNOS  ,NPTF  ,NVIT  ,
     &                RR    ,SINGU ,TAU1  ,TAU2)
C
C --- CALCUL REACTION DE CONTACT ET DE FROTTEMENT
C
          NVEC = 2
          CALL XXLAGM(FFC   ,IDEPL ,IDEPM ,
     &                LACT  ,NDIM  ,
     &                NNOL  ,PLA   ,REAC  ,REAC12,
     &                TAU1  ,TAU2,NVEC)
C
C --- CALCUL DES SECONDS MEMBRES DE CONTACT
C     .....................................

            IF (OPTION.EQ.'CHAR_MECA_CONT') THEN
            CALL XVCONT(ALGOCR,COHES ,COEFCP,COEFCR,DDLM  ,
     &                  DDLS  ,FFC   ,FFP   ,IDEPL ,
     &                  IDEPM ,IFA   ,IFISS ,ZI(JMATE) ,INDCO ,
     &                  IPGF  ,JAC   ,JFISNO,JHEAFA,
     &                  LACT  ,NCOMPH,ND    ,NDDL  ,NDIM  ,NFH,
     &                  NFISS ,NNO   ,NNOL  ,NNOS  ,
     &                  NVIT  ,PLA   ,RELA  ,REAC  ,RR    ,SINGU,
     &                  TAU1  ,TAU2  ,VTMP)
C
C --- CALCUL DES SECONDS MEMBRES DE FROTTEMENT
C     ........................................

            ELSEIF (OPTION.EQ.'CHAR_MECA_FROT'
     &              .AND.RELA.NE.3.D0.AND.RELA.NE.4.D0) THEN
              CALL XVFROT(ALGOFR,COEFFP,COEFFR,DDLM  ,DDLS  ,
     &                    FFC   ,FFP   ,IDEPL ,IDEPM ,IFA   ,
     &                    IFISS ,INDCO ,JAC   ,
     &                    JFISNO,JHEAFA,LACT  ,MU    ,NCOMPH,
     &                    ND    ,NDDL  ,NDIM  ,NFH   ,NFISS ,
     &                    NNO   ,NNOL  ,NNOS  ,
     &                    NVIT  ,PLA   ,REAC12,RR    ,SEUIL ,
     &                    SINGU ,TAU1  ,TAU2  ,VTMP)

            ELSE
              CALL ASSERT(OPTION.EQ.'CHAR_MECA_FROT' .OR.
     &                  OPTION.EQ.'CHAR_MECA_CONT')
            ENDIF

C --- FIN DE BOUCLE SUR LES POINTS DE GAUSS
 110      CONTINUE

C --- FIN DE BOUCLE SUR LES FACETTES
 100    CONTINUE
C --- FIN BOUCLE SUR LES FISSURES
        NBSPG  = NBSPG  + NSPFIS
91      CONTINUE
        JBASEC = JBASEC + NCOMPB
        JPTINT = JPTINT + NCOMPP
        JAINT  = JAINT  + NCOMPA
        JCFACE = JCFACE + NCOMPC
  90  CONTINUE
C
C-----------------------------------------------------------------------
C     COPIE DES CHAMPS DE SORTIES ET FIN
C-----------------------------------------------------------------------
C
      DO 900 I=1,NDDL
        ZR(IVECT-1+I)=VTMP(I)
 900  CONTINUE
C     SUPPRESSION DES DDLS DE DEPLACEMENT SEULEMENT POUR LES XHTC
      IF (NFH.NE.0) THEN
        CALL JEVECH('PSTANO' ,'L',JSTNO)
        CALL XTEDDL(NDIM,NFH,NFE,DDLS,NDDL,NNO,NNOS,ZI(JSTNO),
     &              .FALSE.,LBID,OPTION,NOMTE,RBID,ZR(IVECT),DDLM,
     &              NFISS,JFISNO)
      ENDIF
C     SUPPRESSION DES DDLS DE CONTACT
      IF (LELIM) THEN
        CALL XTEDDL(NDIM,NFH,NFE,DDLS,NDDL,NNO,NNOS,VSTNC,
     &              .TRUE.,.TRUE.,OPTION,NOMTE,RBID,ZR(IVECT),DDLM,
     &              NFISS,JFISNO)
      ENDIF
C
      CALL JEDEMA()
      END
