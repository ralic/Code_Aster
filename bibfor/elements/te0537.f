      SUBROUTINE TE0537(OPTION,NOMTE)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 29/05/2002   AUTEUR VABHHTS J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C              SEE THE FILE "LICENSE.TERMS" FOR INFORMATION ON USAGE AND
C              REDISTRIBUTION OF THIS FILE.
C ======================================================================
      IMPLICIT NONE
      CHARACTER*16 OPTION,NOMTE
C     ------------------------------------------------------------------
C     POU_D_EM : POUTRE MULTIFIBRE EULER BERNOULLI
C     CALCUL DE L'OPTION "DEGE_ELNO_DEPL"
C       - DEFORMATIONS GENERALISEES AUX NOEUDS
C     CALCUL DE L'OPTION EPSI_ELGA_DEPL
C       - DEFORMATIONS DANS LES FIBRES (SOUS PTS DE GAUSS) A PARTIR DES
C         DEPLACEMENTS
C     CALCUL DE L'OPTION SIEF_ELGA_DEPL
C       - CONTRAINTE DANS LES FIBRES (SOUS PTS DE GAUSS) COMPO LINEAIRE
C     ------------------------------------------------------------------
C IN  OPTION : K16 : NOM DE L'OPTION A CALCULER
C IN  NOMTE  : K16 : NOM DU TYPE ELEMENT
C     ------------------------------------------------------------------
C     ----- DEBUT COMMUNS NORMALISES  JEVEUX  --------------------------
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C     -----  FIN  COMMUNS NORMALISES  JEVEUX  --------------------------

      INTEGER JEFFG,JCONT,LORIEN,JDEPL,IMATE,ISECT,LX,NNO,NC,I
      PARAMETER (NNO=2,NC=6)
      CHARACTER*16 CH16
      REAL*8 UL(12),PGL(3,3),DEGE(6),XL,E,NU,ALPHA
      REAL*8 G,XJX,GXJX
      INTEGER NBFIB,NCARFI,JACF,JTAB(7)
      REAL*8 MATSCS(6),XL2,NX,TY,TZ,MX,MY,MZ
      REAL*8 DEP1,DEP2,DEP3,DEP4,CO6,CO12
      REAL*8 ZERO,UN,DEUX,SIX
      PARAMETER (ZERO=0.0D+0,UN=1.0D+0,DEUX=2.D+0,SIX=6.D+0)
      REAL*8 B(4),XI,WI
      INTEGER IN,IP,IPOS
C     ------------------------------------------------------------------
C      write(6,*) 'ajacot te0537',option

C     --- RECUPERATION DES CARACTERISTIQUES DES FIBRES :
      CALL JEVECH('PNBSP_I','L',I)
      NBFIB = ZI(I)
      CALL JEVECH('PFIBRES','L',JACF)
      NCARFI = 3

      IF (OPTION.EQ.'DEGE_ELNO_DEPL') THEN
        CALL JEVECH('PDEFOGR','E',JEFFG)
      ELSE IF (OPTION.EQ.'EPSI_ELGA_DEPL') THEN
        CALL TECACH(.TRUE.,.TRUE.,'PDEFORR',7,JTAB)
        IF (JTAB(7).NE. (NBFIB+6)) CALL UTMESS('F','TE0537','STOP1')
        JCONT = JTAB(1)
      ELSE IF (OPTION.EQ.'SIEF_ELGA_DEPL') THEN
        CALL JEVECH('PMATERC','L',IMATE)
        CALL TECACH(.TRUE.,.TRUE.,'PCONTRR',7,JTAB)
        IF (JTAB(7).NE. (NBFIB+6)) CALL UTMESS('F','TE0537','STOP1')
        JCONT = JTAB(1)
      ELSE
        CH16 = OPTION
        CALL UTMESS('F','ELEMENTS DE POUTRE MULTIFIBRES (TE0537)',
     +              'L''OPTION "'//CH16//'" EST INCONNUE')
      END IF
      CALL JEVECH('PGEOMER','L',LX)
      CALL JEVECH('PCAORIE','L',LORIEN)
      CALL JEVECH('PDEPLAR','L',JDEPL)


C     --- RECUPERATION DES COORDONNEES DES NOEUDS ---
      LX = LX - 1
      XL = SQRT((ZR(LX+4)-ZR(LX+1))**2+ (ZR(LX+5)-ZR(LX+2))**2+
     +     (ZR(LX+6)-ZR(LX+3))**2)
      IF (XL.EQ.ZERO) THEN
        CH16 = ' ?????????'
        CALL UTMESS('F','ELEMENTS DE POUTRE (TE0537)',
     +              'NOEUDS CONFONDUS POUR UN ELEMENT: '//CH16(:8))
      END IF

C     --- RECUPERATION DES ORIENTATIONS ---
      CALL MATROT(ZR(LORIEN),PGL)

C     --- PASSAGE DES DEPLACEMENTS DANS LE REPERE LOCAL ---
      CALL UTPVGL(NNO,NC,PGL,ZR(JDEPL),UL)

C --- SI OPTION DEGE_ELNO_DEPL, ON CALCULE LES DEF. GEN.AUX NOEUDS
      IF ( OPTION .EQ. 'DEGE_ELNO_DEPL' ) THEN
        DO 10 IN=1,2
          CALL PMFPTI(-IN,XL,XI,WI,B)
          CALL PMFDGE(B,UL,DEGE)
          IPOS=JEFFG+6*(IN-1)
          DO 12 I = 1, 6
            ZR(IPOS+I-1) = DEGE(I)
 12       CONTINUE
 10     CONTINUE
        GOTO 50
      ENDIF

C --- SI OPTION EPSI_ELGA_DEPL OU SIEF_ELGA_DEPL

C --- BOUCLE SUR LES POINTS DE GAUSS
      DO 20 IP=1,2
C ---   MATRICE B PUIS DEGE PUIS DEFORMATIONS SUR LES FIBRES
        CALL PMFPTI(IP,XL,XI,WI,B)
        CALL PMFDGE(B,UL,DEGE)
        IPOS=JCONT+(NBFIB+6)*(IP-1)
        CALL PMFDEF(NBFIB,NCARFI,ZR(JACF),DEGE,ZR(IPOS))
  20  CONTINUE

C --- SI EPSI_ELGA_DEPL JCONT EST L'ADRESSE PDEFORR, ON SORT
      IF(OPTION .EQ. 'EPSI_ELGA_DEPL') GOTO 50

C --- SI OPTION SIEF_ELGA_DEPL ON CONTINUE

C --- CARACTERISTIQUES ELASTIQUES (PAS DE TEMPERATURE POUR L'INSTANT)
      CALL MATELA(ZI(IMATE),0,0.D0,E,NU,ALPHA)
      G = E/ (DEUX* (UN+NU))
C --- TORSION A PART
      CALL JEVECH('PCAGNPO','L',ISECT)
      XJX = ZR(ISECT)
      GXJX = G*XJX
C --- ON MULTIPLIE LES ZR(JCONT) (DEFORMATIONS) PAR E
C     POUR AVOIR DES CONTRAINTES
      DO 30 IP=1,2
        IPOS=JCONT+(NBFIB+6)*(IP-1)
        DO 32 I=1,NBFIB
          ZR(IPOS+I-1)=ZR(IPOS+I-1) * E
  32    CONTINUE
  30  CONTINUE

C --- ON DOIT AJOUTER LES EFFORTS GENERALISES (VOIR TE0535 ET TE0536)
C     ICI ON FAIT KELE * UL
C --- ON COMMENCE PAR CALCULER LE K SECTION
      CALL PMFITG(NBFIB,NCARFI,ZR(JACF),MATSCS)
C     ON MULTIPLIE PAR E (CONSTANT SUR LA SECTION EN ELASTICITE)
      DO 40 I = 1,6
        MATSCS(I) = E*MATSCS(I)
   40 CONTINUE

C --- PUIS ON FAIT KELE*UL (TOUTES LES INTEGRATIONS SONT FAITES
C                           ANALYTIQUEMENT)
      XL2 = XL/DEUX
      CO6 = SIX/XL/XL
      CO12 = CO6/XL2
C --- EFORTS TRANCHANTS
C     RAPPEL: KS22=MATSEC(5), KS33=MATSEC(4), KS23=-MATSEC(6)
      DEP1 = UL(11) + UL(5)
      DEP2 = UL(12) + UL(6)
      DEP3 = UL(9) - UL(3)
      DEP4 = UL(8) - UL(2)
      TY = CO6* (MATSCS(6)*DEP1-MATSCS(4)*DEP2) +
     +     CO12* (MATSCS(6)*DEP3+MATSCS(4)*DEP4)
      TZ = CO6* (MATSCS(5)*DEP1-MATSCS(6)*DEP2) +
     +     CO12* (MATSCS(5)*DEP3+MATSCS(6)*DEP4)
C --- EFORT NORMAL ET MOMENTS
C     RAPPEL: KS11=MATSEC(1), KS12=MATSEC(3), KS13=-MATSEC(2)
      DEP1 = UL(7) - UL(1)
      DEP2 = UL(12) - UL(6)
      DEP3 = UL(11) - UL(5)
      NX = (MATSCS(1)*DEP1-MATSCS(2)*DEP2+MATSCS(3)*DEP3)/XL
      MY = (MATSCS(3)*DEP1-MATSCS(6)*DEP2+MATSCS(5)*DEP3)/XL
      MZ = (-MATSCS(2)*DEP1-MATSCS(6)*DEP3+MATSCS(4)*DEP2)/XL
C --- TORSION
      MX = GXJX* (UL(10)-UL(4))/XL

      IPOS=JCONT+NBFIB
      ZR(IPOS-1+1)=NX
      ZR(IPOS-1+2)=TY
      ZR(IPOS-1+3)=TZ
      ZR(IPOS-1+4)=MX
      ZR(IPOS-1+5)=MY
      ZR(IPOS-1+6)=MZ

   50 CONTINUE


      END
