      SUBROUTINE TE0576(OPTION,NOMTE)
      IMPLICIT   NONE
      INCLUDE 'jeveux.h'

      CHARACTER*16 OPTION,NOMTE
C.......................................................................
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 15/01/2013   AUTEUR DELMAS J.DELMAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2013  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.

C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.

C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================

C FONCTIONS REALISEES:

C      CALCUL DE LA DENSITE D'ENERGIE POTENTIELLE THERMOELASTIQUE
C      A L'EQUILIBRE POUR LES ELEMENTS ISOPARAMETRIQUES 3D
C      .SOIT AUX POINTS D'INTEGRATION : OPTION 'ENEL_ELGA'

C      OPTIONS : 'ENEL_ELGA'

C      CALCUL DE LA DENSITE D'ENERGIE TOTALE
C      A L'EQUILIBRE POUR LES ELEMENTS ISOPARAMETRIQUES 3D
C      .SOIT AUX POINTS D'INTEGRATION : OPTION 'ETOT_ELGA'

C      OPTIONS : 'ETOT_ELGA'


C ENTREES  ---> OPTION : OPTION DE CALCUL
C          ---> NOMTE  : NOM DU TYPE ELEMENT
C.......................................................................

      INTEGER JGANO,NBSIGM,NDIM,NNO,I,NNOS,IPOIDS,IVF,NBNOMX,
     &        NBCONT,NPG1,NBSIG,IGAU,ISIG,IGEOM,IDIM,ITEMPS,NBVARI,
     &        IMATE,IDENER,IDFDE,IDEPL,IDEPLM,IDEPMM,IDVARI,
     &        IDSIG,IDSIGM,MXCMEL,IRET,IDENEM,JTAB(7)
      PARAMETER (NBNOMX=27)
      PARAMETER (NBCONT=6)
      PARAMETER (MXCMEL=162)
      REAL*8 EPSI(NBCONT),REPERE(7),INSTAN,ZERO,UNDEMI,ENELEM
      REAL*8 ENERPG(NBNOMX),XYZGAU(3),XYZ(3)
      REAL*8 NHARM,DEUX,INTEG1,INTEG2,INTEG,R
      REAL*8 EPSIM(NBCONT),DELTA(NBCONT),EPSS(MXCMEL)
      REAL*8 EPSSM(MXCMEL),SIGMM(NBCONT),SIGMA(NBCONT),F(3,3)
      REAL*8 DFDX(27),DFDY(27),DFDZ(27),POIDS,EPSBID(6),DFDBID(27*3)
      CHARACTER*4 FAMI
      CHARACTER*16 COMPOR(3)
      LOGICAL GRAND
C DEB ------------------------------------------------------------------

C ---- CARACTERISTIQUES DU TYPE D'ELEMENT :
C ---- GEOMETRIE ET INTEGRATION
C      ------------------------
      FAMI = 'RIGI'
      CALL ELREF4(' ',FAMI,NDIM,NNO,NNOS,NPG1,IPOIDS,IVF,IDFDE,JGANO)

C ---- NOMBRE DE CONTRAINTES ASSOCIE A L'ELEMENT
C      -----------------------------------------
      NBSIG = NBSIGM()

C --- INITIALISATIONS :
C     -----------------
      ZERO = 0.0D0
      UNDEMI = 0.5D0
      DEUX = 2.0D0
      NHARM = ZERO
      ENELEM = ZERO

      DO 20 I = 1,NBNOMX
        ENERPG(I) = ZERO
   20 CONTINUE

C ---- RECUPERATION DES COORDONNEES DES CONNECTIVITES
C      ----------------------------------------------
      CALL JEVECH('PGEOMER','L',IGEOM)

      IF (OPTION(1:4).EQ.'ENEL') THEN

C ----   RECUPERATION DU MATERIAU
C        ------------------------
         CALL JEVECH('PMATERC','L',IMATE)

C ----   RECUPERATION  DES DONNEES RELATIVES AU REPERE D'ORTHOTROPIE
C        -----------------------------------------------------------
C        COORDONNEES DU BARYCENTRE ( POUR LE REPRE CYLINDRIQUE )
         XYZ(1) = ZERO
         XYZ(2) = ZERO
         XYZ(3) = ZERO
         DO 150 I = 1,NNO
            DO 140 IDIM = 1,NDIM
              XYZ(IDIM) = XYZ(IDIM)+ZR(IGEOM+IDIM+NDIM*(I-1)-1)/NNO
140         CONTINUE
150      CONTINUE
         CALL ORTREP(ZI(IMATE),NDIM,XYZ,REPERE)

C ---    RECUPERATION DU CHAMP DE DEPLACEMENT A L'INSTANT COURANT :
C        --------------------------------------------------------
         CALL JEVECH('PDEPLAR','L',IDEPL)

C ----   RECUPERATION DU CHAMP DE CONTRAINTES AUX POINTS D'INTEGRATION
C        -------------------------------------------------------------
         CALL JEVECH('PCONTRR','L',IDSIG)

C ----    RECUPERATION DE L'INSTANT DE CALCUL
C         -----------------------------------
         CALL TECACH('ONN','PTEMPSR','L',1,ITEMPS,IRET)
         IF (ITEMPS.NE.0)  INSTAN = ZR(ITEMPS)

C ----   RECUPERATION DU CHAMP DE VARIABLES INTERNES  :
C        N'EXISTE PAS EN LINEAIRE
         CALL TECACH('ONN','PVARIGR','L',7,JTAB,IRET)
         IF (IRET.EQ.0) THEN
            IDVARI=JTAB(1)
            NBVARI = MAX(JTAB(6),1)*JTAB(7)
         ELSE
            IDVARI=1
            NBVARI=0
         ENDIF

      END IF

C ----RECUPERATION DU TYPE DE COMPORTEMENT  :
C     N'EXISTE PAS EN LINEAIRE
      CALL TECACH('NNN','PCOMPOR','L',7,JTAB,IRET)
      COMPOR(1)='ELAS'
      COMPOR(2)=' '
      COMPOR(3)='PETIT'
      IF (IRET.EQ.0) THEN
         COMPOR(1)=ZK16(JTAB(1))
         COMPOR(3)=ZK16(JTAB(1)+2)
      ENDIF

C     GRANDES DEFORMATIONS

      IF ((COMPOR(3).EQ.'SIMO_MIEHE').OR.
     &    (COMPOR(3).EQ.'GDEF_LOG').OR.
     &    (COMPOR(3).EQ.'GDEF_HYPO_ELAS')) THEN
         GRAND = .TRUE.
      ELSE
         GRAND = .FALSE.
      ENDIF


C --- CAS DU CALCUL DE LA DENSITE D'ENERGIE TOTALE :
C     ============================================
      IF (OPTION(1:4).EQ.'ETOT') THEN

         IF (GRAND) THEN
            CALL U2MESG('F','COMPOR1_79',1,COMPOR(3),0,0,0,0.D0)
         ENDIF

C ---    RECUPERATION DU CHAMP DE DEPLACEMENT A L'INSTANT COURANT :
C        --------------------------------------------------------
         CALL JEVECH('PDEPLR','L',IDEPL)

C ---    RECUPERATION EVENTUELLE DU CHAMP DE DEPLACEMENT A
C ---    L'INSTANT PRECEDENT :
C        -------------------
         CALL TECACH('NNN','PDEPLM','L',1,IDEPLM,IRET)
         IF (IDEPLM.NE.0) THEN
           CALL JEVECH('PDEPLM','L',IDEPMM)
         END IF

C ---    RECUPERATION DU CHAMP DE CONTRAINTES AUX POINTS D'INTEGRATION
C ---    A L'INSTANT COURANT :
C        -------------------
         CALL JEVECH('PCONTPR','L',IDSIG)

C ---    RECUPERATION EVENTUELLE DU CHAMP DE CONTRAINTES A
C ---    L'INSTANT PRECEDENT :
C        -------------------
         CALL TECACH('NNN','PCONTMR','L',1,IDSIGM,IRET)
         IF (IDSIGM.NE.0) THEN
           CALL JEVECH('PCONTMR','L',IDSIGM)
         END IF

C ---    CALCUL DU CHAMP DE DEFORMATIONS AU PREMIER ORDRE
C ---    CORRESPONDANT AU CHAMP DE DEPLACEMENT COURANT :
C        ---------------------------------------------
         CALL EPS1MC(NNO,NDIM,NBSIG,NPG1,IPOIDS,IVF,IDFDE,
     +               ZR(IGEOM),ZR(IDEPL),NHARM,EPSS)

C ---    CALCUL EVENTUEL DU CHAMP DE DEFORMATIONS AU PREMIER ORDRE
C ---    CORRESPONDANT AU CHAMP DE DEPLACEMENT A L'INSTANT PRECEDENT :
C        -----------------------------------------------------------
         IF (IDEPLM.NE.0) THEN
         CALL EPS1MC(NNO,NDIM,NBSIG,NPG1,IPOIDS,IVF,IDFDE,
     +               ZR(IGEOM),ZR(IDEPMM),NHARM,EPSSM)
         END IF

      END IF

C ---- BOUCLE SUR LES POINTS D'INTEGRATION :
C      ===================================
      DO 110 IGAU = 1,NPG1

C  --    CALCUL DU JACOBIEN AU POINT D'INTEGRATION COURANT :
C        -------------------------------------------------
        CALL DFDM3D ( NNO, IGAU, IPOIDS, IDFDE,
     &                ZR(IGEOM), DFDX, DFDY, DFDZ, POIDS )

        DO 30 ISIG = 1,NBSIG
          EPSI(ISIG) = ZERO
   30   CONTINUE

C  --      COORDONNEES AU POINT D'INTEGRATION
C  --      COURANT
C          -------
        XYZGAU(1) = ZERO
        XYZGAU(2) = ZERO
        XYZGAU(3) = ZERO

        DO 50 I = 1,NNO

          DO 40 IDIM = 1,NDIM
            XYZGAU(IDIM) = XYZGAU(IDIM) +
     &                     ZR(IVF+I+NNO* (IGAU-1)-1)*ZR(IGEOM+IDIM+
     &                     NDIM* (I-1)-1)
   40     CONTINUE

   50   CONTINUE

C  --    CALCUL DE LA DENSITE D'ENERGIE POTENTIELLE THERMOELASTIQUE :
C        ==========================================================
        IF (OPTION(1:4).EQ.'ENEL') THEN

C --- TENSEUR DES CONTRAINTES AU POINT D'INTEGRATION COURANT :

          DO 51 I = 1,NBSIG
            SIGMA(I) = ZR(IDSIG+ (IGAU-1)*NBSIG+I-1)
   51     CONTINUE

C --- CALCUL DU JACOBIEN AU POINT D'INTEGRATION COURANT :
          CALL NMGEOM(3,NNO,.FALSE.,GRAND,ZR(IGEOM),IGAU,IPOIDS,
     &              IVF,IDFDE,ZR(IDEPL),.TRUE.,POIDS,DFDBID,F,EPSBID,R)

C ---     CALCUL DE L'ENERGIE ELASTIQUE AU POINT D'INTEGRATION COURANT

          CALL ENELPG(FAMI,ZI(IMATE),INSTAN,IGAU,REPERE,XYZGAU,COMPOR,
     &    F,SIGMA,NBVARI,ZR(IDVARI+(IGAU-1)*NBVARI),ENERPG(IGAU))


C  --    CALCUL DE LA DENSITE D'ENERGIE TOTALE :
C        =====================================
        ELSE IF (OPTION(1:4).EQ.'ETOT') THEN

C  --      TENSEURS DES DEFORMATIONS  ET DES CONTRAINTES AU PAS DE
C  --      TEMPS COURANT ET AU PAS DE TEMPS PRECEDENT S'IL Y A LIEU,
C  --      AU POINT D'INTEGRATION COURANT :
C          ------------------------------
          DO 80 I = 1,NBSIG
            EPSI(I) = EPSS(I+ (IGAU-1)*NBSIG)
            IF (IDEPLM.NE.0) THEN
              EPSIM(I) = EPSSM(I+ (IGAU-1)*NBSIG)
            END IF
            SIGMA(I) = ZR(IDSIG+ (IGAU-1)*NBSIG+I-1)
            IF (IDSIGM.NE.0) THEN
              SIGMM(I) = ZR(IDSIGM+ (IGAU-1)*NBSIG+I-1)
            END IF
   80     CONTINUE

          IF (IDEPLM.NE.0) THEN
            DO 90 I = 1,NBSIG
              DELTA(I) = EPSI(I) - EPSIM(I)
   90       CONTINUE
          ELSE
            DO 100 I = 1,NBSIG
              DELTA(I) = 0.D0
  100       CONTINUE
          END IF

C  --      CALCUL DES TERMES A SOMMER POUR OBTENIR LA DENSITE
C  --      D'ENERGIE TOTALE :
C          ----------------
          INTEG1 = SIGMA(1)*DELTA(1) + SIGMA(2)*DELTA(2) +
     &             SIGMA(3)*DELTA(3) + DEUX*SIGMA(4)*DELTA(4) +
     &             DEUX*SIGMA(5)*DELTA(5) + DEUX*SIGMA(6)*DELTA(6)

          IF (IDEPLM.NE.0 .AND. IDSIGM.NE.0) THEN
            INTEG2 = SIGMM(1)*DELTA(1) + SIGMM(2)*DELTA(2) +
     &               SIGMM(3)*DELTA(3) + DEUX*SIGMM(4)*DELTA(4) +
     &               DEUX*SIGMM(5)*DELTA(5) + DEUX*SIGMM(6)*DELTA(6)

            ENERPG(IGAU) = UNDEMI* (INTEG1+INTEG2)
          ELSE

C  --        CAS D'ORDRE NUMERO 1 :
C            --------------------
            INTEG = SIGMA(1)*EPSI(1) + SIGMA(2)*EPSI(2) +
     &              SIGMA(3)*EPSI(3) + DEUX*SIGMA(4)*EPSI(4) +
     &              DEUX*SIGMA(5)*EPSI(5) + DEUX*SIGMA(6)*EPSI(6)

            ENERPG(IGAU) = UNDEMI*INTEG

          END IF

          ENELEM = ENELEM + ENERPG(IGAU)*POIDS

        END IF

  110 CONTINUE

C ---- RECUPERATION DU CHAMP DES DENSITES D'ENERGIE DE DEFORMATION
C ---- ELASTIQUE EN SORTIE
C      -------------------
      CALL JEVECH('PENERDR','E',IDENER)

C ---- OPTIONS ENEL_* ET ETOT_*
C      ==============================
      IF (OPTION(1:4).EQ.'ETOT') THEN
        CALL JEVECH('PENERDM','L',IDENEM)
C
C ----   OPTION ETOT_ELGA
C        ================
        IF (OPTION.EQ.'ETOT_ELGA') THEN
          DO 120 IGAU = 1,NPG1
            ZR(IDENER+IGAU-1)=ZR(IDENEM+IGAU-1)+ENERPG(IGAU)
  120     CONTINUE
C
C ----   OPTION ETOT_ELEM
C        ================
        ELSE IF (OPTION.EQ.'ETOT_ELEM') THEN
          ZR(IDENER) = ZR(IDENEM)+ENELEM
        END IF
      ELSE
C
C ----   OPTION ENEL_ELGA
C        ================
        IF (OPTION.EQ.'ENEL_ELGA') THEN
          DO 220 IGAU = 1,NPG1
            ZR(IDENER+IGAU-1) = ENERPG(IGAU)
  220     CONTINUE
        END IF
      END IF
      END
