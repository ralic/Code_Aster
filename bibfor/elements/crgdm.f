      SUBROUTINE CRGDM (IMATE,COMPOR,T,LAMBDA,DEUXMU,LAMF,DEUMUF,
     &                  GT,GC,GF,SEUIL,ALPHA,ALFMC,EP,LRGM)
      IMPLICIT NONE
C RESPONSABLE SFAYOLLE S.FAYOLLE
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 19/12/2011   AUTEUR SFAYOLLE S.FAYOLLE 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================

C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ---------------------
C     CHARACTER*32 JEXNUM,JEXNOM,JEXR8,JEXATR
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C --------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ---------------------


      LOGICAL LRGM
      CHARACTER*16 COMPOR,PHENOM
      INTEGER      IMATE, T(2,2)
      REAL*8       LAMBDA, DEUXMU, DEUMUF, LAMF
      REAL*8       GT, GC, GF, SEUIL, ALPHA, ALFMC
C ----------------------------------------------------------------------
C
C BUT : LECTURE DES PARAMETRES MATERIAU POUR LE MODELE GLRC_DM
C
C
C IN:
C       IMATE   : ADRESSE DU MATERIAU
C       COMPOR  : COMPORTMENT
C       EP      : EPAISSEUR DE LA PLAQUE
C OUT:
C       LAMBDA  : PARAMETRE D ELASTICITE - MEMBRANE
C       DEUXMU  : PARAMETRE D ELASTICITE - MEMBRANE
C       LAMF    : PARAMETRE D ELASTICITE - FLEXION
C       DEUMUF  : PARAMETRE D ELASTICITE - FLEXION
C       GT      : PARAMETRE GAMMA POUR LA MEMBRANE EN TRACTION 
C       GC      : PARAMETRE GAMMA POUR LA MEMBRANE EN COMPRESSION
C       GF      : PARAMETRE GAMMA POUR LA FLEXION
C       SEUIL   : INITIAL MEMBRANE
C       ALPHA   : PARAMETRE DE SEUIL FLEXION
C       ALFMC   : PARAMETRE DE DECOUPLAGE SEUILS TRACTION-COMPRESSION
C ----------------------------------------------------------------------

      REAL*8      VALRES(7), E, NU, EP, EF, SYCMAX, RMESG(2)
      REAL*8      SYT, SYC, SYF, NUF, DELAS(6,6)

      INTEGER ICODRE(7)
      CHARACTER*8 NOMRES(7)

      T(1,1)=1
      T(1,2)=3
      T(2,1)=3
      T(2,2)=2

      IF ((.NOT.( COMPOR(1:7) .EQ. 'GLRC_DM'))) THEN
        CALL U2MESK('F','ELEMENTS4_65',1,COMPOR)
      ENDIF

      CALL R8INIR(6*6,0.0D0,DELAS,1)
      PHENOM = 'GLRC_DM         '
      IF(PHENOM . NE . 'GLRC_DM         ' ) THEN
        CALL U2MESK('F','ELEMENTS2_32',1,PHENOM)
      ENDIF

C     EPAISSEUR
C      CALL JEVECH('PCACOQU','L',JCOQU)
C      EPAIS = ZR(JCOQU)
C      NOMRES(1)  = 'EPAIS'
C      CALL RCVALA(IMATE,' ',PHENOM,0,' ',R8B,1,NOMRES,
C     &              VALRES,CODRES,'FM')
C      IF(VALRES(1) .NE. EPAIS) THEN
C        VALRES(2) = EPAIS
C        CALL U2MESG('F','ELEMENTS5_42',0,' ',0,0,2,VALRES)
C      ENDIF

C    LECTURE DES CARACTERISTIQUES DU MATERIAU
      NOMRES(1) = 'E'
      NOMRES(2) = 'NU'

      CALL RCVALA(IMATE,' ','ELAS',0,' ',0.D0,2,
     &              NOMRES,VALRES,ICODRE,1)

      E  = VALRES(1)
      NU = VALRES(2)

      LAMBDA = E * NU / (1.D0+NU) / (1.D0 - 2.D0*NU)*EP
      DEUXMU = E/(1.D0+NU)*EP

      NOMRES(1) = 'EF'
      NOMRES(2) = 'NUF'

      CALL RCVALA(IMATE,' ','GLRC_DM   ',0,' ',0.D0,2,
     &              NOMRES,VALRES,ICODRE, 0)

      IF(ICODRE(1).EQ.0) THEN
        EF = VALRES(1)
      ELSE
        EF = E
      ENDIF

      IF(ICODRE(2).EQ.0) THEN
        NUF = VALRES(2)
      ELSE
        NUF = NU
      ENDIF

      LAMF   = EF*NUF/(1.D0-NUF*NUF) *EP**3/12.0D0
      DEUMUF = EF/(1.D0+NUF) *EP**3/12.0D0

C    LECTURE DES CARACTERISTIQUES D'ENDOMMAGEMENT
      NOMRES(1) = 'GAMMA_T'
      NOMRES(2) = 'GAMMA_C'
      NOMRES(3) = 'GAMMA_F'
      NOMRES(4) = 'SYT'
      NOMRES(5) = 'SYC'
      NOMRES(6) = 'SYF'
      NOMRES(7) = 'ALPHA_C'
      CALL RCVALA(IMATE,' ','GLRC_DM',0,' ',0.D0,7,
     &            NOMRES,VALRES,ICODRE,0)

      GT    = VALRES(1)
      GF    = VALRES(3)
      SYT   = VALRES(4)
      SYF   = VALRES(6)
      ALFMC = VALRES(7)

      IF (ICODRE(2).EQ.0 .AND. ICODRE(5).EQ.0) THEN
C - ON EST DANS LE CAS DE DEFI_GLRC
        GC  = VALRES(2)
        SYC = VALRES(5)
      ELSEIF (ICODRE(2).EQ.0) THEN
C - ON EST DANS LE CAS DE DEFI_MATERIAU
        GC  = VALRES(2)

        IF (GC .EQ. 1.D0 .AND. GT .EQ. 1.D0) THEN
          CALL U2MESR('F','ALGORITH6_1',0,RMESG)
        END IF

        SYC = (1.D0-NU)*(1.D0+2.D0*NU)*(1.D0-GT)+NU**2*(1.D0-GC)
        SYC = SYC/((1.D0-NU)*(1.D0+2.D0*NU)*(1.D0-GC)+NU**2*(1.D0-GT))
        SYC = -SQRT(SYC*SYT**2)
      ELSEIF (ICODRE(5).EQ.0) THEN
C - ON EST DANS LE CAS DE DEFI_MATERIAU
        SYC = VALRES(5)
        SYCMAX = SYT*SQRT((1.D0-NU)*(1.D0+2.D0*NU))/NU

        IF (VALRES(5) .GT. SYCMAX) THEN
          RMESG(1) = SYC
          RMESG(2) = SYCMAX
          CALL U2MESR('F','ALGORITH6_2',2,RMESG)
        END IF

        GC  = (1.D0-GT)*(SYT**2*(1.D0-NU)*(1.D0+2.D0*NU)-SYC**2*NU**2)
        GC  = GC/(SYC**2*(1.D0-NU)*(1.D0+2.D0*NU)-SYT**2*NU**2)
        GC  = 1.D0 - GC
      ELSE
        GC = 1.D0
      END IF

      IF (GC .EQ. 1.D0 .AND. GT .EQ. 1.D0) THEN
        CALL U2MESR('F','ALGORITH6_1',0,RMESG)
      END IF

      IF (ICODRE(7).EQ.0 .AND. GC .NE. 1.D0)THEN
        ALFMC = VALRES(7)
      ELSE
        IF (GC .EQ. 1.D0) THEN
          ALFMC = 1.D0
        ELSE
          ALFMC=(1.D0-GC)*(SYC**2*(1.D0-NU)*(1.D0+2.D0*NU)/SYT**2-NU**2)
     &         /((1.D0-GT)*((1.D0-NU)*(1.D0+2.D0*NU)-(NU*SYC/SYT)**2))
        ENDIF
      ENDIF

C    CALCUL DU SEUIL (k0 DANS R7.01.32) ET DE ALPHA
      IF (LRGM) THEN
        ALPHA = 1.D0
        ALFMC = 1.D0
        SEUIL = 0.D0
      ELSE
        SEUIL = LAMBDA*(1.0D0 - GT)*(1.0D0-2.0D0*NU)**2  +
     &          DEUXMU*(1.0D0 - GT + (1.0D0 - GC)*NU**2/ALFMC)

        SEUIL = SEUIL/(2.0D0*(LAMBDA*(1.0D0-2.0D0*NU) + DEUXMU))**2
        SEUIL = SEUIL*SYT**2

        IF (SEUIL .NE. 0.D0) THEN
          ALPHA = LAMF*(1.0D0-NUF)**2 + DEUMUF
          ALPHA = ALPHA/(2.0D0*(LAMF*(1.0D0-NUF) + DEUMUF)**2)
          ALPHA = ALPHA*(1.0D0 - GF)*SYF**2/SEUIL
        ELSE
          CALL U2MESR('F','ALGORITH6_3',0,RMESG)
        END IF
      END IF

      END
