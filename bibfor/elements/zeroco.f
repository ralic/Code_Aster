      SUBROUTINE ZEROCO(X,Y)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 29/09/2006   AUTEUR VABHHTS J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================

      IMPLICIT NONE
      REAL*8   X(4),Y(4)
C ----------------------------------------------------------------------
C  RESOLUTION D'EQUATIONS SCALAIRES PAR UNE METHODE DE CORDES
C ----------------------------------------------------------------------
C VAR X(1) BORNE DE L'INTERVALLE DE RECHERCHE  TQ Y(1) < 0
C VAR X(2) BORNE DE L'INTERVALLE DE RECHERCHE  TQ Y(2) > 0
C VAR X(3) SOLUTION X(N-1) PUIS SOLUTION EN X(N)
C VAR X(4) SOLUTION X(N)   PUIS SOLUTION EN X(N+1)
C VAR Y(I) VALEUR DE LA FONCTION EN X(I)
C ----------------------------------------------------------------------

      REAL*8   XP,P,S
      REAL*8   INFINI, R8MAEM

C    TEST DES PRE-CONDITIONS
      IF (Y(1).GT.0 .OR. Y(2).LT.0) CALL U2MESS('F','ELEMENTS4_61')


C    TRAITEMENT DES SITUATIONS AVEC BORNES INFINIES -> DICHOTOMIE
      INFINI = R8MAEM()
      IF (ABS(Y(3)).EQ.INFINI .OR. ABS(Y(4)).EQ.INFINI) THEN
        CALL ZERODI(X,Y)
        GOTO 9999
      END IF

C    REACTUALISATION DE L'INTERVALLE DE RECHERCHE
      IF (Y(4) .LT. 0.D0) THEN
        X(1) = X(4)
        Y(1) = Y(4)
      END IF

      IF (Y(4) .GT. 0.D0) THEN
        X(2) = X(4)
        Y(2) = Y(4)
      END IF

C    CONSTRUCTION D'UN NOUVEL ESTIME
      IF (X(3).EQ.X(4))
     &  CALL U2MESS('F','ALGORITH9_84')
      P = (Y(4)-Y(3)) / (X(4)-X(3))
      IF (P.NE.0.D0) THEN
        XP = X(3) - Y(3)/P
        S = (XP-X(1)) / (X(2)-X(1))
      ELSE
        S = -2.D0
      END IF

C    CORRECTION DU NOUVEL ESTIME SI NECESSAIRE (EN DEHORS DES BORNES)
      IF (S.LE.0.D0 .OR. S.GE.1.D0) THEN
        IF (ABS(Y(1)).EQ.INFINI .OR. ABS(Y(2)).EQ.INFINI) THEN
          XP = (X(1)+X(2)) / 2
        ELSE
          P  = (Y(2)-Y(1)) / (X(2)-X(1))
          XP = X(1) - Y(1)/P
        END IF
      END IF

C    DECALAGE DES ITERES
      X(3) = X(4)
      X(4) = XP
      Y(3) = Y(4)

 9999 CONTINUE
      END
