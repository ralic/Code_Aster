      SUBROUTINE XBSIG (NDIM,NNOP,NFH,NFE,DDLC,DDLM,IGEOM,JPINTT,
     &                   CNSET,HEAVT,LONCH,BASLOC,SIGMA,NBSIG,
     &                   LSN,LST,IVECTU,JPMILT,NFISS,JFISNO)

       IMPLICIT NONE
C
       INTEGER       NDIM,NNOP,NFH,NFE,DDLC,DDLM,IGEOM,NBSIG,IVECTU
       INTEGER       JFISNO,NFISS
       INTEGER       CNSET(4*32),HEAVT(*),LONCH(10),JPINTT,JPMILT
       REAL*8        BASLOC(*),SIGMA(*),LSN(NNOP),LST(NNOP)

C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 26/04/2011   AUTEUR DELMAS J.DELMAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE GENIAUT S.GENIAUT
C TOLE CRS_1404
C
C      BSIGMC  -- CALCUL DES FORCES INTERNES B*SIGMA AUX NOEUDS
C                 DE L'ELEMENT DUES AU CHAMP DE CONTRAINTES SIGMA
C                 DEFINI AUX POINTS D'INTEGRATION DANS LE CADRE DE
C                 LA MÉTHODE X-FEM
C
C IN  NDIM    : DIMENSION DE L'ESPACE
C IN  NNOP    : NOMBRE DE NOEUDS DE L'ELEMENT PARENT
C IN  NFH     : NOMBRE DE FONCTIONS HEAVYSIDE
C IN  NFE     : NOMBRE DE FONCTIONS SINGULIÈRES D'ENRICHISSEMENT
C IN  DDLC    : NOMBRE DE DDLS DE CONTACT (PAR NOEUD)
C IN  DDLM    : NOMBRE DE DDL PAR NOEUD MILIEU
C IN  IGEOM   : COORDONEES DES NOEUDS
C IN  PINTT   : COORDONNÉES DES POINTS D'INTERSECTION
C IN  CNSET   : CONNECTIVITE DES SOUS-ELEMENTS
C IN  HEAVT   : VALEURS DE L'HEAVISIDE SUR LES SS-ELTS
C IN  LONCH   : LONGUEURS DES CHAMPS UTILISÉES
C IN  BASLOC  : BASE LOCALE AU FOND DE FISSURE
C IN  SIGMA   : CONTRAINTES DE CAUCHY AUX POINTS DE GAUSS DES SOUS-ÉLTS
C IN  NBSIG   : NOMBRE DE CONTRAINTES ASSOCIE A L'ELEMENT
C IN  LSN     : VALEUR DE LA LEVEL SET NORMALE AUX NOEUDS PARENTS
C IN  LST     : VALEUR DE LA LEVEL SET TANGENTE AUX NOEUDS PARENTS
C IN  PMILT   : COORDONNEES DES POINTS MILIEUX
C IN  NFISS   : NOMBRE DE FISSURES "VUES" PAR L'ÉLÉMENT
C IN  JFISNO  : POINTEUR DE CONNECTIVITÉ FISSURE/HEAVISIDE

C OUT IVECTU  : ADRESSE DU VECTEUR BT.SIGMA
C
C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ---------------------
      INTEGER  ZI
      COMMON  / IVARJE / ZI(1)
      REAL*8             ZR
      COMMON  / RVARJE / ZR(1)
      COMPLEX*16         ZC
      COMMON  / CVARJE / ZC(1)
      LOGICAL            ZL
      COMMON  / LVARJE / ZL(1)
      CHARACTER*8        ZK8
      CHARACTER*16                ZK16
      CHARACTER*24                          ZK24
      CHARACTER*32                                    ZK32
      CHARACTER*80                                              ZK80
      COMMON  / KVARJE / ZK8(1) , ZK16(1) , ZK24(1) , ZK32(1) , ZK80(1)
C --------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ---------------------
C
C     VARIABLES LOCALES
      REAL*8        HE(NFISS)
      CHARACTER*8   ELREFP,ELRESE(6),FAMI(6)
      CHARACTER*24  COORSE
      INTEGER       NSE,IDECPG,IDEBS,JTAB(2),NCOMP,IRET
      INTEGER       ISE,IN,INO,NPG,JCOORS,J
      INTEGER       IRESE,NNO,FISNO(NNOP,NFISS),IFISS,IG,IBID
      LOGICAL       ISMALI

      DATA          ELRESE /'SE2','TR3','TE4','SE3','TR6','TE4'/
      DATA          FAMI   /'BID','XINT','XINT','BID','XINT','XINT'/

C.========================= DEBUT DU CODE EXECUTABLE ==================
C

      CALL ELREF1(ELREFP)

C     NOMBRE DE COMPOSANTES DE PHEAVTO (DANS LE CATALOGUE)
      CALL TECACH('OOO','PHEAVTO',2,JTAB,IRET)
      NCOMP = JTAB(2)

C     SOUS-ELEMENT DE REFERENCE : RECUP DE NNO ET NPG
      IF (.NOT.ISMALI(ELREFP).AND. NDIM.LE.2) THEN
        IRESE=3
      ELSE
        IRESE=0
      ENDIF
      CALL ELREF5(ELRESE(NDIM+IRESE),FAMI(NDIM+IRESE),IBID,NNO,
     &            IBID,NPG,IBID,IBID,IBID,IBID,IBID,IBID)

C     RECUPERATION DE LA CONNECTIVITÉ FISSURE - DDL HEAVISIDES
C     ATTENTION !!! FISNO PEUT ETRE SURDIMENTIONNÉ
      IF (NFISS.EQ.1) THEN
        DO 30 INO = 1, NNOP
          FISNO(INO,1) = 1
  30    CONTINUE
      ELSE
        DO 10 IG = 1, NFH
C    ON REMPLIT JUSQU'A NFH <= NFISS
          DO 20 INO = 1, NNOP
            FISNO(INO,IG) = ZI(JFISNO-1+(INO-1)*NFH+IG)
  20      CONTINUE
  10    CONTINUE
      ENDIF

C     RÉCUPÉRATION DE LA SUBDIVISION DE L'ÉLÉMENT EN NSE SOUS ELEMENT
      NSE=LONCH(1)

C       BOUCLE SUR LES NSE SOUS-ELEMENTS
      DO 110 ISE=1,NSE

C       COORD DU SOUS-ÉLT EN QUESTION
        COORSE='&&XBSIGM.COORSE'
        CALL WKVECT(COORSE,'V V R',NDIM*NNO,JCOORS)

C       BOUCLE SUR LES 4/3 SOMMETS DU SOUS-TETRA/TRIA
        DO 111 IN=1,NNO
          INO=CNSET(NNO*(ISE-1)+IN)

          DO 112 J=1,NDIM
            IF (INO.LT.1000) THEN
              ZR(JCOORS-1+NDIM*(IN-1)+J)=ZR(IGEOM-1+NDIM*(INO-1)+J)
            ELSEIF (INO.GT.1000 .AND. INO.LT.2000) THEN
              ZR(JCOORS-1+NDIM*(IN-1)+J)=
     &                             ZR(JPINTT-1+NDIM*(INO-1000-1)+J)
            ELSEIF (INO.GT.2000 .AND. INO.LT.3000) THEN
              ZR(JCOORS-1+NDIM*(IN-1)+J)=
     &                             ZR(JPMILT-1+NDIM*(INO-2000-1)+J)
            ELSEIF (INO.GT.3000) THEN
              ZR(JCOORS-1+NDIM*(IN-1)+J)=
     &                             ZR(JPMILT-1+NDIM*(INO-3000-1)+J)
            ENDIF
 112      CONTINUE
 111    CONTINUE

C       FONCTION HEAVYSIDE CSTE POUR CHAQUE FISSURE SUR LE SS-ELT
        DO 113 IFISS = 1,NFISS
          HE(IFISS) = HEAVT(NCOMP*(IFISS-1)+ISE)
  113   CONTINUE

C       DEBUT DE LA ZONE MÉMOIRE DE SIGMA CORRESPONDANTE
        IDECPG = NPG   * (ISE-1)
        IDEBS  = NBSIG *   IDECPG

        IF (NDIM .EQ. 3) THEN

          CALL ASSERT(NBSIG.EQ.6)
          CALL XBSIG3(ELREFP,NDIM,COORSE,IGEOM,HE,
     &                NFH,DDLC,NFE,BASLOC,NNOP,
     &                NPG,SIGMA(IDEBS+1),LSN,LST,NFISS,FISNO,
     &                ZR(IVECTU))

        ELSEIF (NDIM.EQ.2) THEN

          CALL ASSERT(NBSIG.EQ.4)
          CALL XBSIG2(ELREFP,ELRESE(NDIM+IRESE),NDIM,COORSE,IGEOM,HE,
     &                NFH,DDLC,DDLM,NFE,BASLOC,NNOP,
     &                NPG,SIGMA(IDEBS+1),LSN,LST,NFISS,FISNO,
     &                IVECTU)

        ENDIF

        CALL JEDETR(COORSE)

 110  CONTINUE


C.============================ FIN DE LA ROUTINE ======================

      END
