      SUBROUTINE VOIUTI(NUMA,CODVOI,NVOIMA,NSCOMA,IAREPE,IADDVO,IADVOI,
     &                  NBVOIS,LIVOIS,TYVOIS,NBNOVO,NBSOCO,LISOCO)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 13/06/2012   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
      IMPLICIT NONE
C   IN  NUMA     : NUMERO DE MAILLE DU MAILLAGE
C       CODVOI   :  CODE DONNANT LES COMBINAISONS
C                   DES VOISINAGES POSSIBLES
C                   3D PAR FACE    : F3 : 1
C                   2D PAR FACE    : F2 : 2
C                   3D PAR ARRETE  : A3 : 3
C                   2D PAR ARRETE  : A2 : 4
C                   1D PAR ARRETE  : A1 : 5
C                   3D PAR SOMMET  : S3 : 6
C                   2D PAR SOMMET  : S2 : 7
C                   1D PAR SOMMET  : S1 : 8
C                   0D PAR SOMMET  : S0 : 9
C       NVOIMA   :  NOMBRE MAX DE VOISINS POSSIBLES
C       NSCOMA   :  NOMBRE MAX DE SOMMETS COMMUNS
C       IAREPE   :  ADRESSE JEVEUX D UN OPJET DE TYPE REPE
C       IADDVO   :  ADRESSE JEVEUX DU POINEUR DE VALEURS DANS VGE
C       IADVOI   :  ADRESSE JEVEUX DES VALEURS DE VGE
C  OUT
C       NBVOIS   :  NOMBRE DE VOISINS
C       LIVOIS   :  LISTE DE CES VOISINS (NUM. DE MAILLES AFFECTEES)
C       TYVOIS   :  TYPE(INTEGER) DES VOISINS
C       NBNOVO   :  NOMBRE DE NOEUDS DE CHACUN DE CES VOISINS
C       NBSOCO   :  POUR CHAQUE VOISINS NOMBRE DE SOMMETS PARTAGES
C       LISOCO   :  LISTE DE CES SOMMETS
C                  (IMA,IS,1) EN NUMEROTATION LOCALE MAILLE NUMA
C                  (IMA,IS,2) EN NUMEROTATION LOCALE MAILLE VOISINE
      INCLUDE 'jeveux.h'
      INTEGER NUMA,NVOIMA,NSCOMA,IAREPE,IADDVO,IADVOI,NBVOIS
C     PARAMETER(NVOIMA=100,NSCOMA=4)
      INTEGER LIVOIS(1:NVOIMA),TYVOIS(1:NVOIMA),NBNOVO(1:NVOIMA),
     &        NBSOCO(1:NVOIMA),LISOCO(1:NVOIMA,1:NSCOMA,1:2)
      CHARACTER*(*) CODVOI
      INTEGER IV,IS,IEL,IDEB,IFIN,ICODE,JCODE,NUMAV,IELV,TYPEV
      INTEGER NTYMAX,LXLGUT
      PARAMETER(NTYMAX=3)
      INTEGER LCOD,ITYVO,NTYVO,LITYVO(1:NTYMAX)
      CHARACTER*2 TYBASE(9)
C-----------FONCTIONS  D ACCES A VGE -----------------------------------
      INTEGER ZZADVO,ZZNBVO,ZZADVE,ZZMAVO,ZZTYVO,ZZNBNO,ZZNBSC,ZZLOC1,
     &        ZZLOC2
C     IADDVO : ADRESSE JEVEUX DU TABLEAU DE POINTEURS DANS LA SD EL_VOIS
C     IADVOI : ADRESSE JEVEUX DE LA SD EL_VOIS
C
C     DES DONNEES DES VOISINS DE LA MAILLE NUMA (0 SI MAILLE PAS ACTIVE)
      ZZADVO(NUMA)=ZI(IADDVO+NUMA-1)+IADVOI

C     NOMBBRE DE VOISINS DE NUMA EME MAILLE
      ZZNBVO(NUMA)=ZI(ZZADVO(NUMA)-1+1)

C     POUR LA MAILLE NUMA
C     POUR LE VOISIN IV
C     ADRESSE DES DONNEES
C FAUX ???      ZZADVE(NUMA,IV) = ZI(ZZADVO(NUMA)-1+1+IV)+IADVOI-1
      ZZADVE(NUMA,IV)=ZI(ZZADVO(NUMA)-1+1+IV)+ZZADVO(NUMA)-1

C     POUR LA MAILLE NUMA
C     POUR LE VOISIN IV
C     TYPE DE VOISINAGE :
C        3D PAR FACE    : F3 : 1
C        2D PAR FACE    : F2 : 2
C        3D PAR ARRETE  : A3 : 3
C        2D PAR ARRETE  : A2 : 4
C        1D PAR ARRETE  : A1 : 5
C        3D PAR SOMMET  : S3 : 6
C        2D PAR SOMMET  : S2 : 7
C        1D PAR SOMMET  : S1 : 8
C        0D PAR SOMMET  : S0 : 9
      ZZTYVO(NUMA,IV)=ZI(ZZADVE(NUMA,IV)-1+1)

C     POUR LA MAILLE NUMA
C     POUR LE VOISIN IV
C     NUMERO DE MAILLE
      ZZMAVO(NUMA,IV)=ZI(ZZADVE(NUMA,IV)-1+2)

C     POUR LA MAILLE NUMA
C     POUR LE VOISIN IV
C     NOMBRE DE NOEUDS DE MAILLE
      ZZNBNO(NUMA,IV)=ZI(ZZADVE(NUMA,IV)-1+3)


C     POUR LA MAILLE NUMA
C     POUR LE VOISIN IV
C        NOMBRE DE SOMMETS COMMUNS
      ZZNBSC(NUMA,IV)=ZI(ZZADVE(NUMA,IV)-1+4)


C     POUR LA MAILLE NUMA
C     POUR LE VOISIN IV
C     POUR LE SOMMET COMMUN IS
C     NUMERO LOCAL DANS NUMA
      ZZLOC1(NUMA,IV,IS)=ZI(ZZADVE(NUMA,IV)-1+4+1+2*(IS-1))



C     POUR LA MAILLE NUMA
C     POUR LE VOISIN IV
C    POUR LE SOMMET COMMUN IS
C     NUMERO LOCAL DANS IV
      ZZLOC2(NUMA,IV,IS)=ZI(ZZADVE(NUMA,IV)-1+4+1+2*(IS-1)+1)
C-----------FIN FONCTIONS  D ACCES A VGE -------------------------------
      DATA TYBASE/'F3','F2','A3','A2','A1','S3','S2','S1','S0'/
C
C  1 CETTE MAILLE EST ELLE AFFECTEE DANS LE MODELE ?
C
      IEL=ZI(IAREPE-1+2*(NUMA-1)+2)
      IF (IEL.EQ.0) THEN
        NBVOIS=0
        GOTO 80

      ENDIF

C
C  1 RECHERCHE DES TYPES DES VOISINAGE ATTENDUS
C
      NTYVO=0
      LCOD=LXLGUT(CODVOI)
      IF (LCOD.GT.2*NTYMAX) THEN
        CALL U2MESG('F','VOLUFINI_7',1,CODVOI,1,LCOD,0,0.D0)
      ENDIF
      DO 30 ICODE=1,LCOD/2
        IDEB=2*(ICODE-1)+1
        IFIN=2*ICODE
        DO 10 JCODE=1,9
          IF (CODVOI(IDEB:IFIN).EQ.TYBASE(JCODE)) THEN
            NTYVO=NTYVO+1
            LITYVO(NTYVO)=JCODE
            GOTO 20

          ENDIF
   10   CONTINUE
        CALL U2MESG('F','VOLUFINI_6',1,CODVOI(IDEB:IFIN),0,0,0,0.D0)
   20   CONTINUE
   30 CONTINUE
      IF (NTYVO.EQ.0) THEN
        NBVOIS=0
        GOTO 80

      ENDIF
C
C      REMPLISSAGE DES TABLEAUX
C
      NBVOIS=0
      DO 70 IV=1,ZZNBVO(NUMA)
        NUMAV=ZZMAVO(NUMA,IV)
        IELV=ZI(IAREPE-1+2*(NUMAV-1)+2)
C
C  LE VOISIN EST IL UNE MAILLE AFFECTEE DANS LE MODELE
C
        IF (IELV.EQ.0) THEN
          GOTO 70

        ENDIF
        TYPEV=ZZTYVO(NUMA,IV)
C
C  LE VOISIN EST IL D UN TYPE ATTENDU
C
        DO 40 ITYVO=1,NTYVO
          IF (TYPEV.EQ.LITYVO(ITYVO)) THEN
            GOTO 50

          ENDIF
   40   CONTINUE
        GOTO 70

   50   CONTINUE
        NBVOIS=NBVOIS+1
        LIVOIS(NBVOIS)=NUMAV
        TYVOIS(NBVOIS)=TYPEV
        NBNOVO(NBVOIS)=ZZNBNO(NUMA,IV)
        NBSOCO(NBVOIS)=ZZNBSC(NUMA,IV)
        DO 60 IS=1,ZZNBSC(NUMA,IV)
          LISOCO(NBVOIS,IS,1)=ZZLOC1(NUMA,IV,IS)
          LISOCO(NBVOIS,IS,2)=ZZLOC2(NUMA,IV,IS)
   60   CONTINUE
   70 CONTINUE
   80 CONTINUE

      END
