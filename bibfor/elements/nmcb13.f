      SUBROUTINE NMCB13(EPS,SIG,ESEC,E,DD,D,BETA,A,B,Y,Y00,Z,CRIT)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 05/07/2011   AUTEUR LABBE M.LABBE 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
      IMPLICIT NONE
C --- CALCUL DE SIGMA DANS LE CAS 1 & 3 PAR DICHOTOMIE
      REAL*8 EPS,SIG,ESEC,E,DD,D,BETA,A,B,Y,Y00,Z,CRIT(*)
      REAL*8 XG,XD,FCTD,ERREUR,TOL
      INTEGER NMAXIT,ITERAT
      REAL*8 D13,YY,ZERO,UN,DEUX,PRESQ1
      PARAMETER (ZERO=0.0D+0,UN=1.D+0,DEUX=2.D+0)

C --- CET ROUTINE EST BASEE SUR LA DOC R7.01.07 CHAPITRE 3


C     COPIE DES PARAMETRES DE CONVERGENCE
      NMAXIT = NINT(CRIT(1))
      TOL = CRIT(3)
      PRESQ1=UN-TOL
C     L'ENDOMMAGEMENT NE PEUT PAS ETRE SUPERIEUR A 1
      IF (DD.GT.UN) THEN
        CALL U2MESS('F','ELEMENTS2_37')
      END IF

C --- VERIFICATION DE DEPASSEMENT DU SEUIL
C     POUR CAS 1 : EQUATION  3.2.1.1.
C     POUR CAS 3 : EQUATION  3.2.3.1
C     ATTENTION : EPS N'EST PAS EGAL A LA MEME CHOSE DANS LES DEUX CAS
C     VOIR ROUTINE NMCB1D L.70 (APRES "IF (X1.LE.EPS") )
      YY = (E*EPS+BETA)* (E*EPS+BETA) - BETA*BETA/ (UN-DD)/ (UN-DD)
      YY = YY/ (DEUX*E)
      IF (YY.LE.Z) THEN
C --- LE SEUIL N'EST PAS DEPASSE, D N'EVOLUE PAS
        Y = YY
        ESEC = E* (UN-DD)
        SIG = EPS*ESEC - BETA*DD
        GO TO 20
      END IF
C --- VERIFICATION DU CRITERE :
C     LE CRITERE FCTD EST OBTENU A PARTIR DES SYSTEMES D'EQUATIONS
C     3.2.1.2. ET 3.2.3.2.(RESP. CAS 1 ET 3) EN INTEGRANT LA 1ERE
C     LIGNE DANS LA SECONDE.
C     (S'IL EST POSITIF CELA IMPLIQUE QUE LA SOLUTION DE LA RECHERCHE
C     DE ZERO SUR LE CRITERE EST INFERIEURE A L'ENDOMMAGEMENT ACTUELLE
C --- DONC ON GARDE DONC L'ANCIENNE VALEUR)
      FCTD = UN - (UN+ (A* (YY-Y00))**B)* (UN-DD)
      IF (FCTD.GT.ZERO) THEN
        Y = YY
        ESEC = E* (UN-DD)
        SIG = EPS*ESEC - BETA*DD
        GO TO 20
      END IF

C --- DEBUT DE LA DICHOTOMIE
C     ON RECHERCHE D13 DANS (D,1),
C     CAR L'ENDOMMAGEMENT NE DIMINUE PAS
      ITERAT=0
      XG=DD
      XD=UN
C --- BOUCLE TANT QUE ERREUR
   10 CONTINUE
C     ON EVALUE LE CRITERE AU MILIEU DE L'INTERVALLE
      D13=XG+(XD-XG)/DEUX

      IF (D13.GT.PRESQ1) THEN
        D13=PRESQ1
        GO TO 15
      ENDIF
      YY = (E*EPS+BETA)* (E*EPS+BETA)/ (DEUX*E) -
     &     (BETA*BETA/ (UN-D13)/ (UN-D13))/ (DEUX*E)
C --- TEST POUR NE PAS CALCULER UN NOMBRE NEGATIF A UNE PUISSANCE NON
C --- ENTIERE
      IF (YY.GT.Y00) THEN
        FCTD = UN - (UN+ (A* (YY-Y00))**B)* (UN-D13)
      ELSE
        FCTD = UN
      END IF

      IF (FCTD.LT.ZERO) THEN
        XG = D13
      ELSE
        XD = D13
      END IF
      ERREUR = ABS(FCTD)
      ITERAT=ITERAT+1
      IF ((ERREUR.GT.TOL).AND.(ITERAT.LE.NMAXIT)) GO TO 10
C --- FIN BOUCLE TANT QUE ERREUR

   15 CONTINUE

C     MISE A JOUR DE Y A PARTIR DE L'EXPRESSION DE FTCB
      Y = Y00 + UN/A* (D13/ (UN-D13))** (UN/B)
C     ENREGISTREMENT DU NOUVEL ENDOMMAGEMENT
      D = D13

C --- MODULE SECANT
      ESEC = E* (UN-D)
C --- CONTRAINTE : EQUATIONS 3.2.1.3 ET 3.2.3.3
C --- (ON NE SOUVIENT QUE EPS EST DIFFERENT DANS LES DEUX CAS)
      SIG = EPS*ESEC - BETA*D

   20 CONTINUE
      END
