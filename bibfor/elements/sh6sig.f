      SUBROUTINE SH6SIG(XETEMP,PARA,XIDEPP,DUSX,SIGMA)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 26/04/2011   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C
C               ELEMENT SHB6
C
      IMPLICIT REAL*8 (A-H,O-Z)
      INTEGER LAG,IRDC
      REAL*8 SIGMA(*),PARA(11)
      REAL*8 XE(18),DUSX(*),XIDEPP(*)
      REAL*8 XXG5(5),XCOQ(3,3),BKSIP(3,6,5),B(3,6)
      REAL*8 XCENT(3),PPP(3,3),PPPT(3,3)
      REAL*8 XL(3,3),XXX(3),YYY(3)
      REAL*8 CMATLO(6,6),LAMBDA
      REAL*8 DEPS(6),DUSDX(9),UE(3,6),UELOC(3,6)
      REAL*8 SIGLOC(6)
      REAL*8 BLOC(6,18),XMODIF(18)
      REAL*8 XETEMP(*),BLOCAL(3,6)
C
C---------------- COMMUNS NORMALISES  JEVEUX  --------------------------
      COMMON /IVARJE/ZI(1)
      COMMON /RVARJE/ZR(1)
      COMMON /CVARJE/ZC(1)
      COMMON /LVARJE/ZL(1)
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
      INTEGER ZI
      REAL*8 ZR
      COMPLEX*16 ZC
      LOGICAL ZL
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
C------------FIN  COMMUNS NORMALISES  JEVEUX  --------------------------
C
C
CCCCCCCCCCCCCC ENTREES CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C          ICLE=7    ON CALCULE LES CONTRAINTES
C    OPTION=SIEF_ELGA    ON CALCULE LES CONTRAINTES
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C INITIALISATIONS
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C INFOS:
C XE EST RANGE COMME CA:
C (XNOEUD1 YNOEUD1 ZNOEUD1, XNOEUD2 YNOEUD2 ZNOEUD2,shb6bg.f...)
C DANS SHB8_TEST_NUM: ATTENTION A LA NUMEROTATION DES NOEUDS
C
C ON DEFINI LES POINTS GAUSS ET LES POIDS
C
      XXG5(1) = -0.906179845938664D0
      XXG5(2) = -0.538469310105683D0
      XXG5(3) =  0.D0
      XXG5(4) =  0.538469310105683D0
      XXG5(5) =  0.906179845938664D0
C
C -----------------------------------------------------
C ON VERIFIE QUE LA CONNECTIVITE DONNE UN REPERE DIRECT
C SI CE N EST PAS LE CAS ON PERMUTE LES NOEUDS
C -----------------------------------------------------
C
C     ON FAIT UNE COPIE DE XETEMP DANS XE
      DO 10 I = 1,18
         XE(I) = XETEMP(I)
   10 CONTINUE
C TYPE DE LOI DE COMPORTEMENT:
C     IRDC = 1 : SHB8 TYPE PLEXUS
C     IRDC = 2 : C.P.
C     IRDC = 3 : 3D COMPLETE
      IRDC = NINT(PARA(5))
      CALL R8INIR(36,0.D0,CMATLO,1)
C
C UE: INCREMENT DE DEPLACEMENT NODAL, REPERE GLOBAL
C
C XE: DEBUT DU PAS
      DO 360 J = 1,6
         DO 350 I = 1,3
            UE(I,J) = XIDEPP((J-1)*3+I)
  350    CONTINUE
  360 CONTINUE
C
      LAG = NINT(PARA(6))
C ON DEFINIT CMATLO LOI MODIFIEE SHB8
C
      LAMBDA = PARA(1)*PARA(2)/(1-PARA(2)*PARA(2))
      XMU = 0.5D0*PARA(1)/ (1+PARA(2))
      CMATLO(1,1) = LAMBDA + 2*XMU
      CMATLO(2,2) = LAMBDA + 2*XMU
      IF (IRDC.EQ.1) THEN
C COMPORTEMENT SHB8 PLEXUS
C         CMATLO(3,3) = PROPEL(1)
          CMATLO(3,3) = PARA(1)
      END IF
C
      IF (IRDC.EQ.2) THEN
C COMPORTEMENT C.P.
          CMATLO(3,3) = 0.D0
      END IF
C
      CMATLO(1,2) = LAMBDA
      CMATLO(2,1) = LAMBDA
      CMATLO(4,4) = XMU
      CMATLO(5,5) = XMU
      CMATLO(6,6) = XMU
C
      IF (IRDC.EQ.3) THEN
C COMPORTEMENT LOI TRIDIM MMC 3D
C
        XNU = PARA(2)
        XCOOEF = PARA(1)/ ((1+XNU)*(1-2*XNU))
        CMATLO(1,1) = (1-XNU)*XCOOEF
        CMATLO(2,2) = (1-XNU)*XCOOEF
        CMATLO(3,3) = (1-XNU)*XCOOEF
        CMATLO(1,2) = XNU*XCOOEF
        CMATLO(2,1) = XNU*XCOOEF
        CMATLO(1,3) = XNU*XCOOEF
        CMATLO(3,1) = XNU*XCOOEF
        CMATLO(2,3) = XNU*XCOOEF
        CMATLO(3,2) = XNU*XCOOEF
        CMATLO(4,4) = (1-2*XNU)*0.5D0*XCOOEF
        CMATLO(5,5) = (1-2*XNU)*0.5D0*XCOOEF
        CMATLO(6,6) = (1-2*XNU)*0.5D0*XCOOEF
      END IF
C
C CALCUL DE BKSIP(3,8,IP) DANS REPERE DE REFERENCE
C      BKSIP(1,*,IP) = VECTEUR BX AU POINT GAUSS IP
C      BKSIP(2,*,IP) = VECTEUR BY AU POINT GAUSS IP
C      BKSIP(3,*,IP) = VECTEUR BZ AU POINT GAUSS IP
C
      CALL SH6KSI(5,XXG5,BKSIP)
C
      DO 450 IP = 1,5
C
C DEFINITION DES 4 POINTS  COQUES
C
         ZETA = XXG5(IP)
         ZLAMB = 0.5D0* (1.D0-ZETA)
         DO 380 I = 1,3
            DO 370 J = 1,3
              XCOQ(J,I) = ZLAMB*XE((I-1)*3+J) +
     &              (1.D0-ZLAMB)*XE((I-1+3)*3+J)
  370       CONTINUE
  380    CONTINUE
C
C CALCUL DE PPP 3*3 PASSAGE DE GLOBAL A LOCAL,COQUE
C XCENT : COORD GLOBAL DU CENTRE DE L'ELEMENT
C
         CALL RLOSH6(XCOQ,XCENT,PPP,XL,XXX,YYY,RBID)
         DO 382 I = 1,3
           DO 381 J = 1,3
              PPPT(J,I) = PPP(I,J)
  381      CONTINUE
  382    CONTINUE
C
C
C CALCUL DE B : U_GLOBAL ---> EPS_GLOBAL
C
         CALL S6CALB(BKSIP(1,1,IP),XE,B,AJAC)
         CALL MULMAT(3,3,6,PPPT,B,BLOCAL)
C
C CALCUL DE EPS DANS LE REPERE GLOBAL: 1 POUR DEFORMATIONS LINEAIRES
C                                     2 POUR TERMES CARRES EN PLUS
         DO 390 I = 1,6
            DEPS(I) = 0.D0
  390    CONTINUE
C Transformer matrice BLOCAL(3,6) dans le repère local en BLOC(6,18)
C dans le repère local et en tenant
C compte également des modifications sur les termes croisés ZY,ZX :
         DATA XMODIF/1.D0,0.D0,0.D0,
     &          0.D0,1.D0,0.D0,
     &          0.D0,0.D0,1.D0,
     &          1.D0,1.D0,0.D0,
     &          0.D0,0.45D0,0.45D0,
     &          0.45D0,0.D0,0.45D0/
         CALL ASSEBG(BLOC,BLOCAL,XMODIF)
C Transformer les déplacements UE dans le repère global en UELOC dans
C le repère local
         CALL MULMAT(3,3,6,PPPT,UE,UELOC)
C
         IF (LAG.EQ.1) THEN
C ON AJOUTE LA PARTIE NON-LINEAIRE DE EPS
            CALL DEPSH6(2,BLOC,UELOC,DEPS,DUSDX)
         ELSE
            CALL DEPSH6(1,BLOC,UELOC,DEPS,DUSDX)
         END IF
C
         DO 420 I = 1,9
            DUSX(I+ (IP-1)*9) = DUSDX(I)
  420    CONTINUE
         DO 430 I = 1,6
           SIGLOC(I) = 0.D0
  430    CONTINUE
C
C CALCUL DE SIGMA DANS LE REPERE LOCAL
C
         CALL MULMAT(6,6,1,CMATLO,DEPS,SIGLOC)
C
C CONTRAINTES ECRITES SOUS LA FORME:
C               [SIG] = [S_11, S_22, S_33, S_12, S_23, S_13]
         DO 440 I = 1,6
C ON LAISSE LES CONTRAINTES DANS LE REPERE LOCAL POUR LA PLASTICITE
            SIGMA((IP-1)*6+I) = SIGLOC(I)
  440    CONTINUE
  450 CONTINUE
C
      END
