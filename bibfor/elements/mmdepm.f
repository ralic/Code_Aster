      SUBROUTINE MMDEPM(NBDM  ,NDIM  ,NNE   ,NNM   ,JDEPM ,
     &                  JDEPDE,FFE   ,FFM   ,DDEPLE,DDEPLM,
     &                  DEPLME,DEPLMM)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 13/06/2012   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE ABBAS M.ABBAS
C
      IMPLICIT NONE
      INCLUDE 'jeveux.h'
      INTEGER      NBDM,NDIM,NNE,NNM
      INTEGER      JDEPDE,JDEPM
      REAL*8       FFE(9),FFM(9)
      REAL*8       DDEPLE(3),DEPLME(3)
      REAL*8       DDEPLM(3),DEPLMM(3)
C      
C ----------------------------------------------------------------------
C
C ROUTINE CONTACT (METHODE CONTINUE - UTILITAIRE)
C
C CALCUL DES INCREMENTS - DEPLACEMENTS
C
C ----------------------------------------------------------------------
C
C
C DEPDEL - INCREMENT DE DEPLACEMENT DEPUIS DEBUT DU PAS DE TEMPS
C DEPMOI - DEPLACEMENT DEBUT DU PAS DE TEMPS
C
C IN  NBDM   : NB DE DDL DE LA MAILLE ESCLAVE
C                NDIM = 2 -> NBDM = DX/DY/LAGR_C/LAGR_F1
C                NDIM = 3 -> NBDM = DX/DY/DZ/LAGR_C/LAGR_F1/LAGR_F2
C IN  NDIM   : DIMENSION DU PROBLEME
C IN  NNE    : NOMBRE DE NOEUDS DE LA MAILLE ESCLAVE
C IN  NNM    : NOMBRE DE NOEUDS DE LA MAILLE MAITRE
C IN  JDEPDE : ADRESSE JEVEUX POUR DEPDEL
C IN  JDEPM  : ADRESSE JEVEUX POUR DEPMOI
C IN  FFE    : FONCTIONS DE FORMES DEPL. ESCL.
C IN  FFM    : FONCTIONS DE FORMES DEPL. MAIT.
C OUT DDEPLE : INCREMENT DEPDEL DU DEPL. DU POINT DE CONTACT
C OUT DDEPLM : INCREMENT DEPDEL DU DEPL. DU PROJETE DU POINT DE CONTACT
C OUT DEPLME : DEPLACEMENTS DEPMOI DU POINT DE CONTACT
C OUT DEPLMM : DEPLACEMENTS DEPMOI DU PROJETE DU POINT DE CONTACTT
C
C
C
C
      INTEGER   IDIM,INOE
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
C
      DO 9 IDIM = 1,3
        DDEPLM(IDIM) = 0.D0
        DEPLMM(IDIM) = 0.D0
        DDEPLE(IDIM) = 0.D0
        DEPLME(IDIM) = 0.D0
 9    CONTINUE
C
C --- DEPLACEMENT PAS DE TEMPS MOINS POUR LES NOEUDS ESCLAVES
C
      DO 31 IDIM = 1,NDIM
        DO 32 INOE = 1,NNE
          DEPLME(IDIM) = DEPLME(IDIM) + FFE(INOE)*
     &                   ZR(JDEPM+(INOE-1)*NBDM+IDIM-1)
32      CONTINUE     
31    CONTINUE
C
C --- INCREMENT DEPDEL DU DEPL. POUR LES NOEUDS ESCLAVES
C
      DO 131 IDIM = 1,NDIM
        DO 132 INOE = 1,NNE
          DDEPLE(IDIM)  = DDEPLE(IDIM)  + FFE(INOE)*
     &                   ZR(JDEPDE+(INOE-1)*NBDM+IDIM-1)
132     CONTINUE      
131   CONTINUE
C
C --- DEPLACEMENT PAS DE TEMPS MOINS POUR LES NOEUDS MAITRES
C
      DO 41 IDIM = 1,NDIM
        DO 42 INOE = 1,NNM
          DEPLMM(IDIM) = DEPLMM(IDIM) + FFM(INOE)*
     &                   ZR(JDEPM+NNE*NBDM+(INOE-1)*NDIM+IDIM-1)
42      CONTINUE
41    CONTINUE
C
C --- INCREMENT DEPDEL DU DEPL. POUR LES NOEUDS MAITRES
C
      DO 141 IDIM = 1,NDIM
        DO 142 INOE = 1,NNM
          DDEPLM(IDIM)  = DDEPLM(IDIM)  + FFM(INOE)* 
     &                   ZR(JDEPDE+NNE*NBDM+(INOE-1)*NDIM+IDIM-1)
142     CONTINUE     
141   CONTINUE
C
      CALL JEDEMA()
C
      END
