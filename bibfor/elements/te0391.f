      SUBROUTINE TE0391(OPTION,NOMTE)
      IMPLICIT   NONE
      CHARACTER*16 OPTION,NOMTE
C ......................................................................
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 29/04/2004   AUTEUR JMBHH01 J.M.PROIX 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.

C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.

C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================

C    - FONCTION REALISEE:  CALCUL MATRICE DE MASSE MEPODTGD
C                          OPTION : 'MASS_MECA'
C                          OPTION : 'M_GAMMA'

C    - ARGUMENTS:
C        DONNEES:      OPTION       -->  OPTION DE CALCUL
C                      NOMTE        -->  NOM DU TYPE ELEMENT
C ......................................................................

C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ---------------------
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C --------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ---------------------

      CHARACTER*8 ELREFE
      CHARACTER*2 CODRET
      REAL*8 EN(3,2),ENPRIM(3,2),X00(3,3),Y0(3),ROT0(3,3),RHO
      REAL*8 GRANI(4),MASS(18,18),R8BID,ZERO
      REAL*8 A,XIY,XIZ,XJX,PJACOB,AJACOB
      INTEGER NNO,NNOS,JGANO,NDIM,NPG,NORD,IPOIDS,IVF,IDFDK,KP,NE,IC,
     &        LSECT
      INTEGER IGEOM,K0,IMATE,LORIEN,IMATUU,IMAT,IACCE,IVECT,I,J,ICO
C ......................................................................

      CALL ELREF1(ELREFE)
      ZERO = 0.0D0
      CALL ELREF4(' ','RIGI',NDIM,NNO,NNOS,NPG,IPOIDS,IVF,IDFDK,JGANO)
      NORD = 6*NNO

      ICO = 0
      DO 20 KP = 1,NPG
        DO 10 NE = 1,NNO
          ICO = ICO + 1
          EN(NE,KP) = ZR(IVF-1+ICO)
          ENPRIM(NE,KP) = ZR(IDFDK-1+ICO)
   10   CONTINUE
   20 CONTINUE

      CALL JEVECH('PGEOMER','L',IGEOM)
      K0 = IGEOM - 1

      DO 40 NE = 1,NNO
        DO 30 IC = 1,3
          K0 = K0 + 1
          X00(IC,NE) = ZR(K0)
   30   CONTINUE
   40 CONTINUE

      CALL JEVECH('PMATERC','L',IMATE)
      CALL RCVALA(ZI(IMATE),' ','ELAS',0,' ',R8BID,1,'RHO',RHO,
     &            CODRET,'FM')

C     --- RECUPERATION DES CARACTERISTIQUES GENERALES DES SECTIONS ---
      CALL JEVECH('PCAGNPO','L',LSECT)
      LSECT = LSECT - 1

C     --- LA SECTION EST SUPPOSEE CONSTANTE ---
      A = ZR(LSECT+1)
      XIY = ZR(LSECT+2)
      XIZ = ZR(LSECT+3)
      XJX = ZR(LSECT+8)
      GRANI(1) = RHO*XJX
      GRANI(2) = RHO*XIY
      GRANI(3) = RHO*XIZ
      GRANI(4) = RHO*A

C     --- RECUPERATION DES ORIENTATIONS INITIALES Y0(1), Y0(2), Y0(3)
      CALL JEVECH('PCAORIE','L',LORIEN)
      Y0(1) = ZR(LORIEN)
      Y0(2) = ZR(LORIEN+1)
      Y0(3) = ZR(LORIEN+2)

      DO 60 J = 1,NORD
        DO 50 I = 1,NORD
          MASS(I,J) = ZERO
   50   CONTINUE
   60 CONTINUE

C* BOUCLE SUR LES POINTS DE GAUSS

      DO 70 KP = 1,NPG
        CALL GDJRG0(KP,NNO,ENPRIM,X00,Y0,AJACOB,ROT0)
        PJACOB = ZR(IPOIDS-1+KP)*AJACOB
        CALL GDMMAS(KP,NNO,PJACOB,EN,GRANI,ROT0,MASS)
   70 CONTINUE

      IF (OPTION.EQ.'MASS_MECA') THEN
        CALL JEVECH('PMATUNS','E',IMATUU)
        IMAT = IMATUU - 1
        DO 90 I = 1,NORD
          DO 80 J = 1,NORD
            IMAT = IMAT + 1
            ZR(IMAT) = MASS(I,J)
   80     CONTINUE
   90   CONTINUE
      ELSE IF (OPTION.EQ.'M_GAMMA') THEN
        CALL JEVECH('PDEPLAR','L',IACCE)
        CALL JEVECH('PVECTUR','E',IVECT)
        CALL PMAVEC('ZERO',NORD,MASS,ZR(IACCE),ZR(IVECT))
      END IF

      END
