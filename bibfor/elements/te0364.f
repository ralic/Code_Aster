       SUBROUTINE TE0364(OPTION,NOMTE)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 18/09/2006   AUTEUR MABBAS M.ABBAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C
      IMPLICIT   NONE
      CHARACTER*16 OPTION,NOMTE
C      
C ----------------------------------------------------------------------
C  CALCUL CALCUL DES MATRICES DE CONTACT ET DE FROTTEMENT
C  DE COULOMB STANDARD  AVEC LA METHODE CONTINUE (ECP)
C
C  OPTION : 'RIGI_CONT' (CALCUL DES MATRICES DE CONTACT )
C           'RIGI_FROT' (CALCUL DES MATRICES DE FROTTEMENT STANDARD)
C
C  ENTREES  ---> OPTION : OPTION DE CALCUL
C           ---> NOMTE  : NOM DU TYPE ELEMENT
C
C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX --------------------
C
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR 
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C --------- FIN  DECLARATIONS  NORMALISEES  JEVEUX --------------------
C
      INTEGER I,J,IJ
      REAL*8 COEFFF,COEFFA,COEFCA,COEASP,CN
      INTEGER NNE,NNM,NDDL,NDIM,INDCO,INADH,INDASP,ICOMPL,INDM
      INTEGER IGEOM,IDEPL,IMATT,JPCF,CMP,IAXIS
      INTEGER IDEPM,IFROTT,IFORM
      INTEGER INI1,INI2,INI3,INDNOR,INDRAC
      INTEGER INDNOB,IMA,IMABAR,TYPBAR
      INTEGER IRET
      REAL*8 XPR,YPR,XPC,YPC,HPG,LAMBDA
      REAL*8 MMAT(81,81),TAU1(3),TAU2(3),NORM(3)      
      REAL*8 RESE(3),NRESE
      REAL*8 DELTAT,ASPERI,JEU
      REAL*8 DELTA,ALPHA,JEUSUP
      REAL*8 GEOMM(3),GEOME(3)
      REAL*8 DEPLME(6),DEPLM(6),DEPLE(6),DEPLMM(6)
      REAL*8 R8BID,R8BID6(6)
      REAL*8 MPROJ(3,3)
      REAL*8 FFPC(9),DFFPC(2,9),DDFFPC(3,9),JACOBI
      REAL*8 FFPR(9),DFFPR(2,9),DDFFPR(3,9)     
      CHARACTER*8 ESC,MAIT
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
C
C --- INFOS SUR LA MAILLE DE CONTACT      
C      
      CALL MMELEM(NOMTE,
     &            NDIM,NDDL,ESC,NNE,MAIT,NNM)
      IF (NDDL.GT.81) THEN
        CALL UTMESS('F','TE0364','TABLEAU SOUS DIMENSIONNE (DVLP)')
      ENDIF
C
C --- INITIALISATIONS
C
      DO 40 I = 1,NDDL
        DO 30 J = 1,NDDL
          MMAT(I,J) = 0.D0
   30   CONTINUE
   40 CONTINUE     
      DO 300 I = 1,3
        DO 290 J = 1,3
          MPROJ(I,J)  = 0.D0
  290   CONTINUE
  300 CONTINUE   
C
C --- RECUPERATION DES DONNEES DE LA CARTE DU CONTACT (VOIR MMCART)
C
      CALL JEVECH('PCONFR','L',JPCF)
      XPC      = ZR(JPCF-1+1)
      YPC      = ZR(JPCF-1+10)      
      XPR      = ZR(JPCF-1+2)
      YPR      = ZR(JPCF-1+3)    
      TAU1(1)  = ZR(JPCF-1+4)
      TAU1(2)  = ZR(JPCF-1+5)
      TAU1(3)  = ZR(JPCF-1+6)
      TAU2(1)  = ZR(JPCF-1+7)
      TAU2(2)  = ZR(JPCF-1+8)
      TAU2(3)  = ZR(JPCF-1+9)
      INDCO    = NINT(ZR(JPCF-1+11))
      LAMBDA   = ZR(JPCF-1+12)
      COEFCA   = ZR(JPCF-1+13)
      COEFFA   = ZR(JPCF-1+14)
      COEFFF   = ZR(JPCF-1+15)     
      IFROTT   = NINT(ZR(JPCF-1+16))
      INDNOR   = NINT(ZR(JPCF-1+17))
      IAXIS    = NINT(ZR(JPCF-1+18))
      HPG      = ZR(JPCF-1+19)
      DELTAT   = ZR(JPCF-1+20)
      IFORM    = NINT(ZR(JPCF-1+21))
      INDM     = NINT(ZR(JPCF-1+22))
      INI1     = NINT(ZR(JPCF-1+23))
      INI2     = NINT(ZR(JPCF-1+24)) 
      INI3     = NINT(ZR(JPCF-1+25))   
      INDASP   = NINT(ZR(JPCF-1+26))      
      ICOMPL   = NINT(ZR(JPCF-1+27))   
      ASPERI   = ZR(JPCF-1+28)
      COEASP   = ZR(JPCF-1+29)
      CN       = ZR(JPCF-1+30) 
      ALPHA    = ZR(JPCF-1+31) 
      DELTA    = ZR(JPCF-1+32)
      JEUSUP   = ZR(JPCF-1+33)
      IMA      = NINT(ZR(JPCF-1+34))
      IMABAR   = NINT(ZR(JPCF-1+35))
      INDNOB   = NINT(ZR(JPCF-1+36))
      TYPBAR   = NINT(ZR(JPCF-1+38))
      INDRAC   = NINT(ZR(JPCF-1+39))
C
C --- RECUPERATION DE LA GEOMETRIE ET DES CHAMPS DE DEPLACEMENT
C
      CALL JEVECH('PGEOMER','E',IGEOM)
      CALL JEVECH('PDEPL_P','E',IDEPL)
      CALL JEVECH('PDEPL_M','L',IDEPM)
C
C --- REACTUALISATION DE LA GEOMETRIE      
C      
      CALL MMREAC(NDIM,NNE,NNM,
     &            IGEOM,IDEPM)
C
C --- FONCTIONS DE FORMES ET DERIVEES POUR LE
C --- POINT DE CONTACT
C    
      IF (IMA .NE. IMABAR) THEN
        TYPBAR = -1
      ENDIF    
      CALL MMMFFD(ESC,XPC,YPC,TYPBAR,
     &            FFPC,DFFPC,DDFFPC,IRET)
C
C --- JACOBIEN POUR LE POINT DE CONTACT
C   
      CALL MMMJAC(ESC,IGEOM,FFPC,DFFPC,IAXIS,NDIM,
     &            JACOBI,IRET)
C
C --- FONCTIONS DE FORMES ET DERIVEES POUR LA
C --- PROJECTION DU POINT DE CONTACT
C 
      CALL MMMFFD(MAIT,XPR,YPR,TYPBAR,
     &            FFPR,DFFPR,DDFFPR,IRET)
C
C --- CALCUL DE LA NORMALE
C     
      CALL MMNORM(NDIM,TAU1,TAU2,NORM)
C
C --- GEOMETRIE ET DEPLACEMENTS DU POINT DE CONTACT ET
C --- DE SON PROJETE
C      
      CALL MMGEOM(NDIM,NNE,NNM,0,
     &            IGEOM,IDEPL,IDEPM,0,0,
     &            FFPC,FFPR,
     &            GEOME,GEOMM,
     &            DEPLE,DEPLM,DEPLME,DEPLMM,
     &            R8BID6,R8BID6,R8BID6,R8BID6)
C
C --- COEFFICIENT ACCELERATION POUR FORMULATION EN VITESSE
C
      IF (IFORM .EQ. 2) THEN
        COEFCA = COEFCA/DELTAT
      ENDIF     
C
C --- TRAITEMENT EN FOND DE FISSURE 
C     
      IF (INDNOB .GT. 0) THEN
        INDCO = 1
      END IF
C
C --- TRAITEMENT DU RACCORD SURFACIQUE     
C
      IF (INDRAC .GT. 0) THEN
        INDCO = 1
      END IF
C
C --- NOEUDS EXCLUS PAR PROJECTION HORS ZONE 
C
      IF (INDNOR .EQ. 1) THEN
        INDCO = 0
      ENDIF
C
C --- NOEUDS EXCLUS PAR SANS GROUP_NO
C
      IF (INDM .GE. 1) THEN
        CMP   = 0
        DO 9 I = 1,NDIM
          IF (TAU1(I) .NE. 0.D0) THEN
            CMP = I
          END IF
 9      CONTINUE
      END IF
C
C --- RECUPERATION DE LA MATRICE 'OUT' (A REMPLIR => MODE ECRITURE)
C      
      CALL JEVECH('PMATUUR','E',IMATT)
C      
C --- CALCUL DES MATRICES DE CONTACT
C
      IF (OPTION.EQ.'RIGI_CONT') THEN

        IF (INDASP.EQ.0) THEN
C
C --- CALCUL DE LA MATRICE A - CAS SANS CONTACT (HORS ASPERITES)
C     
          CALL MMMAA0(NDIM,NNE,
     &                HPG,FFPC,JACOBI,
     &                COEFCA,
     &                MMAT)        
          GOTO 740
        ELSE IF (INDASP.EQ.1) THEN      
          IF (INDCO.EQ.1) THEN         
C
C --- EVALUATION DES JEUX - CAS DU CONTACT
C          
            CALL MMMJEU(NDIM,JEUSUP,0,NORM,ALPHA,DELTA,DELTAT,
     &                  GEOME,GEOMM,
     &                  DEPLE,DEPLM,DEPLME,DEPLMM, 
     &                  R8BID6,R8BID6,R8BID6,R8BID6,    
     &                  JEU,R8BID,R8BID,R8BID)
C
C --- CALCUL DE LA MATRICE A - CAS DU CONTACT
C
            CALL MMMAA1(NDIM,NNE,NNM,
     &                  IMA,IMABAR,INDNOB,INDRAC,
     &                  HPG,FFPC,FFPR,JACOBI,
     &                  COEFCA,ICOMPL,COEASP,ASPERI,JEU,NORM,
     &                  MMAT)
C
C --- CONTRIBUTION DE LA COMPLIANCE A LA MATRICE A - CAS DU CONTACT
C
            IF (ICOMPL .EQ. 1) THEN
              CALL MMMAA2(NDIM,NNE,NNM,
     &                    HPG,FFPC,FFPR,JACOBI,
     &                    ICOMPL,COEASP,ASPERI,JEU,NORM,
     &                    CN,ALPHA,DELTA,DELTAT,
     &                    MMAT)
            ENDIF 
        
          ELSE IF (INDCO .EQ. 0) THEN       
C
C --- CALCUL DE LA MATRICE A - CAS SANS CONTACT
C     
            CALL MMMAA0(NDIM,NNE,
     &                  HPG,FFPC,JACOBI,
     &                  COEFCA,
     &                  MMAT)
          ENDIF
        ELSE
          CALL UTMESS('F','TE0364','SITUATION DE CONTACT IMPOSSIBLE')
        END IF

      ELSE IF (OPTION.EQ.'RIGI_FROT') THEN
        IF (COEFFF.EQ.0.D0) INDCO = 0
        IF (LAMBDA.EQ.0.D0) INDCO = 0
        IF (IFROTT.NE.3)    INDCO = 0

        IF (INDCO.EQ.0) THEN
C        
C --- CALCUL DE B ET DE BT - CAS SANS CONTACT       
C
          CALL MMMAB0(NDIM,NNE,
     &                HPG,FFPC,JACOBI,
     &                TAU1,TAU2,
     &                MMAT)
          GOTO 740
        ELSE IF (INDCO.EQ.1) THEN
C
C ---  ON CALCULE L'ETAT DE CONTACT ADHERENT OU GLISSANT
C 
          CALL TTPRSM(NDIM,DEPLE,DEPLM,
     &                COEFFA,NORM,TAU1,TAU2,
     &                INADH,MPROJ,RESE,NRESE)
          IF (INADH.EQ.1) THEN
C          
C --- CALCUL DE B ET DE BT - CAS ADHERENT
C
            CALL MMMAB1(NDIM,NNE,NNM,
     &                  INDM,INI1,INI2,INI3,CMP,
     &                  HPG,FFPC,FFPR,JACOBI,
     &                  LAMBDA,COEFFA,COEFFF,TAU1,TAU2,
     &                  MPROJ,MMAT)
          ELSE IF (INADH.EQ.0) THEN
C          
C --- CALCUL DE B ET DE BT - CAS GLISSANT
C
            CALL MMMAB2(NDIM,NNE,NNM,
     &                  HPG,FFPC,FFPR,JACOBI,
     &                  LAMBDA,COEFFA,COEFFF,TAU1,TAU2,RESE,NRESE,
     &                  MPROJ,MMAT)
          END IF 
        ELSE
          CALL UTMESS('F','TE0364','SITUATION DE CONTACT IMPOSSIBLE')
        END IF
      ELSE
        CALL UTMESS('F','TE0364','OPTION INCONNUE ')
      END IF
 740  CONTINUE
C
C --- FIN DE CHANGEMENT ET COPIE
C
      DO 760 J = 1,NDDL
        DO 750 I = 1,J
          IJ = (J-1)*J/2 + I
          ZR(IMATT+IJ-1) = MMAT(I,J)
C          write(6,*) 'MMAT:',I,J,MMAT(I,J)
 750    CONTINUE
 760  CONTINUE
C
      CALL JEDEMA()
      END
