      SUBROUTINE TE0021(OPTION,NOMTE)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 29/04/2004   AUTEUR JMBHH01 J.M.PROIX 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.

C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.

C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C.......................................................................
      IMPLICIT REAL*8 (A-H,O-Z)

C     BUT: CALCUL DES MATRICES DE RAIDEUR CENTRIFUGE ELEMENTAIRES
C          ELEMENTS ISOPARAMETRIQUES 3D

C          OPTION : 'RIGI_MECA_RO '

C     ENTREES  ---> OPTION : OPTION DE CALCUL
C          ---> NOMTE  : NOM DU TYPE ELEMENT
C.......................................................................

      CHARACTER*2   CODRET
      CHARACTER*16  NOMTE,OPTION
      REAL*8        A(3,3,27,27)
      REAL*8        DFDX(27),DFDY(27),DFDZ(27),POIDS
      INTEGER       IPOIDS,IVF,IDFDE,IGEOM,IMATE
      INTEGER       JGANO,NNO,KP,I,J,IMATUU

C --- DEBUT DECLARATIONS NORMALISEES JEVEUX ----------------------------
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C --- FIN DECLARATIONS NORMALISEES JEVEUX ------------------------------

      CALL ELREF4(' ','MASS',NDIM,NNO,NNOS,NPG2,IPOIDS,IVF,IDFDE,JGANO)

      CALL JEVECH('PGEOMER','L',IGEOM)
      CALL JEVECH('PMATERC','L',IMATE)
      CALL JEVECH('PMATUUR','E',IMATUU)
      CALL JEVECH('PROTATR','E',IROTA)

      CALL RCVALA(ZI(IMATE),' ','ELAS',0,' ',R8B,1,'RHO',RHO,
     &            CODRET,'FM')
      OMEGA1 = ZR(IROTA+1)*ZR(IROTA)
      OMEGA2 = ZR(IROTA+2)*ZR(IROTA)
      OMEGA3 = ZR(IROTA+3)*ZR(IROTA)

      DO 50 K = 1,3
        DO 40 L = 1,3
          DO 30 I = 1,NNO
            DO 20 J = 1,I
              A(K,L,I,J) = 0.D0
   20       CONTINUE
   30     CONTINUE
   40   CONTINUE
   50 CONTINUE

C    BOUCLE SUR LES POINTS DE GAUSS

      DO 80 KP = 1,NPG2

        L = (KP-1)*NNO
        CALL DFDM3D ( NNO, KP, IPOIDS, IDFDE,
     &                ZR(IGEOM), DFDX, DFDY, DFDZ, POIDS )

        DO 70 I = 1,NNO
          DO 60 J = 1,I
            WIJ = RHO*POIDS*ZR(IVF+L+I-1)*ZR(IVF+L+J-1)
            A(1,1,I,J) = A(1,1,I,J) - (OMEGA2**2+OMEGA3**2)*WIJ
            A(2,2,I,J) = A(2,2,I,J) - (OMEGA1**2+OMEGA3**2)*WIJ
            A(3,3,I,J) = A(3,3,I,J) - (OMEGA1**2+OMEGA2**2)*WIJ
            A(2,1,I,J) = A(2,1,I,J) + OMEGA1*OMEGA2*WIJ
            A(3,1,I,J) = A(3,1,I,J) + OMEGA1*OMEGA3*WIJ
            A(3,2,I,J) = A(3,2,I,J) + OMEGA2*OMEGA3*WIJ
   60     CONTINUE
   70   CONTINUE
   80 CONTINUE

      DO 100 I = 1,NNO
        DO 90 J = 1,I
          A(1,2,I,J) = A(2,1,I,J)
          A(1,3,I,J) = A(3,1,I,J)
          A(2,3,I,J) = A(3,2,I,J)
   90   CONTINUE
  100 CONTINUE

C PASSAGE DU STOCKAGE RECTANGULAIRE (A) AU STOCKAGE TRIANGULAIRE (ZR)

      DO 140 K = 1,3
        DO 130 L = 1,3
          DO 120 I = 1,NNO
            IK = ((3*I+K-4)* (3*I+K-3))/2
            DO 110 J = 1,I
              IJKL = IK + 3* (J-1) + L
              ZR(IMATUU+IJKL-1) = A(K,L,I,J)
  110       CONTINUE
  120     CONTINUE
  130   CONTINUE
  140 CONTINUE

      END
