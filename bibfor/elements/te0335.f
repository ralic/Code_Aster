      SUBROUTINE TE0335(OPTION,NOMTE)
      IMPLICIT NONE
      CHARACTER*16 NOMTE,OPTION
C ----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 09/11/2012   AUTEUR DELMAS J.DELMAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE DELMAS J.DELMAS
C
C     BUT:
C       CALCULER LES GRANDEURS EQUIVALENTES SUIVANTES
C       . CONTRAINTES EQUIVALENTES               (= 17 VALEURS)
C          . VON MISES                               (= 1 VALEUR)
C          . TRESCA                                  (= 1 VALEUR)
C          . CONTRAINTES PRINCIPALES                 (= 3 VALEURS)
C          . VON-MISES * SIGNE (PRESSION)            (= 1 VALEUR)
C          . DIRECTIONS DES CONTRAINTES PRINCIPALES  (= 3*3 VALEURS)
C          . TRACE                                   (= 1 VALEUR)
C          . TAUX DE TRIAXIALITE                     (= 1 VALEUR)
C
C       . DEFORMATIONS EQUIVALENTES              (= 14 VALEURS)
C          . SECOND INVARIANT                        (= 1 VALEUR)
C          . DEFORMATIONS PRINCIPALES                (= 3 VALEURS)
C          . 2EME INV. * SIGNE (1ER.INV.)            (= 1 VALEUR)
C          . DIRECTIONS DES DEFORMATIONS PRINCIPALES (= 3*3 VALEURS)
C
C       AUX POINTS DE GAUSS ET AUX NOEUDS :
C       A PARTIR DE SIGM_ELGA ET SIGM_ELNO POUR LES CONTRAINTES
C       A PARTIR DE EPSI_ELGA ET EPSI_ELNO POUR LES DEFORMATIONS
C       A PARTIR DE EPME_ELGA ET EPME_ELNO POUR LES DEF. HORS THERMIQUE
C
C       OPTION : 'SIEQ_ELGA'
C                'SIEQ_ELNO'
C                'EPEQ_ELGA'
C                'EPEQ_ELNO'
C                'EPMQ_ELGA'
C                'EPMQ_ELNO'
C
C ----------------------------------------------------------------------
C
      INCLUDE 'jeveux.h'


      INTEGER NEEQMX,NCEQMX
      PARAMETER (NEEQMX=14,NCEQMX=17)

      INTEGER NDIM,NDIM1,NNO,NNOS,NPG,IPOIDS,IVF,IDFDE,JGANO
      INTEGER IDEFO,ICONT,IEQUI
      INTEGER IRET,ITABIN(7),ITABOU(7),NBCMP,NCMPEQ,NBSP
      INTEGER IDEC,IDECEQ,IPG,INO,ISP

C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()

      CALL ELREF4(' ','RIGI',NDIM1,NNO,NNOS,NPG,IPOIDS,IVF,IDFDE,JGANO)

      IF ((OPTION.EQ.'EPEQ_ELGA') .OR.
     &    (OPTION.EQ.'EPEQ_ELNO') .OR.
     &    (OPTION.EQ.'EPMQ_ELGA') .OR.
     &    (OPTION.EQ.'EPMQ_ELNO')) THEN
C
        CALL TECACH('OOO','PDEFORR',7,ITABIN,IRET)
        IDEFO=ITABIN(1)
        CALL TECACH('OOO','PDEFOEQ',7,ITABOU,IRET)
        CALL ASSERT(ITABOU(2)/ITABOU(3).EQ.NEEQMX)
C
      ELSEIF ((OPTION.EQ.'SIEQ_ELGA') .OR.
     &        (OPTION.EQ.'SIEQ_ELNO')) THEN
C
        CALL TECACH('OOO','PCONTRR',7,ITABIN,IRET)
        ICONT=ITABIN(1)
        CALL TECACH('OOO','PCONTEQ',7,ITABOU,IRET)
        CALL ASSERT(ITABOU(2)/ITABOU(3).EQ.NCEQMX)
C
      ELSE
        CALL ASSERT(.FALSE.)
      ENDIF

      IEQUI = ITABOU(1)

      NBSP = ITABOU(7)
      CALL ASSERT(NBSP.GE.1)
      CALL ASSERT(NBSP.EQ.ITABIN(7))

      NBCMP = ITABIN(2)/ITABIN(3)
      CALL ASSERT((NBCMP.EQ.1).OR.(NBCMP.EQ.4).OR.(NBCMP.EQ.6))

      NCMPEQ = ITABOU(2)/ITABOU(3)
      CALL ASSERT((NCMPEQ.EQ.NEEQMX).OR.(NCMPEQ.EQ.NCEQMX))

      CALL ASSERT(ITABIN(6).LE.1)
      CALL ASSERT(ITABOU(6).LE.1)

      IF (NBCMP.EQ.6) THEN
        NDIM = 3
      ELSEIF (NBCMP.EQ.4) THEN
        NDIM = 2
      ELSEIF (NBCMP.EQ.1) THEN
        NDIM = 1
      ENDIF
C
C ----------------------------------------------------------------
C --- DEFORMATIONS ET CONTRAINTES EQUIVALENTES AUX POINTS DE GAUSS
C ----------------------------------------------------------------
C
      IF (OPTION(6:9).EQ.'ELGA') THEN
C
C ------ DEFORMATIONS :
C -------------------
        IF ((OPTION.EQ.'EPEQ_ELGA').OR.
     &      (OPTION.EQ.'EPMQ_ELGA')) THEN
          DO 10 IPG = 1,NPG
            DO 11 ISP = 1,NBSP
              IDEC   = IDEFO+(IPG-1)*NBCMP *NBSP+(ISP-1)*NBCMP
              IDECEQ = IEQUI+(IPG-1)*NCMPEQ*NBSP+(ISP-1)*NCMPEQ
              CALL FGEQUI(ZR(IDEC),'EPSI_DIR',NDIM,ZR(IDECEQ))
   11       CONTINUE
   10     CONTINUE
C
C ----- CONTRAINTES :
C -----------------
        ELSEIF (OPTION.EQ.'SIEQ_ELGA') THEN
          DO 20 IPG = 1,NPG
            DO 21 ISP = 1,NBSP
              IDEC   = ICONT+(IPG-1)*NBCMP *NBSP+(ISP-1)*NBCMP
              IDECEQ = IEQUI+(IPG-1)*NCMPEQ*NBSP+(ISP-1)*NCMPEQ
              CALL FGEQUI(ZR(IDEC),'SIGM_DIR',NDIM,ZR(IDECEQ))
   21       CONTINUE
   20     CONTINUE
        ENDIF
C
C -------------------------------------------------------
C --- DEFORMATIONS ET CONTRAINTES EQUIVALENTES AUX NOEUDS
C -------------------------------------------------------
C
      ELSE IF (OPTION(6:9).EQ.'ELNO') THEN
C
C ------ DEFORMATIONS :
C -------------------
        IF ((OPTION.EQ.'EPEQ_ELNO').OR.
     &      (OPTION.EQ.'EPMQ_ELNO')) THEN
          DO 30 INO = 1,NNO
            DO 31 ISP = 1,NBSP
              IDEC   = IDEFO+(INO-1)*NBCMP *NBSP+(ISP-1)*NBCMP
              IDECEQ = IEQUI+(INO-1)*NCMPEQ*NBSP+(ISP-1)*NCMPEQ
              CALL FGEQUI(ZR(IDEC),'EPSI_DIR',NDIM,ZR(IDECEQ))
   31       CONTINUE
   30     CONTINUE
C
C ----- CONTRAINTES :
C -----------------
        ELSEIF (OPTION.EQ.'SIEQ_ELNO') THEN
          DO 40 INO = 1,NNO
            DO 41 ISP = 1,NBSP
              IDEC   = ICONT+(INO-1)*NBCMP *NBSP+(ISP-1)*NBCMP
              IDECEQ = IEQUI+(INO-1)*NCMPEQ*NBSP+(ISP-1)*NCMPEQ
              CALL FGEQUI(ZR(IDEC),'SIGM_DIR',NDIM,ZR(IDECEQ))
   41       CONTINUE
   40     CONTINUE
        ENDIF
C
      ENDIF
C
      CALL JEDEMA()
C
      END
