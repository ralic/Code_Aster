      SUBROUTINE GDMRIG (KP,NNO,AJACOB,PJACOB,EN,ENPRIM,X0PG,ROT0,ROTK,
     &                   GRANC,PN,PM,   RIGI)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 02/10/95   AUTEUR GIBHHAY A.Y.PORTABILITE 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C
C FONCTION: POUR UN ELEMENT DE POUTRE EN GRAND DEPLACEMENT, CALCULE LA
C           CONTRIBUTION DU POINT DE GAUSS KP A LA MATRICE DE RIGIDITE.
C           CETTE MATRICE EST DISSYMETRIQUE, PLEINE ET RANGEE LIGNE PAR
C           LIGNE.
C
C     IN  : KP        : NUMERO DU POINT DE GAUSS
C           NNO       : NOMBRE DE NOEUDS
C           AJACOB    : JACOBIEN
C           PJACOB    : POIDS * JACOBIEN
C           EN        : FONCTIONS DE FORME
C           ENPRIM    : DERIVEES DES FONCTIONS DE FORME
C           X0PG      : DERIVEES DES COORDONNEES PAR RAP. A L'ABS. CURV.
C           ROT0      : MATRICE DE ROTATION DES AXES PRINCIPAUX D'INERT.
C                       AU POINT DE GAUSS DANS LA POSITION DE REFERENCE,
C                       PAR RAPPORT AUX AXES GENERAUX
C           ROTK      : MATRICE DE ROTATION
C           GRANC     : MATRICE DIAGONALE DE COMPORTEMENT ELASTIQUE
C           PN        : RESULTANTE DES FORCES AU PT DE GAUSS EN AX.GENE.
C           PM        : MOMENT RESULTANT AU PT DE GAUSS EN AXES GENERAUX
C
C     OUT : RIGI      : MATRICE DE RIGIDITE (CUMUL DES CONTRIBUTIONS DES
C                       POINTS DE GAUSS)
C ------------------------------------------------------------------
      IMPLICIT REAL*8(A-H,O-Z)
      REAL*8 EN(3,2),ENPRIM(3,2),X0PG(3),ROT0(3,3),ROTK(3,3),GRANC(6),
     &PN(3),PM(3),RIGI(18,18),D(9,9),STOKAJ(9,6,6),PI(6,6),CPIT(6,6),
     &PICPIT(6,6),PICPBJ(6,6),BI(6,6),BJ(6,6),BIBJ(6,6),BIT(6,6),
     &UPSI(9,6),UPSJ(9,6),UPIUPJ(6,6),UPSIT(6,9),DUPSJ(9,6),ROTABS(3,3)
C
      ZERO = 0.D0
C
      DO 7 J=1,6
      DO 6 I=1,6
      PI(I,J) = ZERO
      CPIT(I,J) = ZERO
    6 CONTINUE
    7 CONTINUE
C
      CALL PROMAT (ROTK  ,3,3,3,ROT0  ,3,3,3,   ROTABS)
C
      DO 9 J=1,3
      DO 8 I=1,3
      PI(I,J) = ROTABS(I,J)
      PI(3+I,3+J) = PI(I,J)
      CPIT(I,J) = GRANC(I) * ROTABS(J,I)
      CPIT(3+I,3+J) = GRANC(3+I) * ROTABS(J,I)
    8 CONTINUE
    9 CONTINUE
C
      CALL PROMAT (PI,6,6,6,CPIT,6,6,6,   PICPIT)
C
      CALL GDMD (X0PG,PN,PM,   D)
C
      DO 21 NE=1,NNO
      CALL GDMB (NE,KP,AJACOB,EN,ENPRIM,X0PG,   BI)
      CALL STOKMA (BI,6,6,NE,   STOKAJ)
      CALL GDMUPS (NE,KP,AJACOB,EN,ENPRIM,   UPSI)
      CALL STOKMA (UPSI,9,6,NNO+NE,   STOKAJ)
   21 CONTINUE
      DO 41 J=1,NNO
      CALL EXTRMA (STOKAJ,6,6,J,   BJ)
      CALL PROMAT (PICPIT,6,6,6,BJ,6,6,6,   PICPBJ)
      CALL EXTRMA (STOKAJ,9,6,NNO+J,   UPSJ)
      CALL PROMAT (D,9,9,9,UPSJ,9,9,6,   DUPSJ)
      DO 31 I=1,NNO
      CALL EXTRMA (STOKAJ,6,6,I,   BI)
      CALL TRANSP (BI,6,6,6,   BIT,6)
      CALL PROMAT (BIT,6,6,6,PICPBJ,6,6,6,   BIBJ)
      CALL CUMUMA (I,J,BIBJ,PJACOB,   RIGI)
C
      CALL EXTRMA (STOKAJ,9,6,NNO+I,   UPSI)
      CALL TRANSP (UPSI,9,9,6,   UPSIT,6)
      CALL PROMAT (UPSIT,6,6,9,DUPSJ,9,9,6,   UPIUPJ)
      CALL CUMUMA (I,J,UPIUPJ,PJACOB,   RIGI)
C*** FIN DE I
   31 CONTINUE
C*** FIN DE J
   41 CONTINUE
 9999 CONTINUE
      END
