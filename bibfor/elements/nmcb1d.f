      SUBROUTINE NMCB1D(E0,Y01,Y02,A1,A2,B1,B2,BETA1,BETA2,SIGF1,
     +            ALPHA,TR,TM,TP, SIG0,VAR0,EPSM,DEPS,EF,SIGF,VARF)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 29/05/2002   AUTEUR VABHHTS J.PELLET 
C TOLE CRP_21
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C              SEE THE FILE "LICENSE.TERMS" FOR INFORMATION ON USAGE AND
C              REDISTRIBUTION OF THIS FILE.
C ======================================================================
C  IN :
C       E0 : MODULE D'YOUNG INITIAL
C       Y01,Y02,A1,A2,B1,B2,BETA1,BETA2,SIGF1 = PARAMETRES DE LA LOI
C       ALPHA : COEFF DE DILATATION (CONSTANT)
C       TR  : TEMPERATURE DE REFERENCE
C       TM  : TEMPERATURE A L'INSTANT MOINS
C       TP  : TEMPERATURE A L'INSTANT PLUS
C       SIG0 : CONTRAINTE A L'INSTANT MOINS
C       VAR0 : VARIABLES INTERNES A L'INSTANT MOINS
C       EPSM : DEFORMATION TOTALE A L'INSTANT MOINS
C       DEPS : INCREMENT DE DEFORMATION TOTALE
C
C  OUT :
C       EF : MODULE D'YOUNG TANGENT (ACTUELLEMENT (SIGP - SIGM)/DEPS )
C       SIGF : CONTRAINTE A L'INSTANT PLUS
C       VARF : VARIABLES INTERNES A L'INSTANT PLUS

      IMPLICIT NONE
      REAL*8 E0,EF,EPSM,DEPS,SIG0,VAR0(5),SIGF,VARF(5)
      REAL*8 ZERO,UN
      REAL*8 ALPHA,TR,TM,TP
      PARAMETER (ZERO=0.0D+0,UN=1.D+0)
      REAL*8 ESEC,ETAN,D2,D1,EPS,YLIM1,YLIM2
      REAL*8 Y1,Y2,SIG
      REAL*8 A1,A2,BETA1,BETA2,Y01,Y02,B1,B2,SIGF1
      REAL*8 X1,X2,EPSF
      INTEGER CAS,CCAS


C ----VARIABLES HISTORIQUES
      D1 = VAR0(1)
      D2 = VAR0(2)
      YLIM1 = VAR0(3)
      YLIM2 = VAR0(4)
C ---- ON PASSE EN MECANIQUE
C     (ON ENLEVE LA PARTIE THERMIQUE EN CONSIDERANT ALPHA CONSTANTE)
C  ON A : EPSM = EPSM - ALPHA (TM - TR)
C     ET  DEPS = DEPS - ALPHA (TP - TR)
C     ET EPS = EPSM + DEPS
      EPS = EPSM + DEPS - ALPHA * (TP - TR)

C      Y1 = YLIM1 - UN
C      Y2 = YLIM2 - UN
C --- BOUCLE TANT QUE L'ON CHANGE DE CAS
   10 CONTINUE
      X1 = (BETA1*D1/ (UN-D1)+BETA2*D2/ (UN-D2))/E0
      X2 = (BETA2*D2-SIGF1)/ (UN-D2)/E0
      IF (X1.LE.EPS) THEN
        EPSF = EPS - BETA2*D2/ (UN-D2)/E0
        CALL NMCB13(EPSF,SIG,ESEC,E0,D1,BETA1,A1,B1,Y1,YLIM1,Y01)
        CAS = 1
      ELSE IF (EPS.LE.X2) THEN
C        write(6,*)'on y est ...',X2,Y2,YLIM2
        CALL NMCB13(EPS,SIG,ESEC,E0,D2,BETA2,A2,B2,Y2,YLIM2,Y02)
C        write(6,*)'la aussi ...',X2,Y2,YLIM2
        CAS = 3
      ELSE
        CALL NMCB2(EPS,BETA1,E0,D1,SIGF1,BETA2,D2,SIG,ESEC)
        CAS = 2
      END IF
      X1 = (BETA1*D1/ (UN-D1)+BETA2*D2/ (UN-D2))/E0
      X2 = (BETA2*D2-SIGF1)/ (UN-D2)/E0
      IF (X1.LE.EPS) THEN
        CCAS = 1
      ELSE IF (EPS.LE.X2) THEN
        CCAS = 3
      ELSE
        CCAS = 2
      END IF
      IF (CCAS.NE.CAS) GO TO 10
C --- FIN BOUCLE

C --- MODULE TANGENT
C        IF(CAS.EQ.1)THEN
C          CALL NMCBMT(CAS,E0,D1,D2,A1,B1,Y1,Y01,BETA1,BETA2,
C     +            SIGF1,EPS,ETAN)
C        ELSE
C          CALL NMCBMT(CAS,E0,D1,D2,A2,B2,Y2,Y02,BETA1,BETA2,
C     +            SIGF1,EPS,ETAN)
C        ENDIF
C --- MODULE TANGENT NUMERIQUE + 10% D'INITIAL
       IF (DEPS.NE.ZERO) THEN
         ETAN=(SIG-SIG0)/DEPS + 0.1D0 * E0
         IF(ETAN.GT.E0)ETAN=E0
       ELSE
         IF(VAR0(5).NE.ZERO)THEN
           ETAN=VAR0(5)
         ELSE
           ETAN=E0
         ENDIF
       ENDIF

C --- ACTUALISATION DES SEUILS
C      YLIM1 = DMAX1(Y1,YLIM1)
C      YLIM2 = DMAX1(Y2,YLIM2)
C --- STOCKAGE DES VARIABLES HISTIRIQUES
      VARF(1) = D1
      VARF(2) = D2
      VARF(3) = YLIM1
      VARF(4) = YLIM2
      VARF(5) = ETAN
C --- CONTRAINTE
      SIGF = SIG
C --- MODULE
      EF = ETAN

      END
