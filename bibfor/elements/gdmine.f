      SUBROUTINE GDMINE (KP,NNO,PJACOB,EN,GRANI,ALFNMK,DELNMK,PAS,
     &ROT0,ROTM,ROTKM1,ROTK,RMKM1,RMK,OMGKM,OMPGKM,OMGK,OMPGK,  RIGI)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 02/10/95   AUTEUR GIBHHAY A.Y.PORTABILITE 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C
C FONCTION: POUR UN ELEMENT DE POUTRE EN GRAND DEPLACEMENT, CALCULE LA
C           CONTRIBUTION DU POINT DE GAUSS KP A LA PARTIE ROTATOIRE DE
C           LA MATRICE D'INERTIE.  CETTE MATRICE EST DIRECTEMENT AJOUTEE
C           A LA MATRICE DE RIGIDITE.
C
C     IN  : KP        : NUMERO DU POINT DE GAUSS
C           NNO       : NOMBRE DE NOEUDS
C           PJACOB    : POIDS * JACOBIEN
C           EN        : FONCTIONS DE FORME
C           GRANI     : DIAGONALE DU TENSEUR D'INERTIE EN AXES LOCAUX
C                       POUR LES 3 1ERES COMPOSANTES, RHO*A POUR LA 4EME
C           ALFNMK    : ALPHA DE NEWMARK
C           DELNMK    : DELTA DE NEWMARK
C           PAS       : PAS DE TEMPS
C           ROT0      : MATRICE DE ROTATION DES AXES PRINCIPAUX D'INERT.
C                       AU POINT DE GAUSS DANS LA POSITION DE REFERENCE,
C                       PAR RAPPORT AUX AXES GENERAUX
C           ROTM      : MATRICE DE ROTATION A L'INSTANT N
C           ROTKM1    : MATRICE DE ROTATION A L'ITER. I   DE L'INST. N+1
C           ROTK      : MATRICE DE ROTATION A L'ITER. I+1 DE L'INST. N+1
C           RMKM1     : INCREMENT DE ROTATION ENTRE L'INSTANT N  ET
C                       L'ITER. I   DE L'INSTANT N+1
C           RMK       : INCREMENT DE ROTATION ENTRE L'INSTANT N  ET
C                       L'ITER. I+1 DE L'INSTANT N+1
C           OMGKM     : VITESSE      ANGULAIRE, A L'ITERATION I
C           OMPGKM    : ACCELERATION ANGULAIRE, A L'ITERATION I
C           OMGK      : VITESSE      ANGULAIRE, A L'ITERATION I+1
C           OMPGK     : ACCELERATION ANGULAIRE, A L'ITERATION I+1
C
C     OUT : RIGI      : MATRICE DE RIGIDITE (CUMUL DES CONTRIBUTIONS DES
C                       POINTS DE GAUSS)
C ------------------------------------------------------------------
      IMPLICIT REAL*8(A-H,O-Z)
      REAL*8 INERT(6,6),IRO(3,3),IROOMT(3,3)
      REAL*8 EN(3,2),GRANI(4),ROT0(3,3),ROTM(3,3),ROTKM1(3,3),
     &ROTK(3,3),RMKM1(3),RMK(3),OMGKM(3),OMPGKM(3),OMGK(3),OMPGK(3),
     &RIGI(18,18),ROTABS(3,3),ROTMT(3,3),ROTKMT(3,3),OMTIRO(3,3),
     &AMAT1(3,3),AMAT2(3,3),AMAT3(3,3),AMAT4(3,3),AMAT5(3,3),AMAT6(3,3),
     &AMAT7(3,3),   VECT1(3),VECT2(3)
C
      ZERO = 0.D0
      UN   = 1.D0
C
      CALL PROMAT (ROTK  ,3,3,3,ROT0  ,3,3,3,   ROTABS)
C
      DO 2 J=1,3
      DO 1 I=1,3
      AMAT2(I,J) = GRANI(I) * ROTABS(J,I)
1     CONTINUE
2     CONTINUE
      CALL PROMAT (ROTABS,3,3,3,AMAT2 ,3,3,3,   IRO)
      CALL PROMAT (IRO   ,3,3,3,OMGK  ,3,3,1,   VECT1)
      CALL ANTISY (VECT1,UN,   IROOMT)
C
      CALL ANTISY (OMGK  ,UN,   AMAT2)
      CALL PROMAT (AMAT2 ,3,3,3,IRO   ,3,3,3,   OMTIRO)
      DO 7 J=1,3
      DO 6 I=1,3
      AMAT5(I,J) = -IROOMT(I,J) + OMTIRO(I,J)
      AMAT1(I,J) = (IRO(I,J) + DELNMK*PAS*AMAT5(I,J)) /ALFNMK/PAS/PAS
6     CONTINUE
7     CONTINUE
C
      DO 12 J=1,3
      DO 11 I=1,3
      ROTMT(I,J) = ROTM(J,I)
      ROTKMT(I,J) = ROTKM1(J,I)
11    CONTINUE
      VECT1(J) = RMK(J) - RMKM1(J)
12    CONTINUE
C
      CALL PROMAT (ROTMT ,3,3,3,VECT1 ,3,3,1,   VECT2)
      CALL PROMAT (ROTK,3,3,3,VECT2 ,3,3,1,     VECT1)
      COEF = -UN
      CALL ANTISY (VECT1,COEF,   AMAT2)
C
      CALL GDT (RMK,     AMAT3)
      CALL PROMAT (ROTMT ,3,3,3,AMAT3 ,3,3,3,   AMAT4)
      CALL PROMAT (ROTK,3,3,3,AMAT4 ,3,3,3,     AMAT3)
C
      DO 17 J=1,3
      DO 16 I=1,3
      AMAT2(I,J) = AMAT2(I,J) + AMAT3(I,J)
16    CONTINUE
17    CONTINUE
C
      CALL PROMAT (AMAT1 ,3,3,3,AMAT2 ,3,3,3,   AMAT3 )
CC
      CALL PROMAT (ROTKMT ,3,3,3,OMPGKM ,3,3,1, VECT1 )
      CALL PROMAT (ROTK,3,3,3,VECT1 ,3,3,1,     VECT2 )
      CALL ANTISY (VECT2 ,COEF  ,AMAT2 )
      CALL PROMAT (IRO   ,3,3,3,AMAT2 ,3,3,3,   AMAT1 )
CC
      CALL PROMAT (ROTKMT ,3,3,3,OMGKM ,3,3,1,  VECT1 )
      CALL PROMAT (ROTK,3,3,3,VECT1 ,3,3,1,     VECT2 )
      CALL ANTISY (VECT2 ,COEF  ,AMAT4 )
      CALL PROMAT (AMAT5 ,3,3,3,AMAT4 ,3,3,3,   AMAT2 )
CCC
      CALL PROMAT (IRO   ,3,3,3,OMPGK,3,3,1,   VECT1 )
      CALL ANTISY (VECT1 ,UN    ,AMAT4 )
      CALL ANTISY (OMPGK,UN    ,AMAT5 )
      CALL PROMAT (IRO   ,3,3,3,AMAT5 ,3,3,3,   AMAT6 )
      DO 19 J=1,3
      DO 18 I=1,3
      AMAT4(I,J) = -AMAT4(I,J) + AMAT6(I,J)
18    CONTINUE
19    CONTINUE
      CALL ANTISY (OMGK  ,UN    ,AMAT5 )
      CALL PROMAT (AMAT5 ,3,3,3,IROOMT,3,3,3,   AMAT6 )
      CALL PROMAT (OMTIRO,3,3,3,AMAT5 ,3,3,3,   AMAT7 )
      DO 21 J=1,3
      DO 20 I=1,3
      AMAT5(I,J) = -AMAT6(I,J) + AMAT7(I,J)
20    CONTINUE
21    CONTINUE
CCC
      DO 32 J=1,6
      DO 31 I=1,6
      INERT(I,J) = ZERO
31    CONTINUE
32    CONTINUE
C
C* ON CALCULE CE QU'APPORTE LA MATRICE DE MASSE A LA PARTIE ROTATOIRE,
C* POUR LE RETRANCHER ICI.
      DO 37 J=1,3
      DO 36 I=1,3
      AMAT6(I,J) = GRANI(I) * ROT0(J,I)
36    CONTINUE
37    CONTINUE
      CALL PROMAT (ROT0 ,3,3,3,AMAT6,3,3,3,   AMAT7)
C
      COEF = UN/ALFNMK/PAS/PAS
      DO 47 J=1,3
      DO 46 I=1,3
      INERT(3+I,3+J) = AMAT3(I,J) + AMAT1(I,J) + AMAT2(I,J) +
     &                 AMAT4(I,J) + AMAT5(I,J) - COEF*AMAT7(I,J)
CC    INERT(3+I,3+J) = COEF * ( AMAT3(I,J) + AMAT1(I,J) + AMAT2(I,J) +
CC   &                          AMAT4(I,J) + AMAT5(I,J) - AMAT7(I,J) )
C    &                          AMAT4(I,J) + AMAT5(I,J)              )
46    CONTINUE
47    CONTINUE
      DO 52 J=1,NNO
      DO 51 I=1,NNO
      COEF = PJACOB * EN(I,KP) * EN(J,KP)
      CALL CUMUMA (I,J,INERT,COEF,   RIGI)
51    CONTINUE
52    CONTINUE
 9999 CONTINUE
      END
