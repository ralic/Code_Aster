      SUBROUTINE TE0090(OPTION,NOMTE)
      IMPLICIT   NONE
      INCLUDE 'jeveux.h'
      CHARACTER*16 OPTION,NOMTE
C ......................................................................
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 13/06/2012   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.

C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.

C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================

C    - FONCTION REALISEE:  CALCUL DES VECTEURS ELEMENTAIRES
C                          OPTION : 'CHAR_MECA_FR1D2D'
C    - ARGUMENTS:
C        DONNEES:      OPTION       -->  OPTION DE CALCUL
C                      NOMTE        -->  NOM DU TYPE ELEMENT
C ......................................................................

      INTEGER NNO,NNOS,JGANO,NDIM,KP,NPG,IPOIDS,IVF,IDFDE,IGEOM
      INTEGER IVECTU,K,I,IFORC,II
      REAL*8 POIDS,R,FX,FY,NX,NY
      LOGICAL LTEATT,LAXI
C     ------------------------------------------------------------------

      CALL ELREF4(' ','RIGI',NDIM,NNO,NNOS,NPG,IPOIDS,IVF,IDFDE,JGANO)
      LAXI = .FALSE.
      IF (LTEATT(' ','AXIS','OUI')) LAXI = .TRUE.

      CALL JEVECH('PGEOMER','L',IGEOM)
      CALL JEVECH('PVECTUR','E',IVECTU)
      CALL TEFREP(OPTION,NOMTE,'PFR1D2D',IFORC)


      DO 40 KP = 1,NPG
        K = (KP-1)*NNO
        CALL VFF2DN(NDIM,NNO,KP,IPOIDS,IDFDE,ZR(IGEOM),NX,NY,POIDS)

C        --- CALCUL DE LA FORCE AUX PG (A PARTIR DES NOEUDS) ---
        FX = 0.0D0
        FY = 0.0D0
        DO 10 I = 1,NNO
          II = 2* (I-1)
          FX = FX + ZR(IFORC+II)*ZR(IVF+K+I-1)
          FY = FY + ZR(IFORC+II+1)*ZR(IVF+K+I-1)
   10   CONTINUE

        IF (LAXI) THEN
          R = 0.D0
          DO 20 I = 1,NNO
            R = R + ZR(IGEOM+2* (I-1))*ZR(IVF+K+I-1)
   20     CONTINUE
          POIDS = POIDS*R
        END IF

        DO 30 I = 1,NNO
          ZR(IVECTU+2* (I-1)) = ZR(IVECTU+2* (I-1)) +
     &                          FX*ZR(IVF+K+I-1)*POIDS
          ZR(IVECTU+2* (I-1)+1) = ZR(IVECTU+2* (I-1)+1) +
     &                            FY*ZR(IVF+K+I-1)*POIDS
   30   CONTINUE

   40 CONTINUE

      END
