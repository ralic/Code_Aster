      SUBROUTINE ERHMS2(PERMAN, INO   , NBS   ,
     &                  THETA , JAC   , NX    , NY    ,
     &                  SIELNP, ADSIP , SIELNM, NBCMP ,
     &                  TYPMAV, TBREF1, TBREF2, IVOIS ,
     &                  TM2H1S                         )
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 20/11/2012   AUTEUR DELMAS J.DELMAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C
C =====================================================================
C  ERREUR EN HYDRO-MECANIQUE - TERME DE SAUT - DIMENSION 2
C  **        *     *                    *                *
C =====================================================================
C  - FONCTION REALISEE :  CALCUL DE L'ERREUR DUE AUX TERMES DE SAUT
C                         DE LA MECANIQUE ET DE L'HYDRAULIQUE DANS
C                         L'INDICATEUR HM PERMANENT.
C
C     ARGUMENTS:
C     ----------
C
C      ENTREE :
C-------------
C IN   PERMAN : PERMANENT OU INSTATIONNAIRE ?
C IN   INO    : NUMERO DE L'ARETE
C IN   NBS    : NOMBRE DE SOMMETS SUR L'ARETE = NOMBRE D'ARETES
C IN   THETA  : PARAMETRE THETA DE DISCRETISATION TEMPORELLE
C IN   JAC    : VECTEUR DES JACOBIENS DE LA TRANSFORMATION AUX NOEUDS
C IN   NX     : VECTEUR DES ABSCISSES DES NORMALES AUX NOEUDS
C IN   NY     : VECTEUR DES ORDONNEES DES NORMALES AUX NOEUDS
C IN   SIELNP : CONTRAINTES AUX NOEUDS PAR ELEMENT A L'INSTANT ACTUEL
C IN   ADSIP  : ADRESSE DANS ZR DU TABLEAU DES CONTRAINTES DE PRESSION
C               DE LA MECANIQUE
C IN   SIELNM : CONTRAINTES AUX NOEUDS PAR ELEMENT A L'INSTANT PRECEDENT
C IN   NBCMP  : NOMBRE DE COMPOSANTES DU VECTEUR DES CONTRAINTES
C               GENERALISEES
C IN   TYPMAV : TYPE DE LA MAILLE VOISINE
C IN   TBREF1 : TABLEAU AUXILIAIRE CONTENANT DES ADRESSES/VALEURS
C IN   TBREF2 : TABLEAU AUXILIAIRE 2 CONTENANT DES ADRESSES/VALEURS
C IN   IVOIS  : ADRESSE DANS ZI DES VOISINS
C
C      SORTIE : SAUT AUX INTERFACES DES TERMES DIFFUSIFS
C               DES INDICATEURS HM
C-------------
C OUT TM2H1S : TABLEAU CONTENANT LES TERMES DE SAUT DES TERMES DIFFUSIFS
C              (2 POUR LA MECANIQUE, 1 POUR L'HYDRAULIQUE)
C  1 : MECANIQUE
C  2 : DERIVEE TEMPORELLE DE LA MECA
C  3 : HYDRAULIQUE
C ......................................................................
C
      IMPLICIT NONE
C
C DECLARATION PARAMETRES D'APPELS
C
      INCLUDE 'jeveux.h'
      LOGICAL     PERMAN
      INTEGER     INO,NBS
      INTEGER     IVOIS,NBCMP,ADSIP
      INTEGER     TBREF1(12),TBREF2(12)
      CHARACTER*8 TYPMAV
      REAL*8      THETA,JAC(3),NX(3),NY(3)
      REAL*8      SIELNP(90),SIELNM(90),TM2H1S(3)
C



C DECLARATION VARIABLES LOCALES
C
      REAL*8      SIXXP(3) ,SIYYP(3) ,SIXYP(3)
      REAL*8      SIXXM(3) ,SIYYM(3) ,SIXYM(3)
      REAL*8      SIXXPV(3),SIYYPV(3),SIXYPV(3)
      REAL*8      SIXXMV(3),SIYYMV(3),SIXYMV(3)
      REAL*8      DSGXXP(3),DSGYYP(3),DSGXYP(3)
      REAL*8      DSGXXM(3),DSGYYM(3),DSGXYM(3)
      REAL*8      FLUXP(3) ,FLUYP(3) ,FLUXM(3) ,FLUYM(3)
      REAL*8      FLUXPV(3),FLUYPV(3),FLUXMV(3),FLUYMV(3)
      REAL*8      DMXP(3)  ,DMYP(3)  ,DMXM(3)  ,DMYM(3)
      REAL*8      DMXFIN(3),DMYFIN(3)
C
      REAL*8      INTME1(3),INTME2(3),INTHYD(3)
      REAL*8      TA1
C
      INTEGER     IAREPE,JCELDP,JCELVP,JCELDM,JCELVM,IMAV,IGREL,IEL,
     &            IAVALP,IAVALM,ICONX1,ICONX2,ADMAVP,ADMAVM,
     &            ADINOV,ADJNOV,ADMNOV
      INTEGER     JAD,JADV,NCHER,INDIIS
      INTEGER     NBNV,I,JNO,MNO,INOV,JNOV,MNOV
      INTEGER     IAUX,IBID
      CHARACTER*2 FORMV,NOEUV
C
C =====================================================================
C 1.  RECUPERATION SUR LA MAILLE COURANTE AUX NOEUDS INO ET JNO DE :
C     . CONTRAINTES EFFECTIVES (SIGMA MECANIQUE : SIXX, SIYY, SIXY)
C     . FLUX HYDRAULIQUE M
C
C              X1          X2          X3
C               O-----------O-----------O
C              INO         MNO         JNO
C
C         POINTS  1 --> INO PREMIER POINT DE L'ARETE COURANTE
C                 2 --> JNO DEUXIEME POINT  DE L'ARETE COURANTE
C                 3 --> MNO NOEUD MILIEU S'IL EXISTE
C =====================================================================
C
      IF ( .NOT.PERMAN ) THEN
        IBID = 1
      ELSE
        IBID = 0
        THETA = 1.D0
      ENDIF
      TA1  = 1.D0 - THETA
C
      IF (INO.EQ.NBS) THEN
        JNO=1
      ELSE
        JNO=INO+1
      ENDIF
C
      IAUX = NBCMP*(INO-1)
      SIXXP(1) = SIELNP(IAUX+1)
      SIYYP(1) = SIELNP(IAUX+2)
      SIXYP(1) = SIELNP(IAUX+4)
      FLUXP(1) = SIELNP(IAUX+ADSIP+IBID+1)
      FLUYP(1) = SIELNP(IAUX+ADSIP+IBID+2)
C
      IAUX = NBCMP*(JNO-1)
      SIXXP(2) = SIELNP(IAUX+1)
      SIYYP(2) = SIELNP(IAUX+2)
      SIXYP(2) = SIELNP(IAUX+4)
      FLUXP(2) = SIELNP(IAUX+ADSIP+IBID+1)
      FLUYP(2) = SIELNP(IAUX+ADSIP+IBID+2)
C
C BIEN QUE LA PRESSION NE SOIT CALCULEE QU'AUX NOEUDS SOMMETS, LE
C GRADIENT L'EST EGALEMENT AU NOEUD MILIEU, VIA EPSTHM
C
      MNO=NBS+INO
C
      IAUX = NBCMP*(MNO-1)
      SIXXP(3) = SIELNP(IAUX+1)
      SIYYP(3) = SIELNP(IAUX+2)
      SIXYP(3) = SIELNP(IAUX+4)

C
C BIEN QUE LA PRESSION NE SOIT CALCULEE QU'AUX NOEUDS SOMMETS, LE
C GRADIENT L'EST EGALEMENT AU NOEUD MILIEU, VIA EPSTHM
C
      FLUXP(3) = SIELNP(IAUX+ADSIP+IBID+1)
      FLUYP(3) = SIELNP(IAUX+ADSIP+IBID+2)
C
      IF ( .NOT. PERMAN ) THEN
C
        IAUX = NBCMP*(INO-1)
        SIXXM(1) = SIELNM(IAUX+1)
        SIYYM(1) = SIELNM(IAUX+2)
        SIXYM(1) = SIELNM(IAUX+4)
        FLUXM(1) = SIELNM(IAUX+ADSIP+IBID+1)
        FLUYM(1) = SIELNM(IAUX+ADSIP+IBID+2)
C
        IAUX = NBCMP*(JNO-1)
        SIXXM(2) = SIELNM(IAUX+1)
        SIYYM(2) = SIELNM(IAUX+2)
        SIXYM(2) = SIELNM(IAUX+4)
        FLUXM(2) = SIELNM(IAUX+ADSIP+IBID+1)
        FLUYM(2) = SIELNM(IAUX+ADSIP+IBID+2)
C
C BIEN QUE LA PRESSION NE SOIT CALCULEE QU'AUX NOEUDS SOMMETS, LE
C GRADIENT L'EST EGALEMENT AU NOEUD MILIEU, VIA EPSTHM
C
        MNO=NBS+INO
C
        IAUX = NBCMP*(MNO-1)
        SIXXM(3) = SIELNM(IAUX+1)
        SIYYM(3) = SIELNM(IAUX+2)
        SIXYM(3) = SIELNM(IAUX+4)
        FLUXM(3) = SIELNM(IAUX+ADSIP+IBID+1)
        FLUYM(3) = SIELNM(IAUX+ADSIP+IBID+2)
C
      ENDIF

C
C =====================================================================
C 2. CARACTERISATION DE LA MAILLE VOISINE
C =====================================================================
C
C 2.1. ---- RECHERCHE DES ADRESSES POUR OBTENIR SIGMA SUR LE VOISIN ---
C
      IAREPE = TBREF1(1)
      JCELDP = TBREF1(2)
      JCELVP = TBREF1(3)
      JCELDM = TBREF2(2)
      JCELVM = TBREF2(3)
C
C ADRESSE DE LA MAILLE VOISINE PARTAGEANT L'ARETE INO
      IMAV = ZI(IVOIS+INO)

C NUMERO DU LIGREL DE LA MAILLE VOISINE DE NUMERO GLOBAL IMAV
      IGREL = ZI(IAREPE+2*(IMAV-1))

C INDICE DE LA MAILLE VOISINE DANS LE IGREL
      IEL = ZI(IAREPE+2*(IMAV-1)+1)

C ADRESSE (DANS .CELV DE SIELNP) DU DEBUT DU GREL IGREL
      IAVALP = JCELVP-1+ZI(JCELDP-1+ZI(JCELDP-1+4+IGREL)+8)
      IF ( .NOT. PERMAN ) THEN
C ADRESSE (DANS .CELV DE SIELNM) DU DEBUT DU GREL IGREL
        IAVALM = JCELVM-1+ZI(JCELDM-1+ZI(JCELDM-1+4+IGREL)+8)
      ELSE
        IAVALM = 1
      ENDIF  
C
C 2.2. ----- TESTS SUR LA MAILLE VOISINE ------------------------------
C
      FORMV = TYPMAV(1:2)
      NOEUV = TYPMAV(5:5)
C
      IF (FORMV.EQ.'TR') THEN
        IF (NOEUV.EQ.'3') THEN
          NBNV = 3
        ELSE
          NBNV = 6
        ENDIF
      ELSE IF (FORMV.EQ.'QU') THEN
        IF (NOEUV.EQ.'4') THEN
          NBNV = 4
        ELSE IF (NOEUV.EQ.'8') THEN
          NBNV = 8
        ELSE
          NBNV = 9
        ENDIF
      ENDIF
C
C 2.3 ----- ADRESSE DANS LE VECTEUR DES CONTRAINTES DE LA PREMIERE
C           COMPOSANTE DU 1ER NOEUD DE L'ELEMENT IEL DE NUMERO IGREL
C
      ADMAVP = IAVALP-1+NBCMP*NBNV*(IEL-1)
      ADMAVM = IAVALM-1+NBCMP*NBNV*(IEL-1)
C
C ADRESSE DE LA COLLECTION CONNECTIVITE
C
      ICONX1 = TBREF1(11)

C ADRESSE DU POINTEUR DE LONGUEUR DE LA CONNECTIVITE
      ICONX2 = TBREF1(12)
C
      JAD  = ICONX1-1+ZI(ICONX2+ZI(IVOIS    )-1)
      JADV = ICONX1-1+ZI(ICONX2+ZI(IVOIS+INO)-1)
C
C     NCHER : NUMERO GLOBAL DU NOEUD LOCAL INO DANS LA MAILLE COURANTE
C
      NCHER = ZI(JAD-1+INO)
C
C     ON CHERCHE A QUEL NUMERO LOCAL (INOV) DANS LA MAILLE VOISINE
C     CORRESPOND LE NOEUD DE NUMERO GLOBAL NCHER
C
      INOV = INDIIS(ZI(JADV),NCHER,1,NBNV)
C
C     IDEM POUR JNO LOCAL DE LA MAILLE COURANTE
C
      NCHER = ZI(JAD-1+JNO)
      JNOV  = INDIIS(ZI(JADV),NCHER,1,NBNV)
C
C ADRESSE DES NOEUDS INOV ET JNOV DE LA MAILLE VOISINE
C DE L'ELEMENT COURANT
C
      ADINOV = NBCMP*(INOV-1)
      ADJNOV = NBCMP*(JNOV-1)
C
C CONTRAINTES AUX NOEUDS SUR L'ELEMENT VOISIN, POUR LES 2 NOEUDS DE
C L'ARETE
C
      IAUX = ADMAVP+ADINOV
      SIXXPV(1) = ZR(IAUX+1)
      SIYYPV(1) = ZR(IAUX+2)
      SIXYPV(1) = ZR(IAUX+4)
      FLUXPV(1) = ZR(IAUX+ADSIP+IBID+1)
      FLUYPV(1) = ZR(IAUX+ADSIP+IBID+2)
C
      IAUX = ADMAVP+ADJNOV
      SIXXPV(2) = ZR(IAUX+1)
      SIYYPV(2) = ZR(IAUX+2)
      SIXYPV(2) = ZR(IAUX+4)
      FLUXPV(2) = ZR(IAUX+ADSIP+IBID+1)
      FLUYPV(2) = ZR(IAUX+ADSIP+IBID+2)
C
C 2.4. NOEUD MILIEU
C
      MNO=NBS+INO
C
C MEME OPERATION QUE POUR LES EXTREMITES : ON PASSE PAR LE NUMERO GLOBAL
C
      NCHER = ZI(JAD-1+MNO)
      MNOV = INDIIS(ZI(JADV),NCHER,1,NBNV)
C
C ADRESSE DU NOEUD MNOV DE LA MAILLE VOISINE DE L'ELEMENT COURANT
C
      ADMNOV = NBCMP*(MNOV-1)
C
      IAUX = ADMAVP+ADMNOV
      SIXXPV(3) = ZR(IAUX+1)
      SIYYPV(3) = ZR(IAUX+2)
      SIXYPV(3) = ZR(IAUX+4)
      FLUXPV(3) = ZR(IAUX+ADSIP+IBID+1)
      FLUYPV(3) = ZR(IAUX+ADSIP+IBID+2)
C
      IF ( .NOT. PERMAN ) THEN
C
C CONTRAINTES MECANIQUES ET HYDRAULIQUES AUX NOEUDS
C SUR L'ELEMENT VOISIN, POUR LES 2 NOEUDS DE L'ARETE
C
        IAUX = ADMAVM+ADINOV
C
        SIXXMV(1) = ZR(IAUX+1)
        SIYYMV(1) = ZR(IAUX+2)
        SIXYMV(1) = ZR(IAUX+4)
        FLUXMV(1) = ZR(IAUX+ADSIP+IBID+1)
        FLUYMV(1) = ZR(IAUX+ADSIP+IBID+2)
C
        IAUX = ADMAVM+ADJNOV
C
        SIXXMV(2) = ZR(IAUX+1)
        SIYYMV(2) = ZR(IAUX+2)
        SIXYMV(2) = ZR(IAUX+4)
        FLUXMV(2) = ZR(IAUX+ADSIP+IBID+1)
        FLUYMV(2) = ZR(IAUX+ADSIP+IBID+2)
C
C NOEUD MILIEU
C
        MNO = NBS + INO
C
C MEME OPERATION QUE POUR LES EXTREMITES : ON PASSE PAR LE NUMERO GLOBAL
C
        NCHER = ZI(JAD-1+MNO)
        MNOV = INDIIS(ZI(JADV),NCHER,1,NBNV)
C
C ADRESSE DU NOEUD MNOV DE LA MAILLE VOISINE DE L'ELEMENT COURANT
C
        ADMNOV = NBCMP*(MNOV-1)
C
        IAUX = ADMAVM+ADMNOV
C
        SIXXMV(3) = ZR(IAUX+1)
        SIYYMV(3) = ZR(IAUX+2)
        SIXYMV(3) = ZR(IAUX+4)
        FLUXMV(3) = ZR(IAUX+ADSIP+IBID+1)
        FLUYMV(3) = ZR(IAUX+ADSIP+IBID+2)
C
      ENDIF
C
C =====================================================================
C 3. --- CALCUL DES SAUTS DES TERMES DIFFUSIFS
C =====================================================================
C
C =====================================================================
C 3.A --- PARTIE MECANIQUE
C =====================================================================
C
C 3.1. SAUTS EN CHAQUE NOEUD DE L'ARETE
C
      DO 10 , I=1 , 3
C
        DSGXXP(I) = SIXXP(I) - SIXXPV(I)
        DSGYYP(I) = SIYYP(I) - SIYYPV(I)
        DSGXYP(I) = SIXYP(I) - SIXYPV(I)
C
        DMXP(I) = FLUXP(I) - FLUXPV(I)
        DMYP(I) = FLUYP(I) - FLUYPV(I)
C
C POUR L'INTEGRATION NUMERIQUE, ON UTILISE :
C      . SI 3 NOEUDS SOMMETS, UNE FORMULATION DE SIMPSON
C      . SI 2 NOEUDS SOMMETS, UNE METHODE DES TRAPEZES
C
        INTME1(I) = JAC(I)*
     &         ( (DSGXXP(I)*NX(I) + DSGXYP(I)*NY(I))**2
     &          +(DSGXYP(I)*NX(I) + DSGYYP(I)*NY(I))**2 )
C
        DMXFIN(I) = THETA * DMXP(I)
        DMYFIN(I) = THETA * DMYP(I)
C
        IF ( .NOT. PERMAN ) THEN
C
          DSGXXM(I) = SIXXM(I) - SIXXMV(I)
          DSGYYM(I) = SIYYM(I) - SIYYMV(I)
          DSGXYM(I) = SIXYM(I) - SIXYMV(I)
C
          DMXM(I) = FLUXM(I) - FLUXMV(I)
          DMYM(I) = FLUYM(I) - FLUYMV(I)
C
          INTME2(I) = JAC(I)* (
     &               ( (DSGXXP(I) - DSGXXM(I))*NX(I)
     &                +(DSGXYP(I) - DSGXYM(I))*NY(I))**2
     &              +( (DSGXYP(I) - DSGXYM(I))*NX(I)
     &                +(DSGYYP(I) - DSGYYM(I))*NY(I))**2 )
C
          DMXFIN(I) = THETA * DMXP(I) + TA1 * DMXM(I)
          DMYFIN(I) = THETA * DMYP(I) + TA1 * DMYM(I)
C
        ENDIF
C
C =====================================================================
C 3.B --- PARTIE HYDRAULIQUE
C =====================================================================
C
        INTHYD(I) = JAC(I)*((DMXFIN(I)*NX(I) + DMYFIN(I)*NY(I))**2)
C
 10   CONTINUE
C
C =====================================================================
C 3.C --- ASSEMBLAGE DES DIFFERENTS TERMES
C =====================================================================
C
      TM2H1S(1) = TM2H1S(1)
     &  + ((INTME1(1)+4.D0*INTME1(3)+INTME1(2))/3.D0)
      TM2H1S(3) = TM2H1S(3)
     &  + ((INTHYD(1)+4.D0*INTHYD(3)+INTHYD(2))/3.D0)
C
      IF ( .NOT. PERMAN ) THEN
C
        TM2H1S(2) = TM2H1S(2)
     &    + (INTME2(1)+4.D0*INTME2(3)+INTME2(2))/3.D0
C
      ENDIF
C
      END
