      SUBROUTINE DIALUM ( NNO , NDDL , LDIM, WGT , MASCO , MASDI )
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 03/07/2012   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
      IMPLICIT NONE
      REAL*8        MASCO(*) , MASDI(*)
      INTEGER       NNO, NDDL, LDIM
C     ------------------------------------------------------------------
C     PASSAGE D'UNE MATRICE MASSE CONSISTANTE A UNE MATRICE MASSE 
C     DIAGONALE SUIVANT LA SPECIAL LUMPING TECHNIQUE
C     ------------------------------------------------------------------
C     IN  NNO   : NOMBRE DE NOEUDS
C     IN  NDDL  : NOMBRE DE DDL PAR NOEUD
C     IN  LDIM  : NOMBRE TOTAL DE DDL DE L ELEMENT
C     IN  WGT   : POIDS ELEMENTAIRE
C     IN  MASCO : MATRICE DE MASSE CONSISTANTE
C     OUT MASDI : MATRICE DE MASSE DIAGONALE
C     ------------------------------------------------------------------
C-----------------------------------------------------------------------
      INTEGER I ,K 
      REAL*8 WGT ,XNUL 
C-----------------------------------------------------------------------
      PARAMETER    (XNUL = 1.D-5)
      INTEGER      IDEC, IDIAG, NDIM, IP, ITAB(300)
      INTEGER      IDIRX,IDIRY,IDIRZ,IDIRRX,IDIRRY,IDIRRZ
      REAL*8       SOMMEX, SOMMEY, SOMMEZ , XINF
      REAL*8       ALFAX, ALFAY, ALFAZ
C
      IP = 0
      SOMMEX = 0.D0
      SOMMEY = 0.D0
      SOMMEZ = 0.D0
C-- BOUCLE DE CALCUL DE LA SOMME DES TERMES DIAGONAUX SUIVANT CHAQUE 
C   DIRECTION DE TRANSLATION
      NDIM = LDIM * (LDIM+1) / 2
      DO 130 I=1,NDIM
         MASDI(I)=0.D0
  130 CONTINUE 
      DO 145 I=1,LDIM
        IDIAG = (I*(I+1)/2)
        DO 140 K = 1,NNO
          IDEC = (K-1)*NDDL+1
          IDIRX = IDEC*(IDEC+1)/2
          IDIRY = (IDEC+1)*(IDEC+2)/2
          IDIRZ = (IDEC+2)*(IDEC+3)/2
          IF (IDIAG.EQ.IDIRX)THEN
            SOMMEX = SOMMEX + MASCO(IDIAG)
          ELSEIF (IDIAG.EQ.IDIRY)THEN
            SOMMEY = SOMMEY + MASCO(IDIAG)
          ELSEIF (IDIAG.EQ.IDIRZ)THEN
            SOMMEZ = SOMMEZ + MASCO(IDIAG)
          ENDIF
  140   CONTINUE
  145 CONTINUE
      XINF = MASCO(1)
C-- CALCUL DU COEFFICIENT MULTIPLICATEUR
      ALFAX = WGT / SOMMEX
      ALFAY = WGT / SOMMEY
      ALFAZ = WGT / SOMMEZ
C-- BOUCLE DE CALCUL DES TERMES DIAGONNAUX AVEC PROTECTION CONTRE 
C-- TERMES DE ROTATION NULS ---> MATRICE SINGULIERE
      DO 160 I=1,LDIM
         IDIAG = (I*(I+1)/2)
         IF (MASCO(IDIAG).EQ.0.D0)THEN
            IP=IP+1
            ITAB(IP) = IDIAG
         ELSE
           IF (XINF.GT.MASCO(IDIAG)) XINF= MASCO(IDIAG)
           DO 150 K = 1,NNO
             IDEC = (K-1)*NDDL+1
             IDIRX = IDEC*(IDEC+1)/2
             IDIRY = (IDEC+1)*(IDEC+2)/2
             IDIRZ = (IDEC+2)*(IDEC+3)/2
             IDIRRX = (IDEC+3)*(IDEC+4)/2
             IDIRRY = (IDEC+4)*(IDEC+5)/2
             IDIRRZ = (IDEC+5)*(IDEC+6)/2
             IF (IDIAG.EQ.IDIRX)THEN
               MASDI(IDIAG) = ALFAX * MASCO(IDIAG)
             ELSEIF (IDIAG.EQ.IDIRY)THEN
               MASDI(IDIAG) = ALFAY * MASCO(IDIAG)
             ELSEIF (IDIAG.EQ.IDIRZ)THEN
               MASDI(IDIAG) = ALFAZ * MASCO(IDIAG)
             ELSEIF (IDIAG.EQ.IDIRRX)THEN
               MASDI(IDIAG) = ALFAX * MASCO(IDIAG)
             ELSEIF (IDIAG.EQ.IDIRRY)THEN
               MASDI(IDIAG) = ALFAY * MASCO(IDIAG)
             ELSEIF (IDIAG.EQ.IDIRRZ)THEN
               MASDI(IDIAG) = ALFAZ * MASCO(IDIAG)
             ENDIF
  150      CONTINUE
         ENDIF
  160 CONTINUE       
      IF (IP.NE.0)THEN
         DO 170 I=1,IP
            IDEC = ITAB(I)
            MASDI(IDEC) = XNUL * ALFAX * XINF
  170    CONTINUE       
      ENDIF
      END
