      SUBROUTINE TE0423 ( OPTION , NOMTE )
      IMPLICIT NONE
      CHARACTER*16        OPTION , NOMTE 
C.......................................................................
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 21/01/2004   AUTEUR CIBHHLV L.VIVAN 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C
C     BUT: CALCUL DES VECTEURS ELEMENTAIRES EN MECANIQUE
C          CORRESPONDANT A UN CHARGEMENT EN PRESSION SUIVEUSE
C          SUR DES FACES D'ELEMENTS ISOPARAMETRIQUES 3D
C          (LA PRESSION EST DONNEE SOUS FORME D'UNE FONCTION)
C
C          OPTION : 'CHAR_MECA_PRSU_F '
C
C     ENTREES  ---> OPTION : OPTION DE CALCUL
C          ---> NOMTE  : NOM DU TYPE ELEMENT
C.......................................................................
C
C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ---------------------
      INTEGER            ZI
      COMMON  / IVARJE / ZI(1)
      REAL*8             ZR
      COMMON  / RVARJE / ZR(1)
      COMPLEX*16         ZC
      COMMON  / CVARJE / ZC(1)
      LOGICAL            ZL
      COMMON  / LVARJE / ZL(1)
      CHARACTER*8        ZK8
      CHARACTER*16                ZK16
      CHARACTER*24                          ZK24
      CHARACTER*32                                    ZK32
      CHARACTER*80                                              ZK80
      COMMON  / KVARJE / ZK8(1) , ZK16(1) , ZK24(1) , ZK32(1) , ZK80(1)
C --------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ---------------------
      CHARACTER*8   NOMPAR(4)
      INTEGER       IER,NDIM,NNO,NPG,NNOS,JGANO
      INTEGER       IPOIDS,IVF,IDF,IGEOM,IPRES,ITEMPS,IRES
      INTEGER       IDEPM,IDEPP,KPG,KDEC,N,I
      REAL*8        VALPAR(4),X,Y,Z
C                                   9*27*27
      REAL*8        PR, P(27), MATR(6561)
C
C
      CALL ELREF4(' ','RIGI',NDIM,NNO,NNOS,NPG,IPOIDS,IVF,IDF,JGANO)
C
C    AUTRES VARIABLES DE L'OPTION
      CALL JEVECH('PGEOMER','L',IGEOM)
      CALL JEVECH('PDEPLMR','L',IDEPM)
      CALL JEVECH('PDEPLPR','L',IDEPP)
      CALL JEVECH('PPRESSF','L',IPRES)
      CALL JEVECH('PTEMPSR','L',ITEMPS)
      CALL JEVECH('PVECTUR','E',IRES)
C
C    REACTUALISATION DE LA GEOMETRIE PAR LE DEPLACEMENT
      DO 10 I = 0,NNO*3-1
        ZR(IGEOM+I) = ZR(IGEOM+I) + ZR(IDEPM+I) + ZR(IDEPP+I)
 10   CONTINUE

C    CALCUL DE LA PRESSION AUX PTS DE GAUSS (FONCTION DE T ET/OU X,Y,Z)
      VALPAR(4) = ZR(ITEMPS)
      NOMPAR(4) = 'INST'
      NOMPAR(1) = 'X'
      NOMPAR(2) = 'Y'
      NOMPAR(3) = 'Z'
C
      DO 100 KPG=0,NPG-1
        KDEC = KPG*NNO
C
C      COORDONNEES DU POINT DE GAUSS
        X = 0.D0
        Y = 0.D0
        Z = 0.D0
        DO 105 N = 0,NNO-1
          X = X + ZR(IGEOM+3*N  ) * ZR(IVF+KDEC+N)
          Y = Y + ZR(IGEOM+3*N+1) * ZR(IVF+KDEC+N)
          Z = Z + ZR(IGEOM+3*N+2) * ZR(IVF+KDEC+N)
 105    CONTINUE
C
C      VALEUR DE LA PRESSION
        VALPAR(1) = X
        VALPAR(2) = Y
        VALPAR(3) = Z
        CALL FOINTE('FM',ZK8(IPRES),4,NOMPAR,VALPAR,PR,IER)
        P(1+KPG) = PR
100   CONTINUE
C
C    CALCUL EFFECTIF DU SECOND MEMBRE
      CALL NMPR3D(1,NNO,NPG,ZR(IPOIDS),ZR(IVF),ZR(IDF),ZR(IGEOM),P,
     &            ZR(IRES),MATR)
C
      END
