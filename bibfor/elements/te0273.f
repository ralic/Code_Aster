      SUBROUTINE TE0273(OPTION,NOMTE)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 16/01/2012   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C----------------------------------------------------------------------
C
C     BUT: CALCUL DES VECTEURS ELEMENTAIRES EN THERMIQUE
C          CORRESPONDANT AU FLUX NON-LINEAIRE
C          SUR DES FACES D'ELEMENTS ISOPARAMETRIQUES 3D
C
C          OPTION : 'CHAR_THER_FLUNL'
C
C     ENTREES  ---> OPTION : OPTION DE CALCUL
C              ---> NOMTE  : NOM DU TYPE ELEMENT
C----------------------------------------------------------------------
C CORPS DU PROGRAMME
      IMPLICIT NONE

      CHARACTER*16  OPTION,NOMTE
      REAL*8        NX,NY,NZ,SX(9,9),SY(9,9),SZ(9,9),JAC,THETA,TPG,
     &              ALPHA,RBID
      INTEGER       NDIM,NNO,NPG1,IPOIDS,IVF,IDFDX,IDFDY,
     &              IGEOM,IFLUX,ITEMPR,ITEMPS,INO,JNO,IVECTT,
     &              I,J,KP,KDEC,LDEC,IDEC,JDEC,
     &              NNOS,JGANO
      CHARACTER*8   COEF

C --------------- COMMUNS NORMALISES  JEVEUX  --------------------------
      COMMON /IVARJE/ZI(1)
      COMMON /RVARJE/ZR(1)
      COMMON /CVARJE/ZC(1)
      COMMON /LVARJE/ZL(1)
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
      INTEGER ZI
      REAL*8 ZR
      COMPLEX*16 ZC
      LOGICAL ZL
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
C -----------FIN  COMMUNS NORMALISES  JEVEUX  --------------------------

C====
C 1.1 PREALABLES: RECUPERATION ADRESSES FONCTIONS DE FORMES...
C====
      CALL JEMARQ()

      CALL ELREF4(' ','RIGI',NDIM,NNO,NNOS,NPG1,IPOIDS,IVF,IDFDX,JGANO)
      IDFDY  = IDFDX  + 1
C

C====
C 1.2 PREALABLES LIES AUX RECHERCHES DE DONNEES GENERALES
C====

C RECUPERATION DE T-
      CALL JEVECH('PTEMPER','L',ITEMPR)

      CALL JEVECH('PGEOMER','L',IGEOM )
      CALL JEVECH('PTEMPSR','L',ITEMPS)
C FLUX NON-LIN
      CALL JEVECH('PFLUXNL','L',IFLUX )
      CALL JEVECH('PVECTTR','E',IVECTT)

C====
C 1.3 PREALABLES LIES AUX CALCULS
C====

      THETA  = ZR(ITEMPS+2)
      COEF   = ZK8(IFLUX)
      IF ( COEF(1:7) .EQ. '&FOZERO' ) GOTO 999

C    CALCUL DES PRODUITS VECTORIELS OMI   OMJ

      DO 1 INO = 1,NNO
        I = IGEOM + 3*(INO-1) -1
        DO 2 JNO = 1,NNO
          J = IGEOM + 3*(JNO-1) -1
          SX(INO,JNO) = ZR(I+2) * ZR(J+3) - ZR(I+3) * ZR(J+2)
          SY(INO,JNO) = ZR(I+3) * ZR(J+1) - ZR(I+1) * ZR(J+3)
          SZ(INO,JNO) = ZR(I+1) * ZR(J+2) - ZR(I+2) * ZR(J+1)
 2      CONTINUE
 1    CONTINUE

C====
C 2. CALCULS TERMES
C====
C    BOUCLE SUR LES POINTS DE GAUSS
      DO 101 KP=1,NPG1

        KDEC = (KP-1)*NNO*NDIM
        LDEC = (KP-1)*NNO
        NX = 0.0D0
        NY = 0.0D0
        NZ = 0.0D0

C   CALCUL DE LA NORMALE AU POINT DE GAUSS KP

        DO 102 I=1,NNO
          IDEC = (I-1)*NDIM
          DO 102 J=1,NNO
            JDEC = (J-1)*NDIM
            NX = NX+ ZR(IDFDX+KDEC+IDEC)* ZR(IDFDY+KDEC+JDEC)* SX(I,J)
            NY = NY+ ZR(IDFDX+KDEC+IDEC)* ZR(IDFDY+KDEC+JDEC)* SY(I,J)
            NZ = NZ+ ZR(IDFDX+KDEC+IDEC)* ZR(IDFDY+KDEC+JDEC)* SZ(I,J)
 102    CONTINUE

C   CALCUL DU JACOBIEN AU POINT DE GAUSS KP
        JAC = SQRT(NX*NX + NY*NY + NZ*NZ)

        TPG  = 0.D0
        DO 104 I=1,NNO
C CALCUL DE T-
          TPG  = TPG  + ZR(ITEMPR+I-1) * ZR(IVF+LDEC+I-1)
 104    CONTINUE

C OBTENTION DES PARAMETRES MATERIAU
        CALL FODERI(COEF,TPG,ALPHA,RBID)
C
        DO 108 I=1,NNO
          ZR(IVECTT+I-1) = ZR(IVECTT+I-1) + ZR(IPOIDS+KP-1)*JAC*
     &                (1.D0-THETA)*ALPHA*ZR(IVF+LDEC+I-1)
 108    CONTINUE
 
C FIN BOUCLE SUR LES PT DE GAUSS
 101  CONTINUE
C EXIT SI LE FLUX EST LA FONCTION NULLE
 999  CONTINUE
C FIN ------------------------------------------------------------------
      CALL JEDEMA()
      END
