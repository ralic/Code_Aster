      SUBROUTINE XBSIGM (NNOP,DDLH,DDLC,IGEOM,PINTT,CNSET,
     &                   HEAVT,LONCH,SIGMA,BSIGMA)
      
       IMPLICIT NONE
C
       INTEGER       NNOP,DDLH,DDLC,IGEOM
       INTEGER       CNSET(4*28),HEAVT(36),LONCH(7)
       REAL*8        PINTT(3*9),SIGMA(*),BSIGMA(3+DDLH+DDLC,NNOP)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 18/04/2005   AUTEUR GENIAUT S.GENIAUT 
C ======================================================================
C COPYRIGHT (C) 1991 - 2005  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE GENIAUT S.GENIAUT
C
C
C      BSIGMC  -- CALCUL DES FORCES INTERNES B*SIGMA AUX NOEUDS 
C                 DE L'ELEMENT DUES AU CHAMP DE CONTRAINTES SIGMA 
C                 DEFINI AUX POINTS D'INTEGRATION DANS LE CADRE DE
C                 LA MÉTHODE X-FEM
C
C IN  NNOP    : NOMBRE DE NOEUDS DE L'ELEMENT PARENT
C IN  DDLH    : NOMBRE DE DDLS HEAVYSIDE (PAR NOEUD)
C IN  DDLC    : NOMBRE DE DDLS DE CONTACT (PAR NOEUD)
C IN  IGEOM   : COORDONEES DES NOEUDS
C IN  PINTT   : COORDONNÉES DES POINTS D'INTERSECTION
C IN  CNSET   : CONNECTIVITE DES SOUS-ELEMENTS
C IN  HEAVT   : VALEURS DE L'HEAVISIDE SUR LES SS-ELTS
C IN  LONCH   : LONGUEURS DES CHAMPS UTILISÉES
C IN  SIGMA   : CONTRAINTES DE CAUCHY AUX POINTS DE GAUSS DES SOUS-ÉLTS

C OUT BSIGMA  : BT.SIGMA
C
C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ---------------------
      INTEGER  ZI
      COMMON  / IVARJE / ZI(1)
      REAL*8             ZR
      COMMON  / RVARJE / ZR(1)
      COMPLEX*16         ZC
      COMMON  / CVARJE / ZC(1)
      LOGICAL            ZL
      COMMON  / LVARJE / ZL(1)
      CHARACTER*8        ZK8
      CHARACTER*16                ZK16
      CHARACTER*24                          ZK24
      CHARACTER*32                                    ZK32
      CHARACTER*80                                              ZK80
      COMMON  / KVARJE / ZK8(1) , ZK16(1) , ZK24(1) , ZK32(1) , ZK80(1)
C --------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ---------------------
C
C     VARIABLES LOCALES
      REAL*8        HE
      CHARACTER*8   ELREFP
      CHARACTER*24  COORSE
      INTEGER       CONNEC(6,4),NSE
      INTEGER       IT,ISE,IN,INO,NIT,NSEMAX,CPT,NPG,NCMP,IDEB,JCOORS,I
      PARAMETER    (NSEMAX=6)
C.========================= DEBUT DU CODE EXECUTABLE ==================
C
      CALL ELREF1(ELREFP)

C     RÉCUPÉRATION DE LA SUBDIVISION L'ÉLÉMENT PARENT EN NIT TETRAS 
      NIT=LONCH(1)

      CPT=0
C     BOUCLE SUR LES NIT TETRAS
      DO 100 IT=1,NIT

C       RÉCUPÉRATION DU DÉCOUPAGE EN NSE SOUS-ÉLÉMENTS 
        NSE=LONCH(1+IT)

C       BOUCLE SUR LES NSE SOUS-ÉLÉMENTS
        DO 110 ISE=1,NSE

          CPT=CPT+1

C         COORD DU SOUS-ÉLT EN QUESTION
          COORSE='&&XMEL3D.COORSE'
          CALL WKVECT(COORSE,'V V R',3*4,JCOORS)
C         BOUCLE SUR LES 4 SOMMETS DU SOUS-TÉTRA
          DO 112 IN=1,4
            INO=CNSET(4*(CPT-1)+IN)
            IF (INO.LT.1000) THEN
              ZR(JCOORS-1+3*(IN-1)+1)=ZR(IGEOM-1+3*(INO-1)+1)
              ZR(JCOORS-1+3*(IN-1)+2)=ZR(IGEOM-1+3*(INO-1)+2)
              ZR(JCOORS-1+3*(IN-1)+3)=ZR(IGEOM-1+3*(INO-1)+3)
            ELSE
              ZR(JCOORS-1+3*(IN-1)+1)=PINTT(3*(INO-1000-1)+1)
              ZR(JCOORS-1+3*(IN-1)+2)=PINTT(3*(INO-1000-1)+2)
              ZR(JCOORS-1+3*(IN-1)+3)=PINTT(3*(INO-1000-1)+3)
            ENDIF
 112      CONTINUE

C         FONCTION HEAVYSIDE CSTE SUR LE SS-ÉLT
          HE=HEAVT(NSEMAX*(IT-1)+ISE)

C         DEBUT DE LA ZONE MÉMOIRE DE SIGMA CORRESPONDANTE
          NPG=15
          NCMP=6
          IDEB=(NSEMAX*NPG*(IT-1)+NPG*(ISE-1))*NCMP+1

          CALL XIBSIG(ELREFP,COORSE,IGEOM,HE,DDLH,DDLC,NNOP,NPG,
     &                SIGMA(IDEB),BSIGMA)

          CALL JEDETR(COORSE)

 110    CONTINUE

 100  CONTINUE
 
C.============================ FIN DE LA ROUTINE ======================
      END
