      SUBROUTINE TE0365 ( OPTION, NOMTE )
      IMPLICIT   NONE
      CHARACTER*16        OPTION, NOMTE
C ----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 07/07/2009   AUTEUR DESOZA T.DESOZA 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C  CALCUL DES SECONDS MEMBRES DE CONTACT ET DE FROTTEMENT DE COULOMB STD
C        AVEC LA METHODE CONTINUE DE L'ECP
C  OPTION : 'CHAR_MECA_CONT' (CALCUL DU SECOND MEMBRE DE CONTACT)
C           'CHAR_MECA_FROT' (CALCUL DU SECOND MEMBRE DE
C                              FROTTEMENT STANDARD )
C
C  ENTREES  ---> OPTION : OPTION DE CALCUL
C           ---> NOMTE  : NOM DU TYPE ELEMENT
C ----------------------------------------------------------------------
C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ---------------------
C
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C --------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ---------------------
C
      INTEGER I,II,J,NBCPS,NBDM
      INTEGER NNE,NNM,NDIM,NDDL,INDPIV
      INTEGER INDCO,INADH,INDASP,INDM
      INTEGER IGEOM,IDEPL,IVECT,JPCF,IDEPM,IACCM,IVITM,IVITP
      INTEGER INI1,INI2,INI3,INDNOR,INDRAC,IUSURE
      INTEGER INDNOB,INDNOQ,IMA,IMABAR,TYPBAR
      REAL*8 NOOR,R8PREM      
      REAL*8  COEFFF,COEFFA,COEFCA,COEASP,ASPERI,CN
      REAL*8 COEFFS,COEFCS,COEFFP,COEFCP
      REAL*8 XPC,YPC,XPR,YPR,HPG,JACOBI,LAMBDA,MERESE,R8BID
      REAL*8 TAU1(3),TAU2(3),NORM(3)
      REAL*8 FFPC(9),DFFPC(2,9),DDFFPC(3,9)
      REAL*8 FFPR(9),DFFPR(2,9),DDFFPR(3,9)
      REAL*8 GEOME(3),DEPLE(6),DEPLM(6),GEOMM(3),RESE(3)
      REAL*8 DEPLME(6),DEPLMM(6)
      REAL*8 ALPHA,DELTA,DELTAT,JEUSUP,JEU,JEUVIT,JEVITP
      INTEGER IFROTT,ICOMPL,IAXIS,IFORM
      INTEGER IUSUP,TYALGC,TYALGF
      REAL*8 ACCME(6),ACCMM(6),VITME(6),VITMM(6),R8BI33(3,3)
      REAL*8 VITPE(6),VITPM(3),VTMP(81),PRFUSU
      CHARACTER*8 ESC,MAIT
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
C
C --- INFOS SUR LA MAILLE DE CONTACT
C
      CALL MMELEM (NOMTE,NDIM,NDDL,ESC,NNE,MAIT,NNM)
      CALL ASSERT (NDDL.LE.81)
      CALL ASSERT ((NDIM.EQ.2).OR.(NDIM.EQ.3))
C
C --- INITIALISATION DU VECTEUR DE TRAVAIL
C
      DO 3  J = 1,NDDL
         VTMP(J) = 0.D0
3     CONTINUE
C
C --- RECUPERATION DES DONNEES DU CHAM_ELEM DU CONTACT (VOIR MMCHML)
C
      CALL JEVECH ( 'PCONFR', 'L', JPCF )
      XPC      =      ZR(JPCF-1+1)
      YPC      =      ZR(JPCF-1+10)
      XPR      =      ZR(JPCF-1+2)
      YPR      =      ZR(JPCF-1+3)
      TAU1(1)  =      ZR(JPCF-1+4)
      TAU1(2)  =      ZR(JPCF-1+5)
      TAU1(3)  =      ZR(JPCF-1+6)
      TAU2(1)  =      ZR(JPCF-1+7)
      TAU2(2)  =      ZR(JPCF-1+8)
      TAU2(3)  =      ZR(JPCF-1+9)
      INDCO    = NINT(ZR(JPCF-1+11))
      LAMBDA   =      ZR(JPCF-1+12)
      COEFCA   =      ZR(JPCF-1+13)
      COEFFA   =      ZR(JPCF-1+14)
      COEFFF   =      ZR(JPCF-1+15)
      IFROTT   = NINT(ZR(JPCF-1+16))
      INDNOR   = NINT(ZR(JPCF-1+17))
      IAXIS    = NINT(ZR(JPCF-1+18))
      HPG      =      ZR(JPCF-1+19)
      DELTAT   =      ZR(JPCF-1+20)
      IFORM    = NINT(ZR(JPCF-1+21))
      INDM     = NINT(ZR(JPCF-1+22))
      INI1     = NINT(ZR(JPCF-1+23))
      INI2     = NINT(ZR(JPCF-1+24))
      INI3     = NINT(ZR(JPCF-1+25))
      INDASP   = NINT(ZR(JPCF-1+26))
      ICOMPL   = NINT(ZR(JPCF-1+27))
      ASPERI   =      ZR(JPCF-1+28)
      COEASP   =      ZR(JPCF-1+29)
      CN       =      ZR(JPCF-1+30)
      ALPHA    =      ZR(JPCF-1+31)
      DELTA    =      ZR(JPCF-1+32)
      JEUSUP   =      ZR(JPCF-1+33)
      IMA      = NINT(ZR(JPCF-1+34))
      IMABAR   = NINT(ZR(JPCF-1+35))
      INDNOB   = NINT(ZR(JPCF-1+36))
      INDNOQ   = NINT(ZR(JPCF-1+37))
      TYPBAR   = NINT(ZR(JPCF-1+38))
      INDRAC   = NINT(ZR(JPCF-1+39))
      IUSURE   = NINT(ZR(JPCF-1+40))
      INDPIV   = NINT(ZR(JPCF-1+43))
      TYALGC   = NINT(ZR(JPCF-1+44))
      COEFCS   =      ZR(JPCF-1+45)
      COEFCP   =      ZR(JPCF-1+46)
      TYALGF   = NINT(ZR(JPCF-1+47))
      COEFFS   =      ZR(JPCF-1+48)
      COEFFP   =      ZR(JPCF-1+49)
C
C --- RECUPERATION DE LA GEOMETRIE ET DES CHAMPS DE DEPLACEMENT
C
      CALL JEVECH ( 'PGEOMER', 'E',IGEOM )
      CALL JEVECH ( 'PDEPL_P', 'E',IDEPL )
      CALL JEVECH ( 'PDEPL_M', 'L',IDEPM )
      IF (IUSURE.EQ.1)  CALL JEVECH ( 'PUSULAR', 'L', IUSUP )
      IF (IFORM .EQ.2)  CALL JEVECH ( 'PVITE_P', 'L', IVITP )
      IF (ICOMPL .EQ. 1) THEN
         CALL JEVECH ( 'PDEPLAR', 'L', IVITM )
         CALL JEVECH ( 'PCHDYNR', 'L', IACCM )
      END IF
C
      IF ( IFROTT .EQ. 3 ) THEN
C ------ COMPOSANTES 3D : LAGS_C   LAGS_F1  LAGS_F2
C ------ COMPOSANTES 2D : LAGS_C   LAGS_F1
         NBCPS = NDIM
         NBDM  = NDIM + NBCPS
      ELSE
C ------ COMPOSANTE : LAGS_C
         NBCPS = 1
         NBDM  = NDIM + NBCPS
      ENDIF
C
C --- REACTUALISATION DE LA GEOMETRIE
C
      CALL MMREAC (NBDM,NDIM,NNE,NNM,IGEOM,IDEPM)
C
C --- FONCTIONS DE FORMES ET DERIVEES POUR LE
C --- POINT DE CONTACT
C
      IF (IMA .NE. IMABAR)  TYPBAR = -1
C
      CALL MMMFFD (ESC,NDIM,NNE,XPC,YPC,TYPBAR,FFPC,DFFPC,DDFFPC)
C
C --- JACOBIEN POUR LE POINT DE CONTACT
C
      CALL MMMJAC (ESC,IGEOM,FFPC,DFFPC,IAXIS,
     &             NDIM,JACOBI)
C
C --- FONCTIONS DE FORMES ET DERIVEES POUR LA
C --- PROJECTION DU POINT DE CONTACT
C
      CALL MMMFFD (MAIT,NDIM,NNM,XPR,YPR,TYPBAR,FFPR,DFFPR,DDFFPR)
C
C --- CALCUL DE LA NORMALE
C
      CALL MMNORM (NDIM,TAU1,TAU2,NORM,NOOR)
      IF (NOOR.LT.R8PREM()) THEN
        CALL ASSERT(.FALSE.)
      ENDIF
C
C --- GEOMETRIE ET DEPLACEMENTS DU POINT DE CONTACT ET
C --- DE SON PROJETE
C
      CALL MMGEOM (NBDM,NDIM,NNE,NNM,ICOMPL,
     &             IGEOM,IDEPL,IDEPM,IVITM,IVITP,IACCM,
     &             FFPC,FFPR,GEOME,GEOMM,
     &             DEPLE,DEPLM,DEPLME,DEPLMM,
     &             VITME,ACCME,VITMM,ACCMM,
     &             IFORM,VITPE,VITPM)
C
C --- MODIFICATIONS DES DEPLACEMENTS (RACCORD/FISSURE)
C
      CALL MMGEO2 (NBDM,NDIM,
     &             IMA,IMABAR,INDNOB,INDRAC,INDNOQ,
     &             IDEPL, DEPLE)
C
C --- TRAITEMENT EN FOND DE FISSURE
C
      IF (INDNOB .GT. 0)  INDCO = 1
C
C --- TRAITEMENT DU RACCORD SURFACIQUE
C
      IF (INDRAC .GT. 0)  INDCO = 1
C
C --- NOEUDS EXCLUS PAR PROJECTION HORS ZONE
C
      IF (INDNOR .EQ. 1)  INDCO = 0
C
C --- RECUPERATION DES VECTEURS 'OUT' (A REMPLIR => MODE ECRITURE)
C
      CALL JEVECH('PVECTUR','E',IVECT)
C
C --- CALCUL DES SECOND MEMBRES DE CONTACT/FROTTEMENT
C
      IF (IUSURE .EQ. 1) THEN
         PRFUSU = ZR(IUSUP-1+1)
      ELSE
         PRFUSU = 0.D0
      END IF
C
C     ====================================
      IF (OPTION.EQ.'CHAR_MECA_CONT') THEN
C     ====================================
C
        IF (INDPIV.EQ.1)  INDCO = 0
        IF (INDASP.EQ.0) THEN
C
C --- CALCUL DU VECTEUR - CAS SANS CONTACT (HORS ASPERITES)
C
          CALL MMVEC0(NBDM,NDIM,NNE,DEPLE,HPG,FFPC,JACOBI,
     &                TYALGC,COEFCA,COEFCS,COEFCP, VTMP)

          GO TO 220

        ELSE IF (INDASP.EQ.1) THEN
          IF (INDCO.EQ.1) THEN
C
C --- EVALUATION DES JEUX - CAS DU CONTACT
C
            CALL MMMJEU(NDIM,JEUSUP,ICOMPL,NORM,ALPHA,DELTA,DELTAT,
     &                  GEOME,GEOMM,DEPLE,DEPLM,DEPLME,DEPLMM,
     &                  VITME,VITMM,ACCME,ACCMM,JEU,JEUVIT,
     &                  JEVITP,PRFUSU,IFORM,VITPE,VITPM)
C
C --- VECTEUR SECOND MEMBRE SI CONTACT AVEC COMPLIANCE
C
            CALL MMVEC1(NBDM,NDIM,NNE,NNM,IMA,IMABAR,INDNOB,INDRAC,
     &                  HPG,FFPC,FFPR,JACOBI,DEPLE,
     &                  TYALGC,COEFCA,COEFCS,COEFCP,ICOMPL,IFORM,
     &                  COEASP,ASPERI,JEU,JEUVIT,NORM, VTMP)
C
          ELSE IF (INDCO.EQ.0) THEN
C
C --- CALCUL DU VECTEUR - CAS SANS CONTACT (DANS ASPERITES)
C
            CALL MMVEC0(NBDM,NDIM,NNE,DEPLE,HPG,FFPC,JACOBI,
     &                  TYALGC,COEFCA,COEFCS,COEFCP, VTMP)
C
            IF (ICOMPL .EQ. 1) THEN
C
C --- EVALUATION DES JEUX
C
              CALL MMMJEU(NDIM,JEUSUP,ICOMPL,NORM,ALPHA,DELTA,DELTAT,
     &                    GEOME,GEOMM,DEPLE,DEPLM,DEPLME,DEPLMM,
     &                    VITME,VITMM,ACCME,ACCMM,JEU,JEUVIT,
     &                    JEVITP,PRFUSU,IFORM,VITPE,VITPM)
C
C --- DDL DES DEPLACEMENTS DE LA SURFACE CINEMATIQUE
C
              DO 72 I = 1,NNE
                DO 62 J = 1,NDIM
                  II = (I-1)*NBDM+J
                  VTMP(II) = -HPG*JACOBI*
     &                       (-COEASP*((JEU-ASPERI)**2)-CN*JEVITP)*
     &                       FFPC(I)*NORM(J)
   62           CONTINUE
   72         CONTINUE
C
C --- DDL DES DEPLACEMENTS DE LA SURFACE GEOMETRIQUE
C
              DO 92 I = 1,NNM
                DO 82 J = 1,NDIM
                  II = NNE*NBDM+(I-1)*NDIM+J
                  VTMP(II) = HPG*JACOBI*
     &                       (-COEASP*((JEU-ASPERI)**2)-CN*JEVITP)*
     &                       FFPR(I)*NORM(J)
   82           CONTINUE
   92         CONTINUE

            ENDIF
          ENDIF
          GO TO 220
        ELSE
          CALL U2MESS('F','ELEMENTS3_80')
        ENDIF
C
C     =========================================
      ELSE IF (OPTION.EQ.'CHAR_MECA_FROT') THEN
C     =========================================
C
        IF (COEFFF.EQ.0.D0) INDCO = 0
        IF (IFROTT.NE.3)    INDCO = 0
        IF (LAMBDA.EQ.0.D0) INDCO = 0
        IF (INDPIV.EQ.1)    INDCO = 0

        IF (INDCO.EQ.0) THEN
C
C --- CALCUL DU VECTEUR - CAS SANS CONTACT (HORS ASPERITES)
C
          CALL MMVEF0 (NBDM,NBCPS,NDIM,NNE,HPG,FFPC,JACOBI,DEPLE,
     &                 TAU1,TAU2, VTMP)

          GO TO 220

        ELSE IF (INDCO.EQ.1) THEN
C
C ---  ON CALCULE L'ETAT DE CONTACT ADHERENT OU GLISSANT
C
          CALL TTPRSM (NDIM,DEPLE,DEPLM,
     &                 TYALGF,COEFFA,COEFFS,COEFFP,NORM,
     &                 TAU1,TAU2,INADH,R8BI33,RESE,R8BID)
C
C --- SI GLISSANT, NORMALISATION RESE
C
          IF (INADH.EQ.0)  CALL NORMEV(RESE,MERESE)
C
C --- VECTEUR FROTTEMENT
C
          CALL MMVEF1 (NBDM,NBCPS,NDIM,NNE,NNM,INDM,INI1,INI2,INI3,
     &                 HPG,FFPC,FFPR,JACOBI,RESE,DEPLE,LAMBDA,
     &                 TYALGF,COEFFA,COEFFS,COEFFP,
     &                 COEFFF,TAU1,TAU2,NORM, VTMP)
        ELSE
          CALL U2MESS('F','ELEMENTS3_80')
        END IF
      ELSE
C       SI OPTION NI 'CHAR_MECA_CONT' NI 'CHAR_MECA_FROT'
        CALL ASSERT(OPTION.EQ.'CHAR_MECA_FROT' .OR.
     &              OPTION.EQ.'CHAR_MECA_CONT')  
      END IF
  220 CONTINUE
C
C --- RECOPIE VALEURS FINALES
C
      DO 6 I = 1,NDDL
        ZR(IVECT-1+I) = VTMP(I)
6     CONTINUE
C
      CALL JEDEMA()
      END
