      SUBROUTINE TE0532(OPTION,NOMTE)
      IMPLICIT   NONE
      CHARACTER*16 OPTION,NOMTE

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 12/05/2009   AUTEUR MAZET S.MAZET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2005  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE GENIAUT S.GENIAUT
C.......................................................................
C
C                CONTACT X-FEM MÉTHODE CONTINUE :
C         CONVERGENCE DE LA BOUCLE SUR LES CONTRAINTES ACTIVES
C
C
C  OPTION : 'XCVBCA' (X-FEM CONVERGENCE BOUCLE CONTRAINTES ACTIVES )

C  ENTREES  ---> OPTION : OPTION DE CALCUL
C           ---> NOMTE  : NOM DU TYPE ELEMENT
C
C......................................................................
C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX --------------------
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)

C --------- FIN  DECLARATIONS  NORMALISEES  JEVEUX --------------------

      INTEGER      I,J,L,IJ,IFA,IPGF,INO,ISSPG,NI,NJ,NLI,NLJ,PLI,PLJ
      INTEGER      JINDCO,JDONCO,JLST,IPOIDS,IVF,IDFDE,JGANO,IGEOM
      INTEGER      IDEPM,IDEPL,IMATT,JPTINT,JAINT,JCFACE,JLONCH,JGLISS
      INTEGER      IPOIDF,IVFF,IDFDEF,IADZI,IAZK24,IBID,JOUT1,JOUT2
      INTEGER      JOUT3, JMEMCO,NDIM,DDLH,DDLC,DDLS,NNO,NNOS,NNOM
      INTEGER      NPG,NPGF,XOULA,INCOCA,INTEG,NFE,SINGU,NINTER,NNOF
      INTEGER      INDCO(60),GLISS(60),MEMCO(60),NFACE,CFACE(5,3),NBAR
      INTEGER      NNOL,PLA(27),LACT(40),NOLA(27),AR(12,2),NLINOL(27)
      CHARACTER*8   ELREF,TYPMA,FPG,ELC,LAG
      REAL*8        HE,SIGN,VTMP(204),SOMME,FFI,REAC,JAC,FFP(27),PREC
      REAL*8        ND(3),DN,SAUT(3),LAMBDA,LST,R,RR,E,G(3),RBID
      PARAMETER    (PREC=1.D-16)
      LOGICAL       IMPRIM,MALIN
C......................................................................

      CALL JEMARQ()
C
      IMPRIM=.FALSE.
      INCOCA=0

      CALL ELREF1(ELREF)
      CALL ELREF4(' ','RIGI',NDIM,NNO,NNOS,NPG,IPOIDS,IVF,IDFDE,JGANO)
C
      CALL XTEINI(NOMTE,DDLH,NFE,SINGU,DDLC,NNOM,DDLS,IBID)
C
C     RECUPERATION DES ENTRÉES / SORTIE
      CALL JEVECH('PGEOMER','E',IGEOM)
C     DEPLACEMENT À L'ÉQUILIBRE PRÉCÉDENT  (DEPMOI) : 'PDEPL_M'
      CALL JEVECH('PDEPL_M','L',IDEPM)
C     DEPLACEMENT COURANT (DEPPLU) : 'PDEPL_P'
      CALL JEVECH('PDEPL_P','L',IDEPL)
      CALL JEVECH('PINDCOI','L',JINDCO)
      CALL JEVECH('PDONCO','L',JDONCO)
      CALL JEVECH('PGLISS','L',JGLISS)
      CALL JEVECH('PLST','L',JLST)
      CALL JEVECH('PPINTER','L',JPTINT)
      CALL JEVECH('PAINTER','L',JAINT)
      CALL JEVECH('PCFACE','L',JCFACE)
      CALL JEVECH('PLONCHA','L',JLONCH)
      CALL JEVECH('PMEMCON','L',JMEMCO)

      CALL JEVECH('PINCOCA','E',JOUT1)
      CALL JEVECH('PINDCOO','E',JOUT2)
      CALL JEVECH('PINDMEM','E',JOUT3)

C     RÉCUPÉRATIONS DES DONNÉES SUR LE CONTACT ET
C     SUR LA TOPOLOGIE DES FACETTES
      NINTER=ZI(JLONCH-1+1)
      IF (NINTER.LT.NDIM)  GOTO 9999

      DO 10 I=1,60
        INDCO(I) = ZI(JINDCO-1+I)
 10   CONTINUE
 
      DO 11 I=1,60
        GLISS(I) = ZI(JGLISS-1+I)
 11   CONTINUE
 
      DO 12 I=1,60
        MEMCO(I) = ZI(JMEMCO-1+I)
 12   CONTINUE
 

      NFACE=ZI(JLONCH-1+2)
      DO 15 I=1,NFACE
        DO 16 J=1,NDIM
          CFACE(I,J)=ZI(JCFACE-1+NDIM*(I-1)+J)
   16   CONTINUE
 15   CONTINUE
C
C     SCHEMA D'INTEGRATION NUMERIQUE ET ELEMENT DE REFERENCE DE CONTACT
      INTEG = NINT(ZR(JDONCO-1+4))
      IF (NDIM .EQ. 3) THEN
        IF (INTEG.EQ.1) FPG='NOEU'
        IF (INTEG.EQ.2) FPG='GAUSS'
        IF (INTEG.EQ.3) FPG='SIMP'
        IF (INTEG.EQ.4) FPG='SIMP1'
        IF (INTEG.EQ.6) FPG='COTES'
        IF (INTEG.EQ.10) FPG='XCON'
        IF (INTEG.EQ.14) FPG='FPG4'
        IF (INTEG.EQ.16) FPG='FPG6'
        IF (INTEG.EQ.17) FPG='FPG7'
        ELC='TR3'
      ELSEIF (NDIM.EQ.2) THEN
        IF (INTEG.EQ.1) FPG='NOEU'
        IF (INTEG.EQ.2) FPG='GAUSS'
        IF (INTEG.EQ.3) FPG='SIMP'
        IF (INTEG.EQ.4) FPG='SIMP1'
        IF (INTEG.EQ.6) FPG='COTES'
        IF (INTEG.EQ.7) FPG='COTES1'
        IF (INTEG.EQ.8) FPG='COTES2'
        IF (INTEG.EQ.12) FPG='FPG2'
        IF (INTEG.EQ.13) FPG='FPG3'
        IF (INTEG.EQ.14) FPG='FPG4'
        ELC='SE2'
      ENDIF
C
      CALL ELREF4(ELC,FPG,IBID,NNOF,IBID,NPGF,IPOIDF,IVFF,IDFDEF,IBID)
C
C     RECUPERATION DU COEFFICIENT DE MISE À L'ECHELLE DES PRESSIONS
      E=ZR(JDONCO-1+5)

      CALL TECAEL(IADZI,IAZK24)
      TYPMA=ZK24(IAZK24-1+3+ZI(IADZI-1+2)+3)

C     IMPRESSION (1ERE PARTIE)
      IF (IMPRIM) THEN
        WRITE(6,*)' '
        WRITE(6,697)
 697    FORMAT(4X,'I_IN',7X,'DN',11X,'REAC',7X,'I_OUT')
      ENDIF
C
C     L'ELEMENT EST-IL LINEAIRE OU QUADRATIQUE
C
      CALL TEATTR (NOMTE,'C','XLAG',LAG,IBID)
      IF (IBID.EQ.0.AND.LAG.EQ.'NOEUD') THEN
        MALIN=.TRUE.
      ELSE
        MALIN=.FALSE.
      ENDIF
C
C     LISTES DES LAMBDAS ACTIFS PAR FACETTE
C
      CALL CONARE(TYPMA,AR,NBAR)
      CALL XLACTI(NDIM,NFACE,NNO,NNOM,CFACE,JAINT,AR,LACT)
C
C     BOUCLE SUR LES FACETTES
      DO 100 IFA=1,NFACE

C       NOMBRE DE LAMBDAS ET LEUR PLACE DANS LA MATRICE
        IF (MALIN) THEN
          NNOL=0
          DO 13 I=1,NNO
            IF (LACT(I).NE.0) THEN
              NNOL=NNOL+1
C             NOEUD SOMMET DE L'ELT PARENT
              NOLA(NNOL)=I
C             POINT D'INTERSECTION (POUR BASE COVARIANTE)
C             NLINOL(NNOL)=LACT(I)
C             PLACE DU LAMBDA DANS LA MATRICE
              CALL XPLMAT(NDIM,DDLH,NFE,DDLC,NNO,NNOM,I,PLI)
              PLA(NNOL)=PLI
            ENDIF
 13       CONTINUE
        ELSE
          NNOL=NNOF
          DO 14 I=1,NNOF
C           XOULA  : RENVOIE LE NUMERO DU NOEUD PORTANT CE LAMBDA
            NI=XOULA(CFACE,IFA,I,JAINT,TYPMA)
C           PLACE DU LAMBDA DANS LA MATRICE
            CALL XPLMAT(NDIM,DDLH,NFE,DDLC,NNO,NNOM,NI,PLI)
            PLA(I)=PLI
 14       CONTINUE
        ENDIF

C       BOUCLE SUR LES POINTS DE GAUSS DES FACETTES
        DO 110 IPGF=1,NPGF
C
C         INDICE DE CE POINT DE GAUSS DANS INDCO
          ISSPG=NPGF*(IFA-1)+IPGF
C
C         CALCUL DE JAC (PRODUIT DU JACOBIEN ET DU POIDS)
C         ET DES FF DE L'ÉLÉMENT PARENT AU POINT DE GAUSS
C         ET LA NORMALE ND ORIENTÉE DE ESCL -> MAIT
          IF (NDIM .EQ. 3) THEN
            CALL XJACFF(ELREF,FPG,JPTINT,IFA,CFACE,IPGF,NNO,IGEOM,G,
     &                                        'NON',JAC,FFP,RBID,ND)
          ELSEIF (NDIM.EQ.2) THEN
            CALL XJACF2(ELREF,FPG,JPTINT,IFA,CFACE,IPGF,NNO,IGEOM,G,
     &                                        'NON',JAC,FFP,RBID,ND)
          ENDIF
C
C         CALCUL DE RR = SQRT(DISTANCE AU FOND DE FISSURE)
          IF (SINGU.EQ.1) THEN
            LST=0.D0
            DO 112 I=1,NNO
              LST=LST+ZR(JLST-1+I)*FFP(I)
 112        CONTINUE
            R=ABS(LST)
            RR=SQRT(R)
          ENDIF
C
C         CALCUL DU SAUT ET DE DN EN CE PG (DEPMOI + DEPDEL)
          CALL LCINVN(NDIM,0.D0,SAUT)
          DO 140 I = 1,NNO
            DO 141 J = 1,DDLH
              SAUT(J) = SAUT(J) - 2.D0 * FFP(I) *
     &                         (   ZR(IDEPM-1+DDLS*(I-1)+NDIM+J)
     &                           + ZR(IDEPL-1+DDLS*(I-1)+NDIM+J) )
 141        CONTINUE
            DO 142 J = 1,SINGU*NDIM
              SAUT(J) = SAUT(J) - 2.D0 * FFP(I) * RR *
     &                         (   ZR(IDEPM-1+DDLS*(I-1)+NDIM+DDLH+J)
     &                           + ZR(IDEPL-1+DDLS*(I-1)+NDIM+DDLH+J) )
 142        CONTINUE
 140      CONTINUE
          DN = 0.D0
          DO 143 J = 1,NDIM
            DN = DN + SAUT(J)*ND(J)
 143      CONTINUE

C         CALCUL DE LA REACTION A PARTIR DES LAMBDA DE DEPPLU
C         RQ : LA VALEUR DANS IDEPL EST LA PRESSION DIVISÉE PAR E
          REAC = 0.D0
          DO 150 I = 1,NNOL
            PLI=PLA(I)
            IF (MALIN) THEN
              FFI=FFP(NOLA(I))
            ELSE
              FFI=ZR(IVFF-1+NNOF*(IPGF-1)+I)
            ENDIF
            LAMBDA = ZR(IDEPL-1+PLI)
            REAC = REAC + FFI * LAMBDA * E
 150      CONTINUE
 
          IF (INDCO(ISSPG).EQ.0) THEN

C       ON REGARDE LA DISTANCE DN DES POINTS SUPPOSÉS NON CONTACTANT :
C       INTERPÉNÉPRATION EQUIVAUT À DN > 0 (ICI DN > 1E-16 )

            IF (DN.GT.PREC) THEN
              ZI(JOUT2-1+ISSPG) = 1
              ZI(JOUT3-1+ISSPG) = 1
              INCOCA = 1
            ELSE
              ZI(JOUT2-1+ISSPG) = 0
            END IF
C
C         ON REGARDE LA REACTION POUR LES POINTS SUPPOSÉS CONTACTANT :
          ELSE IF (INDCO(ISSPG).EQ.1) THEN
            IF (REAC.GT.-1.D-3) THEN
C             SI GLISSIERE=OUI ET IL Y A EU DU CONTACT DEJA SUR CE
C             POINT (MEMCON=1), ALORS ON FORCE LE CONTACT
              IF ((GLISS(ISSPG).EQ.1).AND.(MEMCO(ISSPG).EQ.1)) THEN
                ZI(JOUT2-1+ISSPG) = 1
                ZI(JOUT3-1+ISSPG) = 1
              ELSE IF (GLISS(ISSPG).EQ.0) THEN
                ZI(JOUT2-1+ISSPG) = 0
                INCOCA = 1
              ENDIF
            ELSE
              ZI(JOUT2-1+ISSPG) = 1
              ZI(JOUT3-1+ISSPG) = 1
            END IF
C
          ELSE
C             SI INDCO N'EST NI ÉGAL À 0 NI ÉGAL À 1: 
C             PROBLEME DE STATUT DE CONTACT.
              CALL ASSERT(INDCO(ISSPG).EQ.0 .OR. INDCO(ISSPG).EQ.1)
          END IF
C
C         IMPRESSION (2EME PARTIE)
          IF (IMPRIM) THEN
            WRITE(6,698)INDCO(ISSPG),DN,REAC,ZI(JOUT2-1+ISSPG)
 698        FORMAT(5X,I1,4X,E11.5,4X,E11.5,4X,I1)
          ENDIF

 110    CONTINUE
 100  CONTINUE
C
 9999 CONTINUE

C     ENREGISTREMENT DES CHAMPS DE SORTIE
      ZI(JOUT1)=INCOCA

      CALL JEDEMA()
      END
