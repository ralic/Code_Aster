      SUBROUTINE TE0488(OPTION,NOMTE)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 13/06/2012   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.

C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.

C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
      IMPLICIT NONE
      INCLUDE 'jeveux.h'
      CHARACTER*16 OPTION,NOMTE
C ----------------------------------------------------------------------
C     CALCUL DES COORDONNEES DES POINTS DE GAUSS
C     POUR LES ELEMENTS ISOPARAMETRIQUES 3D  ET LEURS ELEMENTS DE PEAU




      INTEGER JGANO,NNO,KP,IPOIDS,IVF,IGEOM
      INTEGER NPG,NNOS,ICOPG,INO,NDIM,IDFDE

      REAL*8 XX,YY,ZZ,RBID81(81),POIDS,COVA(3,3),METR(2,2),JAC
      INTEGER INBF,NBC,ICOQ,DECPO,IC,ISPC,JTAB(7),NBSP,IRET
      REAL*8 EPAIS,EXCEN,BAS,EPC,PGL(3,3),GM1(3),GM2(3)
      INTEGER LZI,LZR, NB1,NB2
      REAL*8 ZERO,VECTG(2,3),VECTT(3,3)
      REAL*8 VECTA(9,2,3),VECTN(9,3),VECTPT(9,2,3)
      REAL*8 POIDC(3),HH
      PARAMETER(ZERO=0.0D0)
      LOGICAL COQ3D,GRILLE
      DATA GM1 / 0.D0,0.D0,1.D0/
      DATA POIDC / 0.16666666666666666D0,0.66666666666666663D0,
     &             0.16666666666666666D0/
C DEB ------------------------------------------------------------------
      COQ3D=NOMTE(1:4).EQ.'MEC3'
      GRILLE= NOMTE(1:4).EQ.'MEGC'
      IF(COQ3D)THEN
        CALL ELREF4(' ','MASS',NDIM,NNO,NNOS,NPG,IPOIDS,IVF,IDFDE,JGANO)
      ELSE
        CALL ELREF4(' ','RIGI',NDIM,NNO,NNOS,NPG,IPOIDS,IVF,IDFDE,JGANO)
      ENDIF

      CALL JEVECH('PGEOMER','L',IGEOM)
      CALL TECACH('OON','PCOORPG',7,JTAB,IRET)
      ICOPG=JTAB(1)
      NBSP=JTAB(7)

C POUR LES GRILLES, LE NOMBRE DE SOUS POINTS N'EST PAS ACTIF DANS LE
C CALCUL ON BLINDE
      IF(GRILLE)THEN
        NBSP=1
        CALL JEVECH ('PCACOQU' , 'L' , ICOQ)
        EXCEN = ZR(ICOQ+3)
        IF (NNO.EQ.3)THEN
          CALL DXTPGL ( ZR(IGEOM), PGL )
        ELSEIF(NNO.EQ.4)THEN
          CALL DXQPGL ( ZR(IGEOM), PGL )
        ENDIF
        CALL UTPVLG(1,3,PGL,GM1,GM2)
      ENDIF

C EN CAS DE SOUS POINTS (DKT,DST,COQUE_3D)
      IF(NBSP.NE.1) THEN
        CALL JEVECH('PNBSP_I','L',INBF)
        NBC = ZI(INBF)
        CALL JEVECH ('PCACOQU' , 'L' , ICOQ)
        EPAIS = ZR(ICOQ)
        IF (COQ3D) THEN
          EXCEN = ZR(ICOQ+5)
C COQUES 3D : ON UTILISE LE VECTEUR NORMAL A CHAQUE POINT DE GAUSS
C  ON PREPARE VECTN POUR LE CALCUL DE GM2 DANS LA BOUCLE PG PLUS BAS
          CALL JEVETE('&INEL.'//NOMTE(1:8)//'.DESI',' ',LZI)
          NB1 = ZI(LZI-1+1)
          NB2 = ZI(LZI-1+2)
          NPG = ZI(LZI-1+4)
          CALL JEVETE('&INEL.'//NOMTE(1:8)//'.DESR',' ', LZR )
          CALL VECTAN(NB1,NB2,ZR(IGEOM),ZR(LZR),VECTA,VECTN,VECTPT)
        ELSE
          EXCEN = ZR(ICOQ+4)
C DKT,DST : ON UTILISE LE VECTEUR NORMAL DE LA PLAQUE
C   ON CALCULE GM2 UNE SEULE FOIS
          IF (NNO.EQ.3)THEN
            CALL DXTPGL ( ZR(IGEOM), PGL )
          ELSEIF(NNO.EQ.4)THEN
            CALL DXQPGL ( ZR(IGEOM), PGL )
          ENDIF
          CALL UTPVLG(1,3,PGL,GM1,GM2)
        ENDIF
        BAS=-EPAIS/2.D0+EXCEN
        EPC=EPAIS/NBC
      ENDIF

C BOUCLE POINT DE GAUSS
      DO 20 KP = 1,NPG
C     CALCUL DES COORDONNEES DES POINTS DE GAUSS
        XX = 0.D0
        YY = 0.D0
        ZZ = 0.D0
        DO 10 INO = 1,NNO
          XX = XX + ZR(IGEOM+3* (INO-1)+0)*ZR(IVF+ (KP-1)*NNO+INO-1)
          YY = YY + ZR(IGEOM+3* (INO-1)+1)*ZR(IVF+ (KP-1)*NNO+INO-1)
          ZZ = ZZ + ZR(IGEOM+3* (INO-1)+2)*ZR(IVF+ (KP-1)*NNO+INO-1)
   10   CONTINUE
        IF (NDIM.EQ.3) THEN
C         -- CAS DES ELEMENTS 3D
          CALL DFDM3D ( NNO, KP, IPOIDS, IDFDE,
     &                ZR(IGEOM),RBID81,RBID81,RBID81,POIDS)
        ELSEIF (NDIM.EQ.2) THEN
C         -- CAS DES ELEMENTS "PEAU" DE 3D
          CALL SUBACO(NNO,ZR(IDFDE+(KP-1)*NDIM*NNO),ZR(IGEOM),COVA)
          CALL SUMETR(COVA,METR,JAC)
          POIDS=JAC*ZR(IPOIDS-1+KP)
        ELSE
          CALL ASSERT(.FALSE.)
        ENDIF


        IF (NBSP.NE.1)THEN
          DECPO=4*3*NBC*(KP-1)
          IF (COQ3D)THEN
C         CALCUL DE LA NORMAL AU POINT DE GAUSS
            CALL VECTGT(1,NB1,ZR(IGEOM),ZERO,KP,ZR(LZR),EPAIS,VECTN,
     &                  VECTG,VECTT)
            GM2(1)=VECTT(3,1)
            GM2(2)=VECTT(3,2)
            GM2(3)=VECTT(3,3)
          ENDIF
          DO 30 IC=1,NBC
            DO 32 ISPC=1,3
              HH=BAS+DBLE(IC-1)*EPC+DBLE(ISPC-1)*EPC/2.D0
              ZR(ICOPG+DECPO+(IC-1)*12+(ISPC-1)*4+0) = XX + HH*GM2(1)
              ZR(ICOPG+DECPO+(IC-1)*12+(ISPC-1)*4+1) = YY + HH*GM2(2)
              ZR(ICOPG+DECPO+(IC-1)*12+(ISPC-1)*4+2) = ZZ + HH*GM2(3)
              ZR(ICOPG+DECPO+(IC-1)*12+(ISPC-1)*4+3) =
     &                                          POIDS*EPC*POIDC(ISPC)
  32        CONTINUE
 30       CONTINUE
        ELSE IF (GRILLE) THEN
          ZR(ICOPG+4*(KP-1)+0) = XX+ EXCEN*GM2(1)
          ZR(ICOPG+4*(KP-1)+1) = YY+ EXCEN*GM2(2)
          ZR(ICOPG+4*(KP-1)+2) = ZZ+ EXCEN*GM2(3)
          ZR(ICOPG+4*(KP-1)+3) = POIDS
        ELSE
          ZR(ICOPG+4*(KP-1)+0) = XX
          ZR(ICOPG+4*(KP-1)+1) = YY
          ZR(ICOPG+4*(KP-1)+2) = ZZ
          ZR(ICOPG+4*(KP-1)+3) = POIDS
        ENDIF
   20 CONTINUE

      END
