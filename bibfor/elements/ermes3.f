      SUBROUTINE ERMES3(NOE,IFA,ITYP,TYPMAV,NPGF,
     &                  IREF1,IVOIS,ISIG,NBCMP,
     &                  DSG11,DSG22,DSG33,DSG12,DSG13,DSG23)
      IMPLICIT NONE
      INTEGER IFA,ITYP,NBNV,NPGF,IREF1,IVOIS,ISIG,NBCMP,NOE(9,6,3)
      REAL*8 DSG11(9),DSG22(9),DSG33(9),DSG12(9),DSG13(9),DSG23(9)
      CHARACTER*8 TYPMAV
C ----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 24/08/2009   AUTEUR DELMAS J.DELMAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2006  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C RESPONSABLE DELMAS J.DELMAS
C =====================================================================
C  ERREUR EN MECANIQUE - TERME DE SAUT - DIMENSION 3
C  **        **                   *                *
C =====================================================================
C
C     BUT:
C         DEUXIEME TERME DE L'ESTIMATEUR D'ERREUR EN RESIDU EXPLICITE : 
C         CALCUL DU SAUT DE CONTRAINTE ENTRE UNE FACE D'UN ELEMENT 
C         ET SON VOISIN EN 3D.
C
C
C     ARGUMENTS:
C     ----------
C
C      ENTREE :
C-------------
C IN   NOE      : TABLEAU DES NUMEROS DE NOEUDS FACE PAR FACE 
C IN   IFA      : NUMERO DE LA FACE
C IN   ITYP     : NUMERO DE TYPE DE LA MAILLE COURANTE
C IN   TYPMAV   : TYPE DE LA MAILLE VOISINE
C IN   NPGF     : NOMBRE DE POINTS DE GAUSS DE LA FACE
C IN   IREF1    : ADRESSE DES CHARGEMENTS DE TYPE FORCE (CONTENANT AUSSI
C                 LES INFOS SUR LES VOISINS)
C IN   IVOIS    : ADRESSE DES VOISINS
C IN   ISIG     : ADRESSE DES CONTRAINTES AUX NOEUDS
C IN   NBCMP    : NOMBRE DE COMPOSANTES DU VECTEUR CONTRAINTE PAR NOEUD
C
C      SORTIE :
C-------------
C OUT  DSG11  :	SAUT DE CONTRAINTE AUX NOEUDS COMPOSANTE 11
C OUT  DSG22  :	SAUT DE CONTRAINTE AUX NOEUDS COMPOSANTE 22
C OUT  DSG33  :	SAUT DE CONTRAINTE AUX NOEUDS COMPOSANTE 33
C OUT  DSG12  :	SAUT DE CONTRAINTE AUX NOEUDS COMPOSANTE 12
C OUT  DSG13  :	SAUT DE CONTRAINTE AUX NOEUDS COMPOSANTE 13
C OUT  DSG23  :	SAUT DE CONTRAINTE AUX NOEUDS COMPOSANTE 23
C
C ......................................................................
C
C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ---------------------

      INTEGER        ZI
      COMMON /IVARJE/ZI(1)
      REAL*8         ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16     ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL        ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8    ZK8
      CHARACTER*16          ZK16
      CHARACTER*24                  ZK24
      CHARACTER*32                          ZK32
      CHARACTER*80                                  ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)

C --------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ---------------------

      INTEGER IAREPE,JCELD,JCELV,ICONX1,ICONX2,JAD,JADV,IMAV,IGREL,IEL
      INTEGER ADIEL,IAVAL,INO,NCHER,INOV,INDIIS,IPGF,NBSPT
      
      REAL*8 SIG11(9),SIG22(9),SIG33(9),SIG12(9),SIG13(9),SIG23(9)
      REAL*8 SIGV11(9),SIGV22(9),SIGV33(9),SIGV12(9),SIGV13(9),SIGV23(9)

C ----------------------------------------------------------------------
C 
C ----- TEST SUR LA MAILLE VOISINE -------------------------------------
C
      IF (TYPMAV.EQ.'HEXA8') THEN
        NBNV=8
      ELSE IF (TYPMAV.EQ.'HEXA20') THEN
        NBNV=20
      ELSE IF (TYPMAV.EQ.'HEXA27') THEN
        NBNV=27
      ELSE IF (TYPMAV.EQ.'PENTA6') THEN
        NBNV=6
      ELSE IF (TYPMAV.EQ.'PENTA15') THEN
        NBNV=15
      ELSE IF (TYPMAV.EQ.'TETRA4') THEN
        NBNV=4
      ELSE IF (TYPMAV.EQ.'TETRA10') THEN
        NBNV=10
      ELSE
        CALL ASSERT(.FALSE.)
      END IF
C
C ----- RECHERCHE DES ADRESSES POUR OBTENIR SIGMA SUR LES VOISINS ------
C
      IAREPE = ZI(IREF1-1+1)
      JCELD  = ZI(IREF1-1+2)
      JCELV  = ZI(IREF1-1+3)
C
      IMAV  = ZI(IVOIS+IFA)
      IGREL = ZI(IAREPE-1+2*(IMAV-1)+1)
      IEL   = ZI(IAREPE-1+2*(IMAV-1)+2)
C
C --- FORMULES MAGIQUES DONNANT LE BON DECALAGE POUR NAVIGUER DANS .CELV
C
      ADIEL=ZI(JCELD-1+ZI(JCELD-1+4+IGREL)+4+4*(IEL-1)+4)
      IAVAL=JCELV-1+ADIEL
C
C --- ON VERIFIE QU'IL N'Y A PAS DE SOUS POINT -------------------------
C
      NBSPT=ZI(JCELD-1+ZI(JCELD-1+4+IGREL)+4+4*(IEL-1)+1)
      CALL ASSERT((NBSPT.EQ.1).OR.(NBSPT.EQ.0))
C
C ----- CALCUL DE LA NUMEROTATION DU VOISIN ----------------------------
C
      ICONX1=ZI(IREF1+10)
      ICONX2=ZI(IREF1+11)
      JAD=ICONX1-1+ZI(ICONX2+ZI(IVOIS)-1)
      JADV=ICONX1-1+ZI(ICONX2+ZI(IVOIS+IFA)-1)
C	
C ----- BOUCLE SUR LES POINTS DE GAUSS DE LA FACE ----------------------
C ----- DANS CE CAS POINTS DE GAUSS = NOEUDS
C
      DO 10 IPGF=1,NPGF
C      
        INO=NOE(IPGF,IFA,ITYP)
        NCHER=ZI(JAD-1+INO)
        INOV=INDIIS(ZI(JADV),NCHER,1,NBNV)
C       ON VERIFIE QUE L'ON TROUVE BIEN UN NOEUD DANS LA LISTE
        CALL ASSERT(INOV.GT.0)
C
C ----- RECUPERATION DE SIGMA SUR LA MAILLE COURANTE ET LE VOISIN ------
C
        SIG11(IPGF)=ZR(ISIG-1+NBCMP*(INO-1)+1)
        SIG22(IPGF)=ZR(ISIG-1+NBCMP*(INO-1)+2)
        SIG33(IPGF)=ZR(ISIG-1+NBCMP*(INO-1)+3)
        SIG12(IPGF)=ZR(ISIG-1+NBCMP*(INO-1)+4)
        SIG13(IPGF)=ZR(ISIG-1+NBCMP*(INO-1)+5)
        SIG23(IPGF)=ZR(ISIG-1+NBCMP*(INO-1)+6)
C
        SIGV11(IPGF)=ZR(IAVAL-1+NBCMP*(INOV-1)+1)
        SIGV22(IPGF)=ZR(IAVAL-1+NBCMP*(INOV-1)+2)
        SIGV33(IPGF)=ZR(IAVAL-1+NBCMP*(INOV-1)+3)
        SIGV12(IPGF)=ZR(IAVAL-1+NBCMP*(INOV-1)+4)
        SIGV13(IPGF)=ZR(IAVAL-1+NBCMP*(INOV-1)+5)
        SIGV23(IPGF)=ZR(IAVAL-1+NBCMP*(INOV-1)+6)

C
C ----- CALCUL DES SAUTS DE CONTRAINTES --------------------------------
C
        DSG11(IPGF)=SIG11(IPGF)-SIGV11(IPGF)  
        DSG22(IPGF)=SIG22(IPGF)-SIGV22(IPGF)  
        DSG33(IPGF)=SIG33(IPGF)-SIGV33(IPGF)  
        DSG12(IPGF)=SIG12(IPGF)-SIGV12(IPGF)  
        DSG13(IPGF)=SIG13(IPGF)-SIGV13(IPGF)  
        DSG23(IPGF)=SIG23(IPGF)-SIGV23(IPGF)  
C
  10  CONTINUE
C
      END
