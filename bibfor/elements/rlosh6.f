      SUBROUTINE RLOSH6(XCOQ,XCENT,PPP,XL,XV2CEN,XV1CEN ,XJ)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 22/09/2008   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2008  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C
C     ------------------------------------------------------------------
C
C          REPERE LOCAL DE LA COQUE BELYTCHKO 
C          ===> MATRICE DE PASSAGE PPP
C  
C     ------------------------------------------------------------------
C
C     XCOQ     : COORDONNEES DES 3 NOEUDS COQUES(REPERE GLOBAL)
C     PPP      : MATRICE DE PASSAGE GLOBAL -> LOCAL
C    XCENT    : COORDONNEES DANS LE REPERE GLOBAL DU CENTRE DE L ELEMENT
C     XL       : COORDONNEES DES 3 NOEUDS COQUES DANS LE
C                  LE REPERE LOCAL (XCENT, E1, E2, E3)
C
      IMPLICIT NONE
C
C   VARIABLES GLOBALES
      REAL *8 XCOQ(3,3),XCENT(3),PPP(3,3),XL(3,3),XV1CEN (3),XV2CEN(3)
      REAL *8 XJ
C
C   VARIABLES LOCALES
      INTEGER NBN
      PARAMETER(NBN=3)
C
      REAL *8 XMEAN(3,3),SS(3),AUX,TMP,UNS2,UNS3,ZERO
      INTEGER IP,II
C
C
      ZERO  =  0.D0
      UNS2  =  1.D0/2.D0
      UNS3  =  1.D0/3.D0
C
C     DEFINITION DES 3 POINTS  MILIEUX DES COTES
      II=NBN
      DO 10 IP=1,NBN
          XMEAN(1,IP) = UNS2*(XCOQ(1,II)+XCOQ(1,IP))
          XMEAN(2,IP) = UNS2*(XCOQ(2,II)+XCOQ(2,IP))
          XMEAN(3,IP) = UNS2*(XCOQ(3,II)+XCOQ(3,IP))
          II=IP
 10   CONTINUE
C
      XCENT(1) = UNS3*(XCOQ(1,1)+XCOQ(1,2)+XCOQ(1,3))
      XCENT(2) = UNS3*(XCOQ(2,1)+XCOQ(2,2)+XCOQ(2,3))
      XCENT(3) = UNS3*(XCOQ(3,1)+XCOQ(3,2)+XCOQ(3,3))
C
C XV1CEN  EST DANS LA DIRECTION DE E2
C 
      XV1CEN (1)  = UNS2*(XCENT(1)-XMEAN(1,1))
      XV1CEN (2)  = UNS2*(XCENT(2)-XMEAN(2,1))
      XV1CEN (3)  = UNS2*(XCENT(3)-XMEAN(3,1))
C
C XV2CEN EST DANS LA DIRECTION DE E1
C
      XV2CEN(1)  = UNS2*(XCENT(1)-XMEAN(1,2))
      XV2CEN(2)  = UNS2*(XCENT(2)-XMEAN(2,2))
      XV2CEN(3)  = UNS2*(XCENT(3)-XMEAN(3,2))
C
C      REPERE LOCAL
C
C
C LE VECTEUR UNITAIRE E1 (PPP(;,1))= XV2CEN / ||XV2CEN||
C
      TMP=SQRT(XV2CEN(1)*XV2CEN(1)+XV2CEN(2)*XV2CEN(2)
     &   +XV2CEN(3)*XV2CEN(3))
      PPP(1,1)=XV2CEN(1)/TMP
      PPP(2,1)=XV2CEN(2)/TMP
      PPP(3,1)=XV2CEN(3)/TMP
C
C LE VECTEUR UNITAIRE E3 (PPP(;,3)) = XV2CEN ^ XV1CEN 
C
      SS(1) = XV2CEN(2)*XV1CEN (3) - XV2CEN(3)*XV1CEN (2)
      SS(2) = XV2CEN(3)*XV1CEN (1) - XV2CEN(1)*XV1CEN (3)
      SS(3) = XV2CEN(1)*XV1CEN (2) - XV2CEN(2)*XV1CEN (1)
      XJ = SQRT(SS(1)*SS(1)+SS(2)*SS(2)+SS(3)*SS(3))
      
      CALL ASSERT(XJ.GT.ZERO)
      AUX=1/XJ
      PPP(1,3) = SS(1) * AUX
      PPP(2,3) = SS(2) * AUX
      PPP(3,3) = SS(3) * AUX
C
C LE VECTEUR UNITAIRE  E2 = E3 ^ E1
C
      PPP(1,2) = PPP(2,3)*PPP(3,1) - PPP(3,3)*PPP(2,1)
      PPP(2,2) = PPP(3,3)*PPP(1,1) - PPP(1,3)*PPP(3,1)
      PPP(3,2) = PPP(1,3)*PPP(2,1) - PPP(2,3)*PPP(1,1)
C
C DANS XMEAN, ON MET XCOQ DANS LE REPERE GLOBAL TRANSLATE AU POINT XCENT
C
      DO 20 IP=1,NBN
        XMEAN(1,IP) = XCOQ(1,IP)-XCENT(1)
        XMEAN(2,IP) = XCOQ(2,IP)-XCENT(2)
        XMEAN(3,IP) = XCOQ(3,IP)-XCENT(3)
 20   CONTINUE
C
C XL : COORD DES 3 NOEUDS COQUE DANS LE REPERE LOCAL (XCENT,E1,E2,E3)
C
      DO 30 IP=1,NBN
        XL(1,IP) =  PPP(1,1)*XMEAN(1,IP) + PPP(2,1)*XMEAN(2,IP)
     &           +  PPP(3,1)*XMEAN(3,IP)
        XL(2,IP) =  PPP(1,2)*XMEAN(1,IP) + PPP(2,2)*XMEAN(2,IP)
     &           +  PPP(3,2)*XMEAN(3,IP)
        XL(3,IP) =  PPP(1,3)*XMEAN(1,IP) + PPP(2,3)*XMEAN(2,IP)
     &           +  PPP(3,3)*XMEAN(3,IP)
 30   CONTINUE
C
      END
