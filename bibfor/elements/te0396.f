      SUBROUTINE TE0396(OPTION,NOMTE)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 29/04/2004   AUTEUR JMBHH01 J.M.PROIX 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.

C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.

C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
      IMPLICIT REAL*8 (A-H,O-Z)

      CHARACTER*16 OPTION,NOMTE
C ......................................................................
C    - FONCTION REALISEE:  OPTION : 'CHAR_MECA_TEMP_R'

C    - ARGUMENTS:
C        DONNEES:      OPTION       -->  OPTION DE CALCUL
C                      NOMTE        -->  NOM DU TYPE ELEMENT
C ......................................................................

      INTEGER NNO,NPG,IPOIDS,IVF,IDFDK,KP,NE,IMATE
      INTEGER LSECT,LORIEN,JEFINT,KC,ITEMPR,ITEREF,IGEOM,I,K0,IFINT,IC
      CHARACTER*8 NOMRES(4),ELREFE
      CHARACTER*2 BL2,CODRET(4)
      REAL*8 EN(3,2),ENPRIM(3,2),VALRES(4),GRANC(6),FINT(6,3),TEMPN(3),
     &       Y0(3),X00(3,3),X0PG(3),ROT0(3,3),NU,GN(3),GM(3),PN(3),
     &       PM(3),QIGK(3),ROTK(3,3),ROTABS(3,3),ALPHA,TREF

C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ---------------------
C      CHARACTER*32       JEXNUM , JEXNOM , JEXR8 , JEXATR
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C --------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ---------------------

      CALL ELREF1(ELREFE)

      CALL ELREF4(' ','RIGI',NDIM,NNO,NNOS,NPG,IPOIDS,IVF,IDFDK,JGANO)

      ICO = 0
      DO 20 KP = 1,NPG
        DO 10 NE = 1,NNO
          ICO = ICO + 1
          EN(NE,KP) = ZR(IVF-1+ICO)
          ENPRIM(NE,KP) = ZR(IDFDK-1+ICO)
   10   CONTINUE
   20 CONTINUE


C PARAMETRES EN ENTREE
      CALL JEVECH('PMATERC','L',IMATE)
      BL2 = '  '
      NOMRES(1) = 'E'
      NOMRES(2) = 'NU'
      NOMRES(3) = 'RHO'
      NOMRES(4) = 'ALPHA'
      CALL RCVALA(ZI(IMATE),' ','ELAS',0,'  ',R8BID,2,NOMRES,VALRES,
     &            CODRET,'FM')
      CALL RCVALA(ZI(IMATE),' ','ELAS',0,'  ',R8BID,1,NOMRES(3),
     &            VALRES(3),CODRET(3),BL2)
      CALL RCVALA(ZI(IMATE),' ','ELAS',0,'  ',R8BID,1,NOMRES(4),
     &            VALRES(4),CODRET(4),BL2)
      IF (CODRET(4).NE.'OK') THEN
        ALPHA = 0.D0
      ELSE
        ALPHA = VALRES(4)
      END IF
      E = VALRES(1)
      NU = VALRES(2)
      G = E/ (2.D0* (1.0D0+NU))

C     --- RECUPERATION DES CARACTERISTIQUES GENERALES DES SECTIONS ---

      CALL JEVECH('PCAGNPO','L',LSECT)
      LSECT = LSECT - 1

C     --- LA SECTION EST SUPPOSEE CONSTANTE ---

      A = ZR(LSECT+1)
      XIY = ZR(LSECT+2)
      XIZ = ZR(LSECT+3)
      AY = ZR(LSECT+4)
      AZ = ZR(LSECT+5)
      XJX = ZR(LSECT+8)
      GRANC(1) = E*A
      GRANC(2) = G*A/AY
      GRANC(3) = G*A/AZ
      GRANC(4) = G*XJX
      GRANC(5) = E*XIY
      GRANC(6) = E*XIZ

C     --- RECUPERATION DES ORIENTATIONS INITIALES Y0(1), Y0(2), Y0(3)

      CALL JEVECH('PCAORIE','L',LORIEN)
      Y0(1) = ZR(LORIEN)
      Y0(2) = ZR(LORIEN+1)
      Y0(3) = ZR(LORIEN+2)

C PARAMETRES EN SORTIE

      CALL JEVECH('PVECTUR','E',JEFINT)
      DO 40 NE = 1,NNO
        DO 30 KC = 1,6
          FINT(KC,NE) = 0.D0
   30   CONTINUE
   40 CONTINUE

      CALL JEVECH('PTEMPER','L',ITEMPR)
      CALL JEVECH('PTEREF','L',ITEREF)
      CALL JEVECH('PGEOMER','L',IGEOM)

      DO 50 I = 1,NNO
        TEMPN(I) = ZR(ITEMPR-1+I)
   50 CONTINUE
      TREF = ZR(ITEREF)

      K0 = IGEOM - 1

      DO 70 NE = 1,NNO
        DO 60 KC = 1,3
          K0 = K0 + 1
          X00(KC,NE) = ZR(K0)
   60   CONTINUE
   70 CONTINUE

C     MATRICE DE ROTATION ASSOCIE AU VECTEUR ROTATION NUL
      DO 80 I = 1,3
        QIGK(I) = 0.D0
   80 CONTINUE
      CALL MAROTA(QIGK,ROTK)

C     BOUCLE SUR LES POINTS DE GAUSS

      DO 140 KP = 1,NPG
        CALL GDJRG0(KP,NNO,ENPRIM,X00,Y0,AJACOB,ROT0)
        PJACOB = ZR(IPOIDS-1+KP)*AJACOB
        CALL PROMAT(ROTK,3,3,3,ROT0,3,3,3,ROTABS)

        DO 90 IC = 1,3
          X0PG(IC) = 0.D0
   90   CONTINUE
        UNSURJ = 1.D0/AJACOB
        DO 110 IC = 1,3
          DO 100 NE = 1,NNO
            X0PG(IC) = X0PG(IC) + UNSURJ*ENPRIM(NE,KP)*X00(IC,NE)
  100     CONTINUE
  110   CONTINUE
        TEMPG = 0.D0
        DO 120 NE = 1,NNO
          TEMPG = TEMPG + EN(NE,KP)*TEMPN(NE)
  120   CONTINUE

        DO 130 I = 1,3
          GN(I) = 0.D0
          GM(I) = 0.D0
  130   CONTINUE

C        DILATATION THERMIQUE : -E*A*ALPHA*(T-TREF)

        GN(1) = GN(1) + GRANC(1)*ALPHA* (TEMPG-TREF)

        CALL PROMAT(ROTABS,3,3,3,GN,3,3,1,PN)
        CALL PROMAT(ROTABS,3,3,3,GM,3,3,1,PM)
        CALL GDFINT(KP,NNO,AJACOB,PJACOB,EN,ENPRIM,X0PG,PN,PM,FINT)

  140 CONTINUE

C     FIN DE BOUCLE SUR LES POINTS DE GAUSS

      IFINT = JEFINT - 1
      DO 160 NE = 1,NNO
        DO 150 KC = 1,6
          IFINT = IFINT + 1
          ZR(IFINT) = FINT(KC,NE)
  150   CONTINUE
  160 CONTINUE
  170 CONTINUE
      END
