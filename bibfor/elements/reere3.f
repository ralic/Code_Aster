      SUBROUTINE REERE3(ELREFP,NNOP  ,IGEOM ,XG    ,DEPL  ,
     &                  GRAND ,NDIM  ,HE    ,FISNO ,NFISS ,
     &                  NFH   ,NFE   ,
     &                  DDLT  ,FE    ,DGDGL  ,CINEM,XE    ,
     &                  FF    ,DFDI  ,F      ,EPS  ,GRAD)

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 13/06/2012   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C TOLE CRP_21
      IMPLICIT NONE
      INCLUDE 'jeveux.h'
      CHARACTER*3  CINEM
      CHARACTER*8  ELREFP
      INTEGER      IGEOM,NNOP,NDIM,NFH,NFE,DDLT
      INTEGER      NFISS,FISNO(NNOP,NFISS)
      REAL*8       XG(NDIM),DEPL(DDLT,NNOP),FE(4),DGDGL(4,NDIM)
      REAL*8       XE(NDIM),FF(NNOP),DFDI(NNOP,NDIM),F(3,3)
      REAL*8       EPS(6),GRAD(NDIM,NDIM),HE(NFISS)
      LOGICAL      GRAND
C
C......................................................................
C TROUVER LES COORDONNEES DANS L'ELEMENT DE REFERENCE D'UN
C POINT DONNE DANS L'ELEMENT REEL PAR LA METHODE NEWTON
C ET CALCUL DES ELEMENTS CINEMATIQUES
C (MEME ROUTINE QUE REEREF A RESORBER LORS DU 3D QUADRATIQUE XFEM)
C......................................................................
C
C IN  ELREFP : TYPE DE L'ELEMENT DE REF PARENT
C IN  NNOP   : NOMBRE DE NOEUDS DE L'ELT DE RÉF PARENT
C   L'ORDRE DES DDLS DOIT ETRE 'DC' 'H1' 'E1' 'E2' 'E3' 'E4' 'LAGC'
C IN  IGEOM  : COORDONNEES DES NOEUDS
C IN  XG     : COORDONNES DU POINT DANS L'ELEMENT REEL
C IN  DEPL   : DEPLACEMENT RÉEL À PARTIR DE LA CONF DE REF
C IN  GRAND  : INDICATEUR SI GRANDES TRANSFORMATIONS
C              SI GRAND = .FALSE.
C                --> MATRICE F: UNITE
C                --> DEFORMATION EPS PETITES
C IN  NDIM   : DIMENSION DE L'ESPACE
C IN  HE     : VALEUR DE LA FONCTION HEAVISIDE SUR LE SOUS-ÉLT
C IN  DDLH   : NOMBRE DE DDL HEAVYSIDE (PAR NOEUD)
C IN  NFE    : NOMBRE DE FONCTIONS SINGULIÈRES D'ENRICHISSEMENT
C IN  DDLT   : NOMBRE DE DDLS TOTAL PAR NOEUD
C IN  FE     : VALEURS AUX NOEUDS DES FONCTIONS D'ENRICHISSEMENT
C IN  DGDGL  : DÉRIVÉES DES FONCTIONS D'ENRICHISSEMENT
C IN  CINEM  : CALCUL DES QUANTITÉS CINÉMATIQUES
C               'NON' : ON S'ARRETE APRES LE CALCUL DES FF
C               'DFF' : ON S'ARRETE APRES LE CALCUL DES DERIVEES DES FF
C               'OUI' : ON VA JUSQU'AU BOUT
C OUT XE     : COORDONNÉES DU POINT DANS L'ÉLÉMENT DE RÉF PARENT
C OUT FF     : FONCTIONS DE FORMES EN XE
C OUT DFDI   : DÉRIVÉES DES FONCTIONS DE FORMES EN XE
C OUT F      : GRADIENT DE LA TRANSFORMATION
C OUT EPS    : DÉFORMATIONS
C OUT GRAD   : GRADIENT DES DÉPLACEMENTS
C
C
C
C
      INTEGER     NBNOMX
      PARAMETER   (NBNOMX = 27)
C
      REAL*8      ZERO,UN,RAC2
      INTEGER     I,J,K,N,P,IG,CPT
      INTEGER     NNO,NDERIV,IRET
      REAL*8      INVJAC(3,3)
      REAL*8      DFF(3,NBNOMX)
      REAL*8      KRON(3,3),TMP,EPSTAB(3,3)
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
C
C --- INITIALISATIONS
C
      CALL ASSERT(CINEM.EQ.'NON'.OR.CINEM.EQ.'OUI'.OR.CINEM.EQ.'DFF')
      ZERO = 0.D0
      UN   = 1.D0
      RAC2 = SQRT(2.D0)
C
C --- RECHERCHE DE XE PAR NEWTON-RAPHSON
C
      CALL REEREG('S',ELREFP,NNOP,ZR(IGEOM),XG,NDIM,XE,IRET)
C
C --- VALEURS DES FONCTIONS DE FORME EN XE: FF
C
      CALL ELRFVF(ELREFP,XE,NBNOMX,FF,NNO)
C
C --- DERIVEES PREMIERES DES FONCTIONS DE FORME EN XE: DFF
C
      CALL ELRFDF(ELREFP,XE,NDIM*NBNOMX,DFF,NNO,NDERIV)
C
C --- CALCUL DE L'INVERSE DE LA JACOBIENNE EN XE: INVJAC
C
      CALL INVJAX('S',NNO   ,NDIM  ,NDERIV,DFF   ,ZR(IGEOM),INVJAC,IRET)
C
      IF (CINEM.EQ.'NON') GOTO 9999
C
C --- DERIVEES DES FONCTIONS DE FORMES CLASSIQUES EN XE : DFDI
C
      CALL MATINI(NNOP,NDIM,ZERO,DFDI)
      DO 300 N=1,NNO
        DO 310 I=1,NDIM
          DO 311 K=1,NDIM
            DFDI(N,I)= DFDI(N,I) + INVJAC(K,I)*DFF(K,N)
 311      CONTINUE
 310    CONTINUE
 300  CONTINUE
C
      IF (CINEM.EQ.'DFF') GOTO 9999
C
C --- MATRICE IDENTITE
C
      CALL MATINI(3,3,ZERO,KRON)
      DO 10 P = 1,3
        KRON(P,P) = UN
 10   CONTINUE
C
C --- CALCUL DES GRADIENTS : GRAD(U) ET F
C
      DO 400 I=1,3
        DO 401 J=1,3
          F(I,J)    = KRON(I,J)
 401    CONTINUE
 400  CONTINUE
C
      DO 399 I=1,NDIM
        DO 398 J=1,NDIM
           GRAD(I,J) = ZERO
 398    CONTINUE
 399  CONTINUE
C
C --- L'ORDRE DES DDLS DOIT ETRE 'DC' 'H1' 'E1' 'E2' 'E3' 'E4' 'LAGC'
C
      DO 402 N=1,NNO
        CPT=0
C -- DDLS CLASSIQUES
        DO 403 I=1,NDIM
          CPT = CPT+1
          DO 404 J=1,NDIM
            GRAD(I,J) = GRAD(I,J) + DFDI(N,J) * DEPL(CPT,N)
 404      CONTINUE
 403    CONTINUE
C -- DDLS HEAVISIDE
        DO 405 IG=1,NFH
          DO 406 I=1,NDIM
            CPT = CPT+1
            DO 407 J=1,NDIM
              GRAD(I,J)=GRAD(I,J)+HE(FISNO(N,IG))*DFDI(N,J)*DEPL(CPT,N)
 407        CONTINUE
 406      CONTINUE
 405    CONTINUE
C -- DDL ENRICHIS EN FOND DE FISSURE
        DO 408 IG=1,NFE
          DO 409 I=1,NDIM
            CPT = CPT+1
            DO 410 J=1,NDIM
              GRAD(I,J) = GRAD(I,J) + DEPL(CPT,N) *
     &                    (DFDI(N,J) * FE(IG) + FF(N) * DGDGL(IG,J))
 410        CONTINUE
 409      CONTINUE
 408    CONTINUE
 402  CONTINUE
      IF (GRAND) THEN
        DO 420 I=1,NDIM
          DO 421 J=1,NDIM
            F(I,J) = F(I,J) + GRAD(I,J)
 421      CONTINUE
 420    CONTINUE
      ENDIF
C
C --- CALCUL DES DÉFORMATIONS : EPS
C
      DO 430 I=1,NDIM
        DO 431 J=1,I
          TMP = GRAD(I,J) + GRAD(J,I)
          IF (GRAND) THEN
            DO 432 K=1,NDIM
              TMP = TMP + GRAD(K,I)*GRAD(K,J)
 432        CONTINUE
          ENDIF
          EPSTAB(I,J) = 0.5D0*TMP
 431    CONTINUE
 430  CONTINUE
      CALL VECINI(6,ZERO,EPS)
      EPS(1) = EPSTAB(1,1)
      EPS(2) = EPSTAB(2,2)
      EPS(4) = EPSTAB(2,1)*RAC2
      IF (NDIM .EQ. 3) THEN
        EPS(3) = EPSTAB(3,3)
        EPS(5) = EPSTAB(3,1)*RAC2
        EPS(6) = EPSTAB(3,2)*RAC2
      ENDIF
C
 9999 CONTINUE
C
      CALL JEDEMA()
      END
