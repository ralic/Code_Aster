      SUBROUTINE TE0367(OPTION,NOMTE)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 12/05/2009   AUTEUR MAZET S.MAZET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C
      IMPLICIT   NONE
      CHARACTER*16 OPTION,NOMTE
C
C ----------------------------------------------------------------------
C  CONTACT XFEM GRANDS GLISSEMENTS
C  CALCUL DES SECONDS MEMBRES DE CONTACT ET DE FROTTEMENT
C
C  OPTION : 'CHAR_MECA_CONT' (CALCUL DU SECOND MEMBRE DE CONTACT)
C           'CHAR_MECA_FROT' (CALCUL DU SECOND MEMBRE DE FROTTEMENT)
C
C  ENTREES  ---> OPTION : OPTION DE CALCUL
C           ---> NOMTE  : NOM DU TYPE ELEMENT
C
C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX ---------------------
C
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C --------- FIN  DECLARATIONS  NORMALISEES  JEVEUX ---------------------
C
      CHARACTER*8   TYPMA,ESC,MAIT,ELC
      INTEGER      NDIM,NDDL,NNE,NNES,NNM,NNC
      INTEGER      JPCPO,JPCPI,JPCPIM,JPCAI,JPCCF,JPCCFM
      INTEGER      IGEOM,IDEPL,IDEPM
      INTEGER      INDCO,INDNOR,IFROTT,NFAES
      INTEGER      IVECT
      REAL*8       COEFCA,LAMBDA,COEFFF,COEFFA,HPG
      REAL*8       JEU,TAU1(3),TAU2(3),NORM(3)
      REAL*8       VTMP(120)
      REAL*8       GEOME(3),GEOMM(3)
      REAL*8       DEPLME(9),DEPLM(6),DEPLE(9),DEPLMM(6)
      REAL*8       FFPC(8),DFFPC(3,3),JACOBI,COORE(3),COORM(3)
      REAL*8       FFES(8),FFMA(8),GEOPI(9),COOR(3)
      REAL*8       DPLM(3),DPLE(6),MPROJ(3,3),RESE(3)
      REAL*8       NOOR,R8PREM,NRESE,RBID
      INTEGER      INADH,NVIT
      INTEGER      I,J,IN,PL,IBID
      INTEGER      CFACE(5,3),XOULA
      LOGICAL      MALIN
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
C
C --- INFOS SUR LA MAILLE DE CONTACT
C
      CALL XMELET(NOMTE,TYPMA,ESC,MAIT,ELC,
     &            NDIM,NDDL,NNE,NNM,NNC,MALIN)
      CALL ASSERT (NDDL.LE.120)
C
C --- INITIALISATIONS
C
      CALL LCINVN(9,0.D0,GEOPI)
      CALL LCINVN(NDIM,0.D0,GEOME)
      CALL LCINVN(NDIM,0.D0,GEOMM)      
      CALL LCINVN(2*NDIM,0.D0,DEPLM)
      CALL LCINVN(2*NDIM,0.D0,DEPLMM)
      CALL LCINVN(3*NDIM,0.D0,DEPLE)
      CALL LCINVN(3*NDIM,0.D0,DEPLME)
C --- INITIALISATION DU VECTEUR DE TRAVAIL
      CALL LCINVN(NDDL,0.D0,VTMP)
C
C --RECUPERATION DES DONNEES DE LA CARTE CONTACT POINT (VOIR XMCART)
C
      CALL JEVECH('PCAR_PT','L',JPCPO)
      COOR(1) = ZR(JPCPO-1+1)
      COOR(2) = ZR(JPCPO-1+10)
      TAU1(1)  = ZR(JPCPO-1+4)
      TAU1(2)  = ZR(JPCPO-1+5)
      TAU1(3)  = ZR(JPCPO-1+6)
      TAU2(1)  = ZR(JPCPO-1+7)
      TAU2(2)  = ZR(JPCPO-1+8)
      TAU2(3)  = ZR(JPCPO-1+9)
      INDCO    = NINT(ZR(JPCPO-1+11))
      LAMBDA   = ZR(JPCPO-1+12)
      COEFCA   = ZR(JPCPO-1+13)
      COEFFA   = ZR(JPCPO-1+14)
      COEFFF   = ZR(JPCPO-1+15)
      IFROTT   = NINT(ZR(JPCPO-1+16))
      INDNOR   = NINT(ZR(JPCPO-1+17))
      HPG      = ZR(JPCPO-1+19)
C --- LES NUMEROS DE LA FACETTE DE CONTACT ESCLAVE
      NFAES    = NINT(ZR(JPCPO-1+22))
C --- LES COORDONNEES ESCLAVE ET MAITRES DANS L'ELEMENT PARENT
      COORE(1)  = ZR(JPCPO-1+24)
      COORE(2)  = ZR(JPCPO-1+25)
      COORE(3)  = ZR(JPCPO-1+26)
      COORM(1)  = ZR(JPCPO-1+27)
      COORM(2)  = ZR(JPCPO-1+28)
      COORM(3)  = ZR(JPCPO-1+29)
C --- POINT D'INTEGRATION VITAL OU PAS
      NVIT      = ZR(JPCPO-1+30)
C
C --- RECUPERATION DES DONNEES DE LA CARTE CONTACT PINTER (VOIR XMCART)
C
      CALL JEVECH('PCAR_PI','L',JPCPI)
C
C --- RECUPERATION DES DONNEES DE LA CARTE CONTACT AINTER (VOIR XMCART)
C
      CALL JEVECH('PCAR_AI','L',JPCAI)
C
C --- RECUPERATION DES DONNEES DE LA CARTE CONTACT CFACE (VOIR XMCART)
C
      CALL JEVECH('PCAR_CF','L',JPCCF)
C
C --- RECUPERATION DE LA GEOMETRIE ET DES CHAMPS DE DEPLACEMENT
C
      CALL JEVECH('PGEOMER','E',IGEOM)
      CALL JEVECH('PDEPL_P','E',IDEPL)
      CALL JEVECH('PDEPL_M','L',IDEPM)
C
C-----NOMBRE DE NOEUDS ESCLAVES SOMMETES
C
      IF (MALIN) THEN
C-----ON EST EN LINEAIRE
        NNES=NNE
      ELSE
C-----ON EST EN QUADRATIQUE     
        NNES=NNE/2
      ENDIF
C
C --- ON CONSTRUIT LA MATRICE DE CONNECTIVITÉ CFACE (MAILLE ESCLAVE)
C --- CE QUI SUIT N'EST VALABLE QU'EN 2D POUR LA FORMULATION QUADRATIQUE
C --- EN 3D ON UTILISE SEULEMENT LA FORMULATION AUX NOEUDS SOMMETS,
C --- CETTE MATRICE EST DONC INUTILE, ON NE LA CONSTRUIT PAS !!!
C
      CFACE(1,1)=1
      CFACE(1,2)=2
C
C --- REACTUALISATION DE LA GEOMETRIE
C
      CALL XMREAC(NDIM,NNE,NNES,NNM,
     &            IGEOM,IDEPM)
C
C --- DERIVEES DES FONCTIONS DE FORMES POUR LE PT DE CONTACT DANS
C --- L'ELEMENT DE CONTACT
C
      CALL ELRFDF(ELC,COOR,NNC*NDIM,DFFPC,IBID,IBID)
C
C --- CALCUL DES COORDONNÉES RÉELES DES POINTS D'INTERSECTION ESCLAVES
C
      CALL XMPINT(ESC,NDIM,IGEOM,JPCPI,JPCCF,NFAES,NNES,
     &            GEOPI)
C
C --- JACOBIEN POUR LE POINT DE CONTACT
C
      CALL XMMJAC(ELC,GEOPI,DFFPC,
     &            JACOBI)
C
C --- FONCTIONS DE FORMES DU POINTS DE CONTACT DANS L'ELEMENT PARENT 
C
      CALL ELRFVF(ESC,COORE,NNES,FFES,NNES)
C
C --- FONCTIONS DE FORMES DE LA PROJ DU PT DE CONTACT DANS L'ELE PARENT
C    
      CALL ELRFVF(MAIT,COORM,NNM,FFMA,NNM)
C
C --- CALCUL DES COORDONNÉES RÉELLES DU POINT DE CONTACT
C
      DO 120 I = 1,NDIM
        DO 130 J = 1,NNES
          GEOME(I) = GEOME(I) + 
     &               FFES(J)*ZR(IGEOM-1+NDIM*(J-1)+I)
 130    CONTINUE
 120  CONTINUE
C
C --- CALCUL DES COORDONNÉES RÉELES DU PROJETE DU POINT DE CONTACT
C
      DO 140 I = 1,NDIM
        DO 150 J = 1,NNM
          GEOMM(I) = GEOMM(I) +
     &               FFMA(J)*ZR(IGEOM+NDIM*NNE-1+NDIM*(J-1)+I)
 150    CONTINUE
 140  CONTINUE
C
C --- FONCTION DE FORMES POUR LES LAGRANGIENS
C --- SI ON EST EN LINEAIRE, ON IMPOSE QUE LE NB DE NOEUDS DE CONTACTS
C --- ET LES FFS DES LAGRANGES DE CONTACT SONT IDENTIQUES A CEUX
C --- DES DEPLACEMENTS DANS LA MAILLE ESCLAVE POUR LE CALCUL DES CONTRIB
C
      IF (MALIN) THEN
        NNC=NNE
        DO 50 I=1,NNE
          FFPC(I)=FFES(I)
 50     CONTINUE
      ELSE 
        CALL ELRFVF(ELC,COOR,NNC,FFPC,NNC)
      ENDIF
C
C --- CALCUL DE LA NORMALE
C
      IF (NDIM.EQ.2) THEN
        CALL MMNORM(NDIM,TAU1,TAU2,NORM,NOOR)
      ELSEIF (NDIM.EQ.3) THEN
        CALL PROVEC(TAU1,TAU2,NORM)
        CALL NORMEV(NORM,NOOR)
      ENDIF
      IF (NOOR.LT.R8PREM()) THEN
        CALL ASSERT(.FALSE.)
      ENDIF
C
C --- CALCUL DES DEPLACEMENTS DU POINT DE CONTACT ET DE SON PROJETE
C
      DO 180 I = 1,2*NDIM
        DO 190 J = 1,NNES
          DEPLE(I)  = DEPLE(I)  +
     &                FFES(J)*ZR(IDEPL+(J-1)*(3*NDIM)+I-1)
          DEPLME(I) = DEPLME(I) +
     &                FFES(J)*ZR(IDEPM+(J-1)*(3*NDIM)+I-1)
 190    CONTINUE
 180  CONTINUE
      DO 200 I = 1,2*NDIM
        DO 210 J = 1,NNM
          DEPLM(I)  = DEPLM(I)  +  
     & FFMA(J)*ZR(IDEPL+NNES*3*NDIM+(NNE-NNES)*NDIM+2*NDIM*(J-1)+I-1)
          DEPLMM(I) = DEPLMM(I) +
     & FFMA(J)*ZR(IDEPM+NNES*3*NDIM+(NNE-NNES)*NDIM+2*NDIM*(J-1)+I-1)
 210    CONTINUE
 200  CONTINUE

C----CALCUL DES REACTIONS DE CONTACT (PRESSION ET FROTTEMENT)

      DO 220 I=1,NNC
C --- XOULA NE SERT PLUS A RIEN AVEC LA FORMULATION NOEUDS SOMMETS !!!
        IN=XOULA(CFACE,NFAES,I,JPCAI,TYPMA)
        CALL XPLMA2(NDIM,NNE,NNES,IN,PL)
        DO 225 J=1,NDIM
          DEPLE(2*NDIM+J)=DEPLE(2*NDIM+J)+FFPC(I)*ZR(IDEPL-1+PL+J-1)
          DEPLME(2*NDIM+J)=DEPLME(2*NDIM+J)+FFPC(I)*ZR(IDEPM-1+PL+J-1)
 225    CONTINUE
 220  CONTINUE

C --- NOEUDS EXCLUS PAR PROJECTION HORS ZONE
C
      IF (INDNOR .EQ. 1) THEN
        INDCO = 0
      ENDIF
C
C --- RECUPERATION DES VECTEURS 'OUT' (A REMPLIR DONC MODE ECRITURE)
C
      CALL JEVECH('PVECTUR','E',IVECT)
C
C --- CALCUL DES SECOND MEMBRES DE CONTACT/FROTTEMENT

      IF (OPTION.EQ.'CHAR_MECA_CONT') THEN
C
        IF (INDCO.EQ.1) THEN
C
C --- VECTEUR SECOND MEMBRE SI CONTACT
          CALL XMMJEU(NDIM,NORM,GEOME,GEOMM,DEPLE,DEPLM,
     &                DEPLME,DEPLMM,JEU,RBID)

          CALL XMVEC1(NDIM,NNE,NNES,NNC,NNM,HPG,NFAES,
     &                FFPC,FFES,FFMA,JACOBI,DEPLE,DEPLME,
     &                JPCAI,CFACE,COEFCA,JEU,NORM,TYPMA,VTMP)

        ELSE IF (INDCO .EQ. 0) THEN
          IF (NVIT.EQ.1) THEN
C
C --- CALCUL DU VECTEUR - CAS SANS CONTACT
C
            CALL XMVEC0(NDIM,NNE,NNES,NNC,NFAES,DEPLE,DEPLME,
     &                HPG,FFPC,JACOBI,CFACE,JPCAI,
     &                COEFCA,TYPMA,VTMP)
          ENDIF
        ELSE
          CALL U2MESS('F','ELEMENTS3_80')
        ENDIF

      ELSEIF (OPTION.EQ.'CHAR_MECA_FROT') THEN
C              ---------------------
        IF (COEFFF.EQ.0.D0) INDCO = 0
        IF (LAMBDA.EQ.0.D0) INDCO = 0
        IF (IFROTT.NE.3)    INDCO = 0

        IF (INDCO.EQ.0) THEN
          IF (NVIT.EQ.1) THEN
C
C --- CALCUL DU VECTEUR - CAS SANS FROTTEMENT
C
            CALL XMVEF0(NDIM,NNC,NNE,NNES,NFAES,JPCAI,
     &                HPG,FFPC,JACOBI,DEPLE,CFACE,
     &                TYPMA,TAU1,TAU2,VTMP)
          ENDIF
        ELSE IF (INDCO.EQ.1) THEN
C
C --- RECUPERATION DES DEPLACEMENTS DU POINT DE CONTACT, DE SON PROJETE
C --- ET DES REACTIONS DE CONTACT
C
          CALL LCINVN(NDIM,0.D0,DPLM)
          CALL LCINVN(2*NDIM,0.D0,DPLE)
          DO 230 I = 1,NDIM
            DPLM(I)  = DEPLM(I) + DEPLM(I+NDIM)
            DPLE(I) = DEPLE(I) - DEPLE(I+NDIM)
 230      CONTINUE
          DO 240 I = 1,NDIM
            DPLE(I+NDIM)  = DEPLE(I+2*NDIM)
 240      CONTINUE
C
C --- ON CALCULE L'ETAT DE CONTACT ADHERENT OU GLISSANT
C
          CALL TTPRSM (NDIM,DPLE,DPLM,1,COEFFA,0.D0,0.D0,
     &                 NORM,TAU1,TAU2,INADH,MPROJ,RESE,NRESE)     
C
C --- SI GLISSANT, NORMALISATION RESE
C
          IF (INADH.EQ.0)  CALL NORMEV(RESE,NRESE)
C
C --- VECTEUR FROTTEMENT
C
          CALL XMVEF1 (NDIM,NNE,NNES,NNC,NNM,NFAES,CFACE,
     &                 HPG,FFPC,FFES,FFMA,JACOBI,JPCAI,    
     &                 LAMBDA,COEFFA,COEFFF,TAU1,TAU2,
     &                 RESE,MPROJ,DEPLE,TYPMA,VTMP)
        ENDIF
      ELSE
C --- SI OPTION NI 'CHAR_MECA_CONT' NI 'CHAR_MECA_FROT'
        CALL ASSERT(OPTION.EQ.'CHAR_MECA_FROT' .OR.
     &              OPTION.EQ.'CHAR_MECA_CONT')
      ENDIF
C
C --- RECOPIE VALEURS FINALES
C
      DO 250 I=1,NDDL
        ZR(IVECT-1+I)=VTMP(I)
 250  CONTINUE
C
      CALL JEDEMA()
      END
