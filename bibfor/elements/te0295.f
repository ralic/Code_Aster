      SUBROUTINE TE0295(OPTION,NOMTE)
      IMPLICIT NONE
      CHARACTER*16 OPTION,NOMTE
C ----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 21/01/2013   AUTEUR DELMAS J.DELMAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2013  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C
C     BUT:
C       CALCUL DES FACTEURS D'INTENSITÉ DES CONTRAINTES
C       A PARTIR DE LA FORME BILINEAIRE SYMETRIQUE G ET
C       DES DEPLACEMENTS SINGULIERS EN FOND DE FISSURE
C      POUR LES ELEMENTS ISOPARAMETRIQUES 3D
C
C       OPTION : 'CALC_K_G'    (CHARGES REELLES)
C                'CALC_K_G_F'  (CHARGES FONCTIONS)
C
C
C ----------------------------------------------------------------------
C
      INCLUDE 'jeveux.h'
C
      INTEGER ICODRE(3)
      INTEGER CODRHO
      INTEGER IPOIDS,IVF,IDFDE,NNO,KP,NPG,COMPT,IER, NNOS
      INTEGER JGANO,ICOMP,IBALO,ICOUR
      INTEGER IGEOM,ITHET, IFICG, IROTA,IPESA,IDEPL,IRET
      INTEGER IMATE,IFORC,IFORF,ITEMPS,K,I,J,KK,L,NDIM,INO,IPULS
      INTEGER JLSN,JLST,JTAB(7)

C
      REAL*8 R8PREM,R8BID
      REAL*8 DFDI(60),F(3,3),EPS(6),FNO(81),E1(3),E2(3),E3(3)
      REAL*8 DUDM(3,4),DFDM(3,4),DTDM(3,4),DER(4)
      REAL*8 U1L(3),U2L(3),U3L(3), DFVDM(3,4)
      REAL*8 DU1DM(3,4),DU2DM(3,4),DU3DM(3,4)
      REAL*8 P(3,3),INVP(3,3)
      REAL*8 COURB(3,3,3)
      REAL*8 RHO,OM,OMO,RBID,E,NU,ALPHA,TREF
      REAL*8 THET,TPG(27),TNO(20),TGDM(3),TTRG,LA,MU, KA
      REAL*8 XG,YG,ZG,FF
      REAL*8 C1,C2,C3, RG, PHIG
      REAL*8 VALRES(3)
      REAL*8 COEFF,COEFF3
      REAL*8 GUV,GUV1,GUV2,GUV3,K1,K2,K3,G,POIDS
      REAL*8 NORME,K3A,TTRGV,TGVDM(3)
      REAL*8 VALPAR(4),LSNG,LSTG,PULS
C
      CHARACTER*4  FAMI
      CHARACTER*8  NOMRES(3),NOMPAR(4)
      CHARACTER*16 PHENOM,COMPOR(4)
C
      LOGICAL LCOUR,LMODA,FONC,LPESA,LROTA
C
C ----------------------------------------------------------------------
C
      CALL JEMARQ()
C
      FAMI = 'RIGI'
      CALL ELREF4(' ',FAMI,NDIM,NNO,NNOS,NPG,IPOIDS,IVF,IDFDE,JGANO)
C
C --- RECUPERATION DES CHAMPS IN
C
      CALL JEVECH('PTHETAR','L',ITHET)
      CALL JEVECH('PGEOMER','L',IGEOM)
      CALL JEVECH('PDEPLAR','L',IDEPL)
      CALL JEVECH('PMATERC','L',IMATE)
      CALL JEVECH('PCOMPOR','L',ICOMP)
      CALL JEVECH('PBASLOR','L',IBALO)
      CALL JEVECH('PCOURB','L',ICOUR)
      CALL JEVECH('PLSN','L',JLSN)
      CALL JEVECH('PLST','L',JLST)
C
C --- RECUPERATION DU CHAMP OUT
C
      CALL JEVECH('PGTHETA','E',IFICG)
C
      G = 0.D0
      K1 = 0.D0
      K2 = 0.D0
      K3 = 0.D0
      COEFF = 1.D0
      COEFF3 = 1.D0
      NOMRES(1) = 'E'
      NOMRES(2) = 'NU'
      NOMRES(3) = 'ALPHA'
C
C --- PAS DE CALCUL DE G POUR LES ELEMENTS OU THETA EST NULLE
C
      COMPT = 0
      DO 10 I = 1,NNO
        THET = 0.D0
        DO 11 J = 1,NDIM
          THET = THET + ABS(ZR(ITHET+NDIM*(I-1)+J-1))
 11     CONTINUE
        IF (THET.LT.R8PREM()) COMPT = COMPT + 1
 10   CONTINUE
      IF (COMPT.EQ.NNO) GOTO 9999

C
C --- RECUPERATION DES FORCES
C
      IF (OPTION.EQ.'CALC_K_G_F') THEN
        FONC = .TRUE.
        CALL JEVECH('PFFVOLU','L',IFORF)
        CALL JEVECH('PTEMPSR','L',ITEMPS)
        NOMPAR(1) = 'X'
        NOMPAR(2) = 'Y'
        IF (NDIM.EQ.2) THEN
          NOMPAR(3) = 'INST'
        ELSEIF (NDIM.EQ.3) THEN
          NOMPAR(3) = 'Z'
          NOMPAR(4) = 'INST'
        ENDIF
        VALPAR(NDIM+1) = ZR(ITEMPS)
      ELSE IF (OPTION.EQ.'CALC_K_G') THEN
        FONC =.FALSE.
        CALL JEVECH('PFRVOLU','L',IFORC)
      ELSE
        CALL ASSERT(.FALSE.)
      ENDIF
C
      LPESA = .FALSE.
      CALL TECACH('ONN','PPESANR','L',7,JTAB,IRET)
      IPESA=JTAB(1)
      IF (IRET.EQ.0) THEN
        LPESA = .TRUE.
      ENDIF
C
      LROTA = .FALSE.
      CALL TECACH('ONN','PROTATR','L',7,JTAB,IRET)
      IROTA=JTAB(1)
      IF (IRET.EQ.0) THEN
        LROTA = .TRUE.
      ENDIF
C
C --- VERFICATION DU COMPORTEMENT
C
      DO 20 I = 1,4
        COMPOR(I) = ZK16(ICOMP+I-1)
 20   CONTINUE
C
      IF ((COMPOR(1).NE.'ELAS'     ) .OR.
     &    (COMPOR(3).EQ.'GROT_GDEP') .OR.
     &    (COMPOR(4).EQ.'COMP_INCR')) THEN
         CALL U2MESS('F','RUPTURE1_24')
      END IF
C
C --- RECUPERATION DE LA PULSATION
C
      LMODA = .FALSE.
      CALL TECACH('ONN','PPULPRO','L',7,JTAB,IRET)
        IPULS=JTAB(1)
      IF (IRET.EQ.0) THEN
        PULS = ZR(IPULS)
        LMODA = .TRUE.
      ELSE
        PULS = 0.D0
      ENDIF
C
C --- RECUPERATION DES CHARGES
C
      IF (FONC) THEN
        DO 50 I = 1,NNO
          DO 30 J = 1,NDIM
            VALPAR(J) = ZR(IGEOM+NDIM*(I-1)+J-1)
 30       CONTINUE
          DO 40 J = 1,NDIM
            KK = NDIM*(I-1) + J
            CALL FOINTE('FM',ZK8(IFORF+J-1),NDIM+1,NOMPAR,VALPAR,
     &                                               FNO(KK),IER)
 40       CONTINUE
 50     CONTINUE
      ELSE
        DO 80 I = 1,NNO
          DO 60 J = 1,NDIM
            FNO(NDIM*(I-1)+J) = ZR(IFORC+NDIM*(I-1)+J-1)
 60     CONTINUE
 80   CONTINUE
      ENDIF
C
C --- RECUPERATION DE LA PESANTEUR ET DE LA ROTATION
C
      IF (LPESA.OR.LROTA) THEN
        CALL RCCOMA(ZI(IMATE),'ELAS',1,PHENOM,ICODRE)
        CALL RCVALB('RIGI',1,1,'+',ZI(IMATE),' ',PHENOM,
     &              1,' ',RBID,1,'RHO',RHO,ICODRE,1)
        IF (LPESA) THEN
          DO 95 I=1,NNO
            DO 90 J=1,NDIM
              KK = NDIM*(I-1)+J
              FNO(KK)=FNO(KK)+RHO*ZR(IPESA)*ZR(IPESA+J)
90          CONTINUE
95        CONTINUE
        ENDIF

        IF (LROTA) THEN
          OM = ZR(IROTA)
          DO 105 I=1,NNO
            OMO = 0.D0
            DO 100 J=1,NDIM
              OMO = OMO + ZR(IROTA+J)* ZR(IGEOM+NDIM*(I-1)+J-1)
100         CONTINUE
            DO 103 J=1,NDIM
              KK = NDIM*(I-1)+J
              FNO(KK)=FNO(KK)+RHO*OM*OM*(ZR(IGEOM+KK-1)-OMO*ZR(IROTA+J))
103         CONTINUE
105       CONTINUE
        ENDIF
      ENDIF
C
C --- RECUPERATION DE LA TEMPERATURE
C
      CALL RCVARC(' ','TEMP','REF','RIGI',1,1,TREF,IRET)
      IF (IRET.NE.0) TREF = 0.D0
      DO 645 KP = 1,NPG
        CALL RCVARC(' ','TEMP','+','RIGI',KP,1,TPG(KP),IRET)
        IF (IRET.NE.0) TPG(KP) = 0.D0
  645 CONTINUE
C
      DO 646 INO = 1,NNO
        CALL RCVARC(' ','TEMP','+','NOEU',INO,1,TNO(INO),IRET)
        IF (IRET.NE.0) TNO(INO) = 0.D0
  646 CONTINUE
C
C ----------------------------------------------------------------------
C
C     BOUCLE SUR LES POINTS DE GAUSS
C
C ----------------------------------------------------------------------
C
      DO 800 KP = 1,NPG
C
        L = (KP-1) * NNO
        XG = 0.D0
        YG = 0.D0
        ZG = 0.D0
        LSNG=0.D0
        LSTG=0.D0
        DO 110 I = 1,3
          TGDM(I) = 0.D0
          TGVDM(I) = 0.D0
          DO 111 J = 1,4
            DUDM(I,J) = 0.D0
            DU1DM(I,J)= 0.D0
            DU2DM(I,J)= 0.D0
            DU3DM(I,J)= 0.D0
            DTDM(I,J) = 0.D0
            DFDM(I,J) = 0.D0
            DFVDM(I,J) = 0.D0
 111      CONTINUE
 110    CONTINUE

C ----- CALCUL DES ELEMENTS CINEMATIQUES (MATRICES F ET E)
C       EN UN PT DE GAUSS

        CALL NMGEOM (NDIM,NNO,.FALSE.,.FALSE.,ZR(IGEOM),KP,
     &               IPOIDS,IVF,IDFDE,
     &               ZR(IDEPL),.TRUE.,POIDS,DFDI,F,EPS,RBID)
C
C ----- CALCULS DES GRADIENTS DE U (DUDM),THETA (DTDM) ET FORCE(DFDM)
C ----- DU GRADIENT DE TEMPERATURE AUX POINTS DE GAUSS (TGDM)
C ----- ET LEVEL SETS

        DO 320 I=1,NNO
          DER(1) = DFDI(I)
          DER(2) = DFDI(I+NNO)
          DER(3) = DFDI(I+2*NNO)
          DER(4) = ZR(IVF+L+I-1)

          XG = XG + ZR(IGEOM-1+NDIM*(I-1)+1)*DER(4)
          YG = YG + ZR(IGEOM-1+NDIM*(I-1)+2)*DER(4)
          ZG = ZG + ZR(IGEOM-1+NDIM*(I-1)+3)*DER(4)
C
          LSNG = LSNG + ZR(JLSN-1+I) * DER(4)
          LSTG = LSTG + ZR(JLST-1+I) * DER(4)
C
          DO 310 J=1,NDIM
            TGDM(J) = TGDM(J) + TNO(I) * DER(J)
            DO 300 K=1,NDIM
              DUDM(J,K) = DUDM(J,K) + ZR(IDEPL+NDIM*(I-1)+J-1)*DER(K)
              DTDM(J,K) = DTDM(J,K) + ZR(ITHET+NDIM*(I-1)+J-1)*DER(K)
              DFDM(J,K) = DFDM(J,K) + FNO(NDIM*(I-1)+J)*DER(K)
300         CONTINUE
            DUDM(J,4) = DUDM(J,4) + ZR(IDEPL+NDIM*(I-1)+J-1)*DER(4)
            DTDM(J,4) = DTDM(J,4) + ZR(ITHET+NDIM*(I-1)+J-1)*DER(4)
            DFDM(J,4) = DFDM(J,4) + FNO(NDIM*(I-1)+J)*DER(4)
310       CONTINUE
320     CONTINUE
C
        TTRG = TPG(KP) - TREF
        TTRGV = 0.D0
C
        CALL RCCOMA(ZI(IMATE),'ELAS',1,PHENOM,ICODRE)
C
C ----- RECUPERATION DE E, NU ET ALPHA
C
        CALL RCVARC(' ','TEMP','+','RIGI',KP,1,R8BID,IRET)
        CALL RCVALB (FAMI,KP,1,'+',ZI(IMATE),' ',PHENOM,0,' ',0.D0,
     &               3,NOMRES,VALRES,ICODRE,0)
        CALL ASSERT(ICODRE(1)+ICODRE(2).EQ.0)
        IF (ICODRE(3).NE.0) THEN
          CALL ASSERT(IRET.NE.0)
          VALRES(3) = 0.D0
        ENDIF
C
C ----- RECUPERATION DE RHO
C
        CALL RCVALB(FAMI,KP,1,'+',ZI(IMATE),' ',PHENOM,0,' ',0.D0,1,
     &              'RHO',RHO,CODRHO,0)
C
        IF ((CODRHO.NE.0).AND. LMODA) THEN
          CALL U2MESS('F','RUPTURE1_26')
        ENDIF
C
        E     = VALRES(1)
        NU    = VALRES(2)
        ALPHA = VALRES(3)
        K3A = ALPHA * E / (1.D0-2.D0*NU)

        LA = NU*E/((1.D0+NU)*(1.D0-2.D0*NU))
        MU = E/(2.D0*(1.D0+NU))
C       EN DP
        KA=3.D0-4.D0*NU
        COEFF=E/(1.D0-NU*NU)
        COEFF3=2.D0 * MU
C       EN CP
C       KA=(3.D0-NU)/(1.D0+NU)
C       COEFF=E
C       COEFF3=2.D0 * MU

        C1 = LA + 2.D0 * MU
        C2 = LA
        C3 = MU
C
C ----- CALCUL DES CHAMPS AUXILIAIRES ET DE LEURS DERIVEES
C
C       COORDONNEES POLAIRES DU POINT
        RG = SQRT(LSNG**2+LSTG**2)
C
        IF (RG.GT.R8PREM()) THEN
C         LE POINT N'EST PAS SUR LE FOND DE FISSURE
          PHIG = SIGN(1.D0,LSNG) * ABS(ATAN2(LSNG,LSTG))
          IRET=1
        ELSE
C         LE POINT EST SUR LE FOND DE FISSURE :
C         L'ANGLE N'EST PAS DÉFINI, ON LE MET À ZÉRO
C         ON NE FERA PAS LE CALCUL DES DÉRIVÉES
          PHIG=0.D0
          IRET=0
        ENDIF
C
C ----- ON A PAS PU CALCULER LES DERIVEES DES FONCTIONS SINGULIERES
C ----- CAR ON SE TROUVE SUR LE FOND DE FISSURE
        CALL ASSERT(IRET.NE.0)
C
C ----- BASE LOCALE ASSOCIÉE AU POINT DE GAUSS KP
C       (E1=GRLT,E2=GRLN,E3=E1^E2)
        DO 124 I=1,3
          E1(I)=0.D0
          E2(I)=0.D0
          DO 125 INO=1,NNO
            FF=ZR(IVF-1+NNO*(KP-1)+INO)
            E1(I) = E1(I)+ZR(IBALO-1+9*(INO-1)+I+3)* FF
            E2(I) = E2(I)+ZR(IBALO-1+9*(INO-1)+I+6)* FF
 125      CONTINUE
 124    CONTINUE
C
C       NORMALISATION DE LA BASE
        CALL NORMEV(E1,NORME)
        CALL NORMEV(E2,NORME)
        CALL PROVEC(E1,E2,E3)
C
C ----- CALCUL DE LA MATRICE DE PASSAGE P TQ 'GLOBAL' = P * 'LOCAL'
C
        DO 120 I=1,3
          P(I,1)=E1(I)
          P(I,2)=E2(I)
          P(I,3)=E3(I)
 120    CONTINUE
C
C ----- CALCUL DE L'INVERSE DE LA MATRICE DE PASSAGE : INV=TRANSPOSE(P)
C
        DO 130 I=1,3
          DO 131 J=1,3
            INVP(I,J)=P(J,I)
 131      CONTINUE
 130    CONTINUE
C
C       PRISE EN COMPTE DE LA COURBURE : OUI
C
        LCOUR=.TRUE.
C       RECUPERATION DU TENSEUR DE COURBURE
        CALL JEVECH('PCOURB','L',ICOUR)
        DO 500 I=1,NDIM
          DO 501 J=1,NDIM
            COURB(I,1,J)=ZR(ICOUR-1+NDIM*(I-1)+J)
            COURB(I,2,J)=ZR(ICOUR-1+NDIM*(I+3-1)+J)
            COURB(I,3,J)=ZR(ICOUR-1+NDIM*(I+6-1)+J)
 501      CONTINUE
 500    CONTINUE

C
        CALL CHAUXI(NDIM,MU,KA,RG,PHIG,INVP,LCOUR,COURB,
     &              DU1DM,DU2DM,DU3DM,U1L,U2L,U3L)
C
C-----------------------------------------------------------------------
C       CALCUL DE G, K1, K2, K3 AU POINT DE GAUSS
C-----------------------------------------------------------------------
C
        GUV = 0.D0
        CALL GBIL3D(DUDM,DUDM,DTDM,DFDM,DFDM,TGDM,TGDM,
     &              TTRG,TTRG,POIDS,C1,C2,C3,K3A,RHO,PULS,GUV)
        G = G + GUV
C
        GUV1 = 0.D0
        CALL GBIL3D(DUDM,DU1DM,DTDM,DFDM,DFVDM,TGDM,TGVDM,
     &              TTRG,TTRGV,POIDS,C1,C2,C3,K3A,RHO,PULS,GUV1)
        K1 = K1 + GUV1
C
        GUV2 = 0.D0
        CALL GBIL3D(DUDM,DU2DM,DTDM,DFDM,DFVDM,TGDM,TGVDM,
     &              TTRG,TTRGV,POIDS,C1,C2,C3,K3A,RHO,PULS,GUV2)
        K2 = K2 + GUV2
C
        GUV3 = 0.D0
        CALL GBIL3D(DUDM,DU3DM,DTDM,DFDM,DFVDM,TGDM,TGVDM,
     &              TTRG,TTRGV,POIDS,C1,C2,C3,K3A,RHO,PULS,GUV3)
        K3 = K3 + GUV3
C
 800  CONTINUE
C
      K1 = K1 * COEFF
      K2 = K2 * COEFF
      K3 = K3 * COEFF3
C
      ZR(IFICG)   = G
      ZR(IFICG+1) = K1 / SQRT(COEFF)
      ZR(IFICG+2) = K2 / SQRT(COEFF)
      ZR(IFICG+3) = K3 / SQRT(COEFF3)
      ZR(IFICG+4) = K1
      ZR(IFICG+5) = K2
      ZR(IFICG+6) = K3
C
 9999 CONTINUE
C
      CALL JEDEMA()
C
      END
