      SUBROUTINE TE0054(OPTION,NOMTE)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 21/01/2013   AUTEUR DELMAS J.DELMAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2013  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.

C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.

C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C.......................................................................
      IMPLICIT NONE

C     BUT: CALCUL DES MATRICES DE MASSE ELEMENTAIRE EN THERMIQUE
C          ELEMENTS ISOPARAMETRIQUES 3D

C          OPTION : 'MASS_THER'

C     ENTREES  ---> OPTION : OPTION DE CALCUL
C              ---> NOMTE  : NOM DU TYPE ELEMENT
C.......................................................................


      INCLUDE 'jeveux.h'

      INTEGER ICODRE
      CHARACTER*8   FAMI,POUM
      CHARACTER*16  NOMTE,OPTION,PHENOM
      REAL*8        VALPAR,DFDX(27),DFDY(27),DFDZ(27),POIDS
      REAL*8        CP, DELTAT
      INTEGER       IPOIDS,IVF,IDFDE,IGEOM,IMATE,LL,NDIM
      INTEGER       JGANO,NNO,KP,NPG2,IJ,I,J,IMATTT,ITEMPS
      INTEGER       NNOS,KPG,SPT
      LOGICAL       LTEATT

      IF ( LTEATT(' ','LUMPE','OUI')) THEN
         CALL ELREF4(' ','NOEU',NDIM,NNO,NNOS,NPG2,IPOIDS,IVF,
     &            IDFDE,JGANO)
      ELSE
         CALL ELREF4(' ','MASS',NDIM,NNO,NNOS,NPG2,IPOIDS,IVF,
     &           IDFDE, JGANO)
      ENDIF

      CALL JEVECH('PGEOMER','L',IGEOM)
      CALL JEVECH('PMATERC','L',IMATE)
      CALL JEVECH('PTEMPSR','L',ITEMPS)
      CALL JEVECH('PMATTTR','E',IMATTT)

      CALL RCCOMA(ZI(IMATE),'THER',1,PHENOM,ICODRE)
      IF (ICODRE.NE.0) CALL U2MESS('A','ELEMENTS2_63')
      FAMI='FPG1'
      KPG=1
      SPT=1
      POUM='+'

      VALPAR = ZR(ITEMPS)
      DELTAT = ZR(ITEMPS+1)
      CALL RCVALB(FAMI,KPG,SPT,POUM,ZI(IMATE),' ',PHENOM,1,'INST',
     &            VALPAR,1,'RHO_CP',CP,ICODRE, 1)

C    BOUCLE SUR LES POINTS DE GAUSS

      DO 40 KP = 1,NPG2

        LL = (KP-1)*NNO
        CALL DFDM3D ( NNO, KP, IPOIDS, IDFDE,
     &                ZR(IGEOM), DFDX, DFDY, DFDZ, POIDS )

        DO 30 I = 1,NNO

          DO 20 J = 1,I
            IJ = (I-1)*I/2 + J
            ZR(IMATTT+IJ-1) = ZR(IMATTT+IJ-1) +
     &                        CP/DELTAT*POIDS*ZR(IVF+LL+I-1)*
     &                        ZR(IVF+LL+J-1)

   20     CONTINUE
   30   CONTINUE

   40 CONTINUE

      END
