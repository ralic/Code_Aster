      SUBROUTINE TE0514(OPTION,NOMTE)
      IMPLICIT   NONE
      CHARACTER*16 OPTION,NOMTE

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 27/06/2011   AUTEUR MASSIN P.MASSIN 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C.......................................................................
C
C       CALCUL DES DONNÉES TOPOLOGIQUES CONCERNANT CONCERNANT
C       LA DÉCOUPE DES ÉLÉMENTS POUR L'INTÉGRATION AVEC X-FEM
C                   (VOIR BOOK IV 27/10/04)
C
C  OPTION : 'TOPOSE' (X-FEM TOPOLOGIE DES SOUS-ÉLÉMENTS)
C
C  ENTREES  ---> OPTION : OPTION DE CALCUL
C           ---> NOMTE  : NOM DU TYPE ELEMENT
C
C......................................................................
C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX --------------------
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)

C --------- FIN  DECLARATIONS  NORMALISEES  JEVEUX --------------------

      CHARACTER*8   ELP,NOMA,TYPMA,LAG,ENR,K8BID,NOMAIL
      CHARACTER*24  PINTER,AINTER,HEAV,PMILIE,NDOUBL,NDOUB2
      INTEGER       IGEOM,JLSN,IFISC,JFISC,NFISC
      INTEGER       JOUT1,JOUT2,JOUT3,JOUT4,JOUT5
      INTEGER       JPTINT,JHEAV,IADZI,IAZK24,JDIM,NNO,NNN,JFISCO
      INTEGER       NINTER,NIT,NSE,NNOSE,ISE2,NCOMPH,NCOMPP,NCOMPC
      INTEGER       NPTS,CNSE(6,6),I,J,IT,NPI,IPT,ISE,IN,NI,CPT
      INTEGER       NDIM,IBID,NDIME,IAD,JTAB(7),JTAB2(2)
      INTEGER       JPTMIL,NPM,NMILIE,PMMAX,NMFIS,JGRLSN,JNDOU2
      INTEGER       XXMMVD,IRET,NFISS,IFISS,JNDOUB,NMAX,NCOMB
      REAL*8        NMIL(3),RBID,R8PREM
      REAL*8        NEWPT(3),P(3),PADIST,LONREF
      LOGICAL       ISMALI,DEJA,AJN

C......................................................................

      CALL JEMARQ()

      CALL ELREF1(ELP)
      CALL ELREF4(' ','RIGI',NDIME,NNO,IBID,IBID,IBID,IBID,IBID,IBID)

      CALL TECAEL(IADZI,IAZK24)
      NOMA=ZK24(IAZK24)
      TYPMA=ZK24(IAZK24-1+3+ZI(IADZI-1+2)+3)
      CALL DISMOI('F','DIM_GEOM',NOMA,'MAILLAGE',NDIM,K8BID,IRET)

C     ATTENTION, NE PAS CONFONDRE NDIM ET NDIME  !!
C     NDIM EST LA DIMENSION DU MAILLAGE
C     NDIME EST DIMENSION DE L'ELEMENT FINI
C     PAR EXEMPLE, POUR LES ELEMENT DE BORDS D'UN MAILLAGE 3D :
C     NDIME = 2 ALORS QUE NDIM = 3

      IF (NDIME.EQ. 2) THEN
         PMMAX=10
      ELSEIF (NDIME.EQ. 1) THEN
         PMMAX=2
      ENDIF
C
C     RECUPERATION DES ENTREES / SORTIE
      CALL JEVECH('PGEOMER','L',IGEOM)
      CALL JEVECH('PLEVSET','L',JLSN)
      CALL JEVECH('PPINTTO','E',JOUT1)
      CALL JEVECH('PCNSETO','E',JOUT2)
      CALL JEVECH('PHEAVTO','E',JOUT3)
      CALL JEVECH('PLONCHA','E',JOUT4)

      CALL TEATTR (NOMTE,'S','XFEM',ENR,IBID)
      IF (IBID.EQ.0 .AND.(ENR.EQ.'XH'.OR.ENR.EQ.'XHC')
     &   .AND. NDIM.LE.2) THEN
        CALL JEVECH('PPMILTO','E',JOUT5)
        IF(NDIME.EQ.2) CALL JEVECH('PGRADLN','L',JGRLSN)
      ENDIF

      CALL TECACH('OOO','PHEAVTO',7,JTAB,IRET)
      NCOMPH = JTAB(2)
      NFISS = JTAB(7)
      IF (NFISS.EQ.1) THEN
        CALL WKVECT('&&TE0514.PFISCO','V V I',2,JFISCO)
C        DO 10 I=1,2
C          ZI(JFISCO-1+I) = 0
C  10    CONTINUE
      ELSE
        CALL JEVECH('PFISCO','L',JFISCO)
      ENDIF

      CALL TECACH('OOO','PPINTTO',2,JTAB2,IRET)
      NCOMPP = JTAB2(2)/NDIM
      CALL TECACH('OOO','PCNSETO',2,JTAB2,IRET)
      NCOMPC = JTAB2(2)

      NPI=0
      NPM=0
      AJN=.FALSE.
      NMFIS=0

C     CALCUL D'UNE LONGUEUR CARACTERISTIQUE DE L'ELEMENT
      CALL LONCAR(NDIM,TYPMA,ZR(IGEOM),LONREF)

C     ON SUBDIVISE L'ELEMENT PARENT EN NIT SOUS-ELEMENTS
      CALL XDIVTE(NDIM,ELP,ZI(JOUT2),NIT,NNOSE)
      CPT = NIT
C     PROBLEME DE DIMENSSIONNEMENT DANS LES CATALOGUES
      CALL ASSERT(NCOMPC.GE.NNOSE*NCOMPH)

      PINTER='&&TE0514.PTINTER'
      AINTER='&&TE0514.ATINTER'
      HEAV  ='&&TE0514.HEAV'
      PMILIE='&&TE0514.PTMILIE'

C     BOUCLE SUR LES FISSURES

      DO 90 IFISS=1,NFISS

        CALL WKVECT('&&TE0514.FISC','V V I',IFISS,JFISC)
        IFISC = IFISS
        NFISC = 0
  80    CONTINUE
        IF (ZI(JFISCO-1+2*IFISC-1).GT.0) THEN
          NFISC = NFISC+1
          ZI(JFISC-1+2*(NFISC-1)+2) = ZI(JFISCO-1+2*IFISC)
          IFISC = ZI(JFISCO-1+2*IFISC-1)
          ZI(JFISC-1+2*(NFISC-1)+1) = IFISC
          GOTO 80
        ENDIF


C       BOUCLE SUR LES NIT TETRAS
        DO 100 IT=1,NIT

C         DECOUPAGE EN NSE SOUS-ELEMENTS

          NMILIE = 0
          NINTER = 0
          NPTS   = 0

          IF (.NOT.ISMALI(ELP) .AND. NDIM.LE.2) THEN
            CALL XDECQU(NNOSE,IT,NDIM,ZI(JOUT2),JLSN,JGRLSN,IGEOM,
     &            PINTER,NINTER,NPTS,AINTER,PMILIE,NMILIE,NMFIS)

            CALL XDECQV(NNOSE,IT,ZI(JOUT2),ZR(JLSN),IGEOM,PINTER,NINTER,
     &                        NPTS,AINTER,NSE,CNSE,HEAV)
          ELSE
            CALL XDECOU(NDIM,ELP,NNO,NNOSE,IT,ZR(JOUT1),ZI(JOUT2),
     &                  ZR(JLSN),ZI(JFISC),IGEOM,NFISS,IFISS,PINTER,
     &                  NINTER,NPTS,AINTER,LONREF,NFISC)
            CALL XDECOV(NDIM,ELP,NNO,NNOSE,IT,ZR(JOUT1),ZI(JOUT2),
     &                  ZI(JOUT3),NCOMPH,ZR(JLSN),ZI(JFISC),IGEOM,NFISS,
     &                  IFISS,PINTER,NINTER,NPTS,AINTER,NSE,CNSE,HEAV,
     &                  NFISC)
          ENDIF

C ----- - BOUCLE SUR LES NINTER POINTS D'INTER : ARCHIVAGE DE PINTTO
          CALL JEVEUO(PINTER,'L',JPTINT)
          DO 200 IPT=1,NINTER
            DO 210 J=1,NDIM
              NEWPT(J)=ZR(JPTINT-1+NDIM*(IPT-1)+J)
 210        CONTINUE

C           VERIF SI EXISTE DEJA DANS PINTTO
            DEJA=.FALSE.
            DO 220 I=1,NPI
              DO 221 J=1,NDIM
                P(J) = ZR(JOUT1-1+NDIM*(I-1)+J)
 221          CONTINUE
              IF (PADIST(NDIM,P,NEWPT) .LT. (LONREF*1.D-6)) THEN
                DEJA = .TRUE.
                NI=I
              ENDIF
 220        CONTINUE
            IF (.NOT.DEJA) THEN
              NPI=NPI+1
              NI =NPI
C             NOMBRE TOTAL DE PT D'INTER LIMITE A LA TAILLE DE LA CARTE
              CALL ASSERT(NPI.LE.NCOMPP)
C             ARCHIVAGE DE PINTTO
              DO 230 J=1,NDIM
                ZR(JOUT1-1+NDIM*(NPI-1)+J)=NEWPT(J)
 230          CONTINUE

C             MISE A JOUR DU CNSE (TRANSFORMATION DES 100 EN 1000...)
              DO 240 ISE=1,NSE
                DO 241 IN=1,NDIME+1
                  IF (CNSE(ISE,IN).EQ.100+IPT) CNSE(ISE,IN)=1000+NI
 241            CONTINUE
 240          CONTINUE
            ELSE
              DO 114 ISE=1,NSE
                DO 115 IN=1,NDIME+1
                  IF (CNSE(ISE,IN).EQ.100+IPT) CNSE(ISE,IN)=1000+NI
 115            CONTINUE
 114          CONTINUE
            ENDIF
 200      CONTINUE
          CALL JEDETR(PINTER)

C ------- BOUCLE SUR LES NMILIE POINTS MILIEUX : ARCHIVAGE DE PMILTO
          IF (.NOT.ISMALI(ELP).AND.NDIM.LE.2) THEN
            CALL JEVEUO(PMILIE,'L',JPTMIL)
            DO 300 IPT=1,NMILIE
              DO 310 J=1,NDIM
                NEWPT(J)=ZR(JPTMIL-1+NDIM*(IPT-1)+J)
 310          CONTINUE

C             VERIF SI EXISTE DEJA DANS PMILTO
              DEJA=.FALSE.
              DO 320 I=1,NPM
                DO 321 J=1,NDIM
                  P(J) = ZR(JOUT5-1+NDIM*(I-1)+J)
 321            CONTINUE
                IF (PADIST(NDIM,P,NEWPT) .LT. (LONREF*1.D-6)) THEN
                  DEJA = .TRUE.
                  NI=I
                ENDIF
 320          CONTINUE
              IF (.NOT.DEJA) THEN
                NPM=NPM+1
C               NOMBRE TOTAL DE POINTS MILIEUX LIMITE A PMMAX
                CALL ASSERT(NPM.LE.PMMAX)
C               ARCHIVAGE DE PMILTO
                DO 330 J=1,NDIM
                  ZR(JOUT5-1+NDIM*(NPM-1)+J)=ZR(JPTMIL-1+NDIM*(IPT-1)+J)
 330            CONTINUE
 
C               MISE A JOUR DU CNSE (TRANSFORMATION DES 200 EN 2000...)
                DO 340 ISE=1,NSE
                  DO 341 IN=1,6
                    IF (CNSE(ISE,IN).EQ.200+IPT) CNSE(ISE,IN)=2000+NPM
 341              CONTINUE
 340            CONTINUE
              ELSE
                DO 350 ISE=1,NSE
                  DO 351 IN=1,6
                    IF (CNSE(ISE,IN).EQ.200+IPT) CNSE(ISE,IN)=2000+NI
 351              CONTINUE
 350            CONTINUE
              ENDIF

 300        CONTINUE
            CALL JEDETR(PMILIE)
          ENDIF

C ------- BOUCLE SUR LES NSE SOUS-ELE : ARCHIVAGE DE PCNSETO, PHEAVTO
          CALL JEVEUO(HEAV,'L',JHEAV)
          DO 120 ISE=1,NSE
            IF (ISE.EQ.1) THEN
              ISE2=IT
            ELSE
              CPT=CPT+1
              ISE2=CPT
            ENDIF
C         NOMBRE TOTAL DE SOUS-ELEMENTS LIMITE A LA TAILLE DE LA CARTE
            CALL ASSERT(CPT.LE.NCOMPH)
C           ARCHIVAGE DE PHEAVTO
            DO 125 I=1,IFISS
              ZI(JOUT3-1+NCOMPH*(I-1)+ISE2)=
     &                NINT(ZR(JHEAV-1+IFISS*(ISE-1)+I))
 125        CONTINUE
C           ARCHIVAGE DE PCNSETO
            DO 121 IN=1,NNOSE
              ZI(JOUT2-1+NNOSE*(ISE2-1)+IN)=CNSE(ISE,IN)
 121        CONTINUE

 120      CONTINUE
          CALL JEDETR(HEAV)

 100    CONTINUE
        NIT = CPT
        CALL JEDETR('&&TE0514.FISC')
  90  CONTINUE

      IF (NFISS.EQ.1) THEN
        CALL JEDETR('&&TE0514.PFISCO')
      ENDIF

C     ARCHIVAGE DE LONCHAM SOUS ELEMENTS
      ZI(JOUT4-1+1)=NIT

C     ARCHIVAGE DE LONCHAM POINTS D'INTERSECTION
      ZI(JOUT4-1+2)=NPI

C     SERT UNIQUEMENT POUR LA VISU

C     POUR COMPTER LES NOEUDS EN DOUBLE (EN POST-TRAITEMENT)
C     NCOMB COMBINAISONS POSSIBLES
      NCOMB = 2**NFISS
C     ON VA JUSQU'A NCOMPP, NOMBRE MAX DE POINTS D'INTERSECTION
      NDOUB2='&&TE0514.NDOUBL2'
      CALL WKVECT(NDOUB2,'V V I',NCOMB*NCOMPP,JNDOU2)
C     POUR COMPTER LES PT D'INTER EN DOUBLE (EN POST-TRAITEMENT)
C     ON VA JUSQU'A NNO+1 POUR PRENDRE EN COMPTE LE 9EME NOEUD DU QUAD8
      NDOUBL='&&TE0514.NDOUBLE'
      CALL WKVECT(NDOUBL,'V V I',NCOMB*(NNO+1),JNDOUB)

      DO 130 ISE = 1,NIT
        DO 127 IN=1,NNOSE
          I = ZI(JOUT2-1+NNOSE*(ISE-1)+IN)
          IF (I.LT.1000) THEN
C ----- ON SE PLACE EN BASE 2, LA DIMENSSION DU PB EST NFISS
C ----- POUR CHAQUE FISSURE H=-1 OU 0=>0
C                           H=+1     =>1
            IAD = NCOMB*(I-1)
            DO 128 IFISS=1,NFISS
              IF (ZI(JOUT3-1+NCOMPH*(IFISS-1)+ISE).EQ.1) THEN
                IAD = IAD + 2**(NFISS-IFISS)
              ENDIF
 128        CONTINUE
            ZI(JNDOUB+IAD) = 1
          ELSEIF (I.GT.1000.AND.I.LT.2000) THEN
            IAD = NCOMB*(I-1001)
            DO 129 IFISS=1,NFISS
              IF (ZI(JOUT3-1+NCOMPH*(IFISS-1)+ISE).EQ.1) THEN
                IAD = IAD + 2**(NFISS-IFISS)
              ENDIF
 129        CONTINUE
            ZI(JNDOU2+IAD) = 1
          ENDIF
 127    CONTINUE
 130  CONTINUE

C     NOMBRE DE NOUVEAUX POINTS : NNN
      NNN = 0
C  -  ON AJOUTE LES PT D'INTER QUI NE SONT PAS DES NOEUDS
C  -  AUTANT DE FOIS QU'IL Y A DE COMBINAISON D'HEAVISIDE DIFFERENTES
      DO 600 I = 1,NCOMB*NCOMPP
        NNN = NNN + ZI(JNDOU2-1+I)
 600  CONTINUE

C  -  ON AJOUTE LES NOEUDS, AUTANT DE FOIS QU'IL Y A DE COMBINAISON
C  -  D'HEAVISIDE DIFFERENTES.
C  -  ON VA JUSQU'A NNO+1 POUR PRENDRE EN COMPTE LE 9EME NOEUD DU QUAD8
      DO 500 I = 1,NCOMB*(NNO+1)
        NNN = NNN + ZI(JNDOUB-1+I)
 500  CONTINUE
C
      CALL JEDETR(NDOUBL)
      CALL JEDETR(NDOUB2)


      IF (.NOT.ISMALI(ELP).AND. NDIM.LE.2) THEN
C  - CAS QUADRATIQUE, ON AJOUTE LES NOUVEAUX NOEUDS MILIEUX DES SE
        NNN=NNN+NMFIS+NPM
      ENDIF

      ZI(JOUT4-1+3)=NNN
      ZI(JOUT4-1+4)=0

C --- LE NOEUD N- CALCULE, STOCKE ET RENUMEROTE
      IF (ELP.EQ.'QU8'.AND.NDIM.EQ.2) THEN
        DO 400 I=1,NIT*NNOSE
          IF (ZI(JOUT2-1+I).EQ.9) THEN
            CALL NDCENT(IGEOM,ZR(JLSN),NMIL,RBID)
            DO 401 J=1,NDIM
              ZR(JOUT5+NPM*NDIM+J-1)=NMIL(J)
 401        CONTINUE
            ZI(JOUT2-1+I)=3000+NPM+1
            AJN=.TRUE.
          ENDIF
 400    CONTINUE
      ENDIF

C     ARCHIVAGE DE LONCHAM POINTS MILIEUX

      IF (.NOT.ISMALI(ELP).AND. NDIM.LE.2) THEN
        IF (AJN) THEN
          ZI(JOUT4-1+4)=NPM+1
        ELSE
          ZI(JOUT4-1+4)=NPM
        ENDIF
      ENDIF

      CALL JEDEMA()
      END
