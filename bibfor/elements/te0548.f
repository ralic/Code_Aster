      SUBROUTINE TE0548(OPTION,NOMTE)
      IMPLICIT   NONE
      CHARACTER*16 OPTION,NOMTE

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 27/06/2011   AUTEUR MASSIN P.MASSIN 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C RESPONSABLE GENIAUT S.GENIAUT
C.......................................................................
C
C                CONTACT X-FEM METHODE CONTINUE :
C             MISE A JOUR DU SEUIL DE FROTTEMENT
C             MISE A JOUR DE LA COHESION DANS LE CAS COHESIF
C
C
C  OPTION : 'XREACL' (X-FEM MISE À JOUR DU SEUIL DE FROTTEMENT)

C  ENTREES  ---> OPTION : OPTION DE CALCUL
C           ---> NOMTE  : NOM DU TYPE ELEMENT
C
C......................................................................
C --------- DEBUT DECLARATIONS NORMALISEES  JEVEUX --------------------
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)

C --------- FIN  DECLARATIONS  NORMALISEES  JEVEUX --------------------

      INTEGER      I,J,IFA,IPGF,ISSPG,NI,PLI
      INTEGER      IDEPPL,JAINT,JCFACE,JLONCH,JSEUIL,IPOIDF,IVFF,IDFDEF
      INTEGER      IADZI,IAZK24,IPOIDS,IVF,IDFDE,JGANO,JDONCO
      INTEGER      NDIM,NNO,NNOS,NPG,NFH,DDLC,NNOM,INTEG,NINTER,NFE
      INTEGER      NFACE,CFACE(5,3),IBID,NNOF,NPGF,XOULA,JPTINT
      INTEGER      SINGU,JBASEC,NPTF
      INTEGER      DDLS,NDDL,NNOL,LACT(8),NLACT,IGEOM,DDLM
      INTEGER      IER,CONTAC
      CHARACTER*8  ELREF,TYPMA,FPG,ELC,LAG,ELREFC
      REAL*8       SEUIL,FFI,E,G(3),RBID,FFP(27),FFC(8),ND(3)
      REAL*8       FFPC(27),DFBID(27,3),R3BID(3)
      REAL*8       CPENCO,CSTACO,COEFEC
      LOGICAL      NOEUD,LPENAC
C......................................................................

      CALL JEMARQ()
C
      CALL ELREF1(ELREF)
      CALL ELREF4(' ','RIGI',NDIM,NNO,NNOS,NPG,IPOIDS,IVF,IDFDE,JGANO)

C     INITIALISATION DES DIMENSIONS DES DDLS X-FEM
      CALL XTEINI(NOMTE,NFH,NFE,SINGU,DDLC,NNOM,DDLS,NDDL,
     &            DDLM,IBID,CONTAC)

      CALL TECAEL(IADZI,IAZK24)
      TYPMA=ZK24(IAZK24-1+3+ZI(IADZI-1+2)+3)

C     RECUPERATION DU TYPE DE CONTACT
      NOEUD=.FALSE.
      CALL TEATTR (NOMTE,'C','XLAG',LAG,IER)
      CALL ASSERT(IER.EQ.0)
      IF (LAG(1:5).EQ.'NOEUD') NOEUD=.TRUE.
      CALL ELELIN(CONTAC,ELREF,ELREFC,IBID,IBID)

C     DEP ACTUEL (DEPPLU) : 'PDEPL_P'
      CALL JEVECH('PDEPL_P','L',IDEPPL)
      CALL JEVECH('PDONCO','L',JDONCO)
      CALL JEVECH('PAINTER','L',JAINT)
      CALL JEVECH('PCFACE' ,'L',JCFACE)
      CALL JEVECH('PLONCHA','L',JLONCH)
      CALL JEVECH('PPINTER','L',JPTINT)
      CALL JEVECH('PGEOMER','L',IGEOM)
      CALL JEVECH('PSEUIL' ,'E',JSEUIL)
      CALL JEVECH('PBASECO','L',JBASEC)
C
C     RECUPERATIONS DES DONNEES SUR LE CONTACT ET
C     SUR LA TOPOLOGIE DES FACETTES
      NINTER=ZI(JLONCH-1+1)
      NFACE=ZI(JLONCH-1+2)
      NPTF=ZI(JLONCH-1+3)

      DO 15 I=1,NFACE
        DO 16 J=1,NPTF
          CFACE(I,J)=ZI(JCFACE-1+NPTF*(I-1)+J)
   16   CONTINUE
 15   CONTINUE
C
C     SCHEMA D'INTEGRATION NUMERIQUE ET ELEMENT DE REFERENCE DE CONTACT
      INTEG = NINT(ZR(JDONCO-1+4))
      CALL XMINTE(NDIM,INTEG,FPG)
C
      IF (NDIM .EQ. 3) THEN
        ELC='TR3'
      ELSEIF (NDIM.EQ.2) THEN
        IF(CONTAC.LE.2) THEN
          ELC='SE2'
        ELSE
          ELC='SE3'
        ENDIF
      ENDIF
C
      CALL ELREF4(ELC,FPG,IBID,NNOF,IBID,NPGF,IPOIDF,IVFF,IDFDEF,IBID)
C
      CALL TECAEL(IADZI,IAZK24)
      TYPMA=ZK24(IAZK24-1+3+ZI(IADZI-1+2)+3)
C
C     LISTE DES LAMBDAS ACTIFS
C
      IF (NOEUD) THEN
        CALL XLACTI(TYPMA,NINTER,JAINT,LACT,NLACT)
        IF (CONTAC.EQ.1 .OR. CONTAC.EQ.4) NNOL=NNO
        IF (CONTAC.EQ.3) NNOL=NNOS
      ELSE
        NNOL=NNOF
      ENDIF
C
C     BOUCLE SUR LES FACETTES
      DO 100 IFA=1,NFACE
C
C       BOUCLE SUR LES POINTS DE GAUSS DES FACETTES
        DO 110 IPGF=1,NPGF
C
C         INDICE DE CE POINT DE GAUSS DANS PSEUIL
          ISSPG=NPGF*(IFA-1)+IPGF
C
C         CALCUL DE JAC (PRODUIT DU JACOBIEN ET DU POIDS)
C         ET DES FF DE L'ELEMENT PARENT AU POINT DE GAUSS
C         ET LA NORMALE ND ORIENTÉE DE ESCL -> MAIT
          IF (NDIM.EQ.3) THEN
            CALL XJACFF(ELREF,ELREFC,ELC,NDIM,FPG,JPTINT,IFA,CFACE,IPGF,
     &      NNO,IGEOM,JBASEC,G,'NON',RBID,FFP,FFPC,DFBID,ND,R3BID,R3BID)
          ELSEIF (NDIM.EQ.2) THEN
            CALL XJACF2(ELREF,ELREFC,ELC,NDIM,FPG,JPTINT,IFA,CFACE,NPTF,
     &      IPGF,NNO,IGEOM,JBASEC,G,'NON',RBID,FFP,FFPC,DFBID,ND,R3BID)
          ENDIF

C        CALCUL DES FONCTIONS DE FORMES DE CONTACT DANS LE CAS LINEAIRE
          IF (CONTAC.EQ.1) THEN
            CALL XMOFFC(LACT,NLACT,NNO,FFP,FFC)
          ELSEIF (CONTAC.EQ.3) THEN
            CALL XMOFFC(LACT,NLACT,NNOS,FFPC,FFC)
          ELSEIF (CONTAC.EQ.4) THEN
            CALL XMOFFC(LACT,NLACT,NNO,FFP,FFC)
          ENDIF

C         CALCUL DU NOUVEAU SEUIL A PARTIR DES LAMBDA DE DEPPLU
          SEUIL = 0.D0
          DO 120 I = 1,NNOL
            IF (NOEUD) THEN
              FFI=FFC(I)
              NI=I
            ELSE
              NI=XOULA(CFACE,IFA,I,JAINT,TYPMA,CONTAC)
              FFI=ZR(IVFF-1+NNOF*(IPGF-1)+I)
            ENDIF
            CALL XPLMAT(NDIM,NFH,NFE,DDLC,DDLM,NNO,NNOM,NI,PLI)
            SEUIL = SEUIL + FFI * ZR(IDEPPL-1+PLI)
 120      CONTINUE

C         LORS D'UNE CONVERGENCE FORCEE, IL SE PEUT QUE LES REACTIONS
C         SOIENT TROP PETITES. LE POINT DOIT ETRE CONSIDERE GLISSANT.
          IF (ABS(SEUIL).LT.1.D-11) THEN
            SEUIL=0.D0
          ENDIF

          ZR(JSEUIL-1+ISSPG)=SEUIL

 110    CONTINUE
 100  CONTINUE

      CALL JEDEMA()
      END
