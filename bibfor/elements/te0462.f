      SUBROUTINE TE0462 ( OPTION , NOMTE )
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 07/01/2013   AUTEUR CHEIGNON E.CHEIGNON 
C ======================================================================
C COPYRIGHT (C) 1991 - 2013  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
      IMPLICIT NONE
      INCLUDE 'jeveux.h'
      CHARACTER*16        OPTION , NOMTE
C ----------------------------------------------------------------------
C     CALCUL DES COORDONNEES DES SOUS POINTS DE GAUSS SUR LES FAMILLE
C     DE LA LISTE MATER
C     POUR LES ELEMENTS : DKT
C ----------------------------------------------------------------------
C     NOMBRE MAX DE FAMILLE DANS MATER
      INTEGER NFPGMX
C     NOMBRE DE NIVEAUX PAR COUCHE
      INTEGER NBNIV
C     DIMENSION
      INTEGER NDIM
      PARAMETER (NFPGMX=10,NBNIV=3,NDIM=3)

      INTEGER  NDIM1,NNO,NNOS,NPG,JGANO,IDFDE,IPOIDS,IVF
      INTEGER  IGEOM,JTAB(7),ICOPG,INBF,ICOQ,IRET,DECPO,IAD
      INTEGER  NBSP,NBCOU,NFPG,DECFPG
      INTEGER  IFPG,IG,ICOU,INIV,INO
      REAL*8   PGL(3,3),XX,YY,ZZ
      REAL*8   EPAIS,EXCEN,GM1(3),GM2(3),EPC,BAS,HH
      LOGICAL  GRILLE
      CHARACTER*8 FAMI(NFPGMX)
      DATA GM1 / 0.D0,0.D0,1.D0/
C ----------------------------------------------------------------------

      IF(NOMTE(1:4).NE.'MEDK'.AND.NOMTE(1:4).NE.'MEGC')THEN
        CALL ASSERT(.FALSE.)
      ENDIF
      GRILLE= NOMTE(1:4).EQ.'MEGC'

C     NOMBRE DE NOEUDS
      IF (NOMTE(5:7).EQ.'QU4')THEN
        NNO = 4
      ELSEIF (NOMTE(5:7).EQ.'TR3')THEN
        NNO = 3
      ELSE
        CALL ASSERT(.FALSE.)
      ENDIF


      CALL JEVECH('PGEOMER','L',IGEOM)

C     ZR(ICOPG) : COORDONNEES DE SOUS-POINTS DE GAUSS
      CALL TECACH('OON','PCOOPGM',7,JTAB,IRET)
      ICOPG=JTAB(1)
      NBSP=JTAB(7)
      CALL ASSERT(NBSP.GT.0)

      CALL JEVECH ('PCACOQU' , 'L' , ICOQ)

      IF (GRILLE) THEN
        EXCEN = ZR(ICOQ+3)
      ELSE
C       ELEMENTS A SOUS POINTS : DKT
        CALL JEVECH('PNBSP_I','L',INBF)
        NBCOU = ZI(INBF)
        EPAIS = ZR(ICOQ)
        EXCEN = ZR(ICOQ+4)
        BAS=-EPAIS/2.D0+EXCEN
        EPC=EPAIS/NBCOU
      ENDIF

C ON UTILISE LE VECTEUR NORMAL DE LA PLAQUE
      IF (NNO.EQ.3)THEN
        CALL DXTPGL ( ZR(IGEOM), PGL )
      ELSEIF(NNO.EQ.4)THEN
        CALL DXQPGL ( ZR(IGEOM), PGL, 'S', IRET )
      ENDIF

      CALL UTPVLG(1,3,PGL,GM1,GM2)

      CALL FMATER(NFPGMX,NFPG,FAMI)
      DECFPG = 0
      DO 200 IFPG=1,NFPG

        CALL ELREF4(' ',FAMI(IFPG),NDIM1,NNO,NNOS,NPG,IPOIDS,IVF,
     &                                               IDFDE,JGANO)

        DO 100 IG=1,NPG

C         CALCUL DES COORDONNEES DES POINTS DE GAUSS
          XX = 0.D0
          YY = 0.D0
          ZZ = 0.D0
          DO 10 INO = 1,NNO
            XX = XX + ZR(IGEOM+3* (INO-1)+0)*ZR(IVF+ (IG-1)*NNO+INO-1)
            YY = YY + ZR(IGEOM+3* (INO-1)+1)*ZR(IVF+ (IG-1)*NNO+INO-1)
            ZZ = ZZ + ZR(IGEOM+3* (INO-1)+2)*ZR(IVF+ (IG-1)*NNO+INO-1)
   10     CONTINUE

          IF (GRILLE)THEN
            DECPO=NDIM*(DECFPG+IG-1)
            IAD = ICOPG+DECPO
            ZR(IAD+0) = XX + EXCEN*GM2(1)
            ZR(IAD+1) = YY + EXCEN*GM2(2)
            ZR(IAD+2) = ZZ + EXCEN*GM2(3)
          ELSE
            DECPO=NBCOU*NBNIV*NDIM*(DECFPG+IG-1)
            DO 110 ICOU=1,NBCOU
              DO 120 INIV=1,NBNIV
                HH=BAS+DBLE(ICOU-1)*EPC+DBLE(INIV-1)*EPC/2.D0
                IAD = ICOPG+DECPO+(ICOU-1)*NBNIV*NDIM+(INIV-1)*NDIM
                ZR(IAD+0) = XX + HH*GM2(1)
                ZR(IAD+1) = YY + HH*GM2(2)
                ZR(IAD+2) = ZZ + HH*GM2(3)
 120          CONTINUE
 110        CONTINUE
          ENDIF
 100    CONTINUE
        DECFPG = DECFPG + NPG
 200  CONTINUE



      END
