      SUBROUTINE TE0095(OPTION,NOMTE)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 30/03/2004   AUTEUR CIBHHLV L.VIVAN 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.

C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.

C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
      IMPLICIT NONE

C.......................................................................
C FONCTION REALISEE:

C      CALCUL
C      DE LA FORME BILINEAIRE SYMETRIQUE G(U,V)

C      POUR LES ELEMENTS ISOPARAMETRIQUES 3D

C      OPTION : 'CALC_G_BILI'

C ENTREES  ---> OPTION : OPTION DE CALCUL
C          ---> NOMTE  : NOM DU TYPE ELEMENT
C.......................................................................

      CHARACTER*2 CODRET(2)
      CHARACTER*8 NOMRES(2)
      CHARACTER*16 NOMTE,OPTION

      REAL*8 EPSI,R8PREM
      REAL*8 DFDI(81),F(3,3),EPS(6)
      REAL*8 DUDM(3,3),DVDM(3,3),DTDM(3,3),DER(4)
      REAL*8 RBID,E,NU
      REAL*8 THET,TG
      REAL*8 XG,YG
      REAL*8 C1,C2,C3
      REAL*8 VALRES(2),DEVRES(3)
      REAL*8 GELEM,GUV3,G,POIDS

      INTEGER JGANO,IPOIDS,IVF,IDFDE,NNO,KP,NPG1,COMPT
      INTEGER IGEOM,ITHET,IFIC,IDEPU,IDEPV,ITEMP
      INTEGER IMATE,JVAL,K,I,J,L,NDIM,NNOS

C---------------- COMMUNS NORMALISES  JEVEUX  --------------------------
      COMMON /IVARJE/ZI(1)
      COMMON /RVARJE/ZR(1)
      COMMON /CVARJE/ZC(1)
      COMMON /LVARJE/ZL(1)
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
      INTEGER ZI
      REAL*8 ZR
      COMPLEX*16 ZC
      LOGICAL ZL
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
C------------FIN  COMMUNS NORMALISES  JEVEUX  --------------------------

      CALL JEMARQ()
      EPSI = R8PREM()

      CALL ELREF4(' ','RIGI',NDIM,NNO,NNOS,NPG1,IPOIDS,IVF,IDFDE,JGANO)

      DO 10 I = 1,1
   10 CONTINUE

      CALL JEVECH('PGEOMER','L',IGEOM)
      CALL JEVECH('PDEPLAU','L',IDEPU)
      CALL JEVECH('PDEPLAV','L',IDEPV)
      CALL JEVECH('PTHETAR','L',ITHET)
      CALL JEVECH('PTEMPER','L',ITEMP)
      CALL JEVECH('PMATERC','L',IMATE)

      CALL JEVECH('PGTHETA','E',IFIC)

C - PAS DE CALCUL DE G POUR LES ELEMENTS OU LA VALEUR DE THETA EST NULLE

      GUV3 = 0.D0
      COMPT = 0
      DO 30 I = 1,NNO
        THET = 0.D0
        DO 20 J = 1,NDIM
          THET = THET + ABS(ZR(ITHET+NDIM* (I-1)+J-1))
   20   CONTINUE
        IF (THET.LT.EPSI) COMPT = COMPT + 1
   30 CONTINUE
      IF (COMPT.EQ.NNO) GO TO 100

      NOMRES(1) = 'E'
      NOMRES(2) = 'NU'

C ======================================================================

      DO 90 KP = 1,NPG1
        L = (KP-1)*NNO
        TG = 0.D0
        XG = 0.D0
        YG = 0.D0
        DO 50 I = 1,NDIM
          DO 40 J = 1,NDIM
            DUDM(I,J) = 0.D0
            DVDM(I,J) = 0.D0
            DTDM(I,J) = 0.D0
   40     CONTINUE
   50   CONTINUE

C - CALCUL DES ELEMENTS GEOMETRIQUES

        CALL NMGEOM(NDIM,NNO,.FALSE.,.FALSE.,ZR(IGEOM),KP,
     &              IPOIDS,IVF,IDFDE,
     &              ZR(IDEPU),POIDS,DFDI,F,EPS,RBID)

C - CALCULS DES GRADIENTS DE U ET V (DUDM ET DVDM),THETA (DTDM)
C   DE LA TEMPERATURE AUX POINTS DE GAUSS (TG)

        DO 80 I = 1,NNO
          DER(1) = DFDI(I)
          DER(2) = DFDI(I+NNO)
          DER(3) = DFDI(I+2*NNO)
          DER(4) = ZR(IVF+L+I-1)
          TG = TG + ZR(ITEMP+I-1)*DER(4)
          XG = XG + ZR(IGEOM+2* (I-1))*DER(4)
          YG = YG + ZR(IGEOM+2* (I-1)+1)*DER(4)
          DO 70 J = 1,NDIM
            DO 60 K = 1,NDIM
              DUDM(J,K) = DUDM(J,K) + ZR(IDEPU+NDIM* (I-1)+J-1)*DER(K)
              DVDM(J,K) = DVDM(J,K) + ZR(IDEPV+NDIM* (I-1)+J-1)*DER(K)
              DTDM(J,K) = DTDM(J,K) + ZR(ITHET+NDIM* (I-1)+J-1)*DER(K)
   60       CONTINUE
   70     CONTINUE
   80   CONTINUE
        CALL RCVADA(ZI(IMATE),'ELAS',TG,2,NOMRES,VALRES,DEVRES,CODRET)
        E = VALRES(1)
        NU = VALRES(2)
        C3 = E/ (2.D0* (1.D0+NU))
        C1 = E* (1.D0-NU)/ ((1.D0+NU)* (1.D0-2.D0*NU))
        C2 = NU/ (1.D0-NU)*C1

C   INTRODUCTION DE U1S ET U2S DANS G(U,V)

        GELEM = 0.D0
        CALL GBIL3D(DUDM,DVDM,DTDM,POIDS,C1,C2,C3,GELEM)
        GUV3 = GUV3 + GELEM
   90 CONTINUE

      G = GUV3

      ZR(IFIC) = G

  100 CONTINUE
      CALL JEDEMA()
      END
