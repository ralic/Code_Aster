      SUBROUTINE TE0029(OPTION,NOMTE)
      IMPLICIT REAL*8 (A-H,O-Z)
C.......................................................................
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 13/06/2012   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C
C     BUT: CALCUL DES VECTEURS ELEMENTAIRES EN MECANIQUE
C          CORRESPONDANT A UN CHARGEMENT FORCE_FACE FONCTION
C          SUR DES FACES D'ELEMENTS ISOPARAMETRIQUES 3D
C
C          OPTION : 'CHAR_MECA_FF2D3D '
C
C     ENTREES  ---> OPTION : OPTION DE CALCUL
C          ---> NOMTE  : NOM DU TYPE ELEMENT
C.......................................................................
C
      INCLUDE 'jeveux.h'
      CHARACTER*8        NOMPAR(4)
      CHARACTER*16       NOMTE,OPTION
      REAL*8             JAC,NX,NY,NZ,SX(9,9),SY(9,9),SZ(9,9)
      REAL*8             VALPAR(4)
      INTEGER            IPOIDS,IVF,IDFDX,IDFDY,IGEOM,ITEMPS
      INTEGER            NDIM,NNO,IPG,NPG1,IFORC,NNOS
      INTEGER            IDEC,JDEC,KDEC,LDEC,JGANO
C
      CALL ELREF4(' ','RIGI',NDIM,NNO,NNOS,NPG1,IPOIDS,IVF,IDFDX,JGANO)
      IDFDY  = IDFDX  + 1
C
      CALL JEVECH('PGEOMER','L',IGEOM)
      CALL JEVECH('PFF2D3D','L',IFORC)
      CALL JEVECH('PTEMPSR','L',ITEMPS)
      CALL JEVECH('PVECTUR','E',IRES)
C
      VALPAR(4) = ZR(ITEMPS)
      NOMPAR(1) = 'X'
      NOMPAR(2) = 'Y'
      NOMPAR(3) = 'Z'
      NOMPAR(4) = 'INST'
C
      DO 10 I = 1,3*NNO
         ZR(IRES+I-1) = 0.0D0
10    CONTINUE
C
C    CALCUL DES PRODUITS VECTORIELS OMI X OMJ
C
      DO 1 INO = 1,NNO
        I = IGEOM + 3*(INO-1) -1
          DO 2 JNO = 1,NNO
            J = IGEOM + 3*(JNO-1) -1
              SX(INO,JNO) = ZR(I+2) * ZR(J+3) - ZR(I+3) * ZR(J+2)
              SY(INO,JNO) = ZR(I+3) * ZR(J+1) - ZR(I+1) * ZR(J+3)
              SZ(INO,JNO) = ZR(I+1) * ZR(J+2) - ZR(I+2) * ZR(J+1)
2         CONTINUE
1     CONTINUE
C
C    BOUCLE SUR LES POINTS DE GAUSS
C
      DO 101 IPG=1,NPG1
        KDEC = (IPG-1)*NNO*NDIM
        LDEC = (IPG-1)*NNO
C
C    CALCUL DE FX,FY,FZ
C
            XX = 0.D0
            YY = 0.D0
            ZZ = 0.D0
            DO 110 I = 1,NNO
               XX = XX + ZR(IGEOM+3*(I-1)) * ZR(IVF+LDEC+I-1)
               YY = YY + ZR(IGEOM+3*I-2) * ZR(IVF+LDEC+I-1)
               ZZ = ZZ + ZR(IGEOM+3*I-1) * ZR(IVF+LDEC+I-1)
 110       CONTINUE
            VALPAR(1) = XX
            VALPAR(2) = YY
            VALPAR(3) = ZZ
            CALL FOINTE('FM',ZK8(IFORC)  ,4,NOMPAR,VALPAR,FX,IER)
            CALL FOINTE('FM',ZK8(IFORC+1),4,NOMPAR,VALPAR,FY,IER)
            CALL FOINTE('FM',ZK8(IFORC+2),4,NOMPAR,VALPAR,FZ,IER)
        NX = 0.0D0
        NY = 0.0D0
        NZ = 0.0D0
C
C   CALCUL DE LA NORMALE AU POINT DE GAUSS IPG
C
        DO 102 I=1,NNO
        IDEC = (I-1)*NDIM
          DO 102 J=1,NNO
          JDEC = (J-1)*NDIM
C
          NX = NX + ZR(IDFDX+KDEC+IDEC) * ZR(IDFDY+KDEC+JDEC) * SX(I,J)
          NY = NY + ZR(IDFDX+KDEC+IDEC) * ZR(IDFDY+KDEC+JDEC) * SY(I,J)
          NZ = NZ + ZR(IDFDX+KDEC+IDEC) * ZR(IDFDY+KDEC+JDEC) * SZ(I,J)
C
102        CONTINUE
C
C   LE JACOBIEN EST EGAL A LA NORME DE LA NORMALE
C
        JAC = SQRT (NX*NX + NY*NY + NZ*NZ)
C
        DO 103 I=1,NNO
          II = 3 * (I-1) -1
C
          ZR(IRES+II+1) = ZR(IRES+II+1) +
     &    ZR(IPOIDS+IPG-1) * FX * ZR(IVF+LDEC+I-1) * JAC
C
          ZR(IRES+II+2) = ZR(IRES+II+2) +
     &    ZR(IPOIDS+IPG-1) * FY * ZR(IVF+LDEC+I-1) * JAC
C
          ZR(IRES+II+3) = ZR(IRES+II+3) +
     &    ZR(IPOIDS+IPG-1) * FZ * ZR(IVF+LDEC+I-1) * JAC
C
103     CONTINUE
C
101   CONTINUE
      END
