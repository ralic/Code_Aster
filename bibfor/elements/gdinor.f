      SUBROUTINE GDINOR(NORM,LOBJ2,IADNUM,COORN,IN2)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 13/06/2012   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
      IMPLICIT REAL*8 (A-H,O-Z)
C
C     ------------------------------------------------------------------
C
C      FONCTION REALISEE:
C
C      CALCUL DE LA DIRECTION DU CHAMP THETA DANS LE CAS OU LA NORMALE
C      AU PLAN DES LEVRES FIGURE DANS LA SD FOND_FISS (OPTION NORMALE
C      DE DEFI_FOND_FISS)
C
C     ------------------------------------------------------------------
C
C ENTREE:
C        NORM   : NORMALE AU PLAN DES LEVRES DE LA FISSURE
C        LOBJ2  : NOMBRE DE NOEUDS DE GAMM0
C        ZI(IADNUM): NUMEROS DES NOEUDS DU FOND DE FISSURE
C        COORN  : NOM DE L'OBJET CONTENANT LES COORDONNEES DU MAILLAGE
C
C SORTIE:
C        ZR(IN2): NORMALE UNITAIRE EN TOUT NOEUD DE GAMMA0
C     ------------------------------------------------------------------
C
      INCLUDE 'jeveux.h'
      CHARACTER*24      NORM,COORN
C
      REAL*8            DIR1X,DIR1Y,DIR1Z
      REAL*8            DIR2X,DIR2Y,DIR2Z
      REAL*8            DIR11X,DIR11Y,DIR11Z
      REAL*8            DIRMOX,DIRMOY,DIRMOZ
      REAL*8            NORME,NX,NY,NZ
C
C
C
      CALL JEMARQ()
      CALL JEVEUO(NORM,'L',IANORM)
      NX = ZR(IANORM)
      NY = ZR(IANORM+1)
      NZ = ZR(IANORM+2)
C
      CALL JEVEUO(COORN,'L',IACOOR)
      DIR1X = 0.D0
      DIR1Y = 0.D0
      DIR1Z = 0.D0
C
C    BOUCLE SUR LES NOEUDS ORDONNES DU FOND DE FISSURE
C
      DO 1 I=1,LOBJ2-1
        DIR2X = DIR1X
        DIR2Y = DIR1Y
        DIR2Z = DIR1Z
        NUM1 = ZI(IADNUM+I-1)
        NUM2 = ZI(IADNUM+I)
        X1 = ZR(IACOOR+(NUM1-1)*3  )
        Y1 = ZR(IACOOR+(NUM1-1)*3+1)
        Z1 = ZR(IACOOR+(NUM1-1)*3+2)
        X2 = ZR(IACOOR+(NUM2-1)*3  )
        Y2 = ZR(IACOOR+(NUM2-1)*3+1)
        Z2 = ZR(IACOOR+(NUM2-1)*3+2)
        X21 = X2-X1
        Y21 = Y2-Y1
        Z21 = Z2-Z1
C
C    CALCUL DU PRODUIT VECTORIEL : U VECT N , OU U EST LE VECTEUR ARETE
C
        DIR1X = Y21*NZ - Z21*NY
        DIR1Y = Z21*NX - X21*NZ
        DIR1Z = X21*NY - Y21*NX
        IF(I.NE.1) THEN
C
C    MOYENNAGE DES 2 DIRECTIONS POUR UN NOEUD APPARTENANT A 2 ARETES
C
          DIRMOX = (DIR1X+DIR2X)/2.D0
          DIRMOY = (DIR1Y+DIR2Y)/2.D0
          DIRMOZ = (DIR1Z+DIR2Z)/2.D0
        ELSE
          DIRMOX = DIR1X
          DIRMOY = DIR1Y
          DIRMOZ = DIR1Z
          DIR11X = DIR1X
          DIR11Y = DIR1Y
          DIR11Z = DIR1Z
        ENDIF
        NORME = SQRT(DIRMOX*DIRMOX+DIRMOY*DIRMOY+DIRMOZ*DIRMOZ)
        ZR(IN2+3*(I-1)  ) = DIRMOX/NORME
        ZR(IN2+3*(I-1)+1) = DIRMOY/NORME
        ZR(IN2+3*(I-1)+2) = DIRMOZ/NORME
1     CONTINUE
C
C    TRAITEMENT DU DERNIER NOEUD
C    PAS DE MOYENNE SI DERNIER NOEUD DIFFERENT DU PREMIER
C    SINON COURBE FERMEE : MOYENNE
C
        IF (ZI(IADNUM+LOBJ2-1).EQ.ZI(IADNUM+1-1)) THEN
          DIRMOX = (DIR1X+DIR11X)/2.D0
          DIRMOY = (DIR1Y+DIR11Y)/2.D0
          DIRMOZ = (DIR1Z+DIR11Z)/2.D0
          NORME = SQRT(DIRMOX*DIRMOX+DIRMOY*DIRMOY+DIRMOZ*DIRMOZ)
          ZR(IN2+3*(LOBJ2-1)  ) = DIRMOX/NORME
          ZR(IN2+3*(LOBJ2-1)+1) = DIRMOY/NORME
          ZR(IN2+3*(LOBJ2-1)+2) = DIRMOZ/NORME
          ZR(IN2+3*(1-1)      ) = DIRMOX/NORME
          ZR(IN2+3*(1-1)    +1) = DIRMOY/NORME
          ZR(IN2+3*(1-1)    +2) = DIRMOZ/NORME
        ELSE
          NORME = SQRT(DIR1X*DIR1X+DIR1Y*DIR1Y+DIR1Z*DIR1Z)
          ZR(IN2+3*(LOBJ2-1)  ) = DIR1X/NORME
          ZR(IN2+3*(LOBJ2-1)+1) = DIR1Y/NORME
          ZR(IN2+3*(LOBJ2-1)+2) = DIR1Z/NORME
        ENDIF
C
      CALL JEDEMA()
      END
