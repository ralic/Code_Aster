      SUBROUTINE PMFCOM(OPTION,COMPOR,NF,TREF,TEMPM,TEMPP,E,ALPHA,
     +ICDMAT,NBVALC,VARIM,VARIMP,CONTM,DEFM,DEFP,MODF,SIGF,VARIP,CODRET,
     + EPSM,CORRM,CORRP) 
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 25/03/2004   AUTEUR OUGLOVA A.OUGLOVA 
C TOLE CRP_21
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
      IMPLICIT NONE
      CHARACTER*16 OPTION,COMPOR(*),COMPO
      INTEGER NF,ICDMAT,NBVALC
      REAL*8 E,VARIM(NBVALC*NF),CONTM(NF),DEFM(NF),DEFP(NF),MODF(NF),
     +       SIGF(NF),VARIMP(NBVALC*NF),VARIP(NBVALC*NF),CORRM
      REAL*8 TEMPM,TEMPP,ALPHA,DSIDEP,TREF,SIGX,SIGXP,EPSX,DEPSX,CORRP
C     ------------------------------------------------------------------
C     AIGUILLAGE COMPORTEMENT DES ELEMENTS DE POUTRE MULTIFIBRES

C IN  COMPO : NOM DU COMPORTEMENT
C IN  NF    : NOMBRE DE FIBRES
C IN  E     : MODULE D'YOUNG (ELASTIQUE)
C IN  ICDMAT : CODE MATERIAU
C IN  NV    : NOMBRE DE VARIABLES INTERNES DU MODELE
C IN  VARIM : VARIABLES INTERNES MOINS
C IN  VARIMP: VARIABLES INTERNES ITERATION PRECEDENTE (POUR DEBORST)
C IN  CONTM : CONTRAINTES MOINS (ATTENTION POUR L'INSTANT EFGEGA ET
C             PAS PAR FIBR
C IN  DEFM  : DEFORMATION MOINS
C IN  DEFP  : INCREMENT DE DEFORMATION
C IN  CORRM  : CORROSION A L'INSTANT MOINS
C IN  CORRP  : CORROSION A L'INSTANT PLUS
C IN  EPSM : DEFORMATION A L'INST M
C OUT MODF  : MODULE TANGENT DES FIBRES
C OUT SIGF  : CONTRAINTE PLUS DES FIBRES
C OUT VARIP : VARIABLES INTERNES PLUS
C     ------------------------------------------------------------------

      INTEGER NBVAL,NBPAR,NBRES,NBVARI,CODREF
      PARAMETER (NBVAL=12)
      REAL*8 VALPAR,VALRES(NBVAL),ALPHAM,ALPHAP,EP,EM
      CHARACTER*2 ARRET,RETOUR,CODRES(NBVAL)
      CHARACTER*8 NOMPAR,NOECLB(9),NOMPIM(12)
      REAL*8 A1,A2,BETA1,BETA2,Y01,Y02,B1,B2,SIGF1
      REAL*8 CSTPM(13),EPSM
      INTEGER I,IVARI,CODRET
      DATA ARRET,RETOUR/'FM','  '/
      DATA NOECLB/'Y01','Y02','A1','A2','B1','B2','BETA1','BETA2',
     +     'SIGF'/
      DATA NOMPIM/'SY','EPSI_ULTM','SIGM_ULTM','EPSP_HARD','R_PM',
     +     'EP_SUR_E','A1_PM','A2_PM','ELAN','A6_PM','C_PM','A_PM'/
     
      COMPO=COMPOR(1)
      IF (COMPO.EQ.'ELAS') THEN
        DO 10 I = 1,NF
          MODF(I) = E
          SIGF(I) = E* (DEFM(I)+DEFP(I))
   10   CONTINUE
      ELSE IF (COMPO.EQ.'LABORD_1D') THEN
C ---   ON RECUPERE LES PARAMETRES MATERIAU
        CALL R8INIR(NBVAL,0.D0,VALRES,1)
        NBPAR = 0
        NOMPAR = '  '
        VALPAR = 0.D0
        NBRES = 9
        CALL RCVALA(ICDMAT,'LABORD_1D',NBPAR,NOMPAR,VALPAR,NBRES,
     +              NOECLB,VALRES,CODRES,ARRET)
        Y01 = VALRES(1)
        Y02 = VALRES(2)
        A1 = VALRES(3)
        A2 = VALRES(4)
        B1 = VALRES(5)
        B2 = VALRES(6)
        BETA1 = VALRES(7)
        BETA2 = VALRES(8)
        SIGF1 = VALRES(9)
C ---   BOUCLE COMPORTEMENT SUR CHAQUE FIBRE
        DO 20 I = 1,NF
          IVARI = NBVALC* (I-1) + 1
          CALL NMCB1D(E,Y01,Y02,A1,A2,B1,B2,BETA1,BETA2,SIGF1,ALPHA,
     +                TREF,TEMPP,CONTM(I),VARIM(IVARI),DEFM(I),
     +                DEFP(I),MODF(I),SIGF(I),VARIP(IVARI))
   20   CONTINUE
      ELSE IF (COMPO.EQ.'PINTO_MENEGOTTO') THEN
        NBRES = 12
        CALL R8INIR(NBVAL,0.D0,VALRES,1)
        NBPAR = 0
        NOMPAR = '  '
        VALPAR = 0.D0
        CALL RCVALA(ICDMAT,'PINTO_MENEGOTTO',NBPAR,NOMPAR,VALPAR,NBRES,
     +              NOMPIM,VALRES,CODRES,RETOUR)
        IF (CODRES(7).NE.'OK') VALRES(7) = -1.D0
        CSTPM(1) = E
        DO 30 I = 1,12
          CSTPM(I+1) = VALRES(I)
   30   CONTINUE
        DO 40 I = 1,NF
          IVARI = NBVALC* (I-1) + 1
          CALL NM1DPM(OPTION,NBVALC,ALPHA,TEMPM,13,CSTPM,CONTM(I),
     +                VARIM(IVARI),TEMPP,DEFP(I),VARIP(IVARI),SIGF(I),
     +                MODF(I))
   40   CONTINUE

      ELSE IF (COMPO.EQ.'VMIS_ISOT_LINE' .OR.
     +         (COMPO.EQ.'VMIS_CINE_LINE') .OR.
     +           (COMPO.EQ.'CORR_ACIER' )) THEN
C        NBRES = 2
C        CALL R8INIR(NBVAL,0.D0,VALRES,1)
C        NBPAR = 0
C        NOMPAR = '  '
C        VALPAR = 0.D0
C     CALL RCVALA(ICDMAT,'ECRO_LINE',NBPAR,NOMPAR,VALPAR,NBRES,NOMMIS,
C     +              VALRES,CODRES,ARRET)
C        ET = VALRES(1)
C        SY = VALRES(2)
      NBPAR = 1
      NOMPAR = 'TEMP'

C --- CARACTERISTIQUES ELASTIQUES A TMOINS

      VALPAR = TEMPM
      CALL RCVALA(ICDMAT,'ELAS',NBPAR,NOMPAR,VALPAR,1,'E',EM,CODRES,
     & ARRET)
      CALL RCVALA(ICDMAT,'ELAS',NBPAR,NOMPAR,VALPAR,1,'ALPHA',ALPHAM,
     &            CODRES,RETOUR)
      IF (CODRES(1).NE.'OK') ALPHAM = 0.D0

C --- CARACTERISTIQUES ELASTIQUES A TPLUS

      VALPAR = TEMPP
      CALL RCVALA(ICDMAT,'ELAS',NBPAR,NOMPAR,VALPAR,1,'E',EP,CODRES,
     &            ARRET)
      CALL RCVALA(ICDMAT,'ELAS',NBPAR,NOMPAR,VALPAR,1,'ALPHA',ALPHAP,
     &            CODRES,RETOUR)
      IF (CODRES(1).NE.'OK') ALPHAP = 0.D0
        IF (COMPO.EQ.'VMIS_CINE_LINE') THEN
          DO 50 I = 1,NF
            IVARI = NBVALC* (I-1) + 1
C            CALL NM1DCI(TEMPM,TEMPP,E,ET,ALPHA,SY,CONTM(I),DEFP(I),
C     +                  VARIM(IVARI),VARIM(IVARI+1),OPTION,SIGF(I),
C     +                  VARIP(IVARI),VARIP(IVARI+1),MODFM,MODF(I))
        CALL NM1DCI(ICDMAT,TEMPM,TEMPP,TREF,EM,EP,ALPHAM,ALPHAP,CONTM(I)
     &       ,DEFP(I),VARIM(IVARI),OPTION,SIGF(I),VARIP(IVARI),MODF(I))
   50     CONTINUE
        ELSE IF (COMPO.EQ.'VMIS_ISOT_LINE') THEN
          DO 60 I = 1,NF
            IVARI = NBVALC* (I-1) + 1
C            CALL NM1DIS(TEMPM,TEMPP,E,ET,ALPHA,SY,CONTM(I),DEFP(I),
C     +                  VARIM(IVARI),VARIM(IVARI+1),OPTION,SIGF(I),
C     +                  VARIP(IVARI),VARIP(IVARI+1),MODFM,MODF(I))
        CALL NM1DIS(ICDMAT,TEMPM,TEMPP,TREF,EM,EP,ALPHAM,ALPHAP,CONTM(I)
     & ,DEFP(I),VARIM(IVARI),OPTION,COMPO,SIGF(I),VARIP(IVARI),MODF(I))
   60     CONTINUE
        ELSE IF (COMPO.EQ.'CORR_ACIER') THEN
        DO 65 I = 1,NF
            IVARI = NBVALC* (I-1) + 1
            
       CALL NM1DCO(OPTION,ICDMAT,TEMPM,TEMPP,EP,CONTM(I),EPSM,
     &       DEFP(I),VARIM(IVARI),SIGF(I),VARIP(IVARI),MODF(I),
     &       CORRM,CORRP)   
   65      CONTINUE     
        END IF
      ELSE IF (COMPO.EQ.'VMIS_ISOT_TRAC') THEN
        DO 55 I = 1,NF
            IVARI = NBVALC* (I-1) + 1
            CALL NM1TRA(ICDMAT,TEMPP,DEFM(I),DEFP(I),
     +                  VARIM(IVARI),VARIM(IVARI+1),
     +                  SIGF(I),VARIP(IVARI),VARIP(IVARI+1),MODF(I))
55      CONTINUE
      ELSE

C       APPEL A COMP1D POUR BENEFICIER DE TOUS LES COMPORTEMENTS AXIS
C       PAR UNE EXTENSION DE LA METHODE DE DEBORST

        IF ((COMPOR(5)(1:7).NE.'DEBORST').AND.
     &      (COMPOR(1)(1:4).NE.'SANS')) THEN
          CALL UTMESS('F','PMF','UTILISER ALGO_1D="DEBORST"'//
     &     ' SOUS COMP_INCR POUR LE COMPORTEMENT '//COMPO)
        ELSE
        
          IF ((OPTION(1:9).EQ.'FULL_MECA') .OR.
     &        (OPTION(1:9).EQ.'RAPH_MECA')) THEN     
               NBVARI = NBVALC*NF
               CALL R8COPY(NBVARI,VARIMP,1,VARIP,1)
          ENDIF

          DO 70 I = 1,NF
            IVARI = NBVALC* (I-1) + 1
            SIGX  = CONTM(I)
            EPSX  = DEFM(I)
            DEPSX = DEFP(I)
            CALL COMP1D(OPTION,SIGX,EPSX,DEPSX,TREF,TEMPM,TEMPP,
     &       VARIM(IVARI),VARIP(IVARI),SIGXP,DSIDEP,CODREF,CORRM,
     &       CORRP)
            IF (CODREF.NE.0) CODRET=CODREF
            SIGF(I)=SIGXP
            MODF(I)=DSIDEP
   70     CONTINUE
        END IF
      END IF

      END
