      SUBROUTINE ARLTEB(NDIM  ,NBSIGM ,NPGS  ,POIJCS,
     &                  NN1   ,DFDX1 ,DFDY1 ,DFDZ1,
     &                  NN2   ,DFDX2 ,DFDY2 ,DFDZ2,
     &                  MCPLB)
C
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 08/06/2009   AUTEUR DELMAS J.DELMAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2009  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================
C
      IMPLICIT     NONE
      INTEGER      NDIM
      INTEGER      NBSIGM
      INTEGER      NPGS
      REAL*8       POIJCS(NPGS)
      INTEGER      NN1,NN2
      REAL*8       DFDX1(NPGS*NN1),DFDY1(NPGS*NN1),DFDZ1(NPGS*NN1)
      REAL*8       DFDX2(NPGS*NN2),DFDY2(NPGS*NN2),DFDZ2(NPGS*NN2)
      REAL*8       MCPLB(NDIM*NN1,NDIM*NN2)
C
C ----------------------------------------------------------------------
C
C CALCUL DES MATRICES DE COUPLAGE ARLEQUIN
C OPTION ARLQ_MATR
C
C
C CALCUL DES INTEGRALES DE COUPLAGE ENTRE MAILLE 1 ET MAILLE 2
C
C TERMES DERIVES (B1)T.B2 (INTEGRALE SUR S)
C
C
C ----------------------------------------------------------------------
C
C
C IN  NDIM   : DIMENSION DE LA MAILLE
C IN  NBSIGM : NOMBRE DE CONTRAINTES ASSOCIEES
C IN  NPGS   : NOMBRE DE POINTS DE GAUSS DE LA MAILLE S
C IN  POIJCS : PRODUIT POIDS DE GAUSS*JACOBIEN  DE LA MAILLE S
C IN  NN1    : NOMBRE DE NOEUDS DE LA MAILLE 1
C IN  DFDX1  : DERIVEE FCT. FORME/X EN CHAQUE PT DE GAUSS DE LA MAILLE 1
C IN  DFDY1  : DERIVEE FCT. FORME/Y EN CHAQUE PT DE GAUSS DE LA MAILLE 1
C IN  DFDZ1  : DERIVEE FCT. FORME/Z EN CHAQUE PT DE GAUSS DE LA MAILLE 1
C IN  NN2    : NOMBRE DE NOEUDS DE LA MAILLE 2
C IN  DFDX2  : DERIVEE FCT. FORME/X EN CHAQUE PT DE GAUSS DE LA MAILLE 2
C IN  DFDY2  : DERIVEE FCT. FORME/Y EN CHAQUE PT DE GAUSS DE LA MAILLE 2
C IN  DFDZ2  : DERIVEE FCT. FORME/Z EN CHAQUE PT DE GAUSS DE LA MAILLE 2
C OUT MCPLB  : MATRICE DES TERMES DE COUPLAGE (B1)T.B2
C              MATRICE RECTANGULAIRE (NDIM*NN1xNDIM*NN2)
C
C ----------------------------------------------------------------------
C
      INTEGER     KPGS
      INTEGER     NBINC1,NBINC2
      REAL*8      B1(NBSIGM,NDIM*NN1),B2(NBSIGM,NDIM*NN2)
      INTEGER     L1,L2
      INTEGER     I,J, K
      REAL*8      POIDS
C
C ----------------------------------------------------------------------
C
      NBINC1 = NN1*NDIM
      NBINC2 = NN2*NDIM
C
C --- INITIALISATIONS MATRICES B1, B2
C
      POIDS = POIJCS(1)
C
C ----- CALCUL DE LA MATRICE B1
C
      CALL BMATF1(NDIM    ,NBSIGM  ,NN1     ,
     &            DFDX1(1),DFDY1(1),DFDZ1(1),B1   )
C
C ----- CALCUL DE LA MATRICE B2
C
      CALL BMATF1(NDIM    ,NBSIGM  ,NN2     ,
     &            DFDX2(1),DFDY2(1),DFDZ2(1),B2   )
C
C ----- CALCUL DU TERME DE COUPLAGE : (B1)T * B2
C
      DO 10 J = 1, NBINC2
         DO 20 I = 1, NBINC1
            MCPLB(I,J) = POIDS*B1(1,I)*B2(1,J)
            DO 30 K = 2, NBSIGM
               MCPLB(I,J) = MCPLB(I,J)+POIDS*B1(K,I)*B2(K,J)
 30         CONTINUE
 20      CONTINUE
 10   CONTINUE
C
C --- CALCUL DES TERMES DE COUPLAGE
C
      DO 99 KPGS = 2,NPGS
C
        L1    = NN1*(KPGS-1)+1
        L2    = NN2*(KPGS-1)+1

        POIDS = POIJCS(KPGS)
C
C ----- CALCUL DE LA MATRICE B1
C
        CALL BMATFR(NDIM     ,NBSIGM   ,NN1      ,
     &              DFDX1(L1),DFDY1(L1),DFDZ1(L1),B1   )
C
C ----- CALCUL DE LA MATRICE B2
C
        CALL BMATFR(NDIM     ,NBSIGM   ,NN2      ,
     &              DFDX2(L2),DFDY2(L2),DFDZ2(L2),B2   )
C
C ----- CALCUL DU TERME DE COUPLAGE : (B1)T * B2
C
        DO 130 J = 1, NBINC2
           DO 140 I = 1, NBINC1
              DO 150 K = 1, NBSIGM
                 MCPLB(I,J) = MCPLB(I,J) + POIDS*B1(K,I)*B2(K,J)
 150          CONTINUE
 140       CONTINUE
 130    CONTINUE
C
 99   CONTINUE
C
      END
