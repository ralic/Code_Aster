      SUBROUTINE TE0426(OPTION,NOMTE)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 09/11/2012   AUTEUR DELMAS J.DELMAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.

C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.

C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
      IMPLICIT NONE
      INCLUDE 'jeveux.h'

      CHARACTER*16 OPTION,NOMTE
C.......................................................................

C     BUT: CALCUL DES VECTEURS ELEMENTAIRES EN MECANIQUE
C          ELEMENTS ISOPARAMETRIQUES 3D

C          OPTION : 'CHAR_MECA_EPSA_R  '

C     ENTREES  ---> OPTION : OPTION DE CALCUL
C              ---> NOMTE  : NOM DU TYPE ELEMENT
C.......................................................................

      CHARACTER*4 FAMI

      REAL*8 SIGI(162),EPSI(162),BSIGMA(81),REPERE(7)
      REAL*8 INSTAN,NHARM,XYZ(3)
      INTEGER NBSIGM,IDIM

C ---- CARACTERISTIQUES DU TYPE D'ELEMENT :
C ---- GEOMETRIE ET INTEGRATION
C      ------------------------
C-----------------------------------------------------------------------
      INTEGER I ,IDFDE ,IGAU ,IGEOM ,IMATE ,IPOIDS ,IRET
      INTEGER ITEMPS ,IVECTU ,IVF ,JGANO ,NBSIG ,NDIM ,NNO
      INTEGER NNOS ,NPG1
      REAL*8 ZERO
C-----------------------------------------------------------------------
      FAMI = 'RIGI'
      CALL ELREF4(' ',FAMI,NDIM,NNO,NNOS,NPG1,IPOIDS,IVF,IDFDE,JGANO)

C ---- NOMBRE DE CONTRAINTES ASSOCIE A L'ELEMENT
C      -----------------------------------------
      NBSIG = NBSIGM()

C --- INITIALISATIONS :
C     -----------------
      ZERO = 0.0D0
      INSTAN = ZERO
      NHARM = ZERO

      DO 10 I = 1,NBSIG*NPG1
        EPSI(I) = ZERO
        SIGI(I) = ZERO
   10 CONTINUE

      DO 20 I = 1,NDIM*NNO
        BSIGMA(I) = ZERO
   20 CONTINUE

C ---- RECUPERATION DES COORDONNEES DES CONNECTIVITES
C      ----------------------------------------------
      CALL JEVECH('PGEOMER','L',IGEOM)

C ---- RECUPERATION DU MATERIAU
C      ------------------------
      CALL JEVECH('PMATERC','L',IMATE)

C ---- RECUPERATION  DES DONNEEES RELATIVES AU REPERE D'ORTHOTROPIE
C      ------------------------------------------------------------
C     COORDONNEES DU BARYCENTRE ( POUR LE REPRE CYLINDRIQUE )

      XYZ(1) = 0.D0
      XYZ(2) = 0.D0
      XYZ(3) = 0.D0
      DO 150 I = 1,NNO
        DO 140 IDIM = 1,NDIM
          XYZ(IDIM) = XYZ(IDIM)+ZR(IGEOM+IDIM+NDIM*(I-1)-1)/NNO
 140    CONTINUE
 150  CONTINUE
      CALL ORTREP(ZI(IMATE),NDIM,XYZ,REPERE)

C ---- RECUPERATION DE L'INSTANT
C      -------------------------
      CALL TECACH('ONN','PTEMPSR',1,ITEMPS,IRET)
      IF (ITEMPS.NE.0) INSTAN = ZR(ITEMPS)


C ---- CONSTRUCTION DU VECTEUR DES DEFORMATIONS ANELASTIQUES DEFINIES
C ---- AUX POINTS D'INTEGRATION A PARTIR DES DONNEES UTILISATEUR
C      --------------------------------------------------------------

      DO 30 IGAU=1,NPG1
        CALL RCVARC(' ','EPSAXX','+','RIGI',IGAU,1,
     &              EPSI(NBSIG*(IGAU-1)+1),IRET)
        IF (IRET.EQ.1) EPSI(NBSIG*(IGAU-1)+1)=0.D0
        CALL RCVARC(' ','EPSAYY','+','RIGI',IGAU,1,
     &              EPSI(NBSIG*(IGAU-1)+2),IRET)
        IF (IRET.EQ.1) EPSI(NBSIG*(IGAU-1)+2)=0.D0
        CALL RCVARC(' ','EPSAZZ','+','RIGI',IGAU,1,
     &              EPSI(NBSIG*(IGAU-1)+3),IRET)
        IF (IRET.EQ.1) EPSI(NBSIG*(IGAU-1)+3)=0.D0
        CALL RCVARC(' ','EPSAXY','+','RIGI',IGAU,1,
     &              EPSI(NBSIG*(IGAU-1)+4),IRET)
        IF (IRET.EQ.1) EPSI(NBSIG*(IGAU-1)+4)=0.D0
        CALL RCVARC(' ','EPSAXZ','+','RIGI',IGAU,1,
     &              EPSI(NBSIG*(IGAU-1)+5),IRET)
        IF (IRET.EQ.1) EPSI(NBSIG*(IGAU-1)+5)=0.D0
        CALL RCVARC(' ','EPSAYZ','+','RIGI',IGAU,1,
     &              EPSI(NBSIG*(IGAU-1)+6),IRET)
        IF (IRET.EQ.1) EPSI(NBSIG*(IGAU-1)+6)=0.D0
 30   CONTINUE

C ---- CALCUL DU VECTEUR DES CONTRAINTES ANELASTIQUES AUX POINTS
C ---- D'INTEGRATION
C      -------------
      CALL SIGIMC(FAMI,NNO,NDIM,NBSIG,NPG1,ZR(IVF),ZR(IGEOM),
     &            INSTAN,ZI(IMATE),REPERE,EPSI,SIGI)

C ---- CALCUL DU VECTEUR DES FORCES DUES AUX CONTRAINTES ANELASTIQUES
C ---- (I.E. BT*SIG_ANELASTIQUES)
C      ----------------------
      CALL BSIGMC (NNO,NDIM,NBSIG,NPG1,IPOIDS,IVF,IDFDE,
     +              ZR(IGEOM), NHARM, SIGI, BSIGMA )

C ---- RECUPERATION ET AFFECTATION DU VECTEUR EN SORTIE AVEC LE
C ---- VECTEUR DES FORCES DUES AUX CONTRAINTES ANELASTIQUES
C      -------------------------------------------------
      CALL JEVECH('PVECTUR','E',IVECTU)

      DO 40 I = 1,NDIM*NNO
        ZR(IVECTU+I-1) = BSIGMA(I)
   40 CONTINUE

C FIN ------------------------------------------------------------------
      END
