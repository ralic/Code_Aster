      SUBROUTINE BCOUDE (IGAU,ICOU,ISECT,L,H,A,M,OMEGA,
     &                      NPG,NNO,NCOU,NSECT,FF,MMT,B)
      IMPLICIT REAL*8 (A-H,O-Z)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 24/01/2000   AUTEUR JMBHH01 J.M.PROIX 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
      REAL*8             L,H,A,B(4,*),FF(*),OMEGA
      INTEGER            NPG,NNO,NCOU,NSECT,M,IGAU,ICOU,ISECT,MMT

C ......................................................................
C
C   BUT      : CALCUL DE LA MATRICE DE DEFORMATION B POUR LES ELEMENTS
C              TUYAU
C
C   ENTREES : <----
C           IGAU,ICOU,ISECT : LES INDICES DES POINTS D'INTEGRATION
C                RESPECTIVEMENT SUR LA LONGUEUR, DANS L'EPAISSEUR ET
C                SUR LA CIRCONFERENCE
C           NNO,NPG : NBRES DE NOEUDS, NBRES DE POINTS DE GAUSS
C           NCOU,NSECT : NBRES DE COUCHES, NBRES DE SECTEURS
C           FF : TABLEAU DES FONCTIONS DE FORMES
C           M : NBRES DE MODES DE FOURIER
C           L : LONGEUR DE L'ELEMENT
C           H : SON EPAISSEUR
C           A : SON RAYON MOYEN
C   SORTIES : ---->
C           B : LA MATRICE DE DEFORMATIONS
C ......................................................................
C
      REAL*8             PI,DEUXPI,COSFI,SINFI,COSMFI,SINMFI,R8PI
      REAL*8             ZETA,R,HK,DHK,D2HK,FI
      INTEGER            K,ICOLON,N,IBLOC
C
      PI = R8PI()
      DEUXPI=2.D0*PI
C
      ZETA   = (ICOU-1)*H/(2.D0*NCOU)-H/2.D0
      FI     = (ISECT-1)*DEUXPI/(2.D0*NSECT)
      COSFI  = COS(FI)
      SINFI  = SIN(FI)
      IF (MMT.EQ.0) THEN
         R = A
      ELSE
         R = A + ZETA
      ENDIF 
C
C  REMLISSAGE DE LA MATRICE
C
      DO  10  K=1, NNO
C
      HK   = FF(NNO*(IGAU-1)+K)
      DHK  = FF(NNO*NPG+NNO*(IGAU-1)+K) * (2.D0/L)
      D2HK = FF(2*NNO*NPG+NNO*(IGAU-1)+K) * (2.D0/L)*(2.D0/L)
CC      D2HK = FF(2*NPG*NNO+K)*(2.D0/L)*(2.D0/L)
C
C LE 2/L EST DU AU PASSAGE DE L'ELEMENT DE REFERENCE A L'ELEMENT
C REEL
C
      IBLOC = (9+6*(M-1))*(K-1)
C
C  PARTIE POUTRE
C
C   1 ERE LIGNE
C
      B(1,IBLOC+1)=  DHK
      B(1,IBLOC+2)=  0.D0
      B(1,IBLOC+3)=  0.D0
      B(1,IBLOC+4)=  0.D0
      B(1,IBLOC+5)= -R*COSFI*DHK
      B(1,IBLOC+6)=  R*SINFI*DHK
C
C   2 EME LIGNE
C
      B(2,IBLOC+1)=  0.D0
      B(2,IBLOC+2)=  0.D0
      B(2,IBLOC+3)=  0.D0
      B(2,IBLOC+4)=  0.D0
      B(2,IBLOC+5)=  0.D0
      B(2,IBLOC+6)=  0.D0
C
C   3 EME LIGNE
C
      B(3,IBLOC+1)=  0.D0
      B(3,IBLOC+2)= -COSFI*DHK
      B(3,IBLOC+3)=  SINFI*DHK
      B(3,IBLOC+4)= -R*DHK
      B(3,IBLOC+5)=  SINFI*HK
      B(3,IBLOC+6)=  COSFI*HK
C
C   4 EME LIGNE
C
      B(4,IBLOC+1)=  0.D0
      B(4,IBLOC+2)= -SINFI*DHK
      B(4,IBLOC+3)= -COSFI*DHK
      B(4,IBLOC+4)=  0.D0
      B(4,IBLOC+5)= -COSFI*HK
      B(4,IBLOC+6)=  SINFI*HK
C
C  FIN PARTIE POUTRE ET DEBUT PARTIE SUPPLEMENTAIRE
C
C      PARTIE IN-PLANE
C
          DO 20 N=2,M
          ICOLON = IBLOC + 6 + 3*(N-2)
          COSMFI = COS(N*FI)
          SINMFI = SIN(N*FI)
C
          B(1,ICOLON+1)=  DHK*COSMFI
          B(1,ICOLON+2)=  0.D0
          B(1,ICOLON+3)=  -ZETA*D2HK*COSMFI
C
          B(2,ICOLON+1)=  0.D0
          B(2,ICOLON+2)=  (N/R)*HK*COSMFI*(1.D0+ZETA/A)
          B(2,ICOLON+3)=  (1.D0/R)*HK*COSMFI*(1.D0+ZETA*N*N/A)
C
C
          B(3,ICOLON+1)=  -(N/R)*HK*SINMFI
          B(3,ICOLON+2)=  DHK*SINMFI*(1+ZETA/A)
          B(3,ICOLON+3)=  ZETA*N*DHK*SINMFI*(1.D0/A+1.D0/R)
C
C
          B(4,ICOLON+1)=  0.D0
          B(4,ICOLON+2)=  0.D0
          B(4,ICOLON+3)=  0.D0
C
C
20        CONTINUE
C
C  FIN PARTIE IN-PLANE DEBUT PARTIE OUT-OF-PLANE
C

          DO 30 N=2,M
          ICOLON = IBLOC + 6 + 3*(M-1) + 3*(N-2)
          COSMFI = COS(N*FI)
          SINMFI = SIN(N*FI)
C
          B(1,ICOLON+1)=  DHK*SINMFI
          B(1,ICOLON+2)=  0.D0
          B(1,ICOLON+3)=  -ZETA*D2HK*SINMFI
C
          B(2,ICOLON+1)=  0.D0
          B(2,ICOLON+2)=  -(N/R)*HK*SINMFI*(1.D0+ZETA/A)
          B(2,ICOLON+3)=  (1.D0/R)*HK*SINMFI*(1.D0+ZETA*N*N/A)
C
C
          B(3,ICOLON+1)=  (N/R)*HK*COSMFI
          B(3,ICOLON+2)=  DHK*COSMFI*(1.D0+ZETA/A)
          B(3,ICOLON+3)=  -ZETA*N*DHK*COSMFI*(1.D0/A+1.D0/R)
C
C
          B(4,ICOLON+1)=  0.D0
          B(4,ICOLON+2)=  0.D0
          B(4,ICOLON+3)=  0.D0
C
C
30        CONTINUE
C
C  FIN OUT-OF-PLANE DEBUT PARTIE GONFLEMENT
C
          ICOLON = IBLOC + 6*(M-1) + 6
          B(1,ICOLON+1)=  -ZETA*D2HK
          B(2,ICOLON+1)=  HK/R
          B(3,ICOLON+1)=  0.D0
          B(4,ICOLON+1)=  0.D0
C
C  FIN PARTIE GONFLEMENT
C
C  1ERS MODES EN W
C
          B(1,ICOLON+2)=  -ZETA*D2HK*COSFI
          B(2,ICOLON+2)=  (2.D0/R)*HK*COSFI*(1.D0+ZETA/A)
          B(3,ICOLON+2)=  DHK*SINFI*(1.D0+2.D0*ZETA/A+ZETA/R)
          B(4,ICOLON+2)=  0.D0
C
          B(1,ICOLON+3)=  -ZETA*D2HK*SINFI
          B(2,ICOLON+3)=  (2.D0/R)*HK*SINFI*(1.D0+ZETA/A)
          B(3,ICOLON+3)=  -DHK*COSFI*(1.D0+2.D0*ZETA/A+ZETA/R)
          B(4,ICOLON+3)=  0.D0
C
C
C  FIN PARTIE GONFLEMENT
C
C FIN DES 1ERS MODES EN W
C
10    CONTINUE
C
C
C FIN REMPLISSAGE DE LA MATRICE
C
C
9999  CONTINUE
      END
