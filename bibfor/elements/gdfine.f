      SUBROUTINE GDFINE (KP,NNO,PJACOB,EN,GRANI,ROT0,ROTK,
     &                   OMGK,OMPGK,   FINT)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 08/03/2004   AUTEUR REZETTE C.REZETTE 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C
C FONCTION: POUR UN ELEMENT DE POUTRE EN GRAND DEPLACEMENT, CALCULE LA
C           CONTRIBUTION DU POINT DE GAUSS NUMERO KP A LA PARTIE ROTA-
C           TOIRE DES FORCES D'INERTIE. CES FORCES D'INERTIE SONT RE-
C           TRANCHEES DES FORCES INTERNES.
C
C     IN  : KP        : NUMERO DU POINT DE GAUSS
C           NNO       : NOMBRE DE NOEUDS DE L'ELEMENT
C           PJACOB    : POIDS * JACOBIEN
C           EN        : FONCTIONS DE FORME
C           GRANI     : DIAGONALE DU TENSEUR D'INERTIE EN AXES LOCAUX
C                       POUR LES 3 1ERES COMPOSANTES, RHO*A POUR LA 4EME
C           ROT0      : MATRICE DE ROTATION DES AXES PRINCIPAUX D'INERT.
C                       AU POINT DE GAUSS DANS LA POSITION DE REFERENCE,
C                       PAR RAPPORT AUX AXES GENERAUX
C           ROTK      : MATRICE DE ROTATION ACTUELLE
C           OMGK      : VITESSE ANGULAIRE ACTUELLE
C           OMPGK     : ACCELERATION ANGULAIRE ACTUELLE
C
C     OUT : FINT      : FORCES INT. (CUMUL DES CONTRIB. DES PTS DE GAUS)
C ------------------------------------------------------------------
      IMPLICIT REAL*8(A-H,O-Z)
      REAL*8 IROTT(3,3)
      REAL*8 EN(3,2),GRANI(4),ROT0(3,3),ROTK(3,3),OMGK(3),
     &OMPGK(3),FINT(6,3),ROTABS(3,3),OMEGAT(3,3),AMATI(3,3),AMAT1(3,3),
     &AMAT2(3,3),FORS(6),V1(3),V2(3),V3(3)
C
      UN   = 1.D0
C
      CALL PROMAT (ROTK,3,3,3,ROT0,3,3,3,   ROTABS)
C
      DO 52 J=1,3
      DO 51 I=1,3
      IROTT(I,J) = GRANI(I) * ROTABS(J,I)
   51 CONTINUE
   52 CONTINUE
      CALL PROMAT (ROTABS,3,3,3,IROTT,3,3,3,     AMATI)
      CALL PROMAT (AMATI,3,3,3,OMGK,3,3,1,   V1)
C
      CALL ANTISY (OMGK,UN,   OMEGAT)
C
      CALL PROMAT (OMEGAT,3,3,3,V1,3,3,1,   V2)
C
      CALL PROMAT (AMATI,3,3,3,OMPGK,3,3,1,   V1)
C
C* ON CALCULE CE QU'APPORTE CHDYNL A LA PARTIE ROTATOIRE POUR LE
C* RETRANCHER ICI.
      DO 2 J=1,3
      DO 1 I=1,3
      AMAT1(I,J) = GRANI(I) * ROT0(J,I)
    1 CONTINUE
    2 CONTINUE
      CALL PROMAT (ROT0 ,3,3,3,AMAT1,3,3,3,   AMAT2)
      CALL PROMAT (AMAT2,3,3,3,OMPGK,3,3,1,   V3   )
C
      DO 4 I=1,3
      FORS(3+I) = V1(I) + V2(I) - V3(I)
    4 CONTINUE
C
      DO 6 NE=1,NNO
        COEF = -PJACOB * EN(NE,KP)
        DO 5 K=4,6
C* ON RETRANCHE LES FORCES D'INERTIE DES FORCES INTERNES:
          FINT(K,NE) = FINT(K,NE) - COEF*FORS(K)
5       CONTINUE
6     CONTINUE
 9999 CONTINUE
      END
