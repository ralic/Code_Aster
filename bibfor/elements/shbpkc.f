      SUBROUTINE SHBPKC(SIGPK,SIGCA,DUSDX,NPG)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ELEMENTS  DATE 12/08/2008   AUTEUR DESROCHES X.DESROCHES 
C ======================================================================
C COPYRIGHT (C) 1991 - 2003  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY  
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY  
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR     
C (AT YOUR OPTION) ANY LATER VERSION.                                   
C                                                                       
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT   
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF            
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU      
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                              
C                                                                       
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE     
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,         
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.         
C ======================================================================

C--------------------------------------------------------
C ELEMENT SHB8-PS A.COMBESCURE, S.BAGUET INSA LYON 2003 /
C-------------------------------------------------------
      IMPLICIT REAL*8 (A-H,O-Z)
      REAL*8 SIGPK(*),SIGCA(*),SIG(6),SIG33(3,3)
      REAL*8 SIGTMP(120)
      REAL*8 DUSDX(*),F(3,3),TMPTAB(3,3),FT(3,3)
      REAL*8 CC1,CC2,CC3,DETF
      INTEGER IP,I,J,NPG,NSIG

CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC

C              TRANSFORMATION DES CONTRAINTES DE PIOLA-KIRCHHOFF
C              EN CONTRAINTES DE CAUCHY AUX 5 PTS D'INTEGRATION DU SHB8

C      SIG(CA) = 1/DET(F) * (F) * SIG(PK) * TRANSP(F)

C  ENTREE

C      DUSDX   : D(1)=DU1/DX1 ;  D(2)=DU2/DX1 ; D(3)=DU3/DX1 ;
C              : D(4)=DU1/DX2 ;  D(5)=DU2/DX2 ; D(6)=DU3/DX2 ;
C              : D(7)=DU1/DX3 ;  D(8)=DU2/DX3 ; D(9)=DU3/DX3

C      SIGPK   : CONTRAINTES AUX 5 PTS D'INTEGRATIONS
C               [SIG] = [S_11, S_22, S_33, S_12, S_23, S_13]

C  SORTIE

C      SIGCA
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      NSIG = NPG*6
      DO 100 IP = 1,NPG
CCCCCCCCCCCCCCC CALCUL DE F: PASSAGE AU PAS FINAL
        DO 20 I = 1,3
          DO 10 J = 1,3
            F(I,J) = DUSDX((I-1)*3+J+ (IP-1)*9)
   10     CONTINUE
   20   CONTINUE
        F(1,1) = F(1,1) + 1.D0
        F(2,2) = F(2,2) + 1.D0
        F(3,3) = F(3,3) + 1.D0
CCCCCCCCCCCCCCC CALCUL DE F^T
        DO 40 I = 1,3
          DO 30 J = 1,3
            FT(I,J) = F(J,I)
   30     CONTINUE
   40   CONTINUE
CCCCCCCCCCCCCCC EXTRACTION DE LA CONTRAINTE DE PK DU POINT IP
        DO 50 I = 1,6
          SIG(I) = SIGPK((IP-1)*6+I)
   50   CONTINUE
        DO 70 I = 1,3
          DO 60 J = 1,3
            SIG33(I,J) = 0.D0
   60     CONTINUE
   70   CONTINUE
        SIG33(1,1) = SIG(1)
        SIG33(2,2) = SIG(2)
        SIG33(3,3) = SIG(3)
        SIG33(1,2) = SIG(4)
        SIG33(2,3) = SIG(5)
        SIG33(1,3) = SIG(6)
CCCCCCCCCCCCCCC MULTIPLICATION POUR AVOIR LA CONTRAINTE DE CAUCHY
        CALL MULMAT(3,3,3,SIG33,FT,TMPTAB)
        CALL MULMAT(3,3,3,F,TMPTAB,SIG33)
CCCCCCCCCCCCCCC CALCUL DE DETERMINANT(F)
        CC1 = F(2,2)*F(3,3) - F(3,2)*F(2,3)
        CC2 = -F(1,2)*F(3,3) + F(3,2)*F(1,3)
        CC3 = F(1,2)*F(2,3) - F(2,2)*F(1,3)
        DETF = F(1,1)*CC1 + F(2,1)*CC2 + F(3,1)*CC3

        DO 90 I = 1,3
          DO 80 J = 1,3
            SIG33(I,J) = SIG33(I,J)/DETF
   80     CONTINUE
   90   CONTINUE
CCCCCCCCCCCCCCC SORTIE DE LA CONTRAINTE DE CAUCHY
        SIGTMP(1+ (IP-1)*6) = SIG33(1,1)
        SIGTMP(2+ (IP-1)*6) = SIG33(2,2)
        SIGTMP(3+ (IP-1)*6) = SIG33(3,3)
        SIGTMP(4+ (IP-1)*6) = SIG33(1,2)
        SIGTMP(5+ (IP-1)*6) = SIG33(2,3)
        SIGTMP(6+ (IP-1)*6) = SIG33(1,3)
  100 CONTINUE
CCCCCCCCCCCCCCC ON SORT LES RESULTATS
      DO 110 I = 1,NSIG
        SIGCA(I) = SIGTMP(I)
  110 CONTINUE
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      END
