      SUBROUTINE ASMATR(NBMAT,TLIMAT,LICOEF,NU,SOLVEU,INFCHA,MOTCLE,
     &                  BASE,TYPE,MATAS)
C MODIF ASSEMBLA  DATE 11/07/2005   AUTEUR LAMARCHE S.LAMARCHE 
C RESPONSABLE VABHHTS J.PELLET
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
      IMPLICIT NONE
      CHARACTER*(*) BASE,MATAS,TLIMAT(*),LICOEF,NU
      CHARACTER*8 TLIMA2(150)
      CHARACTER*(*) SOLVEU,INFCHA
      INTEGER NBMAT,TYPE,NBMAT2
      CHARACTER*4 MOTCLE
      CHARACTER*19 SOLVE2,INFCH2
      INTEGER ILICOE,I,ISLVK,INDSYM,IRET,IBID,IDBGAV,ILIMAT,IER
      INTEGER IDLRE2,NBRESU,IRESU,K
      LOGICAL INDSUI,INDMAS
C-----------------------------------------------------------------------

C --- DESCRIPTION DES PARAMETRES

C IN  I   NBMAT2  : NOMBRE DE MATR_ELEM DE LA LISTE TLIMAT
C IN  K8  TLIMAT : LISTE DES MATR_ELEM
C IN  K24 LICOEF : NOM DU VECTEUR CONTENANT LES COEF. MULT.
C                  DES MATR_ELEM
C                  SI LICOEF=' ' ON PREND 1.D0 COMME COEF.
C IN  K14 NU     : NOM DU NUME_DDL
C IN  K19 SOLVEU : NOM DU SOLVEUR
C IN  K19 INFCHA : NOM DE L'OBJET INFCHA
C                  (POUR LES CHARGES CINEMATIQUES)
C IN  K4 MOTCLE : 'ZERO' OU 'CUMU'
C                 'ZERO':SI UN OBJET DE NOM MATAS ET DE TYPE
C                        MATR_ASSE EXISTE ON ECRASE SON CONTENU.
C                 'CUMU':SI UN OBJET DE NOM MATAS ET DE TYPE
C                        MATR_ASSE EXISTE ON CUMULE DANS .VALE
C IN  K1  BASE   : BASE SUR LAQUELLE ON CREE L'OBJET MATAS
C IN  I   TYPE  : TYPE DES MATRICES ELEMENTAIRES A ASSEMBLER
C                          1 --> REELLES
C                          2 --> COMPLEXES
C OUT K19  MATAS : L'OBJET MATAS DE TYPE MATR_ASSE EST CREE ET REMPLI
C IN  K19  MATAS : NOM DE L'OBJET DE TYPE MATR_ASSE A CREER
C   -------------------------------------------------------------------
C     ASTER INFORMATIONS:
C       05/12/03 (OB): AJOUT POUR SOLVEUR FETI.
C----------------------------------------------------------------------
C     FONCTIONS JEVEUX
C-----------------------------------------------------------------------
      CHARACTER*32 JEXNUM,JEXNOM,JEXATR
C-----------------------------------------------------------------------
C     COMMUNS   JEVEUX
C-----------------------------------------------------------------------
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)

      CHARACTER*1 TYPSYM,TYPMAT
      CHARACTER*2 TYMASY
      CHARACTER*3 SYME
      CHARACTER*7 SYMEL
      CHARACTER*8 MATEL,K8BID
      CHARACTER*24 METRES,LICOE2

CDEB-------------------------------------------------------------------
      CALL JEMARQ()
      CALL JEDBG2(IDBGAV,0)

      SOLVE2 = SOLVEU
      INFCH2 = INFCHA
      LICOE2 = LICOEF
      NBMAT2=NBMAT

      IF (MOTCLE.EQ.'ZERO') CALL DETRSD('MATR_ASSE',MATAS)
      INDSUI=.FALSE.
      INDMAS=.FALSE.

C     -- ON AJOUTE '&&CFMMEL' SI NECESSAIRE :
C      IF (NBMAT2.GT.99) CALL JXABOR()
      DO 10,K = 1,NBMAT2
        TLIMA2(K) = TLIMAT(K)
        IF (TLIMA2(K).EQ.'&&ASCOMA') INDSUI=.TRUE.
        IF ((NBMAT2.EQ.1).AND.(TLIMAT(K).EQ.'&&OP0070.MEMASS')) 
     &  INDMAS=.TRUE.
   10 CONTINUE
      CALL JEEXIN('&&CFMMEL.LISTE_RESU',IRET)
     
      IF ((IRET.GT.0).AND.(.NOT.INDSUI).AND.(.NOT.INDMAS)) THEN
        NBMAT2 = NBMAT2 + 1
        TLIMA2(NBMAT2) = '&&CFMMEL'
      END IF
C     -- TRAITEMENT DE LA LISTE DES COEF. MULTIPLICATEURS :
      IF (LICOE2.EQ.' ') THEN
        CALL WKVECT('&&ASMATR.LICOEF','V V R',NBMAT2,ILICOE)
        DO 20 I = 1,NBMAT2
          ZR(ILICOE+I-1) = 1.D0
   20   CONTINUE
      ELSE
        CALL JEVEUO(LICOE2,'L',ILICOE)
      END IF


      CALL WKVECT('&&ASMATR.LMATEL','V V K8',NBMAT2,ILIMAT)

C PREPARATION DE LA LISTE DE MATR_ELEM RESYMETRISES SI NECESSAIRES
      INDSYM = 0
      TYPSYM = TYPMAT(NBMAT2,TLIMA2)

      CALL JEVEUO(SOLVE2//'.SLVK','L',ISLVK)
      SYME = ZK24(ISLVK+5-1)
      IF (SYME.EQ.'OUI') THEN
        DO 30 I = 1,NBMAT2
          CALL DISMOI('F','TYPE_MATRICE',TLIMA2(I),'MATR_ELEM',IBID,
     &                SYMEL,IER)
          IF (SYMEL.EQ.'NON_SYM') THEN
            CALL GCNCON('.',MATEL)
            ZK8(ILIMAT+I-1) = MATEL
            CALL RESYME(TLIMA2(I),BASE,MATEL)
          ELSE
            ZK8(ILIMAT+I-1) = TLIMA2(I)
          END IF
   30   CONTINUE
        TYPSYM = 'S'
      ELSE
        DO 40 I = 1,NBMAT2
          ZK8(ILIMAT+I-1) = TLIMA2(I)
   40   CONTINUE
      END IF


C REMPLISSAGE DE LA MATR_ASSE MATAS

      METRES = ZK24(ISLVK)

      IF (METRES.EQ.'LDLT') THEN
C     =============================
        CALL JEEXIN(MATAS(1:19)//'.REFA',IRET)
        IF (IRET.NE.0) THEN
          CALL JELIRA(MATAS(1:19)//'.VALE','DOCU',IBID,TYMASY)
          IF (TYMASY.EQ.'MR') THEN
            INDSYM = 1
          END IF
        END IF
        IF (TYPSYM.EQ.'S' .AND. INDSYM.EQ.0) THEN
          CALL ASSMAT(BASE,MATAS,NBMAT2,ZK8(ILIMAT),ZR(ILICOE),NU,
     &              MOTCLE,  TYPE)
        ELSE IF (TYPSYM.EQ.'N' .OR. INDSYM.EQ.1) THEN
          CALL JEEXIN(MATAS(1:19)//'.REFA',IRET)
          IF (IRET.NE.0) THEN
            CALL JELIRA(MATAS(1:19)//'.VALE','DOCU',IBID,TYMASY)
            IF (TYMASY.EQ.'MS') THEN
              CALL MASYNS(MATAS)
            END IF
          END IF
          CALL ASSMNS(BASE,MATAS,NBMAT2,ZK8(ILIMAT),ZR(ILICOE),NU,
     &              MOTCLE,  TYPE)
        END IF


      ELSE IF ((METRES.EQ.'MULT_FRO').OR.
     &         (METRES.EQ.'FETI').OR.(METRES.EQ.'MUMPS')) THEN
C     ==========================================================
        CALL JEEXIN(MATAS(1:19)//'.REFA',IRET)
        IF (IRET.NE.0) THEN
          CALL JELIRA(MATAS(1:19)//'.VALE','DOCU',IBID,TYMASY)
          IF (TYMASY.EQ.'MR') THEN
            INDSYM = 1
          END IF
        END IF
        IF (TYPSYM.EQ.'S' .AND. INDSYM.EQ.0) THEN
          CALL ASSMAM(BASE,MATAS,NBMAT2,ZK8(ILIMAT),ZR(ILICOE),NU,
     &              MOTCLE,  TYPE)
        ELSE IF (TYPSYM.EQ.'N' .OR. INDSYM.EQ.1) THEN
          IF (METRES.EQ.'FETI')
     &      CALL UTMESS('F','ASMATR',
     &      'MATRICE NON SYMETRIQUE POUR L''INSTANT PROSCRITE'//
     &      '  AVEC FETI')
          CALL JEEXIN(MATAS(1:19)//'.REFA',IRET)
          IF (IRET.NE.0) THEN
            CALL JELIRA(MATAS(1:19)//'.VALE','DOCU',IBID,TYMASY)
            IF (TYMASY.EQ.'MS') THEN
              CALL MASYNS(MATAS)
            END IF
          END IF
          CALL ASSMMN(BASE,MATAS,NBMAT2,ZK8(ILIMAT),ZR(ILICOE),NU,
     &               MOTCLE, TYPE)
        END IF


      ELSE IF (METRES.EQ.'GCPC') THEN
C     ================================
        IF (TYPSYM.EQ.'S') THEN
          CALL ASSMAM(BASE,MATAS,NBMAT2,ZK8(ILIMAT),ZR(ILICOE),NU,
     &              MOTCLE,  TYPE)
        ELSE IF (TYPSYM.EQ.'N') THEN
          CALL UTMESS('F','ASMATR',' LE TYPE  : '//TYPSYM//
     &                '  DE LA MATRICE EST INCORRECT. ON ATTEND : "S"'//
     &                'POUR UNE RESOLUTION PAR METHODE ITERATIVE')
        END IF

      END IF



C- TRAITEMENT DES CHARGES CINEMATIQUES :
C  ======================================
      CALL ASCIMA(INFCH2//'.LCHA',INFCH2//'.INFC',NU,MATAS)
      CALL JEDETR('&&ASMATR.LICOEF')

      IF (SYME.EQ.'OUI') THEN
        DO 60 I = 1,NBMAT2
          CALL DISMOI('F','TYPE_MATRICE',TLIMA2(I),'MATR_ELEM',IBID,
     &                SYMEL,IER)
          IF (SYMEL.EQ.'NON_SYM') THEN
            CALL JEVEUO(ZK8(ILIMAT+I-1)//'.LISTE_RESU','E',IDLRE2)
            CALL JELIRA(ZK8(ILIMAT+I-1)//'.LISTE_RESU','LONUTI',NBRESU,
     &                  K8BID)
            DO 50 IRESU = 1,NBRESU
              CALL JEDETR(ZK24(IDLRE2+IRESU-1) (1:8)//
     &                    '           .DESC')
              CALL JEDETR(ZK24(IDLRE2+IRESU-1) (1:8)//
     &                    '           .NOLI')
              CALL JEDETR(ZK24(IDLRE2+IRESU-1) (1:8)//
     &                    '           .RESL')
   50       CONTINUE
            CALL JEDETR(ZK8(ILIMAT+I-1)//'.LISTE_RESU')
            CALL JEDETR(ZK8(ILIMAT+I-1)//'.REFE_RESU')
          END IF
   60   CONTINUE
      END IF


      CALL JEDETR('&&ASMATR.LMATEL')
      CALL JEDBG2(IBID,IDBGAV)
      CALL JEDEMA()
      END
