      SUBROUTINE RENUNO(NU,RENUM)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ASSEMBLA  DATE 12/06/2006   AUTEUR BOITEAU O.BOITEAU 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
      IMPLICIT REAL*8 (A-H,O-Z)

C     ARGUMENTS:
C     ----------
      CHARACTER*14 NU
      CHARACTER*(*) RENUM
C     -------------------------------------------------
C     BUT:  CETTE ROUTINE A 3 FONCTIONS DISTINCTES:

C     1) RENUMEROTER (EVENTUELLEMENT) LES NOEUDS (DU MAILLAGE)
C     D'UN MODELE (ARGUMENT RENUM)
C     POUR L'INSTANT LA SEULE METHODE DISPONIBLE EST CELLE DE :
C     "REVERSE CUTHIL MAC-KEE" (CF. LE LIVRE :
C     "MATRIC COMPUTATION FOR ENGINEERS AND SCIENTISTS"
C     DE ALAN JENNINGS (WILEY ANS SONS))

C     2) RENUMEROTER  LES NOEUDS TARDIFS DU MAILLAGE
C     (NOEUDS DE LAGRANGE PROVENANT DES SOUS_STRUCTURES)
C     (CES NOEUDS DOIVENT EN EFFET TOUJOURS ENCADRER LES
C     NOEUDS PHYSIQUES CONTRAINTS)

C     3) RENDRE A NUME_DDL L'OBJET .EXI1
C     (POUR DIRE QUELS SONT LES NOEUDS (TARDIFS OU NON)
C     INTERVENANT REELLEMENT DANS LA NUMEROTATION)

C     IN:
C     ---
C     RENUM: 'RCMK' :  REVERSE-CUTHIL-MAC-KEE.
C            ' '    :  PAS DE RENUMEROTATION.
C     NU : NOM DU NUME_DDL  AUQUEL ON VA AJOUTER  LES OBJETS
C     .NEWN ET .OLDN
C     (ON SE SERT EN ENTREE DU SEUL OBJET NU//'.NUME.LILI')

C     OUT:
C     ---- NU EST COMPLETE PAR .NEWN , .OLDN ET .EXI1 (BASE : VOLATILE)

C     SOIT NM LE NOMBRE DE NOEUDS PHYSIQUES DU MAILLAGE (LILI(1))
C     NL LE NOMBRE DE NOEUDS TARDIFS DU MAILLAGE
C     N2 LE NOMBRE DE NOEUDS TARDIFS DU MODELE (LILI(2))
C     N3 LE NOMBRE DE NOEUDS TARDIFS DE LA 1ERE CHARGE(LILI(3))
C     .....
C     NP LE NOMBRE DE NOEUDS TARDIFS DE LA DERE CHARGE(LILI(P))

C     NBNOM = NM+NL      (NOMBRE MAX DE NOEUDS DU MAILLAGE)
C     NBNOT = N2+...+NP  (NOMBRE MAX DE NOEUDS TARDIFS DU MODELE
C     ET DE LA LISTE DE CHARGES)
C     NBNTT = NBNOM+NBNOT

C     .NEWN(*) EST DIMENSIONNE A NBNOM
C     .OLDN(*) EST DIMENSIONNE A NBNOM

C     .EXI1(*) EST DIMENSIONNE A NBNTT+1

C     SOIT LA NUMEROTATION IMPLICITE TOTALE :
C     1- LES NOEUDS PHYSIQUES DU MAILLAGE (NI) / ORDRE DE .NOMNOE
C     2- LES NOEUDS TARDIFS DU MAILLAGE (&I)   /
C     3- LES NOEUDS TARDIFS DU MODELE   (&LMI)
C     4- LES NOEUDS TARDIFS DE LA CHARGE 1 (&LCH1I)
C     - ...
C     - LES NOEUDS TARDIFS DE LA CHARGE P (&LCH1P)

C     LA RENUMEROTATION ('RCMK',..) NE CONCERNE EN FAIT QUE LES NOEUDS
C     DU MAILLAGE (NM+NL) CAR LES AUTRES NOEUDS DEVRONT DE TOUTES
C     FACONS ETRE RENUMEROTES PAR NUEFFE (LAGRANGES)

C     POUR I=1,NBNOM
C     .NEWN(I) EST LA POSITION DU NOEUD I DANS LA NOUVELLE NUMEROTATI
C     (SI NEWN(I) = 0 CE NOEUD N'EXISTE PAS DANS NUME_DDL)

C     .OLDN(I) EST LE NUMERO (ANCIEN) DU NOEUD NUMEROTE I DANS LA
C     NOUVELLE NUMEROTATION.


C     POUR I=1,NBNTT
C     .EXI1(1) =1 (CORRESPOND AU NOEUD "ZERO" FICTIF DE NUEFFE)
C     .EXI1(I+1) >0 SI LE NOEUD I EXISTE DANS LE NUME_DDL
C     =0 SINON

C     -----------------------------------------------------
C     VARIABLES LOCALES:
C     ------------------
      INTEGER NM,NL,NBNOM,NBNOT,NBNTT,NBNORE
      CHARACTER*8 MA,MO,CHA,KBID
C----------------COMMUNS NORMALISES  JEVEUX  ---------------
      CHARACTER*32 JEXNUM,JEXNOM,JEXATR,JEXR8
      COMMON /IVARJE/ZI(1)
      COMMON /RVARJE/ZR(1)
      COMMON /CVARJE/ZC(1)
      COMMON /LVARJE/ZL(1)
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
      INTEGER ZI
      REAL*8 ZR
      COMPLEX*16 ZC
      LOGICAL ZL
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
C     ---------------- FIN COMMUNS NORMALISES  JEVEUX  ---------------


C     -- CALCUL DE .EXI1 :
C     --------------------
      CALL JEMARQ()
      CALL REEXI1(NU,MO,MA,NLILI,NM,NL,NBNTT)
      NBNOM = NM + NL

C     -- ALLOCATION DES OBJETS .NEWN ET .OLDN :
C     -----------------------------------------
      CALL WKVECT(NU//'.NEWN','V V I',NBNOM,IANEWN)
      CALL WKVECT(NU//'.OLDN','V V I',NBNOM,IAOLDN)


C     1ERE ETAPE: RENUMEROTATION EVENTUELLE DES NOEUDS PHYSIQUES:
C     (A LA FIN DE CETTE ETAPE, NEWN ET OLDN CONCERNENT UNIQUEMENT
C     LES NOEUDS PHYSIQUES (1->NM))
C     --------------------------------------------------------------
      CALL JEVEUO(NU//'.EXI1','L',IAEXI1)

      IF (RENUM(1:4).EQ.'RCMK') THEN
C     -- 'REVERSE-CUTHIL-MAC-KEE':
        CALL RERCMK(NU,MO,MA,NLILI,NM,NL,NBNTT)
      ELSE IF (RENUM(1:4).EQ.'SANS'  .OR.
     &         RENUM(1:2).EQ.'MD'    .OR.
     &         RENUM(1:5).EQ.'METIS' .OR.
     &         RENUM(1:3).EQ.'AMD'   .OR.
     &         RENUM(1:3).EQ.'AMF'   .OR.
     &         RENUM(1:4).EQ.'QAMD'  .OR.
     &         RENUM(1:4).EQ.'PORD'  .OR.
     &         RENUM(1:4).EQ.'AUTO'   )THEN
C     -- 'SANS RENUMEROTATION CUTHIL-MAC-KEE':
        ICO = 0
        DO 10,I = 1,NM
          IF (ZI(IAEXI1+I).GT.0) THEN
            ICO = ICO + 1
            ZI(IANEWN-1+I) = ICO
            ZI(IAOLDN-1+ICO) = I
          END IF
   10   CONTINUE
      ELSE
        CALL UTMESS('F','RENUNO','METHODE : '//RENUM(1:4)//' INCONNUE.')
      END IF


C     2EME ETAPE:
C     -- ON REMET LES NOEUDS TARDIFS DU MAILLAGE AU BON ENDROIT:
C     (A LA FIN DE CETTE ETAPE, NEWN ET OLDN CONCERNENT TOUS LES
C     NOEUDS DU MAILLAGE (1->NM+NL))
C     --------------------------------------------------------------
      CALL RELAGM(MO,MA,NM,NL,ZI(IANEWN),ZI(IAOLDN))


C     -- ON MET A JOUR 'LONUTI' DE .OLDN : NOMBRE DE NOEUDS DU MAILLAGE
C     (RENUMEROTES OU NON) PARTICIPANT AU NUME_DDL:

      DO 20,I = 1,NBNOM
        IF (ZI(IAOLDN-1+I).EQ.0) THEN
          CALL JEECRA(NU//'.OLDN','LONUTI',I-1,KBID)
          GO TO 30
        END IF
   20 CONTINUE


   30 CONTINUE
      CALL JEDEMA()
      END
