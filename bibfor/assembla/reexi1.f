      SUBROUTINE REEXI1(NU,MO,MA,NLILI,NM,NL,NBNTT)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ASSEMBLA  DATE 09/11/2012   AUTEUR DELMAS J.DELMAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
      IMPLICIT NONE
C
C     ARGUMENTS:
C     ----------
      INCLUDE 'jeveux.h'

      CHARACTER*32 JEXNUM,JEXATR
      CHARACTER*8 MO,MA
      CHARACTER*14 NU
      INTEGER NLILI, NM, NL, NBNTT
C ----------------------------------------------------------------------
C     BUT:  CETTE ROUTINE SERT A :
C
C           1) RENDRE : MO, MA, NLILI, NM, NL, NBNTT
C
C           2) RENDRE DANS LE NUME_DDL L' OBJET .EXI1
C              (POUR DIRE QUELS SONT LES NOEUDS (TARDIFS OU NON)
C               INTERVENANT REELLEMENT DANS LA NUMEROTATION
C               PLUS L'EXISTENCE D'UN NOEUD FACTICE 0 )
C             ATTENTION : CET OBJET A UN CONTENU PLUS RICHE QUE
C                         NECESSAIRE EN PREVISION DE 'RCMK'
C
C     IN:
C     ---
C       NU : NOM DU NUME_DDL  AUQUEL ON VA AJOUTER  L' OBJET .EXI1
C           (ON SE SERT EN ENTREE DU SEUL OBJET NU//'.NUME.LILI')
C
C     OUT:
C     ---- MO : NOM DU MODELE SOUS-JACENT AU NUME_DDL
C          MA : NOM DU MAILLAGE SOUS-JACENT AU NUME_DDL
C          NLILI: NOMBRE DE LIGREL DE L'OBJET .LILI
C
C         SOIT NM LE NOMBRE DE NOEUDS PHYSIQUES DU MAILLAGE (LILI(1))
C              NL LE NOMBRE DE NOEUDS TARDIFS DU MAILLAGE
C              N2 LE NOMBRE DE NOEUDS TARDIFS DU MODELE (LILI(2))
C              N3 LE NOMBRE DE NOEUDS TARDIFS DE LA 1ERE CHARGE(LILI(3))
C              .....
C              NP LE NOMBRE DE NOEUDS TARDIFS DE LA DERE CHARGE(LILI(P))
C
C          NBNOM = NM+NL      (NOMBRE MAX DE NOEUDS DU MAILLAGE)
C          NBNOT = N2+...+NP  (NOMBRE MAX DE NOEUDS TARDIFS DU MODELE
C                              ET DE LA LISTE DE CHARGES)
C          NBNTT = NBNOM+NBNOT
C
C        NU EST COMPLETE PAR .EXI1
C        -------------------------
C        .EXI1(*) EST DIMENSIONNE A NBNTT+1
C
C        SOIT LA NUMEROTATION IMPLICITE TOTALE :
C          1- LE NOEUD FACTICE 0
C          1- LES NOEUDS PHYSIQUES DU MAILLAGE (NI) / ORDRE DE .NOMNOE
C          3- LES NOEUDS TARDIFS DU MAILLAGE (&I)   /
C          4- LES NOEUDS TARDIFS DU MODELE   (&LMI)
C          5- LES NOEUDS TARDIFS DE LA CHARGE 1 (&LCH1I)
C           - ...
C           - LES NOEUDS TARDIFS DE LA CHARGE P (&LCH1P)
C
C        .EXI1(0)=1
C        POUR I=1,NBNTT
C        .EXI1(I) >0 SI LE NOEUD I EXISTE DANS LE NUME_DDL
C                 =0 SINON
C                  (EN FAIT EXI1(I) CONTIENT UN MAJORANT DU NOMBRE
C                  DE NOEUDS CONNECTES AU NOEUD I. CET OBJET SERVIRA
C                  DANS 'RCMK' A ALLOUER LA TABLE DE CONNECTIVITE
C                  INVERSE)
C ----------------------------------------------------------------------
C     VARIABLES LOCALES:
C     ------------------
      CHARACTER*8 KBID,EXIELE
      CHARACTER*24 NOMLI2,MO2
      CHARACTER*19 NOMLIG
C
C
C     -- RECUPERATION DU NOM DU MODELE SOUS-JACENT A LA NUMEROTATION :
C     ----------------------------------------------------------------
C-----------------------------------------------------------------------
      INTEGER I ,IACONX ,IAEXI1 ,IAGREL ,IALIEL ,IAMAIL ,IANBNO
      INTEGER IANEMA ,IASSSA ,IBID ,IEL ,IER ,IERD ,IGREL
      INTEGER IINO ,ILCONX ,ILI ,ILLIEL ,ILNEMA ,IMA ,INO
      INTEGER IRET ,J ,JJNO ,JNO ,NBEL ,NBGREL ,NBNM
      INTEGER NBNOM ,NBNOT ,NBSMA ,NBSSA ,NMA
C-----------------------------------------------------------------------
      CALL JEMARQ()
      CALL JELIRA(NU//'.NUME.LILI','NOMMAX',NLILI,KBID)
      DO 41, I=1,NLILI
        CALL JENUNO(JEXNUM(NU//'.NUME.LILI',I),MO2)
        IF (MO2(9:15).EQ.'.MODELE') THEN
          MO= MO2(1:8)
          GO TO 42
        END IF
 41   CONTINUE
      CALL U2MESK('F','ASSEMBLA_35',1,NU)
C
 42   CONTINUE
      CALL DISMOI('F','NOM_MAILLA',MO,'MODELE',IBID,MA,IER)
C
C     -- CALCUL DE NBNOM:
C     -------------------
      CALL DISMOI('F','NB_NO_MAILLA',MO,'MODELE',NM,KBID,IER)
      CALL DISMOI('F','NB_MA_MAILLA',MO,'MODELE',NMA,KBID,IER)
      CALL DISMOI('F','NB_NL_MAILLA',MO,'MODELE',NL,KBID,IER)
      NBNOM=NM+NL
C
C     -- CALCUL DE NBNOT ET NBNTT :
C     -----------------------------
      NBNOT=0
      DO 1 , ILI=2,NLILI
         CALL JENUNO(JEXNUM(NU//'.NUME.LILI',ILI),NOMLI2)
         NOMLIG=NOMLI2(1:19)
         CALL JEVEUO(NOMLIG//'.NBNO','L',IANBNO)
         NBNOT= NBNOT+ZI(IANBNO)
 1    CONTINUE
      NBNTT= NBNOM+NBNOT
C
C
C     -- ALLOCATION DE .EXI1
C     ----------------------
      CALL WKVECT(NU//'.EXI1','V V I',NBNTT+1,IAEXI1)
C
      ZI(IAEXI1) = 1
C
      IF (NMA.GT.0) THEN
        CALL JEVEUO(MA//'.CONNEX','L',IACONX)
        CALL JEVEUO(JEXATR(MA//'.CONNEX','LONCUM'),'L',ILCONX)
      END IF
C
C
C     -- 1ERE ETAPE : (SUPER)MAILLES DU MAILLAGE:
C     -------------------------------------------
      CALL DISMOI('F','NB_SS_ACTI',MO,'MODELE',NBSSA,KBID,IERD)
      CALL DISMOI('F','NB_SM_MAILLA',MO,'MODELE',NBSMA,KBID,IERD)
      IF (NBSSA.GT.0) THEN
        CALL JEVEUO(MO//'.MODELE    .SSSA','L',IASSSA)
      ELSE
        GO TO 22
      END IF
C
      DO 21, IMA = 1, NBSMA
        IF(ZI(IASSSA-1+IMA).EQ.1) THEN
          CALL JEVEUO(JEXNUM(MA//'.SUPMAIL',IMA),'L',IAMAIL)
          CALL JELIRA(JEXNUM(MA//'.SUPMAIL',IMA),'LONMAX',NBNM,KBID)
          DO 23, I=1,NBNM
            INO=ZI(IAMAIL-1+I)
            IF (INO.EQ.0)
     & CALL U2MESS('F','ASSEMBLA_36')
            DO 24, J=I+1,NBNM
              JNO=ZI(IAMAIL-1+J)
              ZI(IAEXI1+INO)=ZI(IAEXI1+INO) +1
              ZI(IAEXI1+JNO)=ZI(IAEXI1+JNO) +1
 24         CONTINUE
 23       CONTINUE
        END IF
 21   CONTINUE
C
 22   CONTINUE
C
C     -- 2EME ETAPE : MAILLES TARDIVES (OU NON) DES LIGRELS:
C                     (MODELE + LISTE DE CHARGES)
C     ------------------------------------------------------
      NBNOT=0
      DO 31 , ILI=2,NLILI
        CALL JENUNO(JEXNUM(NU//'.NUME.LILI',ILI),NOMLI2)
        NOMLIG=NOMLI2(1:19)
        CALL DISMOI('F','EXI_ELEM',NOMLIG,'LIGREL',IBID,EXIELE,IERD)
        IF (EXIELE(1:3).EQ.'NON') GO TO 31
C
        CALL JEVEUO(NOMLIG//'.LIEL','L',IALIEL)
        CALL JEVEUO(JEXATR(NOMLIG//'.LIEL','LONCUM'),'L',ILLIEL)
        CALL JELIRA(NOMLIG//'.LIEL','NMAXOC',NBGREL,KBID)
C

        CALL JEEXIN(NOMLIG//'.NEMA',IRET)
        IF (IRET.GT.0) THEN
          CALL JEVEUO(NOMLIG//'.NEMA','L',IANEMA)
          CALL JEVEUO(JEXATR(NOMLIG//'.NEMA','LONCUM'),'L',ILNEMA)
        END IF
C
        DO 32, IGREL = 1, NBGREL
          NBEL= ZI(ILLIEL-1+IGREL+1)-ZI(ILLIEL-1+IGREL) -1
          IAGREL= IALIEL + ZI(ILLIEL-1+IGREL) -1
C
          DO 33,IEL= 1, NBEL
            IMA= ZI(IAGREL -1 +IEL)
            IF (IMA.GT.0) THEN
              NBNM= ZI(ILCONX-1+IMA+1)-ZI(ILCONX-1+IMA)
              IAMAIL= IACONX + ZI(ILCONX-1+IMA) -1
            ELSE
              NBNM= ZI(ILNEMA-1-IMA+1)-ZI(ILNEMA-1-IMA) -1
              IAMAIL = IANEMA + ZI(ILNEMA-1-IMA) -1
            END IF
C
            DO 34, I=1,NBNM
              INO=ZI(IAMAIL-1+I)
              IF (INO.EQ.0)
     & CALL U2MESS('F','ASSEMBLA_36')
              IINO=INO
              IF (INO.LT.0) IINO=NBNOM+NBNOT-INO
              IF (NBNM.EQ.1) ZI(IAEXI1+IINO)=ZI(IAEXI1+IINO) +1
              DO 35, J=I+1,NBNM
                JNO=ZI(IAMAIL-1+J)
                JJNO=JNO
                IF (JNO.LT.0) JJNO=NBNOM+NBNOT-JNO
                ZI(IAEXI1+IINO)=ZI(IAEXI1+IINO) +1
                ZI(IAEXI1+JJNO)=ZI(IAEXI1+JJNO) +1
 35           CONTINUE
 34         CONTINUE
 33       CONTINUE
 32     CONTINUE
        CALL JEVEUO(NOMLIG//'.NBNO','L',IANBNO)
        NBNOT= NBNOT+ZI(IANBNO)
 31   CONTINUE
C
C
C
      CALL JEDEMA()
      END
