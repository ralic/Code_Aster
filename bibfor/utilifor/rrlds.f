      SUBROUTINE RRLDS( A , NMAX, NORDRE , X , NVES )
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF UTILIFOR  DATE 29/09/95   AUTEUR GIBHHAY A.Y.PORTABILITE 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
      IMPLICIT REAL*8 (A-H,O-Z)
CA
CA    ARGUMENTS :
CA    ---------
CA -> A      : MATRICE CARREE TRIANGULEE PAR TRLDS
CA -> NMAX   : DIMENSION REELLE DE LA MATRICE A ET DU TABLEAU X
CA -> NORDRE : DIMENSION DE LA MATRICE A
CA <> X      : SECOND MEMBRE ECRASE PAR LA SOLUTION
CA -> NVES   : NOMBRE DE COLONNES DU SECOND MEMBRE
CA
CA    BUT :
CA    ---
CA    RESOLUTION DE  A * X = B ; B ETANT STOCKE DANS X A L'APPEL
      REAL * 8       A(NMAX,NORDRE) , X(NMAX,NVES) , R8VAL
C
      ILIGN1 = 1
      ILIGN2 = NORDRE
C
C     RESOLUTION DESCENDANTE
      DO 25 NV = 1 , NVES
         DO 20 IN = ILIGN1 , ILIGN2-1
            R8VAL = - X ( IN , NV )
            DO 21 I = IN+1 , ILIGN2
               X(I,NV) = X(I,NV) + R8VAL * A (I,IN)
 21         CONTINUE
 20      CONTINUE
 25   CONTINUE
C
C     RESOLUTION DIAGONALE
      DO 39 NV = 1 , NVES
         DO 33 IN = ILIGN1 , ILIGN2
            X ( IN , NV ) = X ( IN , NV ) / A(IN,IN)
 33      CONTINUE
 39   CONTINUE
C
C     RESOLUTION REMONTANTE
      DO 45 NV = 1 , NVES
         DO 40 IN = ILIGN2 , ILIGN1+1 , -1
            R8VAL = - X ( IN , NV )
            DO 41 I = 1 , IN-1
               X(I,NV) = X(I,NV) + R8VAL * A(I,IN)
 41         CONTINUE
 40      CONTINUE
 45   CONTINUE
C
 9999 CONTINUE
      END
