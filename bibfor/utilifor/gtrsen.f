      SUBROUTINE GTRSEN( JOB, COMPQ, SELECT, N, T, LDT, Q, LDQ, W, M, S,
     $                   SEP, WORK, LWORK, INFO )
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF UTILIFOR  DATE 17/02/2003   AUTEUR NICOLAS O.NICOLAS 
C ======================================================================
C COPYRIGHT (C) LAPACK
C ======================================================================
C  -- LAPACK ROUTINE (VERSION 2.0) --
C     UNIV. OF TENNESSEE, UNIV. OF CALIFORNIA BERKELEY, NAG LTD.,
C     COURANT INSTITUTE, ARGONNE NATIONAL LAB, AND RICE UNIVERSITY
C     MARCH 31, 1993
C
C
C  PURPOSE
C  =======
C
C  GTRSEN REORDERS THE SCHUR FACTORIZATION OF A COMPLEX MATRIX
C  A = Q*T*Q**H, SO THAT A SELECTED CLUSTER OF EIGENVALUES APPEARS IN
C  THE LEADING POSITIONS ON THE DIAGONAL OF THE UPPER TRIANGULAR MATRIX
C  T, AND THE LEADING COLUMNS OF Q FORM AN ORTHONORMAL BASIS OF THE
C  CORRESPONDING RIGHT INVARIANT SUBSPACE.
C
C  OPTIONALLY THE ROUTINE COMPUTES THE RECIPROCAL CONDITION NUMBERS OF
C  THE CLUSTER OF EIGENVALUES AND/OR THE INVARIANT SUBSPACE.
C
C  ARGUMENTS
C  =========
C
C  JOB     (INPUT) CHARACTER*1
C          SPECIFIES WHETHER CONDITION NUMBERS ARE REQUIRED FOR THE
C          CLUSTER OF EIGENVALUES (S) OR THE INVARIANT SUBSPACE (SEP):
C          = 'N': NONE;
C          = 'E': FOR EIGENVALUES ONLY (S);
C          = 'V': FOR INVARIANT SUBSPACE ONLY (SEP);
C          = 'B': FOR BOTH EIGENVALUES AND INVARIANT SUBSPACE (S AND
C                 SEP).
C
C  COMPQ   (INPUT) CHARACTER*1
C          = 'V': UPDATE THE MATRIX Q OF SCHUR VECTORS;
C          = 'N': DO NOT UPDATE Q.
C
C  SELECT  (INPUT) LOGICAL ARRAY, DIMENSION (N)
C          SELECT SPECIFIES THE EIGENVALUES IN THE SELECTED CLUSTER. TO
C          SELECT THE J-TH EIGENVALUE, SELECT(J) MUST BE SET TO .TRUE..
C
C  N       (INPUT) INTEGER
C          THE ORDER OF THE MATRIX T. N >= 0.
C
C  T       (INPUT/OUTPUT) COMPLEX*16 ARRAY, DIMENSION (LDT,N)
C          ON ENTRY, THE UPPER TRIANGULAR MATRIX T.
C          ON EXIT, T IS OVERWRITTEN BY THE REORDERED MATRIX T, WITH THE
C          SELECTED EIGENVALUES AS THE LEADING DIAGONAL ELEMENTS.
C
C  LDT     (INPUT) INTEGER
C          THE LEADING DIMENSION OF THE ARRAY T. LDT >= MAX(1,N).
C
C  Q       (INPUT/OUTPUT) COMPLEX*16 ARRAY, DIMENSION (LDQ,N)
C          ON ENTRY, IF COMPQ = 'V', THE MATRIX Q OF SCHUR VECTORS.
C          ON EXIT, IF COMPQ = 'V', Q HAS BEEN POSTMULTIPLIED BY THE
C          UNITARY TRANSFORMATION MATRIX WHICH REORDERS T; THE LEADING M
C          COLUMNS OF Q FORM AN ORTHONORMAL BASIS FOR THE SPECIFIED
C          INVARIANT SUBSPACE.
C          IF COMPQ = 'N', Q IS NOT REFERENCED.
C
C  LDQ     (INPUT) INTEGER
C          THE LEADING DIMENSION OF THE ARRAY Q.
C          LDQ >= 1; AND IF COMPQ = 'V', LDQ >= N.
C
C  W       (OUTPUT) COMPLEX*16
C          THE REORDERED EIGENVALUES OF T, IN THE SAME ORDER AS THEY
C          APPEAR ON THE DIAGONAL OF T.
C
C  M       (OUTPUT) INTEGER
C          THE DIMENSION OF THE SPECIFIED INVARIANT SUBSPACE.
C          0 <= M <= N.
C
C  S       (OUTPUT) DOUBLE PRECISION
C          IF JOB = 'E' OR 'B', S IS A LOWER BOUND ON THE RECIPROCAL
C          CONDITION NUMBER FOR THE SELECTED CLUSTER OF EIGENVALUES.
C          S CANNOT UNDERESTIMATE THE TRUE RECIPROCAL CONDITION NUMBER
C          BY MORE THAN A FACTOR OF SQRT(N). IF M = 0 OR N, S = 1.
C          IF JOB = 'N' OR 'V', S IS NOT REFERENCED.
C
C  SEP     (OUTPUT) DOUBLE PRECISION
C          IF JOB = 'V' OR 'B', SEP IS THE ESTIMATED RECIPROCAL
C          CONDITION NUMBER OF THE SPECIFIED INVARIANT SUBSPACE. IF
C          M = 0 OR N, SEP = NORM(T).
C          IF JOB = 'N' OR 'E', SEP IS NOT REFERENCED.
C
C  WORK    (WORKSPACE) COMPLEX*16 ARRAY, DIMENSION (LWORK)
C          IF JOB = 'N', WORK IS NOT REFERENCED.
C
C  LWORK   (INPUT) INTEGER
C          THE DIMENSION OF THE ARRAY WORK.
C          IF JOB = 'N', LWORK >= 1;
C          IF JOB = 'E', LWORK = M*(N-M);
C          IF JOB = 'V' OR 'B', LWORK >= 2*M*(N-M).
C
C  INFO    (OUTPUT) INTEGER
C          = 0:  SUCCESSFUL EXIT
C          < 0:  IF INFO = -I, THE I-TH ARGUMENT HAD AN ILLEGAL VALUE
C
C  FURTHER DETAILS
C  ===============
C
C  GTRSEN FIRST COLLECTS THE SELECTED EIGENVALUES BY COMPUTING A UNITARY
C  TRANSFORMATION Z TO MOVE THEM TO THE TOP LEFT CORNER OF T. IN OTHER
C  WORDS, THE SELECTED EIGENVALUES ARE THE EIGENVALUES OF T11 IN:
C
C                Z'*T*Z = ( T11 T12 ) N1
C                         (  0  T22 ) N2
C                            N1  N2
C
C  WHERE N = N1+N2 AND Z' MEANS THE CONJUGATE TRANSPOSE OF Z. THE FIRST
C  N1 COLUMNS OF Z SPAN THE SPECIFIED INVARIANT SUBSPACE OF T.
C
C  IF T HAS BEEN OBTAINED FROM THE SCHUR FACTORIZATION OF A MATRIX
C  A = Q*T*Q', THEN THE REORDERED SCHUR FACTORIZATION OF A IS GIVEN BY
C  A = (Q*Z)*(Z'*T*Z)*(Q*Z)', AND THE FIRST N1 COLUMNS OF Q*Z SPAN THE
C  CORRESPONDING INVARIANT SUBSPACE OF A.
C
C  THE RECIPROCAL CONDITION NUMBER OF THE AVERAGE OF THE EIGENVALUES OF
C  T11 MAY BE RETURNED IN S. S LIES BETWEEN 0 (VERY BADLY CONDITIONED)
C  AND 1 (VERY WELL CONDITIONED). IT IS COMPUTED AS FOLLOWS. FIRST WE
C  COMPUTE R SO THAT
C
C                         P = ( I  R ) N1
C                             ( 0  0 ) N2
C                               N1 N2
C
C  IS THE PROJECTOR ON THE INVARIANT SUBSPACE ASSOCIATED WITH T11.
C  R IS THE SOLUTION OF THE SYLVESTER EQUATION:
C
C                        T11*R - R*T22 = T12.
C
C  LET F-NORM(M) DENOTE THE FROBENIUS-NORM OF M AND 2-NORM(M) DENOTE
C  THE TWO-NORM OF M. THEN S IS COMPUTED AS THE LOWER BOUND
C
C                      (1 + F-NORM(R)**2)**(-1/2)
C
C  ON THE RECIPROCAL OF 2-NORM(P), THE TRUE RECIPROCAL CONDITION NUMBER.
C  S CANNOT UNDERESTIMATE 1 / 2-NORM(P) BY MORE THAN A FACTOR OF
C  SQRT(N).
C
C  AN APPROXIMATE ERROR BOUND FOR THE COMPUTED AVERAGE OF THE
C  EIGENVALUES OF T11 IS
C
C                         EPS * NORM(T) / S
C
C  WHERE EPS IS THE MACHINE PRECISION.
C
C  THE RECIPROCAL CONDITION NUMBER OF THE RIGHT INVARIANT SUBSPACE
C  SPANNED BY THE FIRST N1 COLUMNS OF Z (OR OF Q*Z) IS RETURNED IN SEP.
C  SEP IS DEFINED AS THE SEPARATION OF T11 AND T22:
C
C                     SEP( T11, T22 ) = SIGMA-MIN( C )
C
C  WHERE SIGMA-MIN(C) IS THE SMALLEST SINGULAR VALUE OF THE
C  N1*N2-BY-N1*N2 MATRIX
C
C     C  = KPROD( I(N2), T11 ) - KPROD( TRANSPOSE(T22), I(N1) )
C
C  I(M) IS AN M BY M IDENTITY MATRIX, AND KPROD DENOTES THE KRONECKER
C  PRODUCT. WE ESTIMATE SIGMA-MIN(C) BY THE RECIPROCAL OF AN ESTIMATE OF
C  THE 1-NORM OF INVERSE(C). THE TRUE RECIPROCAL 1-NORM OF INVERSE(C)
C  CANNOT DIFFER FROM SIGMA-MIN(C) BY MORE THAN A FACTOR OF SQRT(N1*N2).
C
C  WHEN SEP IS SMALL, SMALL CHANGES IN T CAN CAUSE LARGE CHANGES IN
C  THE INVARIANT SUBSPACE. AN APPROXIMATE BOUND ON THE MAXIMUM ANGULAR
C  ERROR IN THE COMPUTED RIGHT INVARIANT SUBSPACE IS
C
C                      EPS * NORM(T) / SEP
C
C  =====================================================================
C-----------------------------------------------------------------------
C ASTER INFORMATION
C 14/01/2000 TOILETTAGE DU FORTRAN SUIVANT LES REGLES ASTER,
C            REMPLACEMENT DE 1 RETURN PAR GOTO 1000,
C            IMPLICIT NONE.
C-----------------------------------------------------------------------
C CORPS DU PROGRAMME
      IMPLICIT NONE
C     .. SCALAR ARGUMENTS ..
      CHARACTER*1        COMPQ, JOB
      INTEGER            INFO, LDQ, LDT, LWORK, M, N
      REAL*8             S, SEP
C     ..
C     .. ARRAY ARGUMENTS ..
      LOGICAL            SELECT( * )
      COMPLEX*16         Q( LDQ, * ), T( LDT, * ), W( * ), WORK( * )
C     ..
C
C     .. PARAMETERS ..
      REAL*8             ZERO, ONE
      PARAMETER          ( ZERO = 0.0D+0, ONE = 1.0D+0 )
C     ..
C     .. LOCAL SCALARS ..
      LOGICAL            WANTBH, WANTQ, WANTS, WANTSP
      INTEGER            IERR, K, KASE, KS, N1, N2, NN
      REAL*8             EST, RNORM, SCALE
C     ..
C     .. LOCAL ARRAYS ..
      REAL*8             RWORK
C     ..
C     .. EXTERNAL FUNCTIONS ..
      LOGICAL            LLSAME
      REAL*8             GLANGE
C     ..
C     .. EXECUTABLE STATEMENTS ..
C
C     DECODE AND TEST THE INPUT PARAMETERS.
C
      WANTBH = LLSAME( JOB, 'B' )
      WANTS = LLSAME( JOB, 'E' ) .OR. WANTBH
      WANTSP = LLSAME( JOB, 'V' ) .OR. WANTBH
      WANTQ = LLSAME( COMPQ, 'V' )
C
C     SET M TO THE NUMBER OF SELECTED EIGENVALUES.
C
      RWORK=0.D0
      
      M = 0
      DO 10 K = 1, N
         IF( SELECT( K ) )
     $      M = M + 1
   10 CONTINUE
C
      N1 = M
      N2 = N - M
      NN = N1*N2
C
      INFO = 0
      IF( .NOT.LLSAME( JOB, 'N' ) .AND. .NOT.WANTS .AND. .NOT.WANTSP )
     $     THEN
         INFO = -1
      ELSE IF( .NOT.LLSAME( COMPQ, 'N' ) .AND. .NOT.WANTQ ) THEN
         INFO = -2
      ELSE IF( N.LT.0 ) THEN
         INFO = -4
      ELSE IF( LDT.LT.MAX( 1, N ) ) THEN
         INFO = -6
      ELSE IF( LDQ.LT.1 .OR. ( WANTQ .AND. LDQ.LT.N ) ) THEN
         INFO = -8
      ELSE IF( LWORK.LT.1 .OR. ( ( WANTS .AND. .NOT.WANTSP ) .AND.
     $         LWORK.LT.NN ) .OR. ( WANTSP .AND. LWORK.LT.2*NN ) ) THEN
         INFO = -14
      END IF
      IF( INFO.NE.0 ) THEN
         CALL XERBLA( 'GTRSEN', -INFO )
         GOTO 1000
      END IF
C
C     QUICK RETURN IF POSSIBLE
C
      IF( M.EQ.N .OR. M.EQ.0 ) THEN
         IF( WANTS )
     $      S = ONE
         IF( WANTSP )
     $      SEP = GLANGE( '1', N, N, T, LDT, RWORK )
         GO TO 40
      END IF
C
C     COLLECT THE SELECTED EIGENVALUES AT THE TOP LEFT CORNER OF T.
C
      KS = 0
      DO 20 K = 1, N
         IF( SELECT( K ) ) THEN
            KS = KS + 1
C
C           SWAP THE K-TH EIGENVALUE TO POSITION KS.
C
            IF( K.NE.KS )
     $         CALL GTREXC( COMPQ, N, T, LDT, Q, LDQ, K, KS, IERR )
         END IF
   20 CONTINUE
C
      IF( WANTS ) THEN
C
C        SOLVE THE SYLVESTER EQUATION FOR R:
C
C           T11*R - R*T22 = SCALE*T12
C
         CALL GLACPY( 'F', N1, N2, T( 1, N1+1 ), LDT, WORK, N1 )
         CALL GTRSYL( 'N', 'N', -1, N1, N2, T, LDT, T( N1+1, N1+1 ),
     $                LDT, WORK, N1, SCALE, IERR )
C
C        ESTIMATE THE RECIPROCAL OF THE CONDITION NUMBER OF THE CLUSTER
C        OF EIGENVALUES.
C
         RNORM = GLANGE( 'F', N1, N2, WORK, N1, RWORK )
         IF( RNORM.EQ.ZERO ) THEN
            S = ONE
         ELSE
            S = SCALE / ( SQRT( SCALE*SCALE / RNORM+RNORM )*
     $          SQRT( RNORM ) )
         END IF
      END IF
C
      IF( WANTSP ) THEN
C
C        ESTIMATE SEP(T11,T22).
C
         EST = ZERO
         KASE = 0
   30    CONTINUE
         CALL GLACON( NN, WORK( NN+1 ), WORK, EST, KASE )
         IF( KASE.NE.0 ) THEN
            IF( KASE.EQ.1 ) THEN
C
C              SOLVE T11*R - R*T22 = SCALE*X.
C
               CALL GTRSYL( 'N', 'N', -1, N1, N2, T, LDT,
     $                      T( N1+1, N1+1 ), LDT, WORK, N1, SCALE,
     $                      IERR )
            ELSE
C
C              SOLVE T11'*R - R*T22' = SCALE*X.
C
               CALL GTRSYL( 'C', 'C', -1, N1, N2, T, LDT,
     $                      T( N1+1, N1+1 ), LDT, WORK, N1, SCALE,
     $                      IERR )
            END IF
            GO TO 30
         END IF
C
         SEP = SCALE / EST
      END IF
C
   40 CONTINUE
C
C     COPY REORDERED EIGENVALUES TO W.
C
      DO 50 K = 1, N
         W( K ) = T( K, K )
   50 CONTINUE
 1000 CONTINUE
C
C     END OF GTRSEN
C
      END
