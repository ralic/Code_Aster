      REAL*8 FUNCTION CSEVL (X, CS, N)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF UTILIFOR  DATE 03/07/2012   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C
C UTILISEE SOUS NT POUR L'EVALUATION DE LA FONCTION D'ERREUR
C ERFC (PROVIENT DE LA BIBLIOTHEQUE SLATEC)
C
C***BEGIN PROLOGUE  CSEVL
C***PURPOSE  EVALUATE A CHEBYSHEV SERIES.
C***LIBRARY   SLATEC (FNLIB)
C***CATEGORY  C3A2
C***TYPE      DOUBLE PRECISION (CSEVL-S, DCSEVL-D)
C***KEYWORDS  CHEBYSHEV SERIES, FNLIB, SPECIAL FUNCTIONS
C***AUTHOR  FULLERTON, W., (LANL)
C***DESCRIPTION
C
C  EVALUATE THE N-TERM CHEBYSHEV SERIES CS AT X.  ADAPTED FROM
C  A METHOD PRESENTED IN THE PAPER BY BROUCKE REFERENCED BELOW.
C
C       INPUT ARGUMENTS --
C  X    VALUE AT WHICH THE SERIES IS TO BE EVALUATED.
C  CS   ARRAY OF N TERMS OF A CHEBYSHEV SERIES.  IN EVALUATING
C       CS, ONLY HALF THE FIRST COEFFICIENT IS SUMMED.
C  N    NUMBER OF TERMS IN ARRAY CS.
C
C***REFERENCES  R. BROUCKE, TEN SUBROUTINES FOR THE MANIPULATION OF
C                 CHEBYSHEV SERIES, ALGORITHM 446, COMMUNICATIONS OF
C                 THE A.C.M. 16, (1973) PP. 254-256.
C               L. FOX AND I. B. PARKER, CHEBYSHEV POLYNOMIALS IN
C                 NUMERICAL ANALYSIS, OXFORD UNIVERSITY PRESS, 1968,
C                 PAGE 56.
C***END PROLOGUE  DCSEVL
      IMPLICIT NONE
      REAL*8 B0, B1, B2, CS(*), ONEPL, TWOX, X, R8PREM
      LOGICAL FIRST
      SAVE FIRST, ONEPL
C-----------------------------------------------------------------------
      INTEGER I ,N ,NI 
C-----------------------------------------------------------------------
      DATA FIRST /.TRUE./
C***FIRST EXECUTABLE STATEMENT  DCSEVL
      IF (FIRST) ONEPL = 1.0D0 + R8PREM()
      FIRST = .FALSE.
      CALL ASSERT(N .GE. 1)
      CALL ASSERT(N .LE. 1000)
      CALL ASSERT(ABS(X) .LE. ONEPL)
C
      B1 = 0.0D0
      B0 = 0.0D0
      TWOX = 2.0D0*X
      DO 10 I = 1,N
         B2 = B1
         B1 = B0
         NI = N + 1 - I
         B0 = TWOX*B1 - B2 + CS(NI)
   10 CONTINUE
C
      CSEVL = 0.5D0*(B0-B2)
C
      END
