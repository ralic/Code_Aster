      SUBROUTINE PBFLSO(UMOY,RHOF,RMOY,LONG,ICOQ,IMOD,NBM,RKIP,TCOEF,
     &                  S1,S2,HARM,LAMBDA,KCALCU,PASSAG,CONDIT,GAMMA,D,
     &                  YSOL)
      IMPLICIT REAL*8 (A-H,O-Z)
C-----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGELINE  DATE 21/11/97   AUTEUR D6BHHJP J.P.LEFEBVRE 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C-----------------------------------------------------------------------
C COUPLAGE FLUIDELASTIQUE, CONFIGURATIONS DU TYPE "COQUE_COAX"
C RESOLUTION DU PROBLEME FLUIDE INSTATIONNAIRE : CALCUL DU VECTEUR
C SOLUTION T(UI*,VI*,PI*) LE LONG DES STRUCTURES
C APPELANT : PBFLUI
C-----------------------------------------------------------------------
C  IN : UMOY   : VITESSE DE L'ECOULEMENT MOYEN
C  IN : RHOF   : MASSE VOLUMIQUE DU FLUIDE
C  IN : RMOY   : RAYON MOYEN
C  IN : LONG   : LONGUEUR DU DOMAINE DE RECOUVREMENT DES DEUX COQUES
C  IN : ICOQ   : INDICE CARACTERISANT LA COQUE SUR LAQUELLE ON TRAVAILLE
C                ICOQ=1 COQUE INTERNE  ICOQ=2 COQUE EXTERNE
C  IN : IMOD   : INDICE DU MODE CONSIDERE
C  IN : NBM    : NOMBRE DE MODES PRIS EN COMPTE POUR LE COUPLAGE
C  IN : RKIP   : ORDRE DE COQUE DU MODE CONSIDERE, PONDERE PAR LA VALEUR
C                MOYENNE DU PROFIL DE PRESSION
C  IN : TCOEF  : TABLEAU DES COEFFICIENTS DES DEFORMEES AXIALES
C  IN : S1     : PARTIE REELLE     DE LA FREQUENCE COMPLEXE
C  IN : S2     : PARTIE IMAGINAIRE DE LA FREQUENCE COMPLEXE
C  IN : HARM   : VECTEUR DE TRAVAIL
C  IN : LAMBDA : VALEURS PROPRES DE L'OPERATEUR DIFFERENTIEL
C  IN : KCALCU : MATRICE RECTANGULAIRE A COEFFICIENTS CONSTANTS
C                PERMETTANT DE CALCULER UNE SOLUTION PARTICULIERE DU
C                PROBLEME FLUIDE INSTATIONNAIRE, LORSQUE UMOY > 0
C  IN : PASSAG : MATRICE DONT LES COLONNES SONT LES VECTEURS PROPRES DE
C                L'OPERATEUR DIFFERENTIEL
C  IN : CONDIT : COEFFICIENTS DE PRECONDITIONNEMENT
C  IN : GAMMA  : COEFFICIENTS DE LA COMBINAISON LINEAIRE DONNANT LA
C                SOLUTION GENERALE DU PROBLEME FLUIDE INSTATIONNAIRE
C                (DECOMPOSITION SUR UNE FAMILLE D'EXPONENTIELLES)
C                LORSQUE UMOY > 0
C  IN : D      : COEFFICIENTS DE LA COMBINAISON LINEAIRE DONNANT LA
C                PRESSION PERTURBEE (DECOMPOSITION SUR UNE FAMILLE
C                DE FONCTIONS EXPONENTIELLES REELLES ET COMPLEXES)
C                LORSQUE UMOY = 0
C OUT : YSOL   : TABLEAU SOLUTION (VECTEUR T(UI*,VI*,PI*) TABULE EN Z)
C-----------------------------------------------------------------------
C
      REAL*8       UMOY,RHOF,RMOY,LONG
      INTEGER      ICOQ,IMOD,NBM
      REAL*8       RKIP,TCOEF(10,NBM),S1,S2,HARM(6)
      COMPLEX*16   LAMBDA(3),KCALCU(3,4),PASSAG(3,3)
      REAL*8       CONDIT(3)
      COMPLEX*16   GAMMA(3)
      REAL*8       D(6)
      COMPLEX*16   YSOL(3,101)
C
      REAL*8       LN
      COMPLEX*16   PBFLKZ
      COMPLEX*16   SOMM1,SOMM2
C
C-----------------------------------------------------------------------
C
      ITAB = 0
      IF (ICOQ.EQ.2) ITAB = 5
      LN = TCOEF(1+ITAB,IMOD)
      DZ = LONG/100.D0
C
      IF (UMOY .LT. 1.D-5) THEN
C
        DO 10 K = 1,101
          Z = DBLE(K-1)*DZ
          SOMM1 = DCMPLX(0.D0,0.D0)
          U = (LN/LONG)*Z
          HARM(1) = DBLE(COS(U))
          HARM(2) = DBLE(SIN(U))
          HARM(3) = DBLE(COSH(U))
          HARM(4) = DBLE(SINH(U))
          V = -1.D0*(RKIP/RMOY)*(LONG-Z)
          HARM(5) = DBLE(EXP(V))
          V = -1.D0*(RKIP/RMOY)*Z
          HARM(6) = DBLE(EXP(V))
          DO 11 M1 = 1,6
            SOMM1 = SOMM1 + D(M1)*HARM(M1)
  11      CONTINUE
          YSOL(3,K) = SOMM1
  10    CONTINUE
C
      ELSE
C
        DO 20 K = 1,101
          Z = DBLE(K-1)*DZ
          DO 21 M1 = 1,3
            SOMM2 = DCMPLX(0.D0,0.D0)
            DO 22 M2 = 1,3
              SOMM2 = SOMM2 + PASSAG(M1,M2) * GAMMA(M2)
     &              * DCMPLX(EXP(LAMBDA(M2)*(Z-CONDIT(M2)*LONG)))
  22        CONTINUE
            YSOL(M1,K) = PBFLKZ(M1,Z,LONG,LN,KCALCU) + SOMM2
  21      CONTINUE
  20    CONTINUE
C
      ENDIF
C
      END
