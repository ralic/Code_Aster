      SUBROUTINE MEFCEN(CAELEM,IEQUIV,NBCYL,NBZ,IROT,NUMNOG,
     &                  NBNOG,NUMMAG,NUMGRP,COOR,CENT,REQ,
     &                  XINT,YINT,ZINT,RINT,NBGRP)
      IMPLICIT REAL*8 (A-H,O-Z)
C
      INTEGER      IEQUIV,NBCYL,NUMNOG(*),NBNOG(*),NUMMAG(*)
      INTEGER      NUMGRP(*),IROT(3),IDECAL,ITAB,NBGRP
      REAL*8       XINT(NBCYL),YINT(NBCYL),ZINT(NBZ,NBGRP),COOR(*)
      REAL*8       CENT(2*NBCYL),REQ(NBGRP),RINT(NBCYL)
      CHARACTER*19 CAELEM
C ----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGELINE  DATE 29/09/2006   AUTEUR VABHHTS J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C ----------------------------------------------------------------------
C     RECUPERATION DANS LES CARTES ELEMENTS DES RAYONS DANS LE CAS OU
C     IL N Y A PAS DE GROUPES D EQUIVALENCE ET DANS LES DONNEES DANS
C     LE CAS OU IL Y A DES GROUPES D EQUIVALENCE - RECUPERATION DES
C     COORDONNEES DES CENTRES DES CYLINDRES REELS DANS LES DONNEES
C     OPERATEUR APPELANT : OP0144 , FLUST3
C ----------------------------------------------------------------------
C     OPTION DE CALCUL   : CALC_FLUI_STRU , CALCUL DES PARAMETRES DE
C     COUPLAGE FLUIDE-STRUCTURE POUR UNE CONFIGURATION DE TYPE "FAISCEAU
C     DE TUBES SOUS ECOULEMENT AXIAL"
C ----------------------------------------------------------------------
C IN  : CAELEM : NOM DU CONCEPT DE TYPE CARA_ELEM
C IN  : IEQUIV : INDICE D EXISTANCE DES GROUPES D EQUIVALENCE
C IN  : NBCYL  : NOMBRE DE CYLINDRES REELS
C IN  : NBZ    : NOMBRE DE NOEUDS DE LA DISCRETISATION AXIALE
C IN  : IROT   : INDICE DE PERMUTATION CIRCULAIRE DU CHANGEMENT DE
C                REPERE
C IN  : NUMNOG : TABLEAU DES ADRESSES DES NUMEROS DES NOEUDS DES
C                CYLINDRES
C IN  : NBNOG  : TABLEAU DU NOMBRE DE NOEUDS DE CHAQUE CYLINDRE
C IN  : NUMMAG : TABLEAU DES ADRESSES DES NUMEROS DES MAILLES DES
C                CYLINDRES
C IN  : NUMGRP : INDICES DES GROUPES D EQUIVALENCE
C IN  : COOR   : COORDONNEES DES NOEUDS DU MAILLAGE
C IN  : CENT   : COORDONNEES DES CENTRES DONNES DANS LA COMMANDE
C IN  : REQ    : RAYONS DES CYLINDRES DONNES DANS LA COMMANDE
C OUT : XINT   : COORDONNEES 'X' DANS LE REPERE AXIAL DES CENTRES DES
C                CYLINDRES
C OUT : YINT   : COORDONNEES 'Y' DANS LE REPERE AXIAL DES CENTRES DES
C                CYLINDRES
C OUT : ZINT   : COORDONNEES 'Z' DANS LE REPERE AXIAL DES NOEUDS DES
C                CYLINDRES
C OUT : RINT   : RAYONS DES CYLINDRES
C ----------------------------------------------------------------------
C     ----- DEBUT COMMUNS NORMALISES  JEVEUX  --------------------------
      INTEGER          ZI
      COMMON  /IVARJE/ ZI(1)
      REAL*8           ZR
      COMMON  /RVARJE/ ZR(1)
      COMPLEX*16       ZC
      COMMON  /CVARJE/ ZC(1)
      LOGICAL          ZL
      COMMON  /LVARJE/ ZL(1)
      CHARACTER*8      ZK8
      CHARACTER*16            ZK16
      CHARACTER*24                    ZK24
      CHARACTER*32                            ZK32
      CHARACTER*80                                    ZK80
      COMMON  /KVARJE/ ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
      CHARACTER*32     JEXNUM, JEXNOM
C     -----  FIN  COMMUNS NORMALISES  JEVEUX  --------------------------
C     ------------------------------------------------------------------
      INTEGER      I,J,IRET,INDIK8,RANGR1
      CHARACTER*24 CARAD, CARAV, CARAL
      CHARACTER*19 LIGRMO,CARTE,CARSD,KBID
      CHARACTER*3  NOTE
      CHARACTER*1  K1BID
C     ------------------------------------------------------------------
C
C
      CALL JEMARQ()
      EPSIT = 1.D-5
C
C
C --- COORDONNEES DES CENTRES DES CYLINDRES
C --- CAS OU IL N Y A PAS DE GROUPES D EQUIVALENCE
C
      IF(IEQUIV.EQ.0) THEN
         DO 20 I = 1,NBCYL
            NUMNO1 = ZI(NUMNOG(I))
            XINT(I) = COOR((NUMNO1-1)*3 + IROT(1))
            YINT(I) = COOR((NUMNO1-1)*3 + IROT(2))
            ZINT(1,NUMGRP(I)) = COOR((NUMNO1-1)*3 + IROT(3))
            DO 10 J = 2,NBNOG(I)
               NUMNO2 = ZI(NUMNOG(I)+J-1)
               IF(ABS( COOR((NUMNO1-1)*3 + IROT(1))
     &                - COOR((NUMNO2-1)*3 + IROT(1)) ).GT.EPSIT.OR.
     &            ABS( COOR((NUMNO1-1)*3 + IROT(2))
     &                - COOR((NUMNO2-1)*3 + IROT(2)) ).GT.EPSIT) THEN
                  WRITE(NOTE(1:3),'(I3.3)') I
                  CALL U2MESK('F','ALGELINE_73',1,NOTE)
               ENDIF
               ZINT(J,NUMGRP(I)) = COOR((NUMNO2-1)*3 + IROT(3))
  10        CONTINUE
  20     CONTINUE
C
C
C --- COORDONNEES DES CENTRES DES CYLINDRES
C --- CAS OU IL Y A DES GROUPES D EQUIVALENCE
C
      ELSE IF(IEQUIV.EQ.1) THEN
         DO 40 I = 1,NBCYL
            XINT(I) = CENT(2*(I-1)+1)
            YINT(I) = CENT(2*(I-1)+2)
            DO 30 J = 1,NBNOG(NUMGRP(I))
               NUMNO2 = ZI(NUMNOG(NUMGRP(I))+J-1)
               ZINT(J,NUMGRP(I)) = COOR((NUMNO2-1)*3 + IROT(3))
  30        CONTINUE
  40     CONTINUE
      ENDIF
C
C
C --- RAYONS DES CYLINDRES
C --- CAS OU IL Y A DES GROUPES D EQUIVALENCE
C
      IF(IEQUIV.EQ.1) THEN
         DO 50 I = 1,NBCYL
            RINT(I) = REQ(NUMGRP(I))
  50     CONTINUE
C
C --- RAYONS DES CYLINDRES
C --- CAS OU IL N Y A PAS DES GROUPES D EQUIVALENCE
C
      ELSE IF(IEQUIV.EQ.0) THEN
CCC ON RECUPERE LA CARTE ET ON LA TRANSFORME EN CHAMELEM_S
         CARTE=CAELEM(1:8)//'.CARGEOPO'
         CARSD='&&MEFCEN.CARGEOPO'
         CALL CARCES(CARTE,'ELEM',' ','G',CARSD,IRET)
         IF (IRET.NE.0) THEN
           CALL U2MESS('F','ALGELINE_74')
         ENDIF

C --- RECUPERATION DE LA GRANDEUR (ICI R1)  ---
C --- REFERENCEE PAR LA CARTE CARGEOPO           ---
C
        CALL JEVEUO(CARSD//'.CESC','L',ICESC)
        CALL JEVEUO(CARSD//'.CESD','L',ICESD)
        CALL JEVEUO(CARSD//'.CESL','L',ICESL)
        CALL JEVEUO(CARSD//'.CESV','L',ICESV)
C
        CALL JEVEUO(CARTE//'.DESC','L',IDESC)
        CALL JEVEUO(JEXNOM('&CATA.GD.NOMCMP','CAGEPO'),'L',ICMP)
C
        CALL JELIRA(JEXNUM('&CATA.GD.NOMCMP',ZI(IDESC)),
     &       'LONMAX',NPMAX,KBID)
C
         RANGR1= INDIK8(ZK8(ICMP),'R1      ',1,NPMAX)
C
C ---    DEBUT DE LA BOUCLE SUR LES CYLINDRES
C ---    ON RECHERCHE LA MAILLE ASSOCIEE AU PREMIER NOEUDS DE CHAQUE
C ---    CYLINDRE, ET ON LIT LA CARTE ELEMENT QUI LUI CORRESPOND
         DO 160 I = 1,NBCYL
            NUMMA = ZI(NUMMAG(I))
C
            CALL CESEXI('C',ICESD,ICESL,NUMMA,1,1,RANGR1,IAD)
            IF(IAD.GT.0) THEN
C ---       RECUPERATION DU RAYON DE LA PREMIERE MAILLE DE CHAQUE
C ---       CYLINDRE
C
               RINT(I)=ZR(ICESV-1+ABS(IAD))
C
            ELSE
              CALL U2MESS('F','ALGELINE_75')
            ENDIF
  160    CONTINUE
      ENDIF
C
C
      CALL JEDETC('V','&&MEFCEN',1)
      CALL JEDETC('V','&&MEFCEN.CARGEOPO.CESC',1)
      CALL JEDETC('V','&&MEFCEN.CARGEOPO.CESD',1)
      CALL JEDETC('V','&&MEFCEN.CARGEOPO.CESL',1)
      CALL JEDETC('V','&&MEFCEN.CARGEOPO.CESV',1)

      CALL JEDEMA()
      END
