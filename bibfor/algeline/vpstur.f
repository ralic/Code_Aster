      SUBROUTINE VPSTUR(LMATK,VALSHI,LMATM,LMATSH,MANTIS,EXPO,
     &                  PIVOT,IER,SOLVEU,CALDET,CALFAC)
      IMPLICIT NONE
      INCLUDE 'jeveux.h'

      REAL*8          VALSHI,MANTIS
      INTEGER         LMATK,LMATM,LMATSH,EXPO,PIVOT,IER
      CHARACTER*19    SOLVEU
C     ------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGELINE  DATE 09/11/2012   AUTEUR DELMAS J.DELMAS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C     ------------------------------------------------------------------
C     EFFECTUE L'OPERATION DE STURM
C        1) COMBINAISON LINEAIRE MSH =  K - W * M    (W ETANT LE SHIFT)
C        2) DECOMPOSITION DE  MSH
C        3) NOMBRE DE PIVOT NEGATIF
C        4) CALCUL DU DETERMINANT
C     ------------------------------------------------------------------
C IN  VALSHI : R8 : VALEUR DU DECALAGE
C IN  LMATK  : IS : ADRESSE ATTRIBUT MATRICE K
C IN  LMATM  : IS : ADRESSE ATTRIBUT MATRICE M
C IN  LMATSH : IS : ADRESSE ATTRIBUT MATRICE SHIFTEE
C OUT MANTIS : R8 : MANTISSE DU DETERMINANT
C OUT EXPO   : IS : EXPOSANT DU DETERMINANT
C OUT PIVOT  : IS : NOMBRE DE TERMES DIAGONAUX NEGATIFS
C OUT IER    : IS : CODE RETOUR  /= 0 ==> LE SHIFT EST UNE VALEUR PROPRE
C IN  SOLVEU : K19 : SD SOLVEUR POUR PARAMETRER LE SOLVEUR LINEAIRE
C IN  CALDET : LOG : SI TRUE ON CALCULE LE DETERMINANT, SI FALSE ON NE
C                     LE CALCULE PAS (GAIN TEMPS)
C IN  CALFAC : LOG : SI MUMPS: SI TRUE, ON GARDE LES TERMES DE LA FACTO
C                    RISEE, SI FALSE, ON NE LES GARDE PAS (GAIN ESPACE)
C     ------------------------------------------------------------------
C
C
      INTEGER          IRET,NPVNEG,ISLVK,ISLVI,IOLD,IOLD2
      REAL*8           FREQOM,VALR
      COMPLEX*16       CBID
      CHARACTER*16     RBID,NOMCMD
      CHARACTER*19     MATPRE,MATASS
      CHARACTER*24     METRES
      LOGICAL          CALDET,CALFAC
C     ------------------------------------------------------------------
C
C-----------------------------------------------------------------------
C-----------------------------------------------------------------------
      CALL JEMARQ()

C     --- INITIALISATION ---
      CALL JEVEUO(SOLVEU//'.SLVK','L',ISLVK)
      METRES=ZK24(ISLVK)
      CALL JEVEUO(SOLVEU//'.SLVI','E',ISLVI)


C ---- OPTIMISATION MUMPS VIA CALFAC/CALDET: PART 1/2
C ---- SI CALFAC=.FALSE., ON NE STOCKE PAS LES FACTEURS, SEUL LE
C ---- CARACTERE SINGULIER ET LE NBRE DE TERMES <0 DE LA DIAGONALE NOUS
C ---- INTERESSENT
C ---- SI CALDET=.TRUE.: A PARTIR DE MUMPS.4.10.0 ON CALCULE LE DET
      IOLD=-9999
      IOLD2=-9999
      IF (METRES(1:5).EQ.'MUMPS') THEN
        IF (.NOT.CALFAC) THEN
          IOLD=ZI(ISLVI-1+4)
          ZI(ISLVI-1+4)=1
        ENDIF
        IF (CALDET) THEN
          IOLD2=ZI(ISLVI-1+5)
          ZI(ISLVI-1+5)=1
        ENDIF
      ENDIF

C     --- DECALAGE SPECTRAL  K - W * M    (W ETANT LE SHIFT) ---
      CALL VPSHIF(LMATK,VALSHI,LMATM,LMATSH)
C
C     --- FACTORISATION LDLT DE LA MATRICE SHIFTEE---
      IER=0
C
      MATPRE=' '
      MATASS=ZK24(ZI(LMATSH+1))
      CALL PRERES(SOLVEU,'V',IRET,MATPRE,MATASS,NPVNEG,2)
C
      IF (IRET.GE.1) IER = 1
      IF (IRET.GT.1) THEN
        CALL GETRES(RBID,RBID,NOMCMD)
        VALR = FREQOM(VALSHI)
        CALL U2MESG('A','ALGELINE5_27',0,' ',0,0,1,VALR)
      ENDIF
      PIVOT = - NPVNEG
C
C ---  CALCUL OPTIONNEL DU DETERMINANT
C ---- OPTIMISATION VIA CALDET (SI MF OU LDLT OU MUMPS V4.10.0 ET PLUS)
      IF (CALDET) THEN
         IF ((METRES(1:10).NE.'MULT_FRONT').AND.
     &       (METRES(1:4).NE.'LDLT').AND.(METRES(1:5).NE.'MUMPS')) THEN
           CALL U2MESS('F','ALGELINE5_73')
         ELSE
           CALL MTDETE(1,METRES,LMATSH,MANTIS,EXPO,CBID)
         ENDIF
      ENDIF

C ---- OPTIMISATION MUMPSVIA CALFAC/CALDET: PART 2/2
C ---- ON REMET DANS LA SD_SOLVEUR.SLVI(4) L'ANCIENNE VALEUR
C ---- (NORMALEMENT LA VALEUR INITIALISEE PAR DEFAUT -9999).
       IF (METRES(1:5).EQ.'MUMPS') THEN
         IF (.NOT.CALFAC) ZI(ISLVI-1+4)=IOLD
         IF (CALDET) ZI(ISLVI-1+5)=IOLD2
       ENDIF

      CALL JEDEMA()
      END
