      SUBROUTINE PCFULL(N,ICPL,ICPC,ICPD,ICPLP,ICPCP,IND,LCA,IER)
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGELINE  DATE 03/07/2012   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C TOLE CRP_4
C+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
C                    S.P. PCFULL
C                    -----------
C+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
C BUT : CE SP CALCULE LES POINTEURS ICPLP,ICPCP
C ----  CORRESPONDANTS AU 'REMPLISSAGE' DE LA
C       MATRICE EN COURS DE FACTORISATION

C       VERSION GENERALE : MATRICE A STRUCTURE QUELCONQUE

C   PARAMETRES D'ENTREE:
C   -------------------

C   ICPL,ICPD,ICPC : LES POINTEURS ASSOCIES A LA MATRICE A FACTORISER

C   * ICPL(I) = ADRESSE DANS LE RANGEMENT DES COEFFICIENTS A(I,J)
C               DU DERNIER COEFFICIENT DE LA LIGNE I
C   * ICPC(K) = POUR K = ICPL(I-1)+1...ICPL(I) NUMEROS DES INDICES DE
C               COLONNE J, DES COEFFICIENTS A(I,J) DE LA LIGNE I
C               ( RANGES PAR ORDRE DE J CROISSANT)
C   * ICPD(I) = ADRESSE DANS LE RANGEMENT DES COEFFICIENTS A(I,J)
C               DU DERNIER COEFFICIENT DE LA LIGNE I AVEC A(I,J), J < I

C   IND         : TABLEAU UTILITAIRE
C   LCA         : TAILLE MAXIMALE ADMISE POUR LA MATRICE FACTORISEE

C   PARAMETRE DE SORTIE:
C   -------------------

C   ICPLP,ICPCP : LES POINTEURS ASSOCIES AU REMPLISSAGE

C   * ICPLP(I) = ADRESSE DANS LE RANGEMENT DES COEFFICIENTS A(I,J)
C               DU DERNIER COEFFICIENT DE REMPLISSAGE DE LA LIGNE I
C   * ICPCP(K) = POUR K = ICPL(I-1)+1...ICPL(I) NUMEROS DES INDICES DE
C               COLONNE J, DES COEFFICIENTS DE REMPLISSAGE DE LA LIGNE I
C               ( RANGES PAR ORDRE DE J CROISSANT)

C   ICPL,ICPC  : LES POINTEURS ASSOCIES A LA MATRICE FACTORISEE
C                ( REUNION DE ICPL ET ICPLP, ICPC ET ICPCP )
C   NZA        : NOMBRE DE COEFFICIENTS DE LA MATRICE FACTORISEE

C+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
      IMPLICIT NONE
      INTEGER*4 ICPC(*)
      INTEGER ICPL(0:N),ICPD(N)
      INTEGER ICPLP(0:N),ICPCP(*),IND(N)

C=======================================================================

C     ON EFFECTUE UNE FACTORISATION LOGIQUE DE LA MATRICE A, LIGNE PAR
C     LIGNE, SANS ACTUALISATION DU REMPLISSAGE : ON NE CALCULE PAS LES
C      COEFFICIENTS L(I,J) ET U(I,J), MAIS LEUR POSITION (I,J).

C     LES FORMULES DE FACTORISATION UTILISEES :

C (1) L(I,JJ) = [ A(I,JJ) - SOM[J=1,JJ-1] L(I,J).U(J,JJ) ] / U(JJ,JJ)
C      POUR I=1,2,.....N ET JJ=1,2,....I

C (2) U(JJ,I) =   A(JJ,I) - SOM[J=1,JJ-1] L(JJ,J).U(JJ,J)
C      POUR I=1,2,.....N ET JJ=1,2,....I

C     ON PROCEDE AU CALCUL DES L(I,JJ) ET U(JJ,I) : IL Y A REMPLISSAGE
C     SI UN AU MOINS DES PRODUITS DE LA SOMME EST NON NUL

C     => POUR CHACUN DES L(I,J) NON NUL DE LA LIGNE I (BOUCLE 3: J < I)
C        ON RECHERCHE S'IL EXISTE UN U(J,JJ) NON NUL  (BOUCLE 4: J < JJ)
C        SI ON EN TROUVE UN, ON TESTE L'EXISTENCE DU COEFFICIENT A(I,JJ)
C        A L'AIDE DU TABLEAU INDIC. ON CREE UN COEFFICIENT DE REMPLISSAG
C E
C        SI NECESSAIRE.

C     => POUR CHACUN DES U(J,I) NON NUL DE LA LIGNE I (BOUCLE 3: J < I)
C        ON RECHERCHE S'IL EXISTE UN L(JJ,J) NON NUL  (BOUCLE 4: J < JJ)
C        SI ON EN TROUVE UN, ON TESTE L'EXISTENCE DU COEFFICIENT A(I,JJ)
C        A L'AIDE DU TABLEAU INDIC. ON CREE UN COEFFICIENT DE REMPLISSAG
C E
C        SI NECESSAIRE.

C    REMARQUE :  DANS LES FORMULES (1) ET (2) LES SOMMES SONT CALCULEES
C    --------    AVEC A(K,L) A LA PLACE DE L(K,L) OU U(K,L) CE QUI EST
C                COHERENT PUISQU'ON N'ACTUALISE PAS LE REMPLISSAGE

C=======================================================================

C     INITIALISATION DU TABLEAU INDIC
C     -------------------------------
C-----------------------------------------------------------------------
      INTEGER I ,IC1 ,IC2 ,IER ,ISTOP ,J ,JJ 
      INTEGER K ,K1 ,K2 ,KP1 ,KP2 ,L ,LCA 
      INTEGER N ,NCREMX ,NZERO 
C-----------------------------------------------------------------------
      DO 10 I=1,N
        IND(I)=0
   10 CONTINUE
      IC1=0
      IC2=0
      K1=1


C     FACTORISATION LOGIQUE : LIGNE PAR LIGNE
C     ---------------------------------------
      DO 50 I=1,N

C       TEST DE DEPASSEMENT DE DIMENSION (PROTECTION DES TABLEAUX)
C       REMARQUE JP : LA FICHE 12292 MONTRE QUE CE TEST EST INSUFFISANT
C       POUR NE PAS DEBORDER DU TABLEAU ICPCP, J'AI DU AJOUTER UN AUTRE
C       TEST AVANT ICPCP(IC1)=JJ (VOIR CI-DESSOUS)
        NCREMX=I-ICPC(K1)
        IF (IC1+NCREMX.GT.LCA) THEN
          ISTOP=I
          GOTO 90
        ENDIF

C       MISE A JOUR DU TABLEAU INDIC
        K2=ICPL(I)
        DO 20 K=K1,K2
          J=ICPC(K)
          IND(J)=I
   20   CONTINUE
        IND(I)=I

C       RECHERCHE DANS LA LIGNE I DES L(I,J) NON NULS
        DO 40 K=K1,ICPD(I)
          J=ICPC(K)
C         RECHERCHE DANS LA LIGNE J DES U(J,JJ) NON NULS
          DO 30 L=ICPD(J)+1,ICPL(J)
            JJ=ICPC(L)
C           LE COEFFICIENT L(I,JJ) OU U(JJ,I) EXISTE-T-IL ?
            IF (IND(JJ).NE.I) THEN
C             NON ==> CREATION D'UN COEFFICIENT DE REMPLISSAGE
              IC1=IC1+1
C             STOCKAGE DE L'INDICE DE COLONNE DU COEFFICIENT LU(I,JJ)
              IF (IC1.GT.LCA) THEN
                ISTOP=I
                GOTO 90
              ENDIF
              ICPCP(IC1)=JJ
C             MISE A JOUR DU TABLEAU INDIC
              IND(JJ)=I
            ENDIF
   30     CONTINUE
   40   CONTINUE

C       RECLASSEMENT DES INDICES DE COLONNE PAR ORDRE CROISSANT
        CALL PCTRII(ICPCP(IC2+1),IC1-IC2)

C       MISE A JOUR DU POINTEUR ICPLP
        ICPLP(I)=IC1
        IC2=IC1
        K1=K2+1
   50 CONTINUE
      ICPLP(0)=0


C     TEST DE DEPASSEMENT DE DIMENSION (PROTECTION DES TABLEAUX)
      K1=ICPL(N)
      KP1=ICPLP(N)
      NZERO=K1+KP1
      IF (NZERO.GT.LCA) THEN
        IER=NZERO
        GOTO 140
      ENDIF


C     CREATION DES TABLEAUX ICPL ET ICPC
C     POUR LA MATRICE FACTORISEE : REUNION DES TABLEAUX ICPC ET ICPCP
C     ---------------------------------------------------------------
      K=NZERO
      DO 80 I=N,1,-1
        ICPL(I)=K
        KP2=ICPLP(I-1)
        K2=ICPL(I-1)
   60   CONTINUE

        IF (K1.GT.K2) THEN
C          LIGNE DE L EN COURS
          IF (KP1.GT.KP2) THEN
C           LIGNE DE COEF EN COURS
            IF (ICPC(K1).LT.ICPCP(KP1)) THEN
              ICPC(K)=ICPCP(KP1)
              KP1=KP1-1
            ELSE
              ICPC(K)=ICPC(K1)
              K1=K1-1
            ENDIF

          ELSE
C           LIGNE DE COEF FINIE
            ICPC(K)=ICPC(K1)
            K1=K1-1
          ENDIF

        ELSE
C         LIGNE DE L FINIE
          IF (KP1.GT.KP2) THEN
C           LIGNE DE COEF EN COURS
            ICPC(K)=ICPCP(KP1)
            KP1=KP1-1
          ELSE
            GOTO 70
          ENDIF
        ENDIF

        K=K-1
        GOTO 60

   70   CONTINUE
   80 CONTINUE

C     LE NOMBRE DE COEFFICIENTS DE LA MATRICE FACTORISEE
C     NZCA = NZERO
C     WRITE (6,*) ' FIN DU S-P FILLIN  TAILLE FACTORISEE= ',NZCA
      GOTO 140


   90 CONTINUE
C     DEPASSEMENT DE DIMENSION ON CALCULE IC1= PLACE A AJOUTER
      DO 130 I=ISTOP,N
        K2=ICPL(I)
        DO 100 K=K1,K2
          J=ICPC(K)
          IND(J)=I
  100   CONTINUE
        IND(I)=I
        DO 120 K=K1,ICPD(I)
          J=ICPC(K)
          DO 110 L=ICPD(J)+1,ICPL(J)
            JJ=ICPC(L)
            IF (JJ.GE.I)GOTO 110

            IF (IND(JJ).NE.I) THEN
C             NON ==> CREATION D'UN COEFFICIENT DE REMPLISSAGE
              IC1=IC1+1
              IND(JJ)=I
            ENDIF
  110     CONTINUE
  120   CONTINUE
        K1=K2+1
  130 CONTINUE


C     NZERO=TAILLE MAT INI. + TAILLE MAT REMPLIE
      NZERO=ICPL(N)+IC1
      IER=NZERO
  140 CONTINUE

 9000 FORMAT (' NIVEAU',I4,' REMPLISSAGE INTERMEDIAIRE',/,'ARRET DES C',
     &       'ALCULS PLACE MEMOIRE',I12,' INSUFFISANTE  LIGNE ',I9)
      END
