      SUBROUTINE PBFLU0(RHOF,HMOY,RMOY,LONG,ICOQ,IMOD,NBM,RKIP,TCOEF,D)
      IMPLICIT NONE
C-----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGELINE  DATE 03/07/2012   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C-----------------------------------------------------------------------
C COUPLAGE FLUIDELASTIQUE, CONFIGURATIONS DU TYPE "COQUE_COAX"
C RESOLUTION DU PROBLEME FLUIDE INSTATIONNAIRE DANS LE CAS PARTICULIER
C OU UMOY = 0
C APPELANT : PBFLUI
C-----------------------------------------------------------------------
C  IN : RHOF   : MASSE VOLUMIQUE DU FLUIDE
C  IN : HMOY   : JEU ANNULAIRE MOYEN
C  IN : RMOY   : RAYON MOYEN
C  IN : LONG   : LONGUEUR DU DOMAINE DE RECOUVREMENT DES DEUX COQUES
C  IN : ICOQ   : INDICE CARACTERISANT LA COQUE SUR LAQUELLE ON TRAVAILLE
C                ICOQ=1 COQUE INTERNE  ICOQ=2 COQUE EXTERNE
C  IN : IMOD   : INDICE DU MODE CONSIDERE
C  IN : NBM    : NOMBRE DE MODES PRIS EN COMPTE POUR LE COUPLAGE
C  IN : RKIP   : ORDRE DE COQUE DU MODE CONSIDERE, PONDERE PAR LA VALEUR
C                MOYENNE DU PROFIL DE PRESSION
C  IN : TCOEF  : TABLEAU DES COEFFICIENTS DES DEFORMEES AXIALES
C OUT : D      : COEFFICIENTS DE LA COMBINAISON LINEAIRE DONNANT LA
C                PRESSION PERTURBEE (DECOMPOSITION SUR UNE FAMILLE
C                DE FONCTIONS EXPONENTIELLES REELLES ET COMPLEXES)
C                LORSQUE UMOY = 0
C-----------------------------------------------------------------------
C
      REAL*8       RHOF,HMOY,RMOY,LONG
      INTEGER      ICOQ,IMOD,NBM
      REAL*8       RKIP,TCOEF(10,NBM),D(6)
C
      REAL*8       LN
C
C-----------------------------------------------------------------------
C
C-----------------------------------------------------------------------
      INTEGER ITAB 
      REAL*8 A1 ,A2 ,A3 ,A4 ,B1 ,B2 ,B3 
      REAL*8 B4 ,C1 ,C2 ,C3 ,C4 ,POIDS ,TOLE 
      REAL*8 U ,V ,VI1 ,VI2 ,X ,Y ,Z 

C-----------------------------------------------------------------------
      TOLE = 1.D-6
      ITAB = 0
      POIDS = -1.D0
      IF (ICOQ.EQ.2) THEN
        ITAB = 5
        POIDS = 1.D0
      ENDIF
      LN = TCOEF(1+ITAB,IMOD)
      A1 = TCOEF(2+ITAB,IMOD) * POIDS
      A2 = TCOEF(3+ITAB,IMOD) * POIDS
      A3 = TCOEF(4+ITAB,IMOD) * POIDS
      A4 = TCOEF(5+ITAB,IMOD) * POIDS
      B1 = TCOEF(2+ITAB,IMOD) / 2.D0
      B2 = TCOEF(3+ITAB,IMOD) / 2.D0
      B3 = TCOEF(4+ITAB,IMOD) / 2.D0
      B4 = TCOEF(5+ITAB,IMOD) / 2.D0
      C1 = A1/HMOY + B1/RMOY
      C2 = A2/HMOY + B2/RMOY
      C3 = A3/HMOY + B3/RMOY
      C4 = A4/HMOY + B4/RMOY
C
      U = -1.D0*RHOF*(RKIP**2)
      V = (LN/LONG)**2 + (RKIP/RMOY)**2
C
      D(1) = U*C1/V
      D(2) = U*C2/V
C
      V = -1.D0*(LN/LONG)**2 + (RKIP/RMOY)**2
      IF (DBLE(ABS(V)).LT.TOLE) CALL U2MESS('F','ALGELINE3_18')
      D(3) = U*C3/V
      D(4) = U*C4/V
C
      VI1 = D(1) + D(3)
      VI2 = D(1)*DBLE(COS(LN))  + D(2)*DBLE(SIN(LN))
     &    + D(3)*DBLE(COSH(LN)) + D(4)*DBLE(SINH(LN))
      X = -1.D0*RKIP*LONG/RMOY
      Y = DBLE(EXP(X))
      Z = 1.D0/(1.D0-Y*Y)
      D(5) = Z * (VI1*Y-VI2)
      D(6) = Z * (VI2*Y-VI1)
C
      END
