      SUBROUTINE BIJSOM(UMOY,RHOF,R1,R2,LONG,CF0,ICOQ,JCOQ,JMOD,NBM,RKI,
     &                  THETAI,THETAJ,TCOEF,YSOL,BIJ)
      IMPLICIT REAL*8 (A-H,O-Z)
C-----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGELINE  DATE 31/10/96   AUTEUR KXBADNG N.GAY 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C-----------------------------------------------------------------------
C COUPLAGE FLUIDELASTIQUE, CONFIGURATIONS DU TYPE "COQUE_COAX"
C CALCUL DU TERME (I,J) DE LA MATRICE DE TRANSFERT DES FORCES
C FLUIDELASTIQUES PROJETEE SUR LA BASE MODALE DES STRUCTURES
C CALCUL DE L'INTEGRALE DONNANT BIJ APRES RESOLUTION DU PROBLEME FLUIDE
C INSTATIONNAIRE
C APPELANT : BIJMOC, BMOCCA
C-----------------------------------------------------------------------
C  IN : UMOY   : VITESSE DE L'ECOULEMENT MOYEN
C  IN : RHOF   : MASSE VOLUMIQUE DU FLUIDE
C  IN : R1     : RAYON REPERANT LA SURFACE DE LA STRUCTURE INTERNE
C  IN : R2     : RAYON REPERANT LA SURFACE DE LA STRUCTURE EXTERNE
C  IN : LONG   : LONGUEUR DU DOMAINE DE RECOUVREMENT DES DEUX COQUES
C  IN : CF0    : COEFFICIENT DE FROTTEMENT VISQUEUX
C  IN : ICOQ   : INDICE CARACTERISTIQUE DE LA COQUE EN MOUVEMENT POUR LE
C                MODE I, MOUVEMENT POUR LEQUEL ON A RESOLU LE PROBLEME
C                FLUIDE (ICOQ=1 COQUE INTERNE, ICOQ=2 COQUE EXTERNE)
C  IN : JCOQ   : INDICE CARACTERISTIQUE DE LA COQUE EN MOUVEMENT POUR LE
C                MODE J, COQUE SUR LAQUELLE ON INTEGRE LA FORCE FLUIDE
C                SURFACIQUE (JCOQ=1 COQUE INTERNE, JCOQ=2 COQUE EXTERNE)
C  IN : JMOD   : INDICE DU MODE J
C  IN : NBM    : NOMBRE DE MODES PRIS EN COMPTE POUR LE COUPLAGE
C  IN : RKI    : ORDRE DE COQUE COMMUN AUX DEUX MODES CONSIDERES
C  IN : THETAI : DEPHASAGE POUR LE MODE I
C  IN : THETAJ : DEPHASAGE POUR LE MODE J
C  IN : TCOEF  : TABLEAU DES COEFFICIENTS DES DEFORMEES AXIALES
C  IN : YSOL   : TABLEAU SOLUTION (VECTEUR T(UI*,VI*,PI*) TABULE EN Z)
C                DU PROBLEME FLUIDE INSTATIONNAIRE, RESOLU EN
C                CONSIDERANT UN MOUVEMENT DE L'UNE DES COQUES SUIVANT
C                LE MODE I
C OUT : BIJ    : TERME (I,J) DE LA MATRICE B, COMPLEXE
C-----------------------------------------------------------------------
C
      REAL*8       UMOY,RHOF,R1,R2,LONG,CF0
      INTEGER      ICOQ,JCOQ,JMOD,NBM
      REAL*8       RKI,THETAI,THETAJ,TCOEF(10,NBM)
      COMPLEX*16   YSOL(3,101),BIJ
C
      REAL*8       DEFAXE
      COMPLEX*16   P,V,SOM
C
C-----------------------------------------------------------------------
C
      PI = R8PI()
      DZ = LONG/100.D0
      SOM = DCMPLX(0.D0,0.D0)
C
      RAYON = R1
      IF (JCOQ.EQ.2) RAYON = R2
C
      CALL PROFPR(ICOQ,RKI,R1,R2,COEPR1,COEPR2,WPR)
      IF (JCOQ.EQ.1) THEN
        COEP = COEPR1
      ELSE
        COEP = -1.D0*COEPR2
      ENDIF
C
C-----1.SI UMOY NULLE (STRUCTURES EN EAU AU REPOS)
C
      IF (UMOY .LT. 1.D-5) THEN
C
C-----1.1.CONTRIBUTION DU PREMIER POINT DANS LE CALCUL DE L INTEGRALE
C
        P  = YSOL(3,1)
        WJ = DEFAXE(JCOQ,JMOD,0.D0,LONG,NBM,TCOEF)
        SOM = SOM + 0.5D0*P*COEP*WJ
C
C-----1.2.CONTRIBUTIONS DES POINTS INTERMEDIAIRES
C
        DO 10 K = 2,100
          P  = YSOL(3,K)
          Z  = DBLE(K-1)*DZ
          WJ = DEFAXE(JCOQ,JMOD,Z,LONG,NBM,TCOEF)
          SOM = SOM + P*COEP*WJ
  10    CONTINUE
C
C-----1.3.CONTRIBUTION DU DERNIER POINT
C
        P  = YSOL(3,101)
        WJ = DEFAXE(JCOQ,JMOD,LONG,LONG,NBM,TCOEF)
        SOM = SOM + 0.5D0*P*COEP*WJ
C
C-----2.SINON (STRUCTURES SOUS ECOULEMENT)
C
      ELSE
C
C-----2.1.CONTRIBUTION DU PREMIER POINT
C
        P  = YSOL(3,1)
        V  = YSOL(2,1)
        WJ = DEFAXE(JCOQ,JMOD,0.D0,LONG,NBM,TCOEF)
        SOM = SOM + 0.5D0*(P*COEP+0.5D0*RHOF*CF0*UMOY*V)*WJ
C
C-----2.2.CONTRIBUTIONS DES POINTS INTERMEDIAIRES
C
        DO 20 K = 2,100
          P  = YSOL(3,K)
          V  = YSOL(2,K)
          Z  = DBLE(K-1)*DZ
          WJ = DEFAXE(JCOQ,JMOD,Z,LONG,NBM,TCOEF)
          SOM = SOM + (P*COEP+0.5D0*RHOF*CF0*UMOY*V)*WJ
  20    CONTINUE
C
C-----2.3.CONTRIBUTION DU DERNIER POINT
C
        P  = YSOL(3,101)
        V  = YSOL(2,101)
        WJ = DEFAXE(JCOQ,JMOD,LONG,LONG,NBM,TCOEF)
        SOM = SOM + 0.5D0*(P*COEP+0.5D0*RHOF*CF0*UMOY*V)*WJ
C
      ENDIF
C
C-----3.DEDUCTION DE B(I,J)
C
      BIJ = -1.D0*PI*RAYON*DBLE(COS(RKI*(THETAI-THETAJ)))
     &           *DZ*SOM/(RKI*RKI)
C
      END
