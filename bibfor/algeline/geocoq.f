      SUBROUTINE GEOCOQ(NOMA,NOMGRP,CAELEM,IAXE,GEOM)
      IMPLICIT NONE
C-----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGELINE  DATE 03/07/2012   AUTEUR PELLET J.PELLET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2012  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C-----------------------------------------------------------------------
C DETERMINATION DES GRANDEURS GEOMETRIQUES CARACTERISTIQUES D'UNE
C CONFIGURATION "COQUES CYLINDRIQUES COAXIALES"
C APPELANT : FLUST4
C-----------------------------------------------------------------------
C  IN : NOMA   : NOM DU CONCEPT DE TYPE MAILLAGE
C  IN : NOMGRP : LISTE DES NOMS DES GROUPES DE NOEUDS/GROUPES DE MAILLES
C                CORRESPONDANT AUX COQUES (LES GROUPES DE NOEUDS ONT ETE
C                PREALABLEMENT CREES A PARTIR DES GROUPES DE MAILLES ET
C                ON LEUR A AFFECTE LES MEMES NOMS)
C  IN : CAELEM : NOM DU CONCEPT DE TYPE CARA_ELEM
C  IN : IAXE   : INDICE CARACTERISANT L'AXE DE REVOLUTION DES COQUES
C                IAXE = 1 : AXE X DU REPERE GLOBAL
C                IAXE = 2 : AXE Y DU REPERE GLOBAL
C                IAXE = 3 : AXE Z DU REPERE GLOBAL
C OUT : GEOM   : VECTEUR DE GRANDEURS GEOMETRIQUES CARACTERISTIQUES
C       GEOM(1)= HMOY  EPAISSEUR MOYENNE DE L'ESPACE ANNULAIRE
C       GEOM(2)= RMOY  RAYON MOYEN
C       GEOM(3)= LONG  LONGUEUR DU DOMAINE DE RECOUVREMENT DES COQUES
C       GEOM(4)= Z0    BORNE INF DU DOMAINE DE RECOUVREMENT DES COQUES
C       GEOM(5)= Z1    BORNE SUP DU DOMAINE DE RECOUVREMENT DES COQUES
C       GEOM(6)= EPINT EPAISSEUR DE LA COQUE INTERNE
C       GEOM(7)= EPEXT EPAISSEUR DE LA COQUE EXTERNE
C       GEOM(8)= RINT  RAYON DE LA COQUE INTERNE
C       GEOM(9)= REXT  RAYON DE LA COQUE EXTERNE
C-----------------------------------------------------------------------
C
      INCLUDE 'jeveux.h'
      CHARACTER*8  NOMA,NOMGRP(*),CAELEM
      INTEGER      IAXE
      REAL*8       GEOM(9)
C
      REAL*8       LONG
      CHARACTER*1  K1BID
      CHARACTER*8  COQUEI,COQUEX,NOMCMP,K8BID
      CHARACTER*24 GRPNO,GRPMA,COORNO,CARTE,CADESC,CAVALE
C
C-----------------------------------------------------------------------
C-----------------------------------------------------------------------
      INTEGER IAS ,IASCQI ,IASCQX ,IASEDI ,IASMAX ,ICMP ,ICODE 
      INTEGER ICOOR ,IDESC ,IDIR1 ,IDIR2 ,IDIR3 ,IER ,IGRAND 
      INTEGER INO ,INOMCP ,INUNOE ,INUNOI ,IRANG ,IRANV ,IRET 
      INTEGER ITAB ,IVALE ,NBCMP ,NBEC ,NBNOEX ,NBNOIN ,NUCOQI 
      INTEGER NUCOQX ,NUENTI ,NUNOE ,NUNOEX ,NUNOI ,NUNOIN 
      REAL*8 DIFZ ,DIFZ1 ,DIFZ2 ,EPEXT ,EPINT ,HMOY ,R8PREM 
      REAL*8 REXT ,RINT ,RMOY ,TOLE ,X2 ,X3 ,Z0 
      REAL*8 Z0EXT ,Z0INT ,Z1 ,Z1EXT ,Z1INT ,ZNO 
C-----------------------------------------------------------------------
      CALL JEMARQ()
C
C
C --- 1.INITIALISATIONS ET ACCES AUX OBJETS UTILES
C
      TOLE = 100.D0*R8PREM()
C
      IF (IAXE.EQ.1) THEN
        IDIR1 = 1
        IDIR2 = 2
        IDIR3 = 3
      ELSE IF (IAXE.EQ.2) THEN
        IDIR1 = 2
        IDIR2 = 3
        IDIR3 = 1
      ELSE
        IDIR1 = 3
        IDIR2 = 1
        IDIR3 = 2
      ENDIF
C
      COQUEI = NOMGRP(1)
      COQUEX = NOMGRP(2)
C
      GRPMA = NOMA//'.GROUPEMA'
C
      COORNO = NOMA//'.COORDO    .VALE'
      CALL JEVEUO(COORNO,'L',ICOOR)
C
C
C --- 2.DETERMINATION DES BORNES DU DOMAINE DE RECOUVREMENT DES DEUX
C ---   COQUES. DEDUCTION DE LA LONGUEUR DE RECOUVREMENT
C
C --- 2.1.BORNE INF ET BORNE SUP ASSOCIEES A LA COQUE INTERNE
C
      GRPNO='&&MEFGMN.'//COQUEI
      CALL JELIRA(GRPNO,'LONMAX',NBNOIN,K1BID)
      IF (NBNOIN.LT.4) CALL U2MESS('F','ALGELINE_49')
      CALL JEVEUO(GRPNO,'L',INUNOI)
      NUNOI = ZI(INUNOI)
      Z0INT = ZR(ICOOR+3*(NUNOI-1)+IDIR1-1)
      Z1INT = Z0INT
      DO 10 INO = 2,NBNOIN
        NUNOI = ZI(INUNOI+INO-1)
        ZNO = ZR(ICOOR+3*(NUNOI-1)+IDIR1-1)
        IF (ZNO.LT.Z0INT) Z0INT = ZNO
        IF (ZNO.GT.Z1INT) Z1INT = ZNO
  10  CONTINUE
      DIFZ = DBLE(ABS(Z1INT-Z0INT))
      IF (DIFZ.LT.TOLE) CALL U2MESS('F','ALGELINE_50')
C
C --- 2.2.BORNE INF ET BORNE SUP ASSOCIEES A LA COQUE EXTERNE
C
      GRPNO='&&MEFGMN.'//COQUEX
      CALL JELIRA(GRPNO,'LONMAX',NBNOEX,K1BID)
      IF (NBNOEX.LT.4) CALL U2MESS('F','ALGELINE_51')
      CALL JEVEUO(GRPNO,'L',INUNOE)
      NUNOE = ZI(INUNOE)
      Z0EXT = ZR(ICOOR+3*(NUNOE-1)+IDIR1-1)
      Z1EXT = Z0EXT
      DO 20 INO = 2,NBNOEX
        NUNOE = ZI(INUNOE+INO-1)
        ZNO = ZR(ICOOR+3*(NUNOE-1)+IDIR1-1)
        IF (ZNO.LT.Z0EXT) Z0EXT = ZNO
        IF (ZNO.GT.Z1EXT) Z1EXT = ZNO
  20  CONTINUE
      DIFZ = DBLE(ABS(Z1EXT-Z0EXT))
      IF (DIFZ.LT.TOLE) CALL U2MESS('F','ALGELINE_52')
C
C --- 2.3.SORTIE EN ERREUR SI NON RECOUVREMENT DES DOMAINES ASSOCIES
C ---     AUX DEUX COQUES
C
      DIFZ1 = DBLE(ABS(Z1INT-Z0EXT))
      DIFZ2 = DBLE(ABS(Z1EXT-Z0INT))
      IF (Z1INT.LT.Z0EXT .OR. Z1EXT.LT.Z0INT .OR. DIFZ1.LT.TOLE .OR.
     &    DIFZ2.LT.TOLE) CALL U2MESS('F','ALGELINE_53')
C
C --- 2.4.DEDUCTION DES BORNES DU DOMAINE DE RECOUVREMENT
C ---     ET DE LA LONGUEUR DU DOMAINE
C
      Z0 = Z0INT
      IF (Z0EXT.GT.Z0INT) Z0 = Z0EXT
      Z1 = Z1INT
      IF (Z1EXT.LT.Z1INT) Z1 = Z1EXT
      LONG = Z1 - Z0
      GEOM(3) = LONG
      GEOM(4) = Z0
      GEOM(5) = Z1
C
C
C --- 3.RECUPERATION DE EPINT ET EPEXT
C
C --- 3.1.ACCES A LA CARTE DES CARACTERISTIQUES DES ELEMENTS DE COQUE
C ---    (OBJETS DU CONCEPT DE TYPE CARA_ELEM)
C
      CARTE = CAELEM//'.CARCOQUE       '
      CADESC = CARTE(1:19)//'.DESC'
      CAVALE = CARTE(1:19)//'.VALE'
      CALL JEEXIN(CADESC,IRET)
      IF (IRET.EQ.0) CALL U2MESS('F','ALGELINE_54')

      CALL DISMOI('F','NB_EC','CACOQU','GRANDEUR',NBEC,K8BID,IER)
      CALL ASSERT(NBEC.EQ.2)
C     -- ON SUPPOSE EGALEMENT QUE LES CMPS QUI NOUS INTERESSENT SE
C        TROUVENT DANS LE 1ER ENTIER CODE.

      CALL JEVEUO(CADESC,'L',IDESC)
      CALL JEVEUO(CAVALE,'L',IVALE)
C
C --- 3.2.DETECTION DES GROUPES DE MAILLES ASSOCIES AUX COQUES INTERNE
C ---     ET EXTERNE DANS LE DESCRIPTEUR DE LA CARTE
C
      CALL JENONU(JEXNOM(GRPMA,COQUEI),NUCOQI)
      CALL JENONU(JEXNOM(GRPMA,COQUEX),NUCOQX)
      IASMAX = ZI(IDESC+1)
      IASEDI = ZI(IDESC+2)
      IASCQI = 0
      IASCQX = 0
      DO 30 IAS = 1,IASEDI
        ICODE = ZI(IDESC+3+2*(IAS-1))
        IF (ICODE.EQ.2) THEN
          NUENTI = ZI(IDESC+3+2*(IAS-1)+1)
          IF (NUENTI.EQ.NUCOQI) IASCQI = IAS
          IF (NUENTI.EQ.NUCOQX) IASCQX = IAS
        ENDIF
  30  CONTINUE
      IF (IASCQI.EQ.0 .OR. IASCQX.EQ.0) CALL U2MESS('F','ALGELINE_56')
C
C --- 3.3.DETERMINATION DU RANG DE LA COMPOSANTE <EP> DANS LE CHAMP
C ---     DE LA GRANDEUR
C
      IGRAND = ZI(IDESC)
      CALL JELIRA(JEXNUM('&CATA.GD.NOMCMP',IGRAND),'LONMAX',NBCMP,K1BID)
C
      CALL JEVEUO(JEXNOM('&CATA.GD.NOMCMP','CACOQU'),'L',INOMCP)
      NOMCMP = 'EP'
      IRANG = 0
      DO 40 ICMP = 1,NBCMP
        IF (ZK8(INOMCP+ICMP-1).EQ.NOMCMP) IRANG = ICMP
  40  CONTINUE
      IF (IRANG.EQ.0) CALL U2MESS('F','ALGELINE_57')
C
C --- 3.4.EXTRACTION DE LA VALEUR DE L'EPAISSEUR DE LA COQUE INTERNE
C
      CALL WKVECT('&&GEOCOQ.TEMP.TABL','V V I',NBCMP,ITAB)
      ICODE = ZI(IDESC-1+3+2*IASMAX+2*(IASCQI-1)+1)
      CALL DEC2PN(ICODE,ZI(ITAB),NBCMP)
      IRANV = 0
      DO 50 ICMP = 1,IRANG
        IF (ZI(ITAB+ICMP-1).EQ.1) IRANV = IRANV + 1
  50  CONTINUE
      IF (IRANV.EQ.0) CALL U2MESS('F','ALGELINE_58')
C
      EPINT = ZR(IVALE+NBCMP*(IASCQI-1)+IRANV-1)
      IF (EPINT.EQ.0.D0) CALL U2MESS('F','ALGELINE_59')
C
      GEOM(6) = EPINT
C
C --- 3.5.EXTRACTION DE LA VALEUR DE L'EPAISSEUR DE LA COQUE EXTERNE
C
      ICODE = ZI(IDESC-1+3+2*IASMAX+2*(IASCQX-1)+1)
      CALL DEC2PN(ICODE,ZI(ITAB),NBCMP)
      IRANV = 0
      DO 51 ICMP = 1,IRANG
        IF (ZI(ITAB+ICMP-1).EQ.1) IRANV = IRANV + 1
  51  CONTINUE
      IF (IRANV.EQ.0) CALL U2MESS('F','ALGELINE_60')
C
      EPEXT = ZR(IVALE+NBCMP*(IASCQX-1)+IRANV-1)
      IF (EPEXT.EQ.0.D0) CALL U2MESS('F','ALGELINE_61')
C
      GEOM(7) = EPEXT
C
C
C --- 4.DETERMINATION DE RINT, REXT, RMOY ET HMOY
C
      NUNOIN = ZI(INUNOI)
      X2 = ZR(ICOOR+3*(NUNOIN-1)+IDIR2-1)
      X3 = ZR(ICOOR+3*(NUNOIN-1)+IDIR3-1)
      RINT = DBLE(SQRT(X2*X2 + X3*X3))
C
      NUNOEX = ZI(INUNOE)
      X2 = ZR(ICOOR+3*(NUNOEX-1)+IDIR2-1)
      X3 = ZR(ICOOR+3*(NUNOEX-1)+IDIR3-1)
      REXT = DBLE(SQRT(X2*X2 + X3*X3))
C
      IF (RINT.EQ.0.D0 .OR. REXT.EQ.0.D0) CALL U2MESS('F','ALGELINE_62')
C
      GEOM(8) = RINT
      GEOM(9) = REXT
C
      RMOY = ((RINT+EPINT/2.D0)+(REXT-EPEXT/2.D0))/2.D0
      HMOY = (REXT-EPEXT/2.D0) - (RINT+EPINT/2.D0)
      IF (HMOY.LE.0.D0) CALL U2MESS('F','ALGELINE_63')
C
      GEOM(1) = HMOY
      GEOM(2) = RMOY
C
      CALL JEDETC('V','&&GEOCOQ',1)
      CALL JEDEMA()
C
      END
