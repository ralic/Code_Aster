      SUBROUTINE MRMMVR(CUMUL,NOMMAT,SMDI,SMHC,NEQ,
     &                  NEQL,MATD,VECT,XSOL,NBVECT)
      IMPLICIT NONE
      CHARACTER*(*) CUMUL
      INTEGER*4 SMHC(*)
      INTEGER SMDI(*),NEQ,NBVECT,NEQL
      CHARACTER*(*) NOMMAT
      REAL*8 VECT(NEQ,NBVECT),XSOL(NEQ,NBVECT)
      LOGICAL MATD
C     ------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGELINE  DATE 11/01/2011   AUTEUR SELLENET N.SELLENET 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================
C TOLE CRP_4
C     ------------------------------------------------------------------
C                   MULTIPLICATION MATRICE PAR N VECTEURS
C         XSOL(1..NEQ,1..NBVECT) = MATRICE  * VECT(1..NEQ,1..NBVECT)
C     ------------------------------------------------------------------
C     VERSION : LES ENTITES SONT REELLES
C             : LA MATRICE EST STOCKEE MORSE (SYMETRIQUE OU NON)
C     ------------------------------------------------------------------
C
C
C     ----- DEBUT COMMUNS NORMALISES  JEVEUX  --------------------------
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C     -----  FIN  COMMUNS NORMALISES  JEVEUX  --------------------------
C
      REAL*8 ZERO
      CHARACTER*1 KBID
      CHARACTER*14 NUMDDL
      CHARACTER*19 NOM19
      CHARACTER*24 VALM,REFA
      CHARACTER*32 JEXNUM
      INTEGER KFIN,JVALMS,JVALMI,JVEC,KI,KDEB,NBLOC
      INTEGER ILIG,JCOL,JREFA,JNULG,ILIGG,JCOLG,NUMGLO
      LOGICAL NONSYM
C     ------------------------------------------------------------------
C
C
C
      CALL JEMARQ()
      NOM19 = NOMMAT

      VALM = NOM19//'.VALM'
      CALL JELIRA(VALM,'NMAXOC',NBLOC,KBID)
      CALL ASSERT(NBLOC.EQ.1 .OR. NBLOC.EQ.2)
      NONSYM = (NBLOC.EQ.2)

      ZERO = 0.D0
      IF (CUMUL.EQ.'ZERO') THEN
        DO 20 JVEC = 1,NBVECT
          DO 10 ILIG = 1,NEQ
            XSOL(ILIG,JVEC) = ZERO
   10     CONTINUE
   20   CONTINUE
      ENDIF

      IF ( MATD ) THEN
        REFA = NOM19//'.REFA'
        CALL JEVEUO(REFA,'L',JREFA)
        NUMDDL = ZK24(JREFA+2-1)
        CALL JEVEUO(NUMDDL//'.NUML.NULG','L',JNULG)
      ENDIF

C     -- VALM(1) : AU DESSUS DE LA DIAGONALE
      CALL JEVEUO(JEXNUM(VALM,1),'L',JVALMS)
      IF (NONSYM) THEN
C        -- VALM(2) : AU DESSOUS DE LA DIAGONALE
        CALL JEVEUO(JEXNUM(VALM,2),'L',JVALMI)
      ELSE
        JVALMI = JVALMS
      ENDIF


      DO 50 JVEC = 1,NBVECT
        IF ( .NOT.MATD ) THEN
          XSOL(1,JVEC) = XSOL(1,JVEC) + ZR(JVALMS-1+1)*VECT(1,JVEC)
          DO 40 ILIG = 2,NEQ
             KDEB = SMDI(ILIG-1) + 1
             KFIN = SMDI(ILIG) - 1
CCDIR$ IVDEP
             DO 30 KI = KDEB,KFIN
               JCOL = SMHC(KI)
               XSOL(ILIG,JVEC) = XSOL(ILIG,JVEC) +
     &                           ZR(JVALMI-1+KI)*VECT(JCOL,JVEC)
               XSOL(JCOL,JVEC) = XSOL(JCOL,JVEC) +
     &                           ZR(JVALMS-1+KI)*VECT(ILIG,JVEC)
   30        CONTINUE
             XSOL(ILIG,JVEC) = XSOL(ILIG,JVEC) +
     &                         ZR(JVALMS+KFIN)*VECT(ILIG,JVEC)
   40      CONTINUE
        ELSE
          NUMGLO = ZI(JNULG+1-1)
          XSOL(NUMGLO,JVEC) = XSOL(NUMGLO,JVEC) + 
     &                        ZR(JVALMS-1+1)*VECT(NUMGLO,JVEC)
          DO 60 ILIG = 2,NEQL
             ILIGG = ZI(JNULG+ILIG-1)
             KDEB = SMDI(ILIG-1) + 1
             KFIN = SMDI(ILIG) - 1
CCDIR$ IVDEP
             DO 70 KI = KDEB,KFIN
               JCOL = SMHC(KI)
               JCOLG = ZI(JNULG+JCOL-1)
               XSOL(ILIGG,JVEC) = XSOL(ILIGG,JVEC) +
     &                            ZR(JVALMI-1+KI)*VECT(JCOLG,JVEC)
               XSOL(JCOLG,JVEC) = XSOL(JCOLG,JVEC) +
     &                            ZR(JVALMS-1+KI)*VECT(ILIGG,JVEC)
   70        CONTINUE
             XSOL(ILIGG,JVEC) = XSOL(ILIGG,JVEC) +
     &                          ZR(JVALMS+KFIN)*VECT(ILIGG,JVEC)
   60     CONTINUE
        ENDIF
   50 CONTINUE

      CALL JEDEMA()
      END
