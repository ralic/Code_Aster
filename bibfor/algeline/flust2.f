      SUBROUTINE FLUST2 ( MELFLU, TYPFLU, BASE, NOMA, NUOR, AMOR, FREQ,
     &                    MASG, FACT, VITE, NBM, NPV, NIVPAR, NIVDEF )
      IMPLICIT REAL*8 (A-H,O-Z)
C-----------------------------------------------------------------------
C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGELINE  DATE 08/03/2004   AUTEUR REZETTE C.REZETTE 
C ======================================================================
C COPYRIGHT (C) 1991 - 2001  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR   
C (AT YOUR OPTION) ANY LATER VERSION.                                 
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT 
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF          
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU    
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.                            
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE   
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,       
C    1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.      
C ======================================================================
C-----------------------------------------------------------------------
C DESCRIPTION :  CALCUL DES PARAMETRES DE COUPLAGE FLUIDE-STRUCTURE
C -----------    POUR UNE CONFIGURATION DE TYPE "GRAPPE DE COMMANDE"
C
C                OPERATEUR APPELANT : CALC_FLUI_STRU , OP0144
C
C  IN : MELFLU : NOM DU CONCEPT DE TYPE MELASFLU PRODUIT
C  IN : TYPFLU : NOM DU CONCEPT DE TYPE TYPE_FLUI_STRU DEFINISSANT LA
C                CONFIGURATION ETUDIEE
C  IN : BASE   : NOM DU CONCEPT DE TYPE MODE_MECA DEFINISSANT LA BASE
C                MODALE DU SYSTEME AVANT PRISE EN COMPTE DU COUPLAGE
C  IN : NOMA   : NOM DU CONCEPT DE TYPE MAILLAGE
C  IN : NUOR   : LISTE DES NUMEROS D'ORDRE DES MODES SELECTIONNES POUR
C                LE COUPLAGE (PRIS DANS LE CONCEPT MODE_MECA)
C  IN : AMOR   : LISTE DES AMORTISSEMENTS REDUITS MODAUX INITIAUX
C  IN : VITE   : LISTE DES VITESSES D'ECOULEMENT ETUDIEES
C  IN : NBM    : NOMBRE DE MODES PRIS EN COMPTE POUR LE COUPLAGE
C  IN : NPV    : NOMBRE DE VITESSES D'ECOULEMENT
C  IN : NIVPAR : NIVEAU D'IMPRESSION DANS LE FICHIER RESULTAT POUR LES
C                PARAMETRES DU COUPLAGE (FREQ,AMOR)
C  IN : NIVDEF : NIVEAU D'IMPRESSION DANS LE FICHIER RESULTAT POUR LES
C                DEFORMEES MODALES
C  OUT: FREQ   : LISTE DES FREQUENCES ET AMORTISSEMENTS REDUITS MODAUX
C                PERTURBES PAR L'ECOULEMENT
C  OUT: MASG   : MASSES GENERALISEES DES MODES PERTURBES, SUIVANT LA
C                DIRECTION CHOISIE PAR L'UTILISATEUR
C  OUT: FACT   : PSEUDO FACTEUR DE PARTICIPATION
C
C-------------------   DECLARATION DES VARIABLES   ---------------------
C
C COMMUNS NORMALISES JEVEUX
C -------------------------
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
C
C ARGUMENTS
C ---------
      CHARACTER*19  MELFLU
      CHARACTER*8   TYPFLU, BASE, NOMA
      INTEGER       NUOR(*)
      REAL*8        AMOR(*), FREQ(*), MASG(*), FACT(*), VITE(*)
      INTEGER       NBM, NPV, NIVPAR, NIVDEF
C
C VARIABLES LOCALES
C -----------------
      INTEGER       IBID
      REAL*8        LP
      LOGICAL       LNUL, LNEG
      CHARACTER*1   K1B
      CHARACTER*8   NOMNO0, CAELEM, MODELE, K8B
      CHARACTER*24  FREQI, MLGNMA, MLGNNO, FSIC, FSVK, FSVR, CHVALE
C
C FONCTIONS EXTERNES
C ------------------
      REAL*8        R8PI, R8PREM
C     EXTERNAL      R8PI, R8PREM
C
C-------------------   DEBUT DU CODE EXECUTABLE    ---------------------
C
      CALL JEMARQ()
C
C
C-----1.DETERMINATION DU CAS DE CALCUL
C       PRISE EN COMPTE OU NON DU COUPLAGE FLUIDE-STRUCTURE
C
      FSIC = TYPFLU//'           .FSIC'
      CALL JEVEUO(FSIC,'L',LFSIC)
      ICOUPL = ZI(LFSIC+1)
C
C
C-----2.SI PRISE EN COMPTE DU COUPLAGE FLUIDE-STRUCTURE
C       VERIFICATION DU SIGNE DES VITESSES (STRICTEMENT POSITIF)
C
      VLIM = 1.D-5
      IF (ICOUPL.EQ.1) THEN
        LNUL = .FALSE.
        LNEG = .FALSE.
        DO 10 IV = 1,NPV
          IF (DBLE(ABS(VITE(IV))).LT.VLIM) THEN
            LNUL = .TRUE.
            GOTO 11
          ELSE IF (VITE(IV).LT.0.D0) THEN
            LNEG = .TRUE.
            GOTO 11
          ENDIF
  10    CONTINUE
  11    CONTINUE
        IF (LNUL .OR. LNEG) THEN
          CALL UTMESS('F','FLUST2','LES VITESSES ETUDIEES DOIVENT '//
     &      'ETRE STRICTEMENT POSITIVES. LE SENS DE L ECOULEMENT EST '//
     &      'DEFINI PAR LE CHOIX DE LA CONFIGURATION EXPERIMENTALE '//
     &      'GRAPPE2 DE REFERENCE')
        ENDIF
      ENDIF
C
C
C-----3.ACCES AUX OBJETS UTILES
C
      FREQI = BASE//'           .FREQ'
      CALL JEVEUO(FREQI,'L',IFREQI)
C
C
C-----4.REMPLISSAGE DES OBJETS .VALE DES CHAMPS DE DEPLACEMENTS
C
      CALL CPDEPL(MELFLU,BASE,NUOR,NBM)
C
C
C-----5.SI PRISE EN COMPTE DU COUPLAGE FLUIDE-STRUCTURE
C       EXECUTION DU CALCUL
C
      IF (ICOUPL.EQ.1) THEN
C
C-------5.1.CREATION D'OBJETS DE TRAVAIL
C
        CALL WKVECT('&&FLUST2.TEMP.CODIM','V V R',4    ,ICODIM)
        CALL WKVECT('&&FLUST2.TEMP.MIST' ,'V V R',NBM  ,IMIST )
        CALL WKVECT('&&FLUST2.TEMP.AMFR' ,'V V R',2*NBM,IAMFR )
        CALL WKVECT('&&FLUST2.TEMP.POIDS','V V R',2*NBM,IPOIDS)
C
        NT = 2
        LWORK = 2*NT*NT + 10*NT + 2
        CALL WKVECT('&&FLUST2.TEMP.WORK' ,'V V R',LWORK,IWORK )
C
C-------5.2.OPERATIONS SIMULTANEES :
C           - RECUPERATION DES MASSES MODALES INITIALES
C           - RECUPERATION DES FREQUENCES INITIALES
C           - CALCUL DES AMORTISSEMENTS MODAUX INITIAUX
C           - CALCUL DES PSEUDO FACTEURS DE PARTICIPATION EN EAU
C
        PI = R8PI()
C
        DO 30 IMOD = 1,NBM
C
          NUMOD = NUOR(IMOD)
C
          CALL RSADPA(BASE,'L',1,'MASS_GENE',NUMOD,0,LMASG,K8B)
          ZR(IMIST+IMOD-1) = ZR(LMASG)
C
          FI = ZR(IFREQI+NUMOD-1)
          ZR(IAMFR+IMOD-1) = 4.D0*PI*FI*AMOR(IMOD)*ZR(LMASG)
          ZR(IAMFR+NBM+IMOD-1) = FI
C
C
          CALL RSADPA(BASE,'L',1,'FACT_PARTICI_DX',NUMOD,0,LFACT,K8B)
          FACT(3*(IMOD-1)+1) = ZR(LFACT  ) * MASG(IMOD)
          FACT(3*(IMOD-1)+2) = ZR(LFACT+1) * MASG(IMOD)
          FACT(3*(IMOD-1)+3) = ZR(LFACT+2) * MASG(IMOD)
C
  30    CONTINUE
C
C-------5.3.TYPE DE CONFIGURATION GRAPPE --> VARIABLE INDIC ---
C           RECUPERATION DU DIAMETRE EXTERIEUR DU TUBE
C           RECUPERATION DES GRANDEURS GEOMETRIQUES CARACTERISTIQUES
C           DEDUCTION DE COEFFICIENTS DE DIMENSIONNEMENT
C           CALCUL DES PONDERATIONS DUES AUX DEFORMEES MODALES
C           CALCUL DES MASSES MODALES EN EAU
C
        CALL MDCONF(TYPFLU,BASE,NOMA,NBM,IBID,NUOR,0,IGRAP,
     &              LWORK,MASG,ZR(ICODIM),ZR(IPOIDS),PHIE,RBID)
C
C
C-------5.4.CALCUL DES PARAMETRES MODAUX SOUS ECOULEMENT
C
        CALL PACOUC(TYPFLU,MASG,ZR(ICODIM),VITE,ZR(IPOIDS),ZR(IMIST),
     &              FREQ,ZR(IAMFR),NBM,IGRAP,NPV,ZR(IWORK),LWORK,
     &              PHIE,RBID,IER)
C
C-------5.5.IMPRESSIONS DANS LE FICHIER RESULTAT SI DEMANDEES
C
        IF (NIVPAR.EQ.1 .OR. NIVDEF.EQ.1) THEN
          PHID = PHIE * (1172.D0/890.D0 - 1.D0)
          CALL FLUIMP(2,NIVPAR,NIVDEF,MELFLU,NUOR,FREQ,ZR(IFREQI),NBM,
     &                VITE,NPV,PHID)
        ENDIF
C
C
C-----6.SINON (COUPLAGE FLUIDE-STRUCTURE NON PRIS EN COMPTE)
C
      ELSE
C
C-------6.1.REMPLISSAGE DES OBJETS .MASG .FACT
C
        DO 100 IMOD = 1,NBM
          NUMOD = NUOR(IMOD)
          CALL RSADPA ( BASE,'L',1,'MASS_GENE'      ,NUMOD,0,LMASG,K8B)
          CALL RSADPA ( BASE,'L',1,'FACT_PARTICI_DX',NUMOD,0,LFACT,K8B)
          MASG(IMOD) = ZR(LMASG)
          FACT(3*(IMOD-1)+1) = ZR(LFACT  ) * MASG(IMOD)
          FACT(3*(IMOD-1)+2) = ZR(LFACT+1) * MASG(IMOD)
          FACT(3*(IMOD-1)+3) = ZR(LFACT+2) * MASG(IMOD)
 100    CONTINUE
C
C-------6.2.REMPLISSAGE DE L'OBJET .FREQ
C
        DO 110 IV = 1,NPV
          DO 120 IMOD = 1,NBM
            NUMOD = NUOR(IMOD)
            IND = 2*NBM*(IV-1)+2*(IMOD-1)+1
            FREQ(IND) = ZR(IFREQI+NUMOD-1)
            FREQ(IND+1) = AMOR(IMOD)
 120      CONTINUE
 110    CONTINUE
C
      ENDIF
C
      CALL JEDETC('V','&&FLUST2',1)
      CALL JEDEMA()
C
C --- FIN DE FLUST2.
      END
