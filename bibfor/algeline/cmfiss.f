      SUBROUTINE CMFISS(MAIN, GNO1, GNO2, PREFIX, MAINIT, NOMGMA,
     &                  NOMA, CONNEC, TYMA, NGMA, GPMA)

C            CONFIGURATION MANAGEMENT OF EDF VERSION
C MODIF ALGELINE  DATE 26/04/2011   AUTEUR COURTOIS M.COURTOIS 
C ======================================================================
C COPYRIGHT (C) 1991 - 2011  EDF R&D                  WWW.CODE-ASTER.ORG
C THIS PROGRAM IS FREE SOFTWARE; YOU CAN REDISTRIBUTE IT AND/OR MODIFY
C IT UNDER THE TERMS OF THE GNU GENERAL PUBLIC LICENSE AS PUBLISHED BY
C THE FREE SOFTWARE FOUNDATION; EITHER VERSION 2 OF THE LICENSE, OR
C (AT YOUR OPTION) ANY LATER VERSION.
C
C THIS PROGRAM IS DISTRIBUTED IN THE HOPE THAT IT WILL BE USEFUL, BUT
C WITHOUT ANY WARRANTY; WITHOUT EVEN THE IMPLIED WARRANTY OF
C MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE. SEE THE GNU
C GENERAL PUBLIC LICENSE FOR MORE DETAILS.
C
C YOU SHOULD HAVE RECEIVED A COPY OF THE GNU GENERAL PUBLIC LICENSE
C ALONG WITH THIS PROGRAM; IF NOT, WRITE TO EDF R&D CODE_ASTER,
C   1 AVENUE DU GENERAL DE GAULLE, 92141 CLAMART CEDEX, FRANCE.
C ======================================================================

      IMPLICIT NONE

      INTEGER      MAINIT
      CHARACTER*8  MAIN, GNO1, GNO2, PREFIX, NOMGMA
      CHARACTER*24 NOMA, CONNEC, TYMA, NGMA, GPMA

C ----------------------------------------------------------------------
C           CREA_MAILLAGE : CREA_FISS
C ----------------------------------------------------------------------
C IN        MAIN    NOM DU MAILLAGE (INITIAL)
C IN        GNO1    GROUP_NO_1
C IN        GNO2    GROUP_NO_2
C IN        PREFIX  PREFIXE POUR LE NOM DES MAILLES (EX : M, MJ, ...)
C IN        MAINIT  NUMERO INITIAL DES MAILLES CREEES
C IN        NOMGMA  NOM DU GROUP_MA CREE
C IN/JXOUT  NOMA    NOM DES MAILLES CREEES (VECTEUR DE K8)
C IN/JXOUT  CONNEC  CONNECTIVITE DES MAILLES CREEES (LISTE)
C                    NBR DE NOEUDS DE LA MAILLE (ICI, TOUJOURS 4),
C                    NUMEROS DES NDS DE LA MAILLE
C IN/JXOUT  TYMA    TYPE DES MAILLES CREEES (VECTEUR I)
C IN/JXOUT  NGMA    NOM DU GROUP_MA CREE
C IN/JXOUT  GPMA    LISTE DES MAILES DU GROUP_MA CREE
C                    NBR DE MAILLES DU GROUP_MA
C                    NUMERO DES MAILLES (NEGATIF QUAND NOUVELLE MAILLE)
C ----------------------------------------------------------------------

C     ----- DEBUT COMMUNS NORMALISES  JEVEUX  --------------------------
      INTEGER ZI
      COMMON /IVARJE/ZI(1)
      REAL*8 ZR
      COMMON /RVARJE/ZR(1)
      COMPLEX*16 ZC
      COMMON /CVARJE/ZC(1)
      LOGICAL ZL
      COMMON /LVARJE/ZL(1)
      CHARACTER*8 ZK8
      CHARACTER*16 ZK16
      CHARACTER*24 ZK24
      CHARACTER*32 ZK32
      CHARACTER*80 ZK80
      COMMON /KVARJE/ZK8(1),ZK16(1),ZK24(1),ZK32(1),ZK80(1)
      CHARACTER*32 JEXNOM
C     -----  FIN  COMMUNS NORMALISES  JEVEUX  --------------------------

      INTEGER IRET, NB1, NB2, NBMAJO, TYQUA4, MA
      INTEGER N1,N2,N3,N4
      INTEGER INOMA, ICONNE, ITYMA, IGPMA, INGMA,JCON, JGNO1, JGNO2
      INTEGER LXLGUT, LGMA, LGPREF
      CHARACTER*8 K8B
      CHARACTER*80 KNUME
C ----------------------------------------------------------------------



C - VERIFICATIONS DES GROUP_NO

      CALL JEEXIN(JEXNOM(MAIN//'.GROUPENO',GNO1),IRET)
      IF (IRET .EQ. 0) CALL U2MESK('F','ALGELINE_19',1,GNO1)
      CALL JEEXIN(JEXNOM(MAIN//'.GROUPENO',GNO2),IRET)
      IF (IRET .EQ. 0) CALL U2MESK('F','ALGELINE_19',1,GNO2)

      CALL JELIRA(JEXNOM(MAIN//'.GROUPENO',GNO1),'LONUTI',NB1,K8B)
      CALL JELIRA(JEXNOM(MAIN//'.GROUPENO',GNO2),'LONUTI',NB2,K8B)

      IF (NB1 .NE. NB2) CALL U2MESS('F','ALGELINE_20')
      IF (NB1.LE.1) CALL U2MESS('F','ALGELINE_21')


C - INITIALISATION

      NBMAJO = NB1 - 1
      LGPREF = LXLGUT(PREFIX)
      CALL JEVEUO(JEXNOM(MAIN//'.GROUPENO',GNO1),'L',JGNO1)
      CALL JEVEUO(JEXNOM(MAIN//'.GROUPENO',GNO2),'L',JGNO2)
      CALL JENONU(JEXNOM('&CATA.TM.NOMTM','QUAD4'), TYQUA4)


C - CREATION DES VECTEURS RESULTATS

      CALL WKVECT(NOMA  , 'V V K8', NBMAJO  , INOMA )
      CALL WKVECT(CONNEC, 'V V I' , NBMAJO*5, ICONNE)
      CALL WKVECT(TYMA  , 'V V I' , NBMAJO  , ITYMA )
      CALL WKVECT(NGMA  , 'V V K8', 1       , INGMA )
      CALL WKVECT(GPMA  , 'V V I' , NBMAJO+1, IGPMA )


C - NOM ET TAILLE DU NOUVEAU GROUP_MA

      ZK8(INGMA) = NOMGMA
      ZI(IGPMA)  = NBMAJO


C - CREATION DES MAILLES

      JCON = ICONNE
      DO 10 MA = 1, NBMAJO

C      NOM DE LA MAILLE CREEE
        CALL CODENT(MAINIT-1+MA,'G',KNUME)
        LGMA = LXLGUT(KNUME)
        IF (LGMA+LGPREF.GT.8) CALL U2MESS('F','ALGELINE_17')
        ZK8(INOMA-1 + MA) = PREFIX(1:LGPREF) // KNUME

C      TYPE DE LA NOUVELLE MAILLE : QUAD4
        ZI(ITYMA-1 + MA) = TYQUA4

C      CONNECTIVITE DE LA NOUVELLE MAILLE

C ATTENTION QUAND ON DEFINIT DEUX GROUPES DE NOEUD ET QUE
C L'ON VEUT CREER UN JOINT GRACE A CES DEUX GROUPES , IL FAUT COMMENCER
C LA RENUMEROTATION  (NOEUD_ORDO)  DANS LE BON SENS DE FACON A CE QUE
C LA CONNECTIVITE LOCALE DEFINIE CI-APRES DONNE DES ED NUMEROTÉ DANS LE
C SENS TRIGO

        ZI(JCON  ) = 4
        N1 = ZI(JGNO1-1 + MA  )
        N2 = ZI(JGNO1-1 + MA+1)
        N3 = ZI(JGNO2-1 + MA+1)
        N4 = ZI(JGNO2-1 + MA  )
        ZI(JCON+1) = N1
        ZI(JCON+2) = N2
        ZI(JCON+3) = N3
        ZI(JCON+4) = N4
        JCON = JCON + 5

C LES NOEUDS TOPOLOGIQUEMENT CONFONDUS NE SERONT PAS BIEN TRAITES
C PAR CALCUL LORS DE LA PHASE DE RESOLUTION NON LINEAIRE
C ON ARRETE DONC DES CE STADE
        IF (N1.EQ.N2 .OR. N1.EQ.N3 .OR. N1.EQ.N4 .OR.
     &      N2.EQ.N3 .OR. N2.EQ.N4 .OR. N3.EQ.N4)
     &    CALL U2MESS('F','ALGELINE_22')
C      INSERTION DE LA MAILLE DANS LE NOUVEAU GROUP_MA
        ZI(IGPMA + MA) = - MA

 10   CONTINUE

      END
